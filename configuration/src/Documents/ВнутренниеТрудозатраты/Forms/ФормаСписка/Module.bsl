///////////////////////////////////////////////////////////////////////////////////////////////////////
// Copyright (c) 2025, ООО ДНС Технологии
// SDMS (Software Development Management System) — это корпоративная система учета разработки и управления проектами 
// Все права защищены. Эта программа и сопроводительные материалы предоставляются 
// в соответствии с условиями лицензии General Public License (GNU GPL v3)
// Текст лицензии доступен по ссылке:
// https://www.gnu.org/licenses/gpl-3.0.html
///////////////////////////////////////////////////////////////////////////////////////////////////////

#Область ОбработчикиСобытийФормы
	
&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	// Получаем информация о сотруднике для дальнейшего использования на клиенте
	ИнформацияОПользователе = ПолучитьИнформациюОПользователе();
	
	// Устанавливаем фиксированный отбор для не IT
	Если НЕ ИнформацияОПользователе.ПринадлежитIT Тогда
		Список.Параметры.УстановитьЗначениеПараметра("Филиал", ИнформацияОПользователе.Филиал);
	КонецЕсли;
		
	// Устанавливаем ограничения на отбор
	Документы.ВнутренниеТрудозатраты.УстановитьОграниченияСписка(Список, ИнформацияОПользователе.ПринадлежитIT);

КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовФормы

&НаКлиенте
Процедура АктивныйПриИзменении(Элемент)
	
	// Обработка параметров
	Если Активный = 1 Тогда
		Список.Параметры.УстановитьЗначениеПараметра("Пользователь", ИнформацияОПользователе.Пользователь);
		Список.Параметры.УстановитьЗначениеПараметра("Активный", Истина);
	КонецЕсли;
	
	Для Каждого Параметр Из Список.Параметры.Элементы Цикл
		Если Параметр.Параметр <> Новый ПараметрКомпоновкиДанных("Филиал") Тогда
			Параметр.Использование = (Активный = 1);
		ИначеЕсли НЕ ИнформацияОПользователе.ПринадлежитIT Тогда
			Параметр.Использование = (Активный = 0);
		КонецЕсли;
	КонецЦикла;

КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Функция ПолучитьИнформациюОПользователе()
	
	Информация = Новый Структура("Пользователь, Филиал, ПринадлежитIT", 
		ПараметрыСеанса.ТекущийПользователь, ПараметрыСеанса.Филиал, Ложь);
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	Пользователи.Ссылка КАК Пользователь,
	|	Пользователи.Филиал КАК Филиал,
	|	ВЫБОР
	|		КОГДА Филиалы.ТипФилиала <> ЗНАЧЕНИЕ(Перечисление.ТипыФилиалов.НеIT)
	|			ТОГДА ИСТИНА
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ КАК ПринадлежитIT
	|ИЗ
	|	Справочник.Пользователи КАК Пользователи
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.Филиалы КАК Филиалы
	|		ПО Пользователи.Филиал = Филиалы.Ссылка
	|ГДЕ
	|	Пользователи.Ссылка = &Пользователь";
	
	Запрос.УстановитьПараметр("Пользователь", ПараметрыСеанса.ТекущийПользователь);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Если НЕ РезультатЗапроса.Пустой() Тогда 
		Выборка = РезультатЗапроса.Выбрать();
		Выборка.Следующий();
		ЗаполнитьЗначенияСвойств(Информация, Выборка);
	КонецЕсли;
	
	Возврат Информация;
	          	
КонецФункции                      

#КонецОбласти
