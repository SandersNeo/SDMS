///////////////////////////////////////////////////////////////////////////////////////////////////////
// Copyright (c) 2025, ООО ДНС Технологии
// SDMS (Software Development Management System) — это корпоративная система учета разработки и управления проектами 
// Все права защищены. Эта программа и сопроводительные материалы предоставляются 
// в соответствии с условиями лицензии General Public License (GNU GPL v3)
// Текст лицензии доступен по ссылке:
// https://www.gnu.org/licenses/gpl-3.0.html
///////////////////////////////////////////////////////////////////////////////////////////////////////

#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	МассивОбъектов = Параметры.Объекты;  
	ОцениватьВStoryPoint = Параметры.ОцениватьВStoryPoint;
	
	Если Параметры.ВидСпринта = Перечисления.ВидыСпринта.IT Тогда
		СпринтIT = Истина;
	КонецЕсли;
	
	УчитыватьВремяТестирования = СпринтIT И НЕ Параметры.ЭтоСпринтТестирования И НЕ ОцениватьВStoryPoint;
	Элементы.ВремяОбъектаВремяТестирования.Видимость = УчитыватьВремяТестирования;
	
	ЗаполнитьТаблицуВремяОбъектов(МассивОбъектов); 
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)      
	
	Если ВремяОбъекта.Количество() > 0 Тогда 
		Элементы.ВремяОбъекта.ТекущаяСтрока = 0; 
		ЭтотОбъект.ТекущийЭлемент = Элементы.ВремяОбъектаВремя;
	КонецЕсли;
		
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура Записать(Команда)
	
	Результат = Новый Соответствие;
	
	Если СпринтIT И НЕ ОцениватьВStoryPoint Тогда
		РезультатПроверки = ПроверитьЗаполненностьВремени();    
		
		Если НЕ РезультатПроверки Тогда
			Возврат;
		КонецЕсли;                   
	КонецЕсли;
		
	Для Каждого Строка Из ВремяОбъекта Цикл
		ВремяНаСпринт = Новый Структура;
		ВремяНаСпринт.Вставить("Время", Строка.Время);
		ВремяНаСпринт.Вставить("ВремяТестирования", Строка.ВремяТестирования);	
		
		Результат.Вставить(Строка.Объект, ВремяНаСпринт);
	КонецЦикла;
	
	Закрыть(Результат);
	
КонецПроцедуры

&НаКлиенте
Процедура Отмена(Команда)
	
	Закрыть();
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыФункции

&НаСервере
Процедура ЗаполнитьТаблицуВремяОбъектов(Знач МассивОбъектов)
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	Задача.Представление КАК ОбъектПредставление,
	|	Задача.Ссылка КАК Объект
	|ПОМЕСТИТЬ Объекты
	|ИЗ
	|	Документ.Задача КАК Задача
	|ГДЕ
	|	Задача.Ссылка В(&Ссылка)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ВнутреннееЗадание.Представление,
	|	ВнутреннееЗадание.Ссылка
	|ИЗ
	|	Документ.ВнутреннееЗадание КАК ВнутреннееЗадание
	|ГДЕ
	|	ВнутреннееЗадание.Ссылка В(&Ссылка)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ЗаявкаНаРазработку.Представление,
	|	ЗаявкаНаРазработку.Ссылка
	|ИЗ
	|	Документ.ЗаявкаНаРазработку КАК ЗаявкаНаРазработку
	|ГДЕ
	|	ЗаявкаНаРазработку.Ссылка В(&Ссылка)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ПланируемыеТрудозатратыПоСпринтам.Объект КАК Объект,
	|	СУММА(ПланируемыеТрудозатратыПоСпринтам.Трудозатраты) КАК Трудозатраты,
	|	ПланируемыеТрудозатратыПоСпринтам.ТипТрудозатрат КАК ТипТрудозатрат
	|ПОМЕСТИТЬ РаспределенныйПлан
	|ИЗ
	|	РегистрСведений.ПланируемыеТрудозатратыПоСпринтам КАК ПланируемыеТрудозатратыПоСпринтам
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Объекты КАК Объекты
	|		ПО (Объекты.Объект = ПланируемыеТрудозатратыПоСпринтам.Объект)
	|ГДЕ
	|	НЕ &ОцениватьВStoryPoint
	|
	|СГРУППИРОВАТЬ ПО
	|	ПланируемыеТрудозатратыПоСпринтам.Объект,
	|	ПланируемыеТрудозатратыПоСпринтам.ТипТрудозатрат
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Объекты.Объект КАК Объект,
	|	Объекты.ОбъектПредставление КАК ОбъектПредставление,
	|	ВЫБОР
	|		КОГДА НЕ &ОцениватьВStoryPoint
	|			ТОГДА ЕСТЬNULL(ПланТрудозатратТестирования.Трудозатраты, 0) - ЕСТЬNULL(РаспределенныйПланТестирование.Трудозатраты, 0)
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК ВремяТестирования,
	|	ВЫБОР
	|		КОГДА НЕ &ОцениватьВStoryPoint
	|			ТОГДА ЕСТЬNULL(ПланируемыеТрудозатраты.Трудозатраты, 0) - ЕСТЬNULL(РаспределенныйПланРазработка.Трудозатраты, 0)
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК Время
	|ИЗ
	|	Объекты КАК Объекты
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ПланируемыеТрудозатраты КАК ПланируемыеТрудозатраты
	|		ПО Объекты.Объект = ПланируемыеТрудозатраты.Объект
	|			И (ПланируемыеТрудозатраты.ТипТрудозатрат = ЗНАЧЕНИЕ(Перечисление.ТипыТрудозатрат.Разработка))
	|		ЛЕВОЕ СОЕДИНЕНИЕ РаспределенныйПлан КАК РаспределенныйПланРазработка
	|		ПО (РаспределенныйПланРазработка.Объект = Объекты.Объект)
	|			И (РаспределенныйПланРазработка.ТипТрудозатрат = ЗНАЧЕНИЕ(Перечисление.ТипыТрудозатрат.Разработка))
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ПланируемыеТрудозатраты КАК ПланТрудозатратТестирования
	|		ПО (ПланТрудозатратТестирования.Объект = Объекты.Объект)
	|			И (ПланТрудозатратТестирования.ТипТрудозатрат = ЗНАЧЕНИЕ(Перечисление.ТипыТрудозатрат.Тестирование))
	|		ЛЕВОЕ СОЕДИНЕНИЕ РаспределенныйПлан КАК РаспределенныйПланТестирование
	|		ПО (РаспределенныйПланТестирование.Объект = Объекты.Объект)
	|			И (РаспределенныйПланТестирование.ТипТрудозатрат = ЗНАЧЕНИЕ(Перечисление.ТипыТрудозатрат.Тестирование))";
	
	Запрос.УстановитьПараметр("Ссылка", МассивОбъектов);
	Запрос.УстановитьПараметр("ОцениватьВStoryPoint", ОцениватьВStoryPoint);
	
	Выборка = Запрос.Выполнить().Выбрать();	
	
	Пока Выборка.Следующий() Цикл
		ЗаполнитьЗначенияСвойств(ВремяОбъекта.Добавить(), Выборка);		
	КонецЦикла;
	
КонецПроцедуры

&НаКлиенте
Функция ПроверитьЗаполненностьВремени()
	
	Результат = Истина;
	НезаполненныеСтроки = ВремяОбъекта.НайтиСтроки(Новый Структура("Время", 0));
	Если НезаполненныеСтроки.Количество() > 0 Тогда
		Результат = Ложь;
		Для Каждого Строка Из НезаполненныеСтроки Цикл
			Сообщение = Новый СообщениеПользователю;
			Сообщение.УстановитьДанные(ВремяОбъекта);
			Сообщение.Поле = СтрШаблон("ВремяОбъекта[%1].Время", Строка.ПолучитьИдентификатор());
			Сообщение.Текст = "Не заполнено время выполнения.";
			Сообщение.Сообщить();
		КонецЦикла;
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

#КонецОбласти
