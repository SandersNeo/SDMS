///////////////////////////////////////////////////////////////////////////////////////////////////////
// Copyright (c) 2025, ООО ДНС Технологии
// SDMS (Software Development Management System) — это корпоративная система учета разработки и управления проектами 
// Все права защищены. Эта программа и сопроводительные материалы предоставляются 
// в соответствии с условиями лицензии General Public License (GNU GPL v3)
// Текст лицензии доступен по ссылке:
// https://www.gnu.org/licenses/gpl-3.0.html
///////////////////////////////////////////////////////////////////////////////////////////////////////

#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	МассивСтруктур = Параметры.МассивОбъектов;
	Для Каждого Структура Из МассивСтруктур Цикл 
		// Если уже были фактические трудозатраты по задаче, то планируемые трудозатраты отображаем равными нулю  
		Если Структура.Свойство("ФактическиеТрудозатраты") И Структура.ФактическиеТрудозатраты > 0 Тогда   
			Структура.Вставить("ПланируемыеТрудозатраты", 0);	
		КонецЕсли; 
	
		НоваяСтрока = ОбъектыСпринтов.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяСтрока, Структура); 
	КонецЦикла;
	
	ЭтоНевыбранные = Параметры.Вид = "Невыбранные";
	Элементы.ОбъектыСпринтовСпринт.Видимость = НЕ ЭтоНевыбранные;
	Элементы.ОбъектыСпринтовПланируемыеТрудозатраты.Видимость = НЕ ЭтоНевыбранные;
	Элементы.ОбъектыСпринтовФактическиеТрудозатраты.Видимость = НЕ ЭтоНевыбранные;
	
	Элементы.ОбъектыСпринтовПериод.Видимость = ЭтоНевыбранные;
	
	Если ЭтоНевыбранные Тогда
		ОбъектыСпринтов.Сортировать("Период УБЫВ");
	КонецЕсли;		
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура Выбрать(Команда)
	
	Объекты = Новый Соответствие;
	
	Для Каждого Строка Из ОбъектыСпринтов Цикл
		Если Строка.Пометка И Объекты.Получить(Строка.Объект) = Неопределено Тогда
			Объекты.Вставить(Строка.Объект, Строка.Спринт);
		КонецЕсли;
	КонецЦикла;
	
	Закрыть(Новый Структура("Объекты", Объекты));
		
КонецПроцедуры

&НаКлиенте
Процедура ЗакрытьФорму(Команда)
	
	Закрыть(Новый Структура("Объекты", Новый Массив));
	
КонецПроцедуры

&НаКлиенте
Процедура ОбъектыСпринтовВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	ТекущиеДанные = Элементы.ОбъектыСпринтов.ТекущиеДанные;
	СтандартнаяОбработка = Ложь;   
	Тип = ТипЗнч(ТекущиеДанные.Объект);
	
	Если Поле.Имя = "ОбъектыСпринтовОбъект" Тогда 
		ПараметрыОткрытия = Новый Структура("Ключ", ТекущиеДанные.Объект);  

		Если Тип = Тип("ДокументСсылка.Задача") Тогда
			ОткрытьФорму("Документ.Задача.Форма.ФормаДокумента", ПараметрыОткрытия, ЭтотОбъект, КлючУникальности);
		ИначеЕсли Тип = Тип("ДокументСсылка.ЗаявкаНаРазработку") Тогда
			ОткрытьФорму("Документ.ЗаявкаНаРазработку.Форма.ФормаДокумента", ПараметрыОткрытия, ЭтотОбъект, КлючУникальности);
		КонецЕсли;
	ИначеЕсли Поле.Имя = "ОбъектыСпринтовСпринт" Тогда
		ПараметрыОткрытия = Новый Структура("Ключ", ТекущиеДанные.Спринт);  
		ОткрытьФорму("Документ.Спринт.Форма.ФормаДокумента", ПараметрыОткрытия, ЭтотОбъект, КлючУникальности);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти
