///////////////////////////////////////////////////////////////////////////////////////////////////////
// Copyright (c) 2025, ООО ДНС Технологии
// SDMS (Software Development Management System) — это корпоративная система учета разработки и управления проектами 
// Все права защищены. Эта программа и сопроводительные материалы предоставляются 
// в соответствии с условиями лицензии General Public License (GNU GPL v3)
// Текст лицензии доступен по ссылке:
// https://www.gnu.org/licenses/gpl-3.0.html
///////////////////////////////////////////////////////////////////////////////////////////////////////

#Если Сервер ИЛИ ВнешнееСоединение ИЛИ ТолстыйКлиентОбычноеПриложение Тогда
	
#Область ОбработчикиСобытий

Процедура ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	Если ПометкаУдаления Тогда
		
		Запрос = Новый Запрос;		
		Запрос.Текст = 
		"ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	ПланируемыеТрудозатратыПоСпринтам.Объект КАК Объект
		|ИЗ
		|	РегистрСведений.СоставСпринтов КАК СоставСпринтов
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.ПланируемыеТрудозатратыПоСпринтам КАК ПланируемыеТрудозатратыПоСпринтам
		|		ПО (ПланируемыеТрудозатратыПоСпринтам.Объект = СоставСпринтов.Объект)
		|			И (ПланируемыеТрудозатратыПоСпринтам.Спринт = СоставСпринтов.Спринт)
		|ГДЕ
		|	СоставСпринтов.Спринт = &Спринт";  
		
		Запрос.УстановитьПараметр("Спринт", Ссылка);
		
		Выборка = Запрос.Выполнить().Выбрать();
		
		Пока Выборка.Следующий() Цикл  
			РегистрыСведений.ПланируемыеТрудозатратыПоСпринтам.Удалить(Выборка.Объект, Ссылка);	
		КонецЦикла;
		
		ОбъектыСпринта = Новый СписокЗначений;
		РегистрыСведений.СоставСпринтов.Добавить(Ссылка, ОбъектыСпринта); 

		ТекущаяДатаСеанса = ТекущаяДатаСеанса();
		РегистрыСведений.ИсторияУчастияОбъектовВСпринтах.Добавить(Ссылка, ОбъектыСпринта, ТекущаяДатаСеанса);
	КонецЕсли;
	
	// Проверка отсутствия пересечения спринтов по периоду в разрезе филиал-команда
	Если ВидСпринта <> Перечисления.ВидыСпринта.Планирование Тогда  
		ПроверитьПересечения(Отказ);
	КонецЕсли;
	
КонецПроцедуры

Процедура ОбработкаПроверкиЗаполнения(Отказ, ПроверяемыеРеквизиты)
	
	// Реквизиты, которые будут исключены из платформенной проверки
	НепроверяемыеРеквизиты = Новый Массив;

	Если ДатаНачала > ДатаОкончания Тогда
		НепроверяемыеРеквизиты.Добавить("ДатаНачала");
		НепроверяемыеРеквизиты.Добавить("ДатаОкончания");
		
		Сообщение = Новый СообщениеПользователю(); 
		Сообщение.Текст = "Дата начала спринта не может быть позже, чем дата его окончания";
		Сообщение.Поле = "ДатаНачала";
		Сообщение.УстановитьДанные(ЭтотОбъект);
		Сообщение.Сообщить();
		
		Отказ = Истина;
	КонецЕсли;
	
	// Удаление реквизитов, которые платформе уже не нужно проверять
	ОбщегоНазначения.УдалитьНепроверяемыеРеквизиты(ПроверяемыеРеквизиты, НепроверяемыеРеквизиты);
	
КонецПроцедуры

Процедура ОбработкаЗаполнения(ДанныеЗаполнения, ТекстЗаполнения, СтандартнаяОбработка)
	
	Если ТипЗнч(ДанныеЗаполнения) = Тип("Структура") Тогда  
		ЗаполнитьЗначенияСвойств(ЭтотОбъект, ДанныеЗаполнения);
	КонецЕсли; 
	
	Если НЕ ЗначениеЗаполнено(ВидСпринта) Тогда
		ВидСпринта = Перечисления.ВидыСпринта.IT;
	КонецЕсли;
	
КонецПроцедуры

Процедура ПриКопировании(ОбъектКопирования)

	ДокументОснование = Документы.Спринт.ПустаяСсылка();
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Процедура ПроверитьПересечения(Отказ)
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ПРЕДСТАВЛЕНИЕ(Спринт.Ссылка) КАК Ссылка
	|ИЗ
	|	Документ.Спринт КАК Спринт
	|ГДЕ
	|	Спринт.Ссылка <> &Ссылка
	|	И НЕ Спринт.ПометкаУдаления
	|	И Спринт.Филиал = &Филиал
	|	И Спринт.ВидСпринта = &ВидСпринта
	|	И Спринт.Команда = &Команда
	|	И Спринт.ДатаНачала <= &ДатаОкончания
	|	И Спринт.ДатаОкончания >= &ДатаНачала";
	
	Запрос.УстановитьПараметр("Ссылка", Ссылка);
	Запрос.УстановитьПараметр("ВидСпринта", ВидСпринта);
	Запрос.УстановитьПараметр("Филиал", Филиал);
	Запрос.УстановитьПараметр("Команда", Команда);
	Запрос.УстановитьПараметр("ДатаНачала", ДатаНачала);
	Запрос.УстановитьПараметр("ДатаОкончания", ДатаОкончания);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Если НЕ РезультатЗапроса.Пустой() Тогда	
		Отказ = Истина;
		
		Выборка = РезультатЗапроса.Выбрать();
		КоличествоЗаписей = Выборка.Количество();
		ТексСообщения = "Изменения не сохранены. Период текущего спринта пересекается со следующим%1:";
		
		// Количество перых записей, которые отображаются в тексте сообщения
		ОтображатьПервые = 5;
		
		Если КоличествоЗаписей = 1 Тогда
			ТекстПодстановки = " спринтом";
		Иначе
			ТекстПодстановки = "и спринтами";
		КонецЕсли;
		
		КоличествоВыведенных = 0;
		
		Пока Выборка.Следующий() Цикл
			Если (КоличествоВыведенных = ОтображатьПервые) И (КоличествоЗаписей > КоличествоВыведенных) Тогда
				ТексСообщения = ТексСообщения + "
					|и еще " + Формат(КоличествоЗаписей - ОтображатьПервые, "ЧГ=0");
				Прервать;
			КонецЕсли;
			
			ТексСообщения = ТексСообщения + "
				|- " + Выборка.Ссылка;
			
			КоличествоВыведенных = КоличествоВыведенных + 1;
		КонецЦикла;
		
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(СтрШаблон(ТексСообщения, ТекстПодстановки));
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти
	
#КонецЕсли
