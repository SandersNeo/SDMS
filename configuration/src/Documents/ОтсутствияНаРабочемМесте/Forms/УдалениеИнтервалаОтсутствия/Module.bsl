///////////////////////////////////////////////////////////////////////////////////////////////////////
// Copyright (c) 2025, ООО ДНС Технологии
// SDMS (Software Development Management System) — это корпоративная система учета разработки и управления проектами 
// Все права защищены. Эта программа и сопроводительные материалы предоставляются 
// в соответствии с условиями лицензии General Public License (GNU GPL v3)
// Текст лицензии доступен по ссылке:
// https://www.gnu.org/licenses/gpl-3.0.html
///////////////////////////////////////////////////////////////////////////////////////////////////////

#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ПериодыОтсутствия = Параметры.ПериодыОтсутствия;
	Сотрудник = Параметры.Сотрудник;
	
	ТаблицаДат = Новый ТаблицаЗначений;
	ТаблицаДат.ЗаполнитьЗначения(Параметры.ПериодыОтсутствия);
	ТаблицаДат.Колонки.Добавить("ДатаОтсутствия", Новый ОписаниеТипов("Дата"));
	ТаблицаДат.Колонки.Добавить("Комментарий", ОбщегоНазначенияКлиентСервер.ОписаниеТипаСтрока(250));
	
	Для Каждого ПараметрОтсутсвия Из ПериодыОтсутствия Цикл 
		ЗаполнитьЗначенияСвойств(ТаблицаДат.Добавить(), ПараметрОтсутсвия);
	КонецЦикла;	
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ТаблицаДат.ДатаОтсутствия КАК ДатаОтсутствия,
	|	ТаблицаДат.Комментарий КАК Комментарий
	|ПОМЕСТИТЬ ТаблицаДат
	|ИЗ
	|	&ТаблицаДат КАК ТаблицаДат
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ПроизводственныйКалендарь.ДатаКалендаря КАК ДатаКалендаря,
	|	ПроизводственныйКалендарь.ВидДня КАК ВидДня
	|ПОМЕСТИТЬ ПроизводственныйКалендарь
	|ИЗ
	|	РегистрСведений.ПроизводственныйКалендарь КАК ПроизводственныйКалендарь
	|ГДЕ
	|	ПроизводственныйКалендарь.ДатаКалендаря МЕЖДУ &ДатаНачала И &ДатаОкончания
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТаблицаДат.ДатаОтсутствия КАК ДатаОтсутствия,
	|	ТаблицаДат.Комментарий КАК Комментарий,
	|	ВЫБОР
	|		КОГДА ПроизводственныйКалендарь.ВидДня = ЗНАЧЕНИЕ(Перечисление.ВидыДнейПроизводственногоКалендаря.Выходной)
	|			ТОГДА ИСТИНА
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ КАК Выходной
	|ИЗ
	|	ТаблицаДат КАК ТаблицаДат
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ПроизводственныйКалендарь КАК ПроизводственныйКалендарь
	|		ПО ТаблицаДат.ДатаОтсутствия = ПроизводственныйКалендарь.ДатаКалендаря";
	
	Запрос.УстановитьПараметр("ТаблицаДат", ТаблицаДат);
	Запрос.УстановитьПараметр("ДатаНачала", Параметры.НачалоПериода);
	Запрос.УстановитьПараметр("ДатаОкончания", Параметры.КонецПериода);

	
	РезультатЗапроса = Запрос.Выполнить();
	
	Если НЕ РезультатЗапроса.Пустой() Тогда
		Выборка = РезультатЗапроса.Выбрать();
		Пока Выборка.Следующий() Цикл
			ЗаполнитьЗначенияСвойств(ИнтервалыОтсутствия.Добавить(), Выборка);
		КонецЦикла;			
	КонецЕсли;	
		
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКоманд

&НаКлиенте
Процедура ПрименитьИзменения(Команда)
	
	ВыделенныеДаты = Новый Массив;
	
	Для Каждого Строка Из ЭтотОбъект.ИнтервалыОтсутствия Цикл
		Если Строка.Пометка = Ложь Тогда
			Продолжить;
		КонецЕсли;
		
		ВыделенныеДаты.Добавить(Строка.ДатаОтсутствия);		
	КонецЦикла;	
	
	ПараметрыЗакрытия = Новый Структура;
	ПараметрыЗакрытия.Вставить("Сотрудник", Сотрудник);
	ПараметрыЗакрытия.Вставить("ДатыОтсутствия", ВыделенныеДаты);	
	ПараметрыЗакрытия.Вставить("ДобавлениеИнтервала", Ложь);	
	
	Закрыть(ПараметрыЗакрытия);
	
КонецПроцедуры

&НаКлиенте
Процедура УстановитьПометки(Команда)
	
	ИзменитьПометки(Истина);
	
КонецПроцедуры

&НаКлиенте
Процедура СброситьПометки(Команда)
	
	ИзменитьПометки(Ложь);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура ИзменитьПометки(Пометка)
	
	Для Каждого Элемент Из ЭтотОбъект.ИнтервалыОтсутствия Цикл
		Элемент.Пометка = Пометка;
	КонецЦикла;	
	
КонецПроцедуры

#КонецОбласти
