///////////////////////////////////////////////////////////////////////////////////////////////////////
// Copyright (c) 2025, ООО ДНС Технологии
// SDMS (Software Development Management System) — это корпоративная система учета разработки и управления проектами 
// Все права защищены. Эта программа и сопроводительные материалы предоставляются 
// в соответствии с условиями лицензии General Public License (GNU GPL v3)
// Текст лицензии доступен по ссылке:
// https://www.gnu.org/licenses/gpl-3.0.html
///////////////////////////////////////////////////////////////////////////////////////////////////////

#Если Сервер ИЛИ ТолстыйКлиентОбычноеПриложение ИЛИ ВнешнееСоединение Тогда
	
#Область ПрограммныйИнтерфейс

// Добавляет обработчики обновления
//
// Параметры:
//  Обработчики	 - ТаблицаЗначений	 - см. ПриложениеВызовСервера.ПолучитьОбработчикиДанных
//
Процедура ДобавлениеОбработчиковОбновления(Обработчики) Экспорт     
	
	Обработчик = Обработчики.Добавить();
	Обработчик.Версия = "*";
	Обработчик.Процедура = "Справочники.СерьезностьОшибок.ПроверитьПредопределенныеЭлементы";
	
КонецПроцедуры

// Возвращает исходное описание Серьезности ошибок
// 
// Возвращаемое значение:
// Таблица значений с колонками Ссылка, ИмяКартинки, Порядок, Наименование
//
Функция СтандартныеОписанияСерьезностиОшибок()
	
	МетаданныеСправочника = Метаданные.Справочники.СерьезностьОшибок;
	
	ТаблицаСерьезностиОшибок = Новый ТаблицаЗначений;
	ТаблицаСерьезностиОшибок.Колонки.Добавить("Ссылка", Новый ОписаниеТипов("СправочникСсылка.СерьезностьОшибок"));
	ТаблицаСерьезностиОшибок.Колонки.Добавить("ИмяКартинки", МетаданныеСправочника.Реквизиты.ИмяКартинки.Тип);
	ТаблицаСерьезностиОшибок.Колонки.Добавить("Порядок", МетаданныеСправочника.Реквизиты.Порядок.Тип);
	ТаблицаСерьезностиОшибок.Колонки.Добавить("Наименование", ОбщегоНазначенияКлиентСервер.ОписаниеТипаСтрока(0));
	
	НоваяСтрока = ТаблицаСерьезностиОшибок.Добавить();
	НоваяСтрока.Ссылка = Справочники.СерьезностьОшибок.Блокирующая;
	НоваяСтрока.ИмяКартинки = "БлокирующаяСерьезностьОшибки";
	НоваяСтрока.Порядок = 0;
	НоваяСтрока.Наименование = "Блокирующая";
	
	НоваяСтрока = ТаблицаСерьезностиОшибок.Добавить();
	НоваяСтрока.Ссылка = Справочники.СерьезностьОшибок.Критическая;
	НоваяСтрока.ИмяКартинки = "КритическаяСерьезностьОшибки";
	НоваяСтрока.Порядок = 1;
	НоваяСтрока.Наименование = "Критическая";
	
	НоваяСтрока = ТаблицаСерьезностиОшибок.Добавить();
	НоваяСтрока.Ссылка = Справочники.СерьезностьОшибок.Средняя;
	НоваяСтрока.ИмяКартинки = "СредняяСерьезностьОшибки";
	НоваяСтрока.Порядок = 2;
	НоваяСтрока.Наименование = "Средняя";
	
	НоваяСтрока = ТаблицаСерьезностиОшибок.Добавить();
	НоваяСтрока.Ссылка = Справочники.СерьезностьОшибок.Низкая;
	НоваяСтрока.ИмяКартинки = "НизкаяСерьезностьОшибки";
	НоваяСтрока.Порядок = 3;
	НоваяСтрока.Наименование = "Низкая";
	
	Возврат ТаблицаСерьезностиОшибок;
	
КонецФункции

// Сбрасывает значение реквизита объекта СерьезностьОшибок в значение по умолчанию
//
// Параметры:
//  СерьезностьОшибки  - СправочникСсылка.СерьезностьОшибок - реквизит копируемого объекта
//  Назначение  - СправочникСсылка.НазначенияЗадач - значение реквизита копируемого объекта
//
Процедура ПриКопированииОбъекта(СерьезностьОшибки, Знач Назначение) Экспорт
	
	Если Назначение = Справочники.НазначенияЗадач.Ошибка Тогда
		СерьезностьОшибки = Справочники.СерьезностьОшибок.Средняя;
	КонецЕсли;
	
КонецПроцедуры

// Проверяет предопределенные настройки
//
Процедура ПроверитьПредопределенныеЭлементы() Экспорт     
	
	ЗаполнитьПредопределенныйЭлемент("Блокирующая", "БлокирующаяСерьезностьОшибки", 0);
	ЗаполнитьПредопределенныйЭлемент("Критическая", "КритическаяСерьезностьОшибки", 1);
	ЗаполнитьПредопределенныйЭлемент("Средняя", "СредняяСерьезностьОшибки", 2);	
	ЗаполнитьПредопределенныйЭлемент("Низкая", "НизкаяСерьезностьОшибки", 3);
	
КонецПроцедуры     

// Выполняет заполнение предопределенных элементов справочника СерьезностьОшибок стандартными настройками
//
Процедура УстановитьСтандартныеНастройки() Экспорт
	
	ТаблицаСерьезностиОшибок = СтандартныеОписанияСерьезностиОшибок();
	
	НачатьТранзакцию();
	
	БлокировкаДанных = Новый БлокировкаДанных;
	ЭлементБлокировки = БлокировкаДанных.Добавить("Справочник.СерьезностьОшибок");
	ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
	БлокировкаДанных.Заблокировать();
	
	Попытка
		Для Каждого Строка Из ТаблицаСерьезностиОшибок Цикл
			СправочникОбъект = Строка.Ссылка.ПолучитьОбъект();
			ЗаполнитьЗначенияСвойств(СправочникОбъект, Строка);
			СправочникОбъект.Записать();
		КонецЦикла;
		
		ЗафиксироватьТранзакцию();
	Исключение
		ОтменитьТранзакцию();
		ЗаписьЖурналаРегистрации("Заполнение справочника Серьезность ошибок", УровеньЖурналаРегистрации.Ошибка, , , "При заполнении справочника произошла ошибка: " + ОписаниеОшибки());
	КонецПопытки;
	
КонецПроцедуры

#КонецОбласти 

#Область СлужебныеПроцедурыИФункции

Процедура ЗаполнитьПредопределенныйЭлемент(ИмяЭлемента, ИмяКартинки, Порядок) 
	
	Попытка
		Значение = ОбщегоНазначения.ПредопределенныйЭлемент("Справочник.СерьезностьОшибок." + ИмяЭлемента);
	Исключение
		Сообщение = НСтр("ru = 'Предопределенный элемент ""%Параметр%"" не найден.'");
		Сообщение = СтрЗаменить(Сообщение, "%Параметр%", "Справочник.СерьезностьОшибок." + ИмяЭлемента);
		ВызватьИсключение Сообщение;
	КонецПопытки;
	
	Если Значение <> Неопределено Тогда  
		Объект = Значение.ПолучитьОбъект();    
		
		Если Объект.ИмяКартинки = "" Тогда
			Объект.ИмяКартинки = ИмяКартинки;
			Объект.Порядок = Порядок;
			Объект.Записать();
		КонецЕсли;
	КонецЕсли;

КонецПроцедуры

#КонецОбласти

#КонецЕсли
