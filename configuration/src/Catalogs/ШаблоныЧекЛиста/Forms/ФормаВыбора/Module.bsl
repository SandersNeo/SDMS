///////////////////////////////////////////////////////////////////////////////////////////////////////
// Copyright (c) 2025, ООО ДНС Технологии
// SDMS (Software Development Management System) — это корпоративная система учета разработки и управления проектами 
// Все права защищены. Эта программа и сопроводительные материалы предоставляются 
// в соответствии с условиями лицензии General Public License (GNU GPL v3)
// Текст лицензии доступен по ссылке:
// https://www.gnu.org/licenses/gpl-3.0.html
///////////////////////////////////////////////////////////////////////////////////////////////////////

#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Параметры.Свойство("ЧекЛистЗаполнен", ЧекЛистЗаполнен);
	
	Список.Параметры.УстановитьЗначениеПараметра("ТекущийПользователь", ПараметрыСеанса.ТекущийПользователь);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовФормы

&НаКлиенте
Процедура СписокВыборЗначения(Элемент, Значение, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	Если ЧекЛистЗаполнен Тогда
		Ответы = Новый СписокЗначений;
		Ответы.Добавить(Истина, "Перезаполнить");
		Ответы.Добавить(Ложь, "Дополнить");
		Ответы.Добавить(Неопределено, "Отмена");
		
		ОписаниеОповещения = Новый ОписаниеОповещения("ОбработатьВыборВариантаИспользованияШаблона", ЭтотОбъект, Значение);
		ТекстВопроса = "Перезаполнить текущий список или дополнить данными из шаблона?";
		ПоказатьВопрос(ОписаниеОповещения, ТекстВопроса, Ответы, , , "Чек-лист заполнен");
	Иначе
		ОбработатьВыборВариантаИспользованияШаблона(Ложь, Значение)
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура СписокПриАктивизацииСтроки(Элемент)
	
	ТекущиеДанные = Элементы.Список.ТекущиеДанные;
	
	Если ТекущиеДанные <> Неопределено Тогда
		Комментарий = ТекущиеДанные.Комментарий;
	Иначе
		Комментарий = "";
	КонецЕсли; 
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура ОбработатьВыборВариантаИспользованияШаблона(Ответ, Шаблон) Экспорт
	
	Если Ответ <> Неопределено Тогда
		Результат = Новый Структура;
		Результат.Вставить("ПерезаполнитьСписок", Ответ);
		Результат.Вставить("ЧекЛист", ПолучитьШаблонЧекЛиста(Шаблон));
		
		ОповеститьОВыборе(Результат);
	КонецЕсли; 
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция ПолучитьШаблонЧекЛиста(Знач Шаблон)
	
	Результат = Новый Массив;
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ШаблоныЧекЛистаШаблон.Описание КАК Описание
	|ИЗ
	|	Справочник.ШаблоныЧекЛиста.Шаблон КАК ШаблоныЧекЛистаШаблон
	|ГДЕ
	|	ШаблоныЧекЛистаШаблон.Ссылка = &Ссылка
	|
	|УПОРЯДОЧИТЬ ПО
	|	ШаблоныЧекЛистаШаблон.НомерСтроки";
	
	Запрос.УстановитьПараметр("Ссылка", Шаблон);
	
	РезультатЗапроса = Запрос.Выполнить();
	Если НЕ РезультатЗапроса.Пустой() Тогда
		Выборка = РезультатЗапроса.Выбрать();
		Пока Выборка.Следующий() Цикл
			Результат.Добавить(Выборка.Описание);
		КонецЦикла;
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

#КонецОбласти
