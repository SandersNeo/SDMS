///////////////////////////////////////////////////////////////////////////////////////////////////////
// Copyright (c) 2025, ООО ДНС Технологии
// SDMS (Software Development Management System) — это корпоративная система учета разработки и управления проектами 
// Все права защищены. Эта программа и сопроводительные материалы предоставляются 
// в соответствии с условиями лицензии General Public License (GNU GPL v3)
// Текст лицензии доступен по ссылке:
// https://www.gnu.org/licenses/gpl-3.0.html
///////////////////////////////////////////////////////////////////////////////////////////////////////

#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОписаниеПеременных

// Хранит состояние табличной части до изменения
Перем РолиКонфигурацииДоЗаписи;

// Хранит состояние табличной части до изменения
Перем УчастникиДоЗаписи;

#КонецОбласти

#Область ОбработчикиСобытий

Процедура ПередЗаписью(Отказ)
	
	ЗаполнитьСостояниеДоЗаписи();
	СортироватьУчастников();
			
КонецПроцедуры

Процедура ПриЗаписи(Отказ)
	
	ОбновитьРолиУчастников();
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Процедура ЗаполнитьСостояниеДоЗаписи()

	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ГруппыДоступаРолиКонфигурации.Роль КАК Роль
	|ИЗ
	|	Справочник.ГруппыДоступа.РолиКонфигурации КАК ГруппыДоступаРолиКонфигурации
	|ГДЕ
	|	ГруппыДоступаРолиКонфигурации.Ссылка = &Ссылка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ГруппыДоступаУчастники.Элемент КАК Элемент
	|ИЗ
	|	Справочник.ГруппыДоступа.Участники КАК ГруппыДоступаУчастники
	|ГДЕ
	|	ГруппыДоступаУчастники.Ссылка = &Ссылка";
	
	Запрос.УстановитьПараметр("Ссылка", Ссылка);	
	Пакеты = Запрос.ВыполнитьПакет();
	
	РолиКонфигурацииДоЗаписи = Пакеты[0].Выгрузить();
	УчастникиДоЗаписи = Пакеты[1].Выгрузить();	

КонецПроцедуры

Процедура ОбновитьРолиУчастников()

	Запрос = Новый Запрос;
	Запрос.Текст = 
	#Область ТекстЗапроса
	"ВЫБРАТЬ
	|	РолиСтарые.Роль КАК Роль
	|ПОМЕСТИТЬ РолиСтарые
	|ИЗ
	|	&РолиКонфигурацииДоЗаписи КАК РолиСтарые
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	УчастникиСтарые.Элемент КАК Участник
	|ПОМЕСТИТЬ УчастникиСтарые
	|ИЗ
	|	&УчастникиДоЗаписи КАК УчастникиСтарые
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	РолиНовые.Роль КАК Роль
	|ПОМЕСТИТЬ РолиНовые
	|ИЗ
	|	&Роли КАК РолиНовые
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	УчастникиНовые.Элемент КАК Участник
	|ПОМЕСТИТЬ УчастникиНовые
	|ИЗ
	|	&Участники КАК УчастникиНовые
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВЫРАЗИТЬ(УчастникиСтарые.Участник КАК Справочник.Пользователи) КАК Участник
	|ПОМЕСТИТЬ ПользователиСтарые
	|ИЗ
	|	УчастникиСтарые КАК УчастникиСтарые
	|ГДЕ
	|	УчастникиСтарые.Участник ССЫЛКА Справочник.Пользователи
	|
	|ОБЪЕДИНИТЬ
	|
	|ВЫБРАТЬ
	|	Пользователи.Ссылка
	|ИЗ
	|	УчастникиСтарые КАК УчастникиСтарые
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.Пользователи КАК Пользователи
	|		ПО (Пользователи.Должность = (ВЫРАЗИТЬ(УчастникиСтарые.Участник КАК Справочник.Должности)))
	|ГДЕ
	|	УчастникиСтарые.Участник ССЫЛКА Справочник.Должности
	|
	|ОБЪЕДИНИТЬ
	|
	|ВЫБРАТЬ
	|	Пользователи.Ссылка
	|ИЗ
	|	УчастникиСтарые КАК УчастникиСтарые
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.Пользователи КАК Пользователи
	|		ПО (Пользователи.Филиал = (ВЫРАЗИТЬ(УчастникиСтарые.Участник КАК Справочник.Филиалы)))
	|ГДЕ
	|	УчастникиСтарые.Участник ССЫЛКА Справочник.Филиалы
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВЫРАЗИТЬ(УчастникиНовые.Участник КАК Справочник.Пользователи) КАК Участник
	|ПОМЕСТИТЬ ПользователиНовые
	|ИЗ
	|	УчастникиНовые КАК УчастникиНовые
	|ГДЕ
	|	УчастникиНовые.Участник ССЫЛКА Справочник.Пользователи
	|
	|ОБЪЕДИНИТЬ
	|
	|ВЫБРАТЬ
	|	Пользователи.Ссылка
	|ИЗ
	|	УчастникиНовые КАК УчастникиНовые
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.Пользователи КАК Пользователи
	|		ПО (Пользователи.Должность = (ВЫРАЗИТЬ(УчастникиНовые.Участник КАК Справочник.Должности)))
	|ГДЕ
	|	УчастникиНовые.Участник ССЫЛКА Справочник.Должности
	|
	|ОБЪЕДИНИТЬ
	|
	|ВЫБРАТЬ
	|	Пользователи.Ссылка
	|ИЗ
	|	УчастникиНовые КАК УчастникиНовые
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.Пользователи КАК Пользователи
	|		ПО (Пользователи.Филиал = (ВЫРАЗИТЬ(УчастникиНовые.Участник КАК Справочник.Филиалы)))
	|ГДЕ
	|	УчастникиНовые.Участник ССЫЛКА Справочник.Филиалы
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	РолиСтарые.Роль КАК Роль,
	|	ПользователиСтарые.Участник КАК Участник
	|ПОМЕСТИТЬ Старые
	|ИЗ
	|	РолиСтарые КАК РолиСтарые,
	|	ПользователиСтарые КАК ПользователиСтарые
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	РолиНовые.Роль КАК Роль,
	|	ПользователиНовые.Участник КАК Участник
	|ПОМЕСТИТЬ Новые
	|ИЗ
	|	РолиНовые КАК РолиНовые,
	|	ПользователиНовые КАК ПользователиНовые
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Старые.Роль КАК Роль,
	|	Старые.Участник КАК Участник,
	|	ИСТИНА КАК Удален
	|ПОМЕСТИТЬ ДобавленныеУдаленные
	|ИЗ
	|	Старые КАК Старые
	|		ЛЕВОЕ СОЕДИНЕНИЕ Новые КАК Новые
	|		ПО Старые.Роль = Новые.Роль
	|			И Старые.Участник = Новые.Участник
	|ГДЕ
	|	Новые.Роль ЕСТЬ NULL
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	Новые.Роль,
	|	Новые.Участник,
	|	ЛОЖЬ
	|ИЗ
	|	Новые КАК Новые
	|		ЛЕВОЕ СОЕДИНЕНИЕ Старые КАК Старые
	|		ПО (Старые.Роль = Новые.Роль)
	|			И (Старые.Участник = Новые.Участник)
	|ГДЕ
	|	Старые.Роль ЕСТЬ NULL
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ГруппыДоступаРолиКонфигурации.Ссылка КАК Ссылка,
	|	ГруппыДоступаРолиКонфигурации.Роль КАК Роль,
	|	ГруппыДоступаУчастники.Элемент КАК Участник
	|ПОМЕСТИТЬ РолиУчастниковОстальныхГрупп
	|ИЗ
	|	Справочник.ГруппыДоступа.РолиКонфигурации КАК ГруппыДоступаРолиКонфигурации
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.ГруппыДоступа.Участники КАК ГруппыДоступаУчастники
	|		ПО ГруппыДоступаРолиКонфигурации.Ссылка = ГруппыДоступаУчастники.Ссылка
	|ГДЕ
	|	ГруппыДоступаРолиКонфигурации.Ссылка <> &ТекущаяГруппа
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	РолиУчастниковОстальныхГрупп.Роль КАК Роль,
	|	ВЫРАЗИТЬ(РолиУчастниковОстальныхГрупп.Участник КАК Справочник.Пользователи) КАК Участник
	|ПОМЕСТИТЬ РолиПользователейОстальныхГрупп
	|ИЗ
	|	РолиУчастниковОстальныхГрупп КАК РолиУчастниковОстальныхГрупп
	|ГДЕ
	|	РолиУчастниковОстальныхГрупп.Участник ССЫЛКА Справочник.Пользователи
	|
	|ОБЪЕДИНИТЬ
	|
	|ВЫБРАТЬ
	|	РолиУчастниковОстальныхГрупп.Роль,
	|	Пользователи.Ссылка
	|ИЗ
	|	РолиУчастниковОстальныхГрупп КАК РолиУчастниковОстальныхГрупп
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.Пользователи КАК Пользователи
	|		ПО (Пользователи.Должность = (ВЫРАЗИТЬ(РолиУчастниковОстальныхГрупп.Участник КАК Справочник.Должности)))
	|ГДЕ
	|	РолиУчастниковОстальныхГрупп.Участник ССЫЛКА Справочник.Должности
	|
	|ОБЪЕДИНИТЬ
	|
	|ВЫБРАТЬ
	|	РолиУчастниковОстальныхГрупп.Роль,
	|	Пользователи.Ссылка
	|ИЗ
	|	РолиУчастниковОстальныхГрупп КАК РолиУчастниковОстальныхГрупп
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.Пользователи КАК Пользователи
	|		ПО (Пользователи.Филиал = (ВЫРАЗИТЬ(РолиУчастниковОстальныхГрупп.Участник КАК Справочник.Филиалы)))
	|ГДЕ
	|	РолиУчастниковОстальныхГрупп.Участник ССЫЛКА Справочник.Филиалы
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ДобавленныеУдаленные.Участник КАК Участник,
	|	ДобавленныеУдаленные.Роль КАК Роль,
	|	ДобавленныеУдаленные.Удален КАК Удален,
	|	Пользователи.ИдентификаторПользователяИБ КАК ИдентификаторПользователяИБ
	|ИЗ
	|	ДобавленныеУдаленные КАК ДобавленныеУдаленные
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.Пользователи КАК Пользователи
	|		ПО (Пользователи.Ссылка = ДобавленныеУдаленные.Участник)
	|		ЛЕВОЕ СОЕДИНЕНИЕ РолиПользователейОстальныхГрупп КАК РолиПользователейОстальныхГрупп
	|		ПО (РолиПользователейОстальныхГрупп.Роль = ДобавленныеУдаленные.Роль)
	|			И (РолиПользователейОстальныхГрупп.Участник = ДобавленныеУдаленные.Участник)
	|ГДЕ
	|	РолиПользователейОстальныхГрупп.Участник ЕСТЬ NULL
	|ИТОГИ
	|	МАКСИМУМ(ИдентификаторПользователяИБ)
	|ПО
	|	Участник";
	#КонецОбласти
		
	Запрос.УстановитьПараметр("РолиКонфигурацииДоЗаписи", РолиКонфигурацииДоЗаписи);
	Запрос.УстановитьПараметр("УчастникиДоЗаписи", УчастникиДоЗаписи);
	Запрос.УстановитьПараметр("Роли", РолиКонфигурации.Выгрузить());
	Запрос.УстановитьПараметр("Участники", Участники.Выгрузить());
	Запрос.УстановитьПараметр("ТекущаяГруппа", Ссылка);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Если РезультатЗапроса.Пустой() Тогда
		Возврат;
	КонецЕсли;
	
	ВыборкаИдентификатор = РезультатЗапроса.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	
	Пока ВыборкаИдентификатор.Следующий() Цикл
		ПользовательИБ = ПользователиИнформационнойБазы.НайтиПоУникальномуИдентификатору(ВыборкаИдентификатор.ИдентификаторПользователяИБ);
		
		Если ПользовательИБ = Неопределено Тогда
			Продолжить;
		КонецЕсли;
		
		ИзмененСоставРолей = Ложь;
		ВыборкаРоли = ВыборкаИдентификатор.Выбрать();
		
		Пока ВыборкаРоли.Следующий() Цикл
			Роль = Метаданные.Роли.Найти(ВыборкаРоли.Роль);
			
			Если Роль <> Неопределено Тогда
				Если ВыборкаРоли.Удален Тогда
					Если ПользовательИБ.Роли.Содержит(Роль)
						И Роль <> Метаданные.Роли.Пользователь Тогда
						
						ПользовательИБ.Роли.Удалить(Роль);
						ИзмененСоставРолей = Истина;
						
						ИнтеграцияДополнительныхПодсистем.НастроитьОповещениеОЗнаниях(Роль, ВыборкаИдентификатор.Участник, Истина);
					КонецЕсли;
				Иначе
					Если НЕ ПользовательИБ.Роли.Содержит(Роль) Тогда
						ПользовательИБ.Роли.Добавить(Роль);
						ИзмененСоставРолей = Истина;
						
						ИнтеграцияДополнительныхПодсистем.НастроитьОповещениеОЗнаниях(Роль, ВыборкаИдентификатор.Участник);
					КонецЕсли;
				КонецЕсли;
			КонецЕсли;
		КонецЦикла;
		
		Если ИзмененСоставРолей Тогда
			ПользовательИБ.Записать();
		КонецЕсли;
	КонецЦикла;	

КонецПроцедуры

Процедура СортироватьУчастников()
	
	Запрос = Новый Запрос();
	
	Запрос.Текст =
	"ВЫБРАТЬ
	|	Участники.Элемент КАК Элемент
	|ПОМЕСТИТЬ ТаблицаУчастники
	|ИЗ
	|	&Участники КАК Участники
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТаблицаУчастники.Элемент КАК Элемент
	|ИЗ
	|	ТаблицаУчастники КАК ТаблицаУчастники
	|
	|УПОРЯДОЧИТЬ ПО
	|	ТИПЗНАЧЕНИЯ(ТаблицаУчастники.Элемент) УБЫВ,
	|	ТаблицаУчастники.Элемент
	|АВТОУПОРЯДОЧИВАНИЕ";
	
	Запрос.УстановитьПараметр("Участники", Участники.Выгрузить());
	
	ТаблицаУчастников = Запрос.Выполнить().Выгрузить();
	
	Участники.Загрузить(ТаблицаУчастников);
	
КонецПроцедуры

#КонецОбласти

#КонецЕсли
