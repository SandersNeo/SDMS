///////////////////////////////////////////////////////////////////////////////////////////////////////
// Copyright (c) 2025, ООО ДНС Технологии
// SDMS (Software Development Management System) — это корпоративная система учета разработки и управления проектами 
// Все права защищены. Эта программа и сопроводительные материалы предоставляются 
// в соответствии с условиями лицензии General Public License (GNU GPL v3)
// Текст лицензии доступен по ссылке:
// https://www.gnu.org/licenses/gpl-3.0.html
///////////////////////////////////////////////////////////////////////////////////////////////////////

#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ПостроительЗапроса = Новый ПостроительЗапроса;
	ПостроительЗапроса.Текст =
	#Область ТекстЗапроса
	"ВЫБРАТЬ
	|	ГруппыЗаказчиковНаправлений.Ссылка КАК Ссылка,
	|	ГруппыЗаказчиковНаправлений.Код КАК Код,
	|	ГруппыЗаказчиковНаправлений.Наименование КАК Наименование,
	|	ЕСТЬNULL(ГруппыЗаказчиковНаправленийНаправления.Направление, ЗНАЧЕНИЕ(Справочник.НаправленияРазработки.ПустаяСсылка)) КАК Направление,
	|	ЕСТЬNULL(НаправленияРазработки.Наименование, ""Пустое направление"") КАК НаправлениеНаименование
	|ИЗ
	|	Справочник.ГруппыЗаказчиковНаправлений КАК ГруппыЗаказчиковНаправлений
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ГруппыЗаказчиковНаправлений.Направления КАК ГруппыЗаказчиковНаправленийНаправления
	|		ПО (ГруппыЗаказчиковНаправленийНаправления.Ссылка = ГруппыЗаказчиковНаправлений.Ссылка)
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.НаправленияРазработки КАК НаправленияРазработки
	|		ПО (НаправленияРазработки.Ссылка = ГруппыЗаказчиковНаправленийНаправления.Направление)
	|ГДЕ
	|	НЕ ГруппыЗаказчиковНаправлений.ПометкаУдаления
	|{ГДЕ
	|	ГруппыЗаказчиковНаправленийНаправления.Ссылка КАК ГруппыЗаказчиков}
	|
	|УПОРЯДОЧИТЬ ПО
	|	НаправлениеНаименование,
	|	Наименование
	|ИТОГИ
	|	МИНИМУМ(НаправлениеНаименование)
	|ПО
	|	Направление";
	#КонецОбласти
	
	Если Параметры.Свойство("ГруппыЗаказчиков") Тогда
		ГруппыЗаказчиков = Новый СписокЗначений;
		ГруппыЗаказчиков.ЗагрузитьЗначения(Параметры.ГруппыЗаказчиков);
		ОбщегоНазначения.ДобавитьЭлементОтбораПостроителяЗапросов(ПостроительЗапроса, 
			"ГруппыЗаказчиков", ВидСравнения.ВСписке, ГруппыЗаказчиков);
	КонецЕсли;
	
	ПостроительЗапроса.Выполнить();	
	ВыборкаНаправление = ПостроительЗапроса.Результат.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	
	Пока ВыборкаНаправление.Следующий() Цикл
		СтрокаНаправление = Список.ПолучитьЭлементы().Добавить();	
		СтрокаНаправление.Наименование = ВыборкаНаправление.НаправлениеНаименование;
		СтрокаНаправление.ЭтоГруппа = Истина;
		
		Выборка = ВыборкаНаправление.Выбрать();
		
		Пока Выборка.Следующий() Цикл
			СтрокаГруппаЗаказчиков = СтрокаНаправление.ПолучитьЭлементы().Добавить();
			ЗаполнитьЗначенияСвойств(СтрокаГруппаЗаказчиков, Выборка);
			СтрокаГруппаЗаказчиков.ИндексКартинки = 1;
		КонецЦикла;
	КонецЦикла;
		
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовФормы

&НаКлиенте
Процедура СписокВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	ТекущеДанные = Элемент.ТекущиеДанные;
	
	Если ТекущеДанные = Неопределено Тогда
		Возврат;
	КонецЕсли;
		
	Если ТекущеДанные.ЭтоГруппа Тогда
		Возврат;
	КонецЕсли;
	
	ОповеститьОВыборе(ТекущеДанные.Ссылка);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура Выбрать(Команда) 
	 	
	ТекущеДанные = Элементы.Список.ТекущиеДанные;
	
	Если ТекущеДанные = Неопределено Тогда
		Возврат;
	КонецЕсли;
		
	Если ТекущеДанные.ЭтоГруппа Тогда
		ТекстПредупреждения = 
		"Выберите элемент, а не группу!
		|Для раскрытия групп используйте ""Ctrl"" и стрелки вверх/вниз или
		|клавиши ""+"" и ""-"" на дополнительной клавиатуре.";
		
		ПоказатьПредупреждение(, ТекстПредупреждения);
		Возврат;
	КонецЕсли;
	
	ОповеститьОВыборе(ТекущеДанные.Ссылка);
	
КонецПроцедуры

#КонецОбласти
