///////////////////////////////////////////////////////////////////////////////////////////////////////
// Copyright (c) 2025, ООО ДНС Технологии
// SDMS (Software Development Management System) — это корпоративная система учета разработки и управления проектами 
// Все права защищены. Эта программа и сопроводительные материалы предоставляются 
// в соответствии с условиями лицензии General Public License (GNU GPL v3)
// Текст лицензии доступен по ссылке:
// https://www.gnu.org/licenses/gpl-3.0.html
///////////////////////////////////////////////////////////////////////////////////////////////////////

#Область ОбъявлениеПеременных

// Хранит состояние объекта до записи
Перем ДанныеДоЗаписи;

#КонецОбласти

#Область ОбработчикиСобытий

Процедура ОбработкаПроверкиЗаполнения(Отказ, ПроверяемыеРеквизиты)
	
	Если Страна = Справочники.Страны.Россия Тогда
		Если НЕ ИННКорректный(ИНН) Тогда
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю("Введён некорректный ИНН", , , "Объект.ИНН", Отказ);
		КонецЕсли;
	Иначе
		ИндексКПП = ПроверяемыеРеквизиты.Найти("КПП");
		
		Если ИндексКПП <> Неопределено Тогда
			ПроверяемыеРеквизиты.Удалить(ИндексКПП);
		КонецЕсли; 
	КонецЕсли;
	
КонецПроцедуры

Процедура ПередЗаписью(Отказ)
	
	ИНН = СокрЛП(ИНН);
	
	Если ЭтоНовый() Тогда
		Автор = ПараметрыСеанса.ТекущийПользователь;
		ДатаСоздания = ТекущаяДатаСеанса();
	КонецЕсли;
	
	ДанныеДоЗаписи = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(Ссылка, "ЯвляетсяИТОрганизацией");
	
КонецПроцедуры

Процедура ПриЗаписи(Отказ)
	
	Если ДанныеДоЗаписи.ЯвляетсяИТОрганизацией <> ЯвляетсяИТОрганизацией Тогда
		РегистрыСведений.ОчередьСобытийНаОбработку.ДобавитьЗаданиеПроверитьТипыФилиалов(Ссылка);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция ИННКорректный(Знач ИНН)
	
	ИНН = СокрЛП(ИНН);
	ДлинаИНН = СтрДлина(ИНН);
	ИННКорректный = Ложь;
	
	Если ДлинаИНН = 10 Тогда
		КонтрольнаяСумма = 0;
		
		Множитель10 = Новый Соответствие;
		Множитель10.Вставить(1, 2);
		Множитель10.Вставить(2, 4);
		Множитель10.Вставить(3, 10);
		Множитель10.Вставить(4, 3);
		Множитель10.Вставить(5, 5);
		Множитель10.Вставить(6, 9);
		Множитель10.Вставить(7, 4);
		Множитель10.Вставить(8, 6);
		Множитель10.Вставить(9, 8);
		
		Для НомерСимвола = 1 По 9 Цикл
			КонтрольнаяЦифра = Число(Сред(ИНН, НомерСимвола, 1));
			КонтрольнаяСумма = КонтрольнаяСумма + КонтрольнаяЦифра * Множитель10[НомерСимвола];
		КонецЦикла;
		
		КонтрольнаяЦифра = Число(Сред(ИНН, 10, 1));
		ИННКорректный = ((КонтрольнаяСумма % 11) % 10 = КонтрольнаяЦифра);
	ИначеЕсли ДлинаИНН = 12 Тогда
		КонтрольнаяСумма11 = 0;
		КонтрольнаяСумма12 = 0;
		
		Множитель11 = Новый Соответствие;
		Множитель11.Вставить(1, 7);
		Множитель11.Вставить(2, 2);
		Множитель11.Вставить(3, 4);
		Множитель11.Вставить(4, 10);
		Множитель11.Вставить(5, 3);
		Множитель11.Вставить(6, 5);
		Множитель11.Вставить(7, 9);
		Множитель11.Вставить(8, 4);
		Множитель11.Вставить(9, 6);
		Множитель11.Вставить(10, 8);
		Множитель11.Вставить(11, 0);
		
		Множитель12 = Новый Соответствие;
		Множитель12.Вставить(1, 3);
		Множитель12.Вставить(2, 7);
		Множитель12.Вставить(3, 2);
		Множитель12.Вставить(4, 4);
		Множитель12.Вставить(5, 10);
		Множитель12.Вставить(6, 3);
		Множитель12.Вставить(7, 5);
		Множитель12.Вставить(8, 9);
		Множитель12.Вставить(9, 4);
		Множитель12.Вставить(10, 6);
		Множитель12.Вставить(11, 8);
		
		Для НомерСимвола = 1 По 11 Цикл
			КонтрольнаяЦифра = Число(Сред(ИНН, НомерСимвола, 1));
			КонтрольнаяСумма11 = КонтрольнаяСумма11 + КонтрольнаяЦифра * Множитель11[НомерСимвола];
			КонтрольнаяСумма12 = КонтрольнаяСумма12 + КонтрольнаяЦифра * Множитель12[НомерСимвола];
		КонецЦикла;
		
		КонтрольнаяЦифра11 = Число(Сред(ИНН, 11, 1));
		КонтрольнаяЦифра12 = Число(Сред(ИНН, 12, 1));
		
		ИННКорректный11 = ((КонтрольнаяСумма11 % 11) % 10 = КонтрольнаяЦифра11);
		ИННКорректный12 = ((КонтрольнаяСумма12 % 11) % 10 = КонтрольнаяЦифра12);
		ИННКорректный = ИННКорректный11 И ИННКорректный12;
	КонецЕсли;
	
	Возврат ИННКорректный
	
КонецФункции

#КонецОбласти
