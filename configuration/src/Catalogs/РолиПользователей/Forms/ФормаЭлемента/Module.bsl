///////////////////////////////////////////////////////////////////////////////////////////////////////
// Copyright (c) 2025, ООО ДНС Технологии
// SDMS (Software Development Management System) — это корпоративная система учета разработки и управления проектами 
// Все права защищены. Эта программа и сопроводительные материалы предоставляются 
// в соответствии с условиями лицензии General Public License (GNU GPL v3)
// Текст лицензии доступен по ссылке:
// https://www.gnu.org/licenses/gpl-3.0.html
///////////////////////////////////////////////////////////////////////////////////////////////////////

#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ЗаполнитьИнструментыСистемы();
	
КонецПроцедуры

&НаКлиенте
Процедура ПередЗаписью(Отказ, ПараметрыЗаписи)
	
	ГруппыИнструментов = ИнструментыСистемы.ПолучитьЭлементы();
	Объект.ДоступныеИнструменты.Очистить();
	
	Для Каждого Группа Из ГруппыИнструментов Цикл
		ЭлементыГруппы = Группа.ПолучитьЭлементы();
		Для Каждого Элемент Из ЭлементыГруппы Цикл
			НоваяСтрока = Объект.ДоступныеИнструменты.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяСтрока, Элемент);
		КонецЦикла;
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовФормы

&НаКлиенте
Процедура ИнструментыСистемыПередНачаломДобавления(Элемент, Отказ, Копирование, Родитель, Группа, Параметр)
	
	Отказ = Истина;
	
	ТекущиеДанные = Элементы.ИнструментыСистемы.ТекущиеДанные;
	Если ТекущиеДанные <> Неопределено Тогда
		Если ТекущиеДанные.Представление <> "Отчеты" И ТекущиеДанные.Представление <> "Инструменты" Тогда
			Родитель = ТекущиеДанные.ПолучитьРодителя();
		Иначе 
			Родитель = ТекущиеДанные;
		КонецЕсли;
	КонецЕсли;
	
	Если Родитель = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ПараметрыОткрытия = Новый Структура;
	ПараметрыОткрытия.Вставить("ВыбратьОтчеты", Родитель.Представление = "Отчеты");
	
	ОписаниеОповещения = Новый ОписаниеОповещения("ПослеВыбораИнструмента", ЭтотОбъект, ПараметрыОткрытия);
	ОткрытьФорму("Справочник.ИнструментыСистемы.ФормаВыбора", ПараметрыОткрытия, , , , , 
		ОписаниеОповещения, РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура ЗаполнитьИнструментыСистемы()
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	РолиПользователейДоступныеИнструменты.Инструмент КАК Инструмент,
	|	РолиПользователейДоступныеИнструменты.Порядок КАК Порядок,
	|	РолиПользователейДоступныеИнструменты.ЭтоОтчет КАК ЭтоОтчет,
	|	ИнструментыСистемы.Наименование КАК Представление
	|ИЗ
	|	Справочник.РолиПользователей.ДоступныеИнструменты КАК РолиПользователейДоступныеИнструменты
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.ИнструментыСистемы КАК ИнструментыСистемы
	|		ПО РолиПользователейДоступныеИнструменты.Инструмент = ИнструментыСистемы.Ссылка
	|ГДЕ
	|	РолиПользователейДоступныеИнструменты.Ссылка = &Ссылка
	|
	|УПОРЯДОЧИТЬ ПО
	|	Порядок
	|ИТОГИ ПО
	|	ЭтоОтчет";
	
	Запрос.УстановитьПараметр("Ссылка", Объект.Ссылка);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	ЭлементыДерева = ИнструментыСистемы.ПолучитьЭлементы();
	
	СтрокаГруппа = ЭлементыДерева.Добавить();
	СтрокаГруппа.Представление = "Инструменты";

	СтрокаГруппа = ЭлементыДерева.Добавить();
	СтрокаГруппа.Представление = "Отчеты";
	
	ВыборкаГруппа = РезультатЗапроса.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	
	Пока ВыборкаГруппа.Следующий() Цикл	
		Если НЕ ВыборкаГруппа.ЭтоОтчет Тогда
			СтрокаГруппа = ЭлементыДерева[0];
		Иначе
			СтрокаГруппа = ЭлементыДерева[1];
		КонецЕсли;
		
		Выборка = ВыборкаГруппа.Выбрать();
		
		Пока Выборка.Следующий() Цикл
			СтрокаИнструмент = СтрокаГруппа.ПолучитьЭлементы().Добавить();
			ЗаполнитьЗначенияСвойств(СтрокаИнструмент, Выборка);			
		КонецЦикла;
	КонецЦикла;
	
КонецПроцедуры

&НаКлиенте
Процедура ПослеВыбораИнструмента(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ГруппыИнструментов = ИнструментыСистемы.ПолучитьЭлементы();
	
	Для Каждого Группа Из ГруппыИнструментов Цикл
		Если (ДополнительныеПараметры.ВыбратьОтчеты И Группа.Представление = "Отчеты") 
			ИЛИ (НЕ ДополнительныеПараметры.ВыбратьОтчеты И Группа.Представление = "Инструменты") Тогда
			
			ТекущаяГруппа = Группа.ПолучитьЭлементы();
			Прервать;
		КонецЕсли;
	КонецЦикла;
	
	Если ТекущаяГруппа = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	НоваяСтрока = ТекущаяГруппа.Добавить();
	НоваяСтрока.Представление = Строка(Результат);
	НоваяСтрока.Инструмент = Результат;
	НоваяСтрока.Порядок = ТекущаяГруппа.Количество();
	НоваяСтрока.ЭтоОтчет = ДополнительныеПараметры.ВыбратьОтчеты;
	
КонецПроцедуры

#КонецОбласти
