///////////////////////////////////////////////////////////////////////////////////////////////////////
// Copyright (c) 2025, ООО ДНС Технологии
// SDMS (Software Development Management System) — это корпоративная система учета разработки и управления проектами 
// Все права защищены. Эта программа и сопроводительные материалы предоставляются 
// в соответствии с условиями лицензии General Public License (GNU GPL v3)
// Текст лицензии доступен по ссылке:
// https://www.gnu.org/licenses/gpl-3.0.html
///////////////////////////////////////////////////////////////////////////////////////////////////////

#Если Сервер ИЛИ ВнешнееСоединение ИЛИ ТолстыйКлиентОбычноеПриложение Тогда
	
#Область ОписаниеПеременных

// Хранит описание изменений объекта
Перем ОписаниеИзменений;

#КонецОбласти

#Область ОбработчикиСобытий

Процедура ПередЗаписью(Отказ)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	ОписаниеИзменений = ОбщегоНазначения.ПолучитьСостояниеОбъектаДоЗаписи(Ссылка);
	ОбщегоНазначения.ОчисткаДублейТабличнойЧасти(Системы, "Система");
	
	Если НЕ ЗначениеЗаполнено(ВидОчереди) Тогда
		ВидОчереди = Перечисления.ВидыОчереди.Продукт;
	КонецЕсли;
	
КонецПроцедуры

Процедура ПриЗаписи(Отказ)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	ОтветственныйСтароеЗначение = ОписаниеИзменений.Значения.Реквизиты.Ответственный;
	
	// Проверка на изменение владельца продукта 
	// для актуализации информации в группе доступа Ответственные за изменение списка продуктов.
	Если Ответственный <> ОтветственныйСтароеЗначение Тогда
		ГруппаДоступаИзменениеПродукта = Справочники.ГруппыДоступа.ОтветственныеЗаИзменениеСпискаПродуктов;
		
		Если ЗначениеЗаполнено(ОтветственныйСтароеЗначение) И НЕ ВладелецДругихПродуктов(ОтветственныйСтароеЗначение) Тогда
			Справочники.ГруппыДоступа.Удалить(ГруппаДоступаИзменениеПродукта, ОтветственныйСтароеЗначение);
		КонецЕсли;
		
		Справочники.ГруппыДоступа.Добавить(ГруппаДоступаИзменениеПродукта, Ответственный);
	КонецЕсли;
	
	ОбщегоНазначения.ОбработатьИзменениеОбъекта(Ссылка, ОписаниеИзменений);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция ВладелецДругихПродуктов(Знач Ответственный)
	
	Запрос = Новый Запрос;
	
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	Продукты.Ссылка КАК Продукт
	|ИЗ
	|	Справочник.Продукты КАК Продукты
	|ГДЕ
	|	Продукты.Ответственный = &Ответственный";
	
	Запрос.УстановитьПараметр("Ответственный", Ответственный);
	
	Результат = Запрос.Выполнить();
	
	Возврат НЕ Результат.Пустой();
	
КонецФункции

#КонецОбласти

#КонецЕсли
