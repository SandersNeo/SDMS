///////////////////////////////////////////////////////////////////////////////////////////////////////
// Copyright (c) 2025, ООО ДНС Технологии
// SDMS (Software Development Management System) — это корпоративная система учета разработки и управления проектами 
// Все права защищены. Эта программа и сопроводительные материалы предоставляются 
// в соответствии с условиями лицензии General Public License (GNU GPL v3)
// Текст лицензии доступен по ссылке:
// https://www.gnu.org/licenses/gpl-3.0.html
///////////////////////////////////////////////////////////////////////////////////////////////////////

#Область ПрограммныйИнтерфейс

// Получает настройку безопасного хранилища для конкретного мессенджера
//
// Параметры:
//  Мессенджер	 - СправочникСсылка.Мессенджеры	 - Мессенджер
// 
// Возвращаемое значение:
//  Настройка - ПеречислениеСсылка.НазначенияДанныхБезопасногоХранилища
//
Функция ПолучитьНастройкуБезопасногоХранилища(Мессенджер) Экспорт
	
	Результат = Неопределено;
	
	Если Мессенджер = Справочники.Мессенджеры.RocketChat Тогда
		Результат = Перечисления.НазначенияДанныхБезопасногоХранилища.НастройкиПодключенияКRocketChat;
	ИначеЕсли Мессенджер = Справочники.Мессенджеры.Mattermost Тогда
		Результат = Перечисления.НазначенияДанныхБезопасногоХранилища.НастройкиПодключенияКMattermost;
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

// Получает объект обработки для конкретного мессенджера
//
// Параметры:
//  Мессенджер	 - СправочникСсылка.Мессенджеры	 - Мессенджер
// 
// Возвращаемое значение:
//  Обработка - ОбработкаОбъект
//
Функция ПолучитьОбработкуМессенджера(Мессенджер = Неопределено) Экспорт
	
	Результат = Неопределено; 
	
	Если Мессенджер = Неопределено Тогда
		Мессенджер = Константы.ОсновнойМессенджер.Получить();
	КонецЕсли;
	
	Если Мессенджер = Справочники.Мессенджеры.RocketChat Тогда 
		Результат = Обработки.RocketChatAPI.Создать();
	ИначеЕсли Мессенджер = Справочники.Мессенджеры.Mattermost Тогда 
		Результат = Обработки.MattermostAPI.Создать();
	КонецЕсли; 
	
	Возврат Результат;

КонецФункции

// Возвращает настройки для создания соединения с мессенджером.
//
// Параметры:
//  Мессенджер	 - СправочникСсылка.Мессенджеры, Неопределено	 - Мессенджер
//					Если не передан или Неопределено, то возвращает настройки основного мессенджера.
// 
// Возвращаемое значение:
//  Структура
//		* АдресСервера - Строка.   
//		* ЗащищенноеСоединение - Объект защищенного соединения OpenSSL.
//		* Порт - Число. Если порт не указан в настройках мессенджера, 
//						то возвращается 443 для защищенного соединения, иначе 80.  
//		* Таймаут - Число. Если не указан, возвращает 15 секунд.  
//
Функция НастройкиПодключения(Знач Мессенджер = Неопределено) Экспорт

	Если Мессенджер = Неопределено Тогда
		Мессенджер = Константы.ОсновнойМессенджер.Получить();
	КонецЕсли;
	
	НастройкиПодключения = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(Мессенджер, "АдресСервера, Порт, ЗащищенноеСоединение, Таймаут"); 
		
	Если НЕ ЗначениеЗаполнено(НастройкиПодключения.Порт) Тогда
		НастройкиПодключения.Порт = ?(НастройкиПодключения.ЗащищенноеСоединение, 443, 80);
	КонецЕсли;
	
	ЗащищенноеСоединение = Неопределено;
	Если НастройкиПодключения.ЗащищенноеСоединение Тогда
		ЗащищенноеСоединение = Новый ЗащищенноеСоединениеOpenSSL;
	КонецЕсли;
	
	НастройкиПодключения.ЗащищенноеСоединение = ЗащищенноеСоединение;  
	
	Если НастройкиПодключения.Таймаут = 0 Тогда
		НастройкиПодключения.Таймаут = 15;
	КонецЕсли;
	
	Возврат НастройкиПодключения;
	
КонецФункции

#КонецОбласти
