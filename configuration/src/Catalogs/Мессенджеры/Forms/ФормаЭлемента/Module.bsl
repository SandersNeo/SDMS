///////////////////////////////////////////////////////////////////////////////////////////////////////
// Copyright (c) 2025, ООО ДНС Технологии
// SDMS (Software Development Management System) — это корпоративная система учета разработки и управления проектами 
// Все права защищены. Эта программа и сопроводительные материалы предоставляются 
// в соответствии с условиями лицензии General Public License (GNU GPL v3)
// Текст лицензии доступен по ссылке:
// https://www.gnu.org/licenses/gpl-3.0.html
///////////////////////////////////////////////////////////////////////////////////////////////////////

#Область ОбработчикиСобытий

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ЗаполнитьМессенджерыНаФорме();	
	ЗаполнитьНастройкиПодключения();	
	
КонецПроцедуры      

&НаСервере
Процедура ПередЗаписьюНаСервере(Отказ, ТекущийОбъект, ПараметрыЗаписи)
	
	МассивМессенджеры = Новый Массив;   
	
	Для Каждого Мессенджер Из Мессенджеры Цикл                
		АдресФорма = ЭтотОбъект["Адрес" + Мессенджер.Ключ];
		ИДФорма = ЭтотОбъект["Идентификатор" + Мессенджер.Ключ];
		РазличаетсяАдрес = АдресФорма <> Мессенджер.Значение.Адрес;
		РазличаетсяИД = ИДФорма <>  Мессенджер.Значение.Идентификатор;
		Если РазличаетсяАдрес ИЛИ РазличаетсяИД Тогда
			МассивМессенджеры.Добавить(Новый Структура("Ссылка, Адрес, Идентификатор", 
				Мессенджер.Значение.Ссылка, АдресФорма, ИДФорма)); 
		КонецЕсли;
	КонецЦикла;    
	
	Если МассивМессенджеры.Количество() > 0 Тогда 
		ТекущийОбъект.ДополнительныеСвойства.Вставить("Мессенджеры", МассивМессенджеры);
	КонецЕсли;  
	
	Если ЭтотОбъект.Пользователь <> ЭтотОбъект.ПользовательИсходный ИЛИ ЭтотОбъект.Пароль <> ЭтотОбъект.ПарольИсходный Тогда  
		Данные = Новый Структура("Пользователь, Пароль", ЭтотОбъект.Пользователь, ЭтотОбъект.Пароль);
		ТекущийОбъект.ДополнительныеСвойства.Вставить("НастройкиПодключения", Данные);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовФормы

&НаКлиенте
Процедура ДанныеПриИзменении(Элемент)
	
	Модифицированность = Истина;
	
КонецПроцедуры

#КонецОбласти   

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура ЗаполнитьМессенджерыНаФорме() 
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ПРЕДСТАВЛЕНИЕ(ТипыСлужебногоКанала.Ссылка) КАК Наименование,
	|	ЕСТЬNULL(АдресаПолучателей.Адрес, """") КАК Адрес,
	|	ЕСТЬNULL(АдресаПолучателей.Идентификатор, """") КАК Идентификатор,
	|	ТипыСлужебногоКанала.Порядок КАК Имя,
	|	ТипыСлужебногоКанала.Ссылка КАК Ссылка
	|ИЗ
	|	Перечисление.ТипыСлужебногоКанала КАК ТипыСлужебногоКанала
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.АдресаПолучателей КАК АдресаПолучателей
	|		ПО (АдресаПолучателей.Мессенджер = &Мессенджер)
	|			И (АдресаПолучателей.Получатель = ТипыСлужебногоКанала.Ссылка)";
	
	Запрос.УстановитьПараметр("Мессенджер", Объект.Ссылка);
	Выборка = Запрос.Выполнить().Выбрать();
	ДобавляемыеРеквизиты = Новый Массив;
	ДанныеМессенджеры = Новый Соответствие;
	
	Пока Выборка.Следующий() Цикл
		Идентификатор = ОбщегоНазначенияКлиентСервер.СтрУникальныйИдентификатор(УникальныйИдентификатор);
		ИмяРеквизита = СтрШаблон("_%1_%2", Выборка.Имя, Идентификатор);
		ИмяРеквизитаАдрес = СтрШаблон("Адрес%1", ИмяРеквизита);		  
		НовыйРеквизит = Новый РеквизитФормы(ИмяРеквизитаАдрес, 
			ОбщегоНазначенияКлиентСервер.ОписаниеТипаСтрока(100),, СтрШаблон("Адрес канала ""%1""", Выборка.Наименование)); 			
		ДобавляемыеРеквизиты.Добавить(НовыйРеквизит);	                                                                    
			
		ИмяРеквизитаИД = СтрШаблон("Идентификатор%1", ИмяРеквизита);		  
		НовыйРеквизит = Новый РеквизитФормы(ИмяРеквизитаИД, 
		ОбщегоНазначенияКлиентСервер.ОписаниеТипаСтрока(100),, СтрШаблон("Идентификатор канала ""%1""", Выборка.Наименование)); 			
			ДобавляемыеРеквизиты.Добавить(НовыйРеквизит); 
		Данные = Новый Структура("Ссылка, Адрес, Идентификатор", Выборка.Ссылка, Выборка.Адрес, Выборка.Идентификатор);
			
		ДанныеМессенджеры.Вставить(ИмяРеквизита, Данные);		
	КонецЦикла;
	
	ИзменитьРеквизиты(ДобавляемыеРеквизиты); 
	Мессенджеры = Новый ФиксированноеСоответствие(ДанныеМессенджеры);
		
	Для Каждого Мессенджер Из Мессенджеры Цикл 
		ПутьКДанным = СтрШаблон("Адрес%1", Мессенджер.Ключ);
		НовыйЭлемент = Элементы.Добавить(ПутьКДанным, Тип("ПолеФормы"), Элементы.СлужебныеКаналы);
		НовыйЭлемент.ПутьКДанным = ПутьКДанным;
		НовыйЭлемент.Вид = ВидПоляФормы.ПолеВвода;
		ЭтотОбъект[ПутьКДанным] = Мессенджер.Значение.Адрес;
		НовыйЭлемент.УстановитьДействие("ПриИзменении", "ДанныеПриИзменении");
		
		Если Объект.ИспользуетсяИдентификатор Тогда 
			ПутьКДанным = СтрШаблон("Идентификатор%1", Мессенджер.Ключ);
			НовыйЭлемент = Элементы.Добавить(ПутьКДанным, Тип("ПолеФормы"), Элементы.СлужебныеКаналы);
			НовыйЭлемент.ПутьКДанным = ПутьКДанным;
			НовыйЭлемент.Вид = ВидПоляФормы.ПолеВвода;
			ЭтотОбъект[ПутьКДанным] = Мессенджер.Значение.Идентификатор;
			НовыйЭлемент.УстановитьДействие("ПриИзменении", "ДанныеПриИзменении"); 		
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьНастройкиПодключения()

	УстановитьПривилегированныйРежим(Истина);
	Настройка = Справочники.Мессенджеры.ПолучитьНастройкуБезопасногоХранилища(Объект.Ссылка); 
	Данные = РегистрыСведений.БезопасноеХранилищеДанных.ПолучитьДанные(Настройка); 
	
	Если Данные <> Неопределено Тогда
		ЗаполнитьЗначенияСвойств(ЭтотОбъект, Данные);
		ЭтотОбъект.ПользовательИсходный = ЭтотОбъект.Пользователь;
		ЭтотОбъект.ПарольИсходный = ЭтотОбъект.Пароль;
	КонецЕсли;

КонецПроцедуры

#КонецОбласти
