///////////////////////////////////////////////////////////////////////////////////////////////////////
// Copyright (c) 2025, ООО ДНС Технологии
// SDMS (Software Development Management System) — это корпоративная система учета разработки и управления проектами 
// Все права защищены. Эта программа и сопроводительные материалы предоставляются 
// в соответствии с условиями лицензии General Public License (GNU GPL v3)
// Текст лицензии доступен по ссылке:
// https://www.gnu.org/licenses/gpl-3.0.html
///////////////////////////////////////////////////////////////////////////////////////////////////////

#Если Сервер ИЛИ ВнешнееСоединение ИЛИ ТолстыйКлиентОбычноеПриложение Тогда

#Область ОбработчикиСобытий

Процедура ОбработкаПроверкиЗаполнения(Отказ, ПроверяемыеРеквизиты)
	
	// Поле "Порядок" будет проверяться нестандартным способом. Поэтому удаляется из
	// массива проверяемых реквизитов.
	ОбщегоНазначенияКлиентСервер.УдалитьЗначениеИзМассива("Порядок", ПроверяемыеРеквизиты);
	
	// Для служебного статуса требуется проверить, не используется ли новый порядок статуса
	Если Предопределенный Тогда
		ПроверитьИспользованиеПорядка(Отказ);
	
	// Для пользовательского статуса требуется обязательное указание родительского статуса
	ИначеЕсли НЕ ЗначениеЗаполнено(Родитель) Тогда
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю("Требуется заполнение родительского статуса.",
			Ссылка, "Объект.Родитель", , Отказ);
	КонецЕсли;
	
КонецПроцедуры

Процедура ПриЗаписи(Отказ)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	РаботаСПроцессами.ОбновитьПорядкиПользовательскихСтатусов(Ссылка, Порядок);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Процедура ПроверитьИспользованиеПорядка(Отказ)
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ ПЕРВЫЕ 1
	|	СтатусыОбъектов.Ссылка КАК Ссылка
	|ИЗ
	|	Справочник.СтатусыОбъектов КАК СтатусыОбъектов
	|ГДЕ
	|	СтатусыОбъектов.Ссылка <> &Ссылка
	|	И СтатусыОбъектов.Порядок = &Порядок
	|	И СтатусыОбъектов.Предопределенный";
	
	Запрос.УстановитьПараметр("Ссылка", Ссылка);
	Запрос.УстановитьПараметр("Порядок", Порядок);
	
	Если НЕ Запрос.Выполнить().Пустой() Тогда
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю("Указанный порядковый номер уже используется.",
			Ссылка, "Объект.Порядок", , Отказ);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#КонецЕсли
