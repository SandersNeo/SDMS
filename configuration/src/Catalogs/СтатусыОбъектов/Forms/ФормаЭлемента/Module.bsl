///////////////////////////////////////////////////////////////////////////////////////////////////////
// Copyright (c) 2025, ООО ДНС Технологии
// SDMS (Software Development Management System) — это корпоративная система учета разработки и управления проектами 
// Все права защищены. Эта программа и сопроводительные материалы предоставляются 
// в соответствии с условиями лицензии General Public License (GNU GPL v3)
// Текст лицензии доступен по ссылке:
// https://www.gnu.org/licenses/gpl-3.0.html
///////////////////////////////////////////////////////////////////////////////////////////////////////

#Область ОбработчикиСобытий

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	  
	ОписанияСтатусов = Справочники.СтатусыОбъектов.ПолучитьСписокКартинокДляСтатусов();     
	
	Если Объект.Предопределенный Тогда
		Элементы.СтраницыРеквизитов.ТекущаяСтраница = Элементы.СтраницаСлужебныйСтатус;
	Иначе
		Элементы.СтраницыРеквизитов.ТекущаяСтраница = Элементы.СтраницаПользовательскийСтатус;
	КонецЕсли;
	
	СписокВыбора = Элементы.ИндексКартинки.СписокВыбора;
	СписокВыбораВторой = Элементы.ИндексКартинки1.СписокВыбора;
	Для Каждого ЭлементСписка Из ОписанияСтатусов Цикл
		СписокВыбора.Добавить(ЭлементСписка.Значение, ЭлементСписка.Представление, , ЭлементСписка.Картинка);
		СписокВыбораВторой.Добавить(ЭлементСписка.Значение, ЭлементСписка.Представление, , ЭлементСписка.Картинка);
	КонецЦикла;
	
	ЗаполнитьСписокПричинИзмененияСтатуса();
	
КонецПроцедуры

&НаСервере
Процедура ПередЗаписьюНаСервере(Отказ, ТекущийОбъект, ПараметрыЗаписи)
	
	ТекущийОбъект.ПричиныИзмененияСтатуса.Очистить();
	ИспользуемыеПричины = ПричиныИзменения.НайтиСтроки(Новый Структура("Использовать", Истина));
	
	Порядок = 0;
	Для Каждого Строка Из ИспользуемыеПричины Цикл
		НоваяСтрока = ТекущийОбъект.ПричиныИзмененияСтатуса.Добавить();
		НоваяСтрока.Причина = Строка.Причина;
		НоваяСтрока.Порядок = Порядок;
		
		Порядок = Порядок + 1;
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКоманд 

&НаКлиенте
Процедура ИндексКартинкиПриИзменении(Элемент)
	
	Объект.ИмяКартинкиВБиблиотеке = Элементы.ИндексКартинки.СписокВыбора.НайтиПоЗначению(Объект.ИндексКартинки).Представление;
	
КонецПроцедуры

&НаКлиенте
Процедура РодительПриИзменении(Элемент)
	
	ДанныеРодительскогоСтатуса = РодительПриИзмененииНаСервере(Объект.Родитель);
	ЗаполнитьЗначенияСвойств(Объект, ДанныеРодительскогоСтатуса);
	
КонецПроцедуры 

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура ЗаполнитьСписокПричинИзмененияСтатуса()
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ПричиныИзмененияСтатусов.Ссылка КАК Причина,
	|	ПРЕДСТАВЛЕНИЕ(ПричиныИзмененияСтатусов.Ссылка) КАК ПричинаПредставление,
	|	ВЫБОР
	|		КОГДА СтатусыОбъектовПричиныИзмененияСтатуса.Ссылка ЕСТЬ NULL
	|			ТОГДА 99
	|		ИНАЧЕ СтатусыОбъектовПричиныИзмененияСтатуса.Порядок
	|	КОНЕЦ КАК Порядок,
	|	ВЫБОР
	|		КОГДА СтатусыОбъектовПричиныИзмененияСтатуса.Ссылка ЕСТЬ NULL
	|			ТОГДА ЛОЖЬ
	|		ИНАЧЕ ИСТИНА
	|	КОНЕЦ КАК Использовать
	|ИЗ
	|	Справочник.ПричиныИзмененияСтатусов КАК ПричиныИзмененияСтатусов
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.СтатусыОбъектов.ПричиныИзмененияСтатуса КАК СтатусыОбъектовПричиныИзмененияСтатуса
	|		ПО ПричиныИзмененияСтатусов.Ссылка = СтатусыОбъектовПричиныИзмененияСтатуса.Причина
	|			И (СтатусыОбъектовПричиныИзмененияСтатуса.Ссылка = &Статус)
	|
	|УПОРЯДОЧИТЬ ПО
	|	Порядок,
	|	Причина";
	Запрос.УстановитьПараметр("Статус", Объект.Ссылка);
	РезультатЗапроса = Запрос.Выполнить();
	
	ПричиныИзменения.Загрузить(РезультатЗапроса.Выгрузить());
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция РодительПриИзмененииНаСервере(Знач Родитель)
	
	ИменаРеквизитов = "ИмяКартинкиВБиблиотеке, Порядок, ИндексКартинки";
	ЗначенияРеквизитов = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(Родитель, ИменаРеквизитов);
	
	Возврат ЗначенияРеквизитов;
	
КонецФункции

#КонецОбласти
