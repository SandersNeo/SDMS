///////////////////////////////////////////////////////////////////////////////////////////////////////
// Copyright (c) 2025, ООО ДНС Технологии
// SDMS (Software Development Management System) — это корпоративная система учета разработки и управления проектами 
// Все права защищены. Эта программа и сопроводительные материалы предоставляются 
// в соответствии с условиями лицензии General Public License (GNU GPL v3)
// Текст лицензии доступен по ссылке:
// https://www.gnu.org/licenses/gpl-3.0.html
///////////////////////////////////////////////////////////////////////////////////////////////////////

#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ОткрытаИзФормыПроцесса = Параметры.ОткрытаИзФормыПроцесса;
	
	Если ОткрытаИзФормыПроцесса Тогда
		
		ЭлементОформления = Список.УсловноеОформление.Элементы.Добавить();
		ЭлементОформления.Оформление.УстановитьЗначениеПараметра("Доступность", Ложь);
		ЭлементОформления.Оформление.УстановитьЗначениеПараметра("ЦветТекста", Новый Цвет(150, 150, 150));
		ЭлементОформления.Использование = Истина;
		
		ЭлементОтбора = ЭлементОформления.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
		ЭлементОтбора.ВидСравнения = ВидСравненияКомпоновкиДанных.ВСписке;
		ЭлементОтбора.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("Ссылка");
		ЭлементОтбора.ПравоеЗначение = Параметры.ИсключаемыеСтатусы;
		ЭлементОтбора.Использование = Истина;
		
		Элементы.ФормаВыбрать.Видимость = Ложь;
		Элементы.ФормаВыбратьСтатус.Видимость = Истина;
		Элементы.ФормаВыбратьСтатус.КнопкаПоУмолчанию = Истина;
		
	ИначеЕсли ТипЗнч(Параметры.ИсключаемыеСтатусы) = Тип("Массив")
		И Параметры.ИсключаемыеСтатусы.Количество() > 0 Тогда
		
		ЭлементОтбора = Список.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
		ЭлементОтбора.ВидСравнения = ВидСравненияКомпоновкиДанных.НеВСписке;
		ЭлементОтбора.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("Ссылка");
		ЭлементОтбора.ПравоеЗначение = Параметры.ИсключаемыеСтатусы;
		ЭлементОтбора.Использование = Истина;
		ЭлементОтбора.РежимОтображения = РежимОтображенияЭлементаНастройкиКомпоновкиДанных.Недоступный;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКоманд

&НаКлиенте
Процедура ВыбратьСтатус(Команда)
	
	ОбработатьВыборСтатуса();
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементов

&НаКлиенте
Процедура СписокВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	Если ОткрытаИзФормыПроцесса Тогда
		ОбработатьВыборСтатуса();
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура ОбработатьВыборСтатуса()
	
	ТекущиеДанные = Элементы.Список.ТекущиеДанные;
	Если ТекущиеДанные <> Неопределено Тогда
		Результат = Новый Структура;
		Результат.Вставить("Статус", ТекущиеДанные.Ссылка);
		Результат.Вставить("Порядок", ТекущиеДанные.Порядок);
		Результат.Вставить("Наименование", ТекущиеДанные.Наименование);
		Результат.Вставить("ИндексКартинки", ТекущиеДанные.ИндексКартинки);
	Иначе
		Результат = Неопределено;
	КонецЕсли;
	
	Закрыть(Результат);
	
КонецПроцедуры

#КонецОбласти
