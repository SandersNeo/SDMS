///////////////////////////////////////////////////////////////////////////////////////////////////////
// Copyright (c) 2025, ООО ДНС Технологии
// SDMS (Software Development Management System) — это корпоративная система учета разработки и управления проектами 
// Все права защищены. Эта программа и сопроводительные материалы предоставляются 
// в соответствии с условиями лицензии General Public License (GNU GPL v3)
// Текст лицензии доступен по ссылке:
// https://www.gnu.org/licenses/gpl-3.0.html
///////////////////////////////////////////////////////////////////////////////////////////////////////

#Если Сервер ИЛИ ТолстыйКлиент ИЛИ ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

Процедура ОбработатьИзменениеУслуги(Знач Услуга) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ВидыДеятельностиУслуг.Ссылка КАК Ссылка,
	|	ВидыДеятельностиУслуг.Услуга КАК Услуга,
	|	ВидыДеятельностиУслуг.ВидДеятельности КАК ВидДеятельности,
	|	ВидыДеятельностиУслуг.ПометкаУдаления КАК ПометкаУдаления
	|ПОМЕСТИТЬ СтарыеЗначения
	|ИЗ
	|	Справочник.ВидыДеятельностиУслуг КАК ВидыДеятельностиУслуг
	|ГДЕ
	|	ВидыДеятельностиУслуг.Услуга = &Услуга
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	УслугиВидыДеятельности.Ссылка КАК Услуга,
	|	УслугиВидыДеятельности.ВидДеятельности КАК ВидДеятельности,
	|	Услуги.ПометкаУдаления КАК ПометкаУдаления,
	|	Услуги.Используется КАК Используется
	|ПОМЕСТИТЬ НовыеЗначения
	|ИЗ
	|	Справочник.Услуги.ВидыДеятельности КАК УслугиВидыДеятельности
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.Услуги КАК Услуги
	|		ПО УслугиВидыДеятельности.Ссылка = Услуги.Ссылка
	|ГДЕ
	|	УслугиВидыДеятельности.Ссылка = &Услуга
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	СтарыеЗначения.Ссылка КАК Ссылка,
	|	ИСТИНА КАК ПометкаУдаления
	|ИЗ
	|	СтарыеЗначения КАК СтарыеЗначения
	|		ЛЕВОЕ СОЕДИНЕНИЕ НовыеЗначения КАК НовыеЗначения
	|		ПО СтарыеЗначения.Услуга = НовыеЗначения.Услуга
	|			И СтарыеЗначения.ВидДеятельности = НовыеЗначения.ВидДеятельности
	|ГДЕ
	|	НовыеЗначения.Услуга ЕСТЬ NULL
	|
	|ОБЪЕДИНИТЬ
	|
	|ВЫБРАТЬ
	|	СтарыеЗначения.Ссылка,
	|	ИСТИНА
	|ИЗ
	|	СтарыеЗначения КАК СтарыеЗначения
	|		ЛЕВОЕ СОЕДИНЕНИЕ НовыеЗначения КАК НовыеЗначения
	|		ПО СтарыеЗначения.Услуга = НовыеЗначения.Услуга
	|			И СтарыеЗначения.ВидДеятельности = НовыеЗначения.ВидДеятельности
	|ГДЕ
	|	ЕСТЬNULL(НовыеЗначения.ПометкаУдаления, ЛОЖЬ)
	|	И НЕ СтарыеЗначения.ПометкаУдаления
	|
	|ОБЪЕДИНИТЬ
	|
	|ВЫБРАТЬ
	|	СтарыеЗначения.Ссылка,
	|	ИСТИНА
	|ИЗ
	|	СтарыеЗначения КАК СтарыеЗначения
	|		ЛЕВОЕ СОЕДИНЕНИЕ НовыеЗначения КАК НовыеЗначения
	|		ПО СтарыеЗначения.Услуга = НовыеЗначения.Услуга
	|			И СтарыеЗначения.ВидДеятельности = НовыеЗначения.ВидДеятельности
	|ГДЕ
	|	НЕ ЕСТЬNULL(НовыеЗначения.Используется, ЛОЖЬ)
	|	И НЕ СтарыеЗначения.ПометкаУдаления
	|
	|ОБЪЕДИНИТЬ
	|
	|ВЫБРАТЬ
	|	СтарыеЗначения.Ссылка,
	|	ЛОЖЬ
	|ИЗ
	|	НовыеЗначения КАК НовыеЗначения
	|		ЛЕВОЕ СОЕДИНЕНИЕ СтарыеЗначения КАК СтарыеЗначения
	|		ПО НовыеЗначения.Услуга = СтарыеЗначения.Услуга
	|			И НовыеЗначения.ВидДеятельности = СтарыеЗначения.ВидДеятельности
	|ГДЕ
	|	НЕ НовыеЗначения.ПометкаУдаления
	|	И ЕСТЬNULL(СтарыеЗначения.ПометкаУдаления, ЛОЖЬ)
	|
	|ОБЪЕДИНИТЬ
	|
	|ВЫБРАТЬ
	|	СтарыеЗначения.Ссылка,
	|	ЛОЖЬ
	|ИЗ
	|	НовыеЗначения КАК НовыеЗначения
	|		ЛЕВОЕ СОЕДИНЕНИЕ СтарыеЗначения КАК СтарыеЗначения
	|		ПО НовыеЗначения.Услуга = СтарыеЗначения.Услуга
	|			И НовыеЗначения.ВидДеятельности = СтарыеЗначения.ВидДеятельности
	|ГДЕ
	|	НовыеЗначения.Используется
	|	И ЕСТЬNULL(СтарыеЗначения.ПометкаУдаления, ЛОЖЬ)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	НовыеЗначения.Услуга КАК Услуга,
	|	НовыеЗначения.ВидДеятельности КАК ВидДеятельности
	|ИЗ
	|	НовыеЗначения КАК НовыеЗначения
	|		ЛЕВОЕ СОЕДИНЕНИЕ СтарыеЗначения КАК СтарыеЗначения
	|		ПО НовыеЗначения.Услуга = СтарыеЗначения.Услуга
	|			И НовыеЗначения.ВидДеятельности = СтарыеЗначения.ВидДеятельности
	|ГДЕ
	|	СтарыеЗначения.Услуга ЕСТЬ NULL
	|	И НовыеЗначения.Используется
	|	И НЕ НовыеЗначения.ПометкаУдаления";
	
	Запрос.УстановитьПараметр("Услуга", Услуга);
	
	Пакеты = Запрос.ВыполнитьПакет();
	ИзменениеПометкиУдаления = Пакеты.Получить(2);
	СозданиеЭлементов = Пакеты.Получить(3);
	
	Если НЕ ИзменениеПометкиУдаления.Пустой() Тогда
		Выборка = ИзменениеПометкиУдаления.Выбрать();
		Пока Выборка.Следующий() Цикл
			ОбновитьЭлемент(Выборка.Ссылка, Выборка.ПометкаУдаления);
		КонецЦикла;
	КонецЕсли;
	
	Если НЕ СозданиеЭлементов.Пустой() Тогда
		Выборка = СозданиеЭлементов.Выбрать();
		Пока Выборка.Следующий() Цикл
			ДобавитьЭлемент(Выборка.Услуга, Выборка.ВидДеятельности);
		КонецЦикла;
	КонецЕсли;
	
КонецПроцедуры

Процедура ОбработатьИзменениеВидаДеятельности(Знач ВидДеятельности) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ВидыДеятельностиУслуг.Ссылка КАК Ссылка
	|ИЗ
	|	Справочник.ВидыДеятельностиУслуг КАК ВидыДеятельностиУслуг
	|ГДЕ
	|	ВидыДеятельностиУслуг.ВидДеятельности = &ВидДеятельности
	|	И НЕ ВидыДеятельностиУслуг.ПометкаУдаления";
	
	Запрос.УстановитьПараметр("ВидДеятельности", ВидДеятельности);
	РезультатЗапроса = Запрос.Выполнить();
	
	Если НЕ РезультатЗапроса.Пустой() Тогда
		Выборка = РезультатЗапроса.Выбрать();
		
		Пока Выборка.Следующий() Цикл
			ОбновитьЭлемент(Выборка.Ссылка);
		КонецЦикла;
	КонецЕсли;
	
КонецПроцедуры

#КонецОБласти

#Область СлужебныеПроцедурыИФункции

Процедура ДобавитьЭлемент(Знач Услуга, Знач ВидДеятельности)
	
	ЭлементСправочника = СоздатьЭлемент();
	ЭлементСправочника.Услуга = Услуга;
	ЭлементСправочника.ВидДеятельности = ВидДеятельности;
	ЭлементСправочника.Записать();
	
КонецПроцедуры

Процедура ОбновитьЭлемент(Знач Ссылка, Знач ПометкаУдаления = Неопределено)
	
	ЭлементСправочника = Ссылка.ПолучитьОбъект();
	
	Если ЭлементСправочника <> Неопределено Тогда
		Если ПометкаУдаления <> Неопределено Тогда
			ЭлементСправочника.ПометкаУдаления = ПометкаУдаления;
		КонецЕсли;
		
		ЭлементСправочника.Записать();
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#КонецЕсли
