///////////////////////////////////////////////////////////////////////////////////////////////////////
// Copyright (c) 2025, ООО ДНС Технологии
// SDMS (Software Development Management System) — это корпоративная система учета разработки и управления проектами 
// Все права защищены. Эта программа и сопроводительные материалы предоставляются 
// в соответствии с условиями лицензии General Public License (GNU GPL v3)
// Текст лицензии доступен по ссылке:
// https://www.gnu.org/licenses/gpl-3.0.html
///////////////////////////////////////////////////////////////////////////////////////////////////////

#Если Сервер ИЛИ ВнешнееСоединение ИЛИ ТолстыйКлиентОбычноеПриложение Тогда

#Область ОписаниеПеременных

// Хранит описание изменений объекта
Перем ОписаниеИзменений;

#КонецОбласти

#Область ОбработчикиСобытий

Процедура ОбработкаПроверкиЗаполнения(Отказ, ПроверяемыеРеквизиты)
	
	Данные = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(Филиал, "Руководитель, ЗаместительРуководителя");
	
	Если НЕ (Данные.Руководитель = ПараметрыСеанса.ТекущийПользователь
			ИЛИ Данные.ЗаместительРуководителя = ПараметрыСеанса.ТекущийПользователь
			ИЛИ УправлениеДоступомПовтИсп.ПроверитьДоступностьРоли("Администратор")) Тогда
		
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю("Нет прав на редактирование процесса.", , , , Отказ);
		Возврат;
	КонецЕсли;
	
	// С пустым филиалом должен быть только один процесс
	Если НЕ ЗначениеЗаполнено(Филиал) Тогда
		Запрос = Новый Запрос;
		Запрос.Текст = 
		"ВЫБРАТЬ
		|	Процессы.Ссылка КАК Ссылка
		|ИЗ
		|	Справочник.Процессы КАК Процессы
		|ГДЕ
		|	Процессы.Ссылка <> &Ссылка
		|	И Процессы.ТипОбъекта = &ТипОбъекта
		|	И Процессы.Филиал = Значение(Справочник.Филиалы.ПустаяСсылка)";
		Запрос.УстановитьПараметр("Ссылка", Ссылка);
		Запрос.УстановитьПараметр("ТипОбъекта", ТипОбъекта);
		Результат = Запрос.Выполнить();
		
		Если НЕ Результат.Пустой() Тогда
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю("Нельзя создать процесс с пустым филиалом", , , , Отказ);
			Возврат;
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

Процедура ПередЗаписью(Отказ)
	
	Если ОбменДанными.Загрузка = Истина Тогда
		Возврат;
	КонецЕсли;
	
	ОписаниеИзменений = ОбщегоНазначения.ПолучитьСостояниеОбъектаДоЗаписи(Ссылка);

	Если ЭтоНовый() Тогда
		Автор = ПараметрыСеанса.ТекущийПользователь;
		ДатаСоздания = ТекущаяДатаСеанса();
	Иначе
		ДатаИзменения = ТекущаяДатаСеанса();	
	КонецЕсли;
		
КонецПроцедуры

Процедура ПриЗаписи(Отказ)
	
	Если ОбменДанными.Загрузка = Истина Тогда
		Возврат;
	КонецЕсли;
	
	Если ПометкаУдаления Тогда
		Запрос = Новый Запрос;
		Запрос.Текст = 
		"ВЫБРАТЬ
		|	ЭтапыКанбана.Ссылка КАК Ссылка
		|ИЗ
		|	Справочник.ЭтапыКанбана КАК ЭтапыКанбана
		|ГДЕ
		|	ЭтапыКанбана.Владелец = &Ссылка";
		
		Запрос.УстановитьПараметр("Ссылка", Ссылка);
		Выборка = Запрос.Выполнить().Выбрать();
		
		Пока Выборка.Следующий() Цикл
			ЭтапОбъект = Выборка.Ссылка.ПолучитьОбъект();
			ЭтапОбъект.УстановитьПометкуУдаления(Истина);
		КонецЦикла;
		
	ИначеЕсли (ТипОбъекта = Справочники.ТипыОбъектовВладельцев.Документ_Задача 
		ИЛИ ТипОбъекта = Справочники.ТипыОбъектовВладельцев.Документ_ВнутреннееЗадание) 
		И НастройкиСменыСтатусов.Количество() > 0 Тогда
		
		ОбработатьИзменениеЭтаповКанбана(Ссылка, НастройкиСменыСтатусов);
	КонецЕсли;
	
	ОбщегоНазначения.ОбработатьИзменениеОбъекта(Ссылка, ОписаниеИзменений, , , Истина);
	
КонецПроцедуры

Процедура ПриКопировании(ОбъектКопирования)
	
	Автор = ПараметрыСеанса.ТекущийПользователь;
	ДатаСоздания = ТекущаяДатаСеанса();
	ДатаИзменения = Дата("00010101");
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Процедура ОбработатьИзменениеЭтаповКанбана(Ссылка, Знач НастройкиСменыСтатусов)
	
	Запрос = Новый Запрос;
	
	Запрос.Текст = 
	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ПроцессыНастройкиСменыСтатусов.ТекущийСтатус КАК ТекущийСтатус,
	|	ПроцессыНастройкиСменыСтатусов.СледующийСтатус КАК СледующийСтатус,
	|	ПроцессыНастройкиСменыСтатусов.ИдентификаторПерехода КАК ИдентификаторПерехода
	|ПОМЕСТИТЬ НовыеНастройки
	|ИЗ
	|	Справочник.Процессы КАК Процессы
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.Процессы.НастройкиСменыСтатусов КАК ПроцессыНастройкиСменыСтатусов
	|		ПО (ПроцессыНастройкиСменыСтатусов.Ссылка = Процессы.Ссылка)
	|ГДЕ
	|	Процессы.Ссылка = &Владелец
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ЭтапыКанбана.Ссылка КАК Ссылка,
	|	ЭтапыКанбанаЭлементыЭтапа.Элемент КАК Статус,
	|	ЭтапыКанбана.ПометкаУдаления КАК ПометкаУдаления
	|ПОМЕСТИТЬ СуществующиеЭтапы
	|ИЗ
	|	Справочник.ЭтапыКанбана КАК ЭтапыКанбана
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.ЭтапыКанбана.ЭлементыЭтапа КАК ЭтапыКанбанаЭлементыЭтапа
	|		ПО (ЭтапыКанбанаЭлементыЭтапа.Ссылка = ЭтапыКанбана.Ссылка)
	|ГДЕ
	|	ЭтапыКанбана.Владелец = &Владелец
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	НовыеНастройки.ТекущийСтатус КАК Статус,
	|	СтатусыОбъектов.Наименование КАК Наименование,
	|	СтатусыОбъектов.Порядок КАК ПорядокОтображения,
	|	ЕСТЬNULL(СуществующиеЭтапы.Ссылка, НЕОПРЕДЕЛЕНО) КАК Этап
	|ИЗ
	|	НовыеНастройки КАК НовыеНастройки
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.СтатусыОбъектов КАК СтатусыОбъектов
	|		ПО НовыеНастройки.ТекущийСтатус = СтатусыОбъектов.Ссылка
	|		ЛЕВОЕ СОЕДИНЕНИЕ СуществующиеЭтапы КАК СуществующиеЭтапы
	|		ПО НовыеНастройки.ТекущийСтатус = СуществующиеЭтапы.Статус
	|ГДЕ
	|	ЕСТЬNULL(СуществующиеЭтапы.ПометкаУдаления, ИСТИНА)
	|
	|ОБЪЕДИНИТЬ
	|
	|ВЫБРАТЬ
	|	НовыеНастройки.СледующийСтатус,
	|	СтатусыОбъектов.Наименование,
	|	СтатусыОбъектов.Порядок,
	|	ЕСТЬNULL(СуществующиеЭтапы.Ссылка, НЕОПРЕДЕЛЕНО)
	|ИЗ
	|	НовыеНастройки КАК НовыеНастройки
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.СтатусыОбъектов КАК СтатусыОбъектов
	|		ПО НовыеНастройки.СледующийСтатус = СтатусыОбъектов.Ссылка
	|		ЛЕВОЕ СОЕДИНЕНИЕ СуществующиеЭтапы КАК СуществующиеЭтапы
	|		ПО НовыеНастройки.СледующийСтатус = СуществующиеЭтапы.Статус
	|ГДЕ
	|	ЕСТЬNULL(СуществующиеЭтапы.ПометкаУдаления, ИСТИНА)
	|
	|УПОРЯДОЧИТЬ ПО
	|	ПорядокОтображения
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ЭтапыКанбана.Ссылка КАК Ссылка
	|ИЗ
	|	Справочник.ЭтапыКанбана КАК ЭтапыКанбана
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.ЭтапыКанбана.ЭлементыЭтапа КАК ЭтапыКанбанаЭлементыЭтапа
	|		ПО ЭтапыКанбана.Ссылка = ЭтапыКанбанаЭлементыЭтапа.Ссылка
	|		ЛЕВОЕ СОЕДИНЕНИЕ НовыеНастройки КАК ПереходИзСтатуса
	|		ПО (ЭтапыКанбанаЭлементыЭтапа.Элемент = ПереходИзСтатуса.ТекущийСтатус)
	|		ЛЕВОЕ СОЕДИНЕНИЕ НовыеНастройки КАК ПереходВСтатус
	|		ПО (ЭтапыКанбанаЭлементыЭтапа.Элемент = ПереходВСтатус.СледующийСтатус)
	|ГДЕ
	|	ЭтапыКанбана.Владелец = &Владелец
	|	И НЕ ЭтапыКанбана.ПометкаУдаления
	|	И ПереходИзСтатуса.ТекущийСтатус ЕСТЬ NULL
	|	И ПереходВСтатус.СледующийСтатус ЕСТЬ NULL";
	
	Запрос.УстановитьПараметр("Владелец", Ссылка);

	Если ТипОбъекта = Справочники.ТипыОбъектовВладельцев.Документ_Задача Тогда
		ТипКанбана = Перечисления.ТипыКанбана.ЗадачаНовый;
	ИначеЕсли ТипОбъекта = Справочники.ТипыОбъектовВладельцев.Документ_ВнутреннееЗадание Тогда
		ТипКанбана = Перечисления.ТипыКанбана.ВнутреннееЗаданиеНовый;
	Иначе
		Возврат;
	КонецЕсли;
	
	РезультатЗапроса = Запрос.ВыполнитьПакет();
	
	ДобавляемыеЭтапы = РезультатЗапроса[2].Выбрать();
	МассивДобавленных = Новый Массив;
	
	Пока ДобавляемыеЭтапы.Следующий() Цикл
		Если ДобавляемыеЭтапы.Этап = Неопределено Тогда
			НовыйЭтап = Справочники.ЭтапыКанбана.СоздатьЭлемент();

			ЗаполнитьЗначенияСвойств(НовыйЭтап, ДобавляемыеЭтапы);
			НовыйЭтап.ТипКанбана = ТипКанбана;
			НовыйЭтап.Владелец = Ссылка;
			Элемент = НовыйЭтап.ЭлементыЭтапа.Добавить();
			Элемент.Элемент = ДобавляемыеЭтапы.Статус;
		Иначе
			НовыйЭтап = ДобавляемыеЭтапы.Этап.ПолучитьОбъект();
			НовыйЭтап.ПометкаУдаления = Ложь;
		КонецЕсли;
		
		НовыйЭтап.Записать();
		МассивДобавленных.Добавить(НовыйЭтап.Ссылка);
	КонецЦикла;
	
	Если МассивДобавленных.Количество() > 0 Тогда
		РегистрыСведений.ПорядокЭтаповКанбана.ДобавитьЭтапы(МассивДобавленных, ТипКанбана, Ссылка);
	КонецЕсли;
	
	УдаляемыеЭтапы = РезультатЗапроса[3].Выбрать();
	МассивУдаленных = Новый Массив;
	
	Пока УдаляемыеЭтапы.Следующий() Цикл
		ЭтапОбъект = УдаляемыеЭтапы.Ссылка.ПолучитьОбъект();
		ЭтапОбъект.Переходы.Очистить();
		ЭтапОбъект.ПометкаУдаления = Истина;
		
		ЭтапОбъект.Записать();
		МассивУдаленных.Добавить(УдаляемыеЭтапы.Ссылка);
	КонецЦикла;	
	
	Если МассивУдаленных.Количество() > 0 Тогда
		РегистрыСведений.ПорядокЭтаповКанбана.УдалитьЭтапы(МассивУдаленных, ТипКанбана, Ссылка);
	КонецЕсли;
		
	Запрос.Текст = 
	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ПроцессыНастройкиСменыСтатусов.ТекущийСтатус КАК ТекущийСтатус,
	|	ПроцессыНастройкиСменыСтатусов.СледующийСтатус КАК СледующийСтатус,
	|	ПроцессыНастройкиСменыСтатусов.ИдентификаторПерехода КАК ИдентификаторПерехода
	|ПОМЕСТИТЬ НовыеНастройки
	|ИЗ
	|	Справочник.Процессы КАК Процессы
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.Процессы.НастройкиСменыСтатусов КАК ПроцессыНастройкиСменыСтатусов
	|		ПО (ПроцессыНастройкиСменыСтатусов.Ссылка = Процессы.Ссылка)
	|ГДЕ
	|	Процессы.Ссылка = &Владелец
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ	
	|	ЭтапыКанбана.Ссылка КАК Этап,
	|	ЭтапыКанбанаСтатус.Элемент КАК СтатусЭтапа,
	|	СправочникЭтапыКанбанаПереходы.ЭлементПерехода КАК ЭлементПерехода,
	|	ЭтапыКанбанаЭлементыЭтапа.Элемент КАК СтатусПерехода
	|ПОМЕСТИТЬ СуществующиеПереходы
	|ИЗ
	|	Справочник.ЭтапыКанбана КАК ЭтапыКанбана
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.ЭтапыКанбана.ЭлементыЭтапа КАК ЭтапыКанбанаСтатус
	|		ПО (ЭтапыКанбанаСтатус.Ссылка = ЭтапыКанбана.Ссылка)
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.ЭтапыКанбана.Переходы КАК СправочникЭтапыКанбанаПереходы
	|		ПО (СправочникЭтапыКанбанаПереходы.Ссылка = ЭтапыКанбана.Ссылка)
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.ЭтапыКанбана.ЭлементыЭтапа КАК ЭтапыКанбанаЭлементыЭтапа
	|		ПО (СправочникЭтапыКанбанаПереходы.ЭлементПерехода = ЭтапыКанбанаЭлементыЭтапа.Ссылка)
	|ГДЕ
	|	ЭтапыКанбана.Владелец = &Владелец
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ЭтапыКанбана.Ссылка КАК Ссылка,
	|	НовыеНастройки.СледующийСтатус КАК СледующийСтатус
	|ПОМЕСТИТЬ ЭтапСтатусПерехода
	|ИЗ
	|	Справочник.ЭтапыКанбана КАК ЭтапыКанбана
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.ЭтапыКанбана.ЭлементыЭтапа КАК ЭтапыКанбанаЭлементыЭтапа
	|		ПО ЭтапыКанбана.Ссылка = ЭтапыКанбанаЭлементыЭтапа.Ссылка
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ НовыеНастройки КАК НовыеНастройки
	|		ПО (ЭтапыКанбанаЭлементыЭтапа.Элемент = НовыеНастройки.ТекущийСтатус)
	|		ЛЕВОЕ СОЕДИНЕНИЕ СуществующиеПереходы КАК СуществующиеПереходы
	|		ПО (ЭтапыКанбанаЭлементыЭтапа.Ссылка = СуществующиеПереходы.Этап)
	|			И (СуществующиеПереходы.СтатусПерехода = НовыеНастройки.СледующийСтатус)
	|ГДЕ
	|	ЭтапыКанбана.Владелец = &Владелец
	|	И СуществующиеПереходы.СтатусПерехода ЕСТЬ NULL
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ЭтапСтатусПерехода.Ссылка КАК Этап,
	|	ЭтапыКанбанаЭлементыЭтапа.Ссылка КАК Переход
	|ИЗ
	|	ЭтапСтатусПерехода КАК ЭтапСтатусПерехода
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.ЭтапыКанбана.ЭлементыЭтапа КАК ЭтапыКанбанаЭлементыЭтапа
	|		ПО ЭтапСтатусПерехода.СледующийСтатус = ЭтапыКанбанаЭлементыЭтапа.Элемент
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.ЭтапыКанбана КАК ЭтапыКанбана
	|		ПО (ЭтапыКанбанаЭлементыЭтапа.Ссылка = ЭтапыКанбана.Ссылка)
	|			И (ЭтапыКанбана.Владелец = &Владелец)
	|ИТОГИ ПО
	|	Этап
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ЭтапыКанбана.Ссылка КАК Этап,
	|	СуществующиеПереходы.ЭлементПерехода КАК Переход
	|ИЗ
	|	Справочник.ЭтапыКанбана КАК ЭтапыКанбана
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.ЭтапыКанбана.ЭлементыЭтапа КАК ЭтапыКанбанаЭлементыЭтапа
	|		ПО ЭтапыКанбана.Ссылка = ЭтапыКанбанаЭлементыЭтапа.Ссылка
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ СуществующиеПереходы КАК СуществующиеПереходы
	|		ПО (ЭтапыКанбанаЭлементыЭтапа.Ссылка = СуществующиеПереходы.Этап)
	|		ЛЕВОЕ СОЕДИНЕНИЕ НовыеНастройки КАК НовыеНастройки
	|		ПО (СуществующиеПереходы.СтатусЭтапа = НовыеНастройки.ТекущийСтатус)
	|			И (СуществующиеПереходы.СтатусПерехода = НовыеНастройки.СледующийСтатус)
	|ГДЕ
	|	ЭтапыКанбана.Владелец = &Владелец
	|	И НовыеНастройки.ИдентификаторПерехода ЕСТЬ NULL
	|ИТОГИ ПО
	|	Этап";
	
	РезультатЗапроса = Запрос.ВыполнитьПакет();
	ВГраница = РезультатЗапроса.ВГраница();
	
	ВыборкаДобавить = РезультатЗапроса[ВГраница - 1].Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	
	Пока ВыборкаДобавить.Следующий() Цикл
		ЭтапОбъект = ВыборкаДобавить.Этап.ПолучитьОбъект();
		ЭтапОбъект.ПометкаУдаления = Ложь;
		
		ДобавляемыеПереходы = ВыборкаДобавить.Выбрать();
		Пока ДобавляемыеПереходы.Следующий() Цикл
			НовыйПереход = ЭтапОбъект.Переходы.Добавить();
			НовыйПереход.ЭлементПерехода = ДобавляемыеПереходы.Переход;
		КонецЦикла;
		
		ЭтапОбъект.Записать();
	КонецЦикла;
	
	ВыборкаУдалить = РезультатЗапроса[ВГраница].Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	
	Пока ВыборкаУдалить.Следующий() Цикл
		ЭтапОбъект = ВыборкаУдалить.Этап.ПолучитьОбъект();
		
		УдаляемыеПереходы = ВыборкаУдалить.Выбрать();
		Пока УдаляемыеПереходы.Следующий() Цикл
			ЭтапОбъект.Переходы.Удалить(ЭтапОбъект.Переходы.Найти(УдаляемыеПереходы.Переход));
		КонецЦикла;
		
		ЭтапОбъект.Записать();
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти

#КонецЕсли
