///////////////////////////////////////////////////////////////////////////////////////////////////////
// Copyright (c) 2025, ООО ДНС Технологии
// SDMS (Software Development Management System) — это корпоративная система учета разработки и управления проектами 
// Все права защищены. Эта программа и сопроводительные материалы предоставляются 
// в соответствии с условиями лицензии General Public License (GNU GPL v3)
// Текст лицензии доступен по ссылке:
// https://www.gnu.org/licenses/gpl-3.0.html
///////////////////////////////////////////////////////////////////////////////////////////////////////

#Если Сервер ИЛИ ТолстыйКлиентОбычноеПриложение ИЛИ ВнешнееСоединение Тогда

#Область ОписаниеПеременных

// Хранит признак, что значение по умолчанию было
// установлено принудительно в процессе записи объекта
Перем ПринудительнаяУстановкаЗначенияПоУмолчанию;

#КонецОбласти

#Область ОбработчикиСобытий

Процедура ПередЗаписью(Отказ)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	ПринудительнаяУстановкаЗначенияПоУмолчанию = Ложь;
	
	Если ЭтоНовый() И НЕ ЗначениеПоУмолчанию Тогда
		
		// Если это новое значение, и у него не установлен признак
		// "ЗначениеПоУмолчанию", то нужно проверить наличие других
		// элементов от владельца этого элемента и если таковых нет
		// нужно установить элементу отметку "ЗначениеПоУмолчанию".
		
		Если ЭлементыУВладельцаОтсутствуют() Тогда
			ЗначениеПоУмолчанию = Истина;
			ПринудительнаяУстановкаЗначенияПоУмолчанию = Истина;
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

Процедура ПриЗаписи(Отказ)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	// При записи элемента, у него может быть выставлен признак "ЗначениеПоУмолчанию".
	// В разрезе владельца может быть только один элемент с таким признаком. Требуется
	// проверить и сбросить при необходимости это флаг у предыдущего элемента по умолчанию.
	
	Если ЗначениеПоУмолчанию И НЕ ПринудительнаяУстановкаЗначенияПоУмолчанию Тогда
		СброситьФлагПредыдущегоЗначенияПоУмолчанию();
	КонецЕсли;
	
КонецПроцедуры

Процедура ПередУдалением(Отказ)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	// Для обеспечения ссылочной целостности необходимо проверить
	// использование этого значения в качестве вида реквизита
	Если ЭтотЭлементИспользуется() Тогда
		Отказ = Истина;
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю("Удаляемый элемент используется в качестве значения дополнительного реквизита.");
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Процедура СброситьФлагПредыдущегоЗначенияПоУмолчанию()
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ДоступныеЗначенияРеквизитов.Ссылка КАК Ссылка
	|ИЗ
	|	Справочник.ДоступныеЗначенияРеквизитов КАК ДоступныеЗначенияРеквизитов
	|ГДЕ
	|	ДоступныеЗначенияРеквизитов.Владелец = &Владелец
	|	И ДоступныеЗначенияРеквизитов.Ссылка <> &Ссылка
	|	И ДоступныеЗначенияРеквизитов.ЗначениеПоУмолчанию";
	
	Запрос.УстановитьПараметр("Владелец", Владелец);
	Запрос.УстановитьПараметр("Ссылка", Ссылка);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Если НЕ РезультатЗапроса.Пустой() Тогда
		Выборка = РезультатЗапроса.Выбрать();
		Выборка.Следующий();
		
		// Исключительная блокировка элемента справочника
		БлокировкаДанныхЗапись = Новый БлокировкаДанных;
		ЭлементБлокировки = БлокировкаДанныхЗапись.Добавить("Справочник.ДоступныеЗначенияРеквизитов");
		ЭлементБлокировки.УстановитьЗначение("Ссылка", Выборка.Ссылка);
		ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
		БлокировкаДанныхЗапись.Заблокировать();
		
		// Изменение объекта и его запись
		СправочникОбъект = Выборка.Ссылка.ПолучитьОбъект();
		СправочникОбъект.ЗначениеПоУмолчанию = Ложь;
		СправочникОбъект.ОбменДанными.Загрузка = Истина;
		СправочникОбъект.Записать();
	КонецЕсли;
	
КонецПроцедуры

Функция ЭлементыУВладельцаОтсутствуют()
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ИСТИНА КАК ЕстьЗначения
	|ИЗ
	|	Справочник.ДоступныеЗначенияРеквизитов КАК ДоступныеЗначенияРеквизитов
	|ГДЕ
	|	ДоступныеЗначенияРеквизитов.Владелец = &Владелец
	|	И ДоступныеЗначенияРеквизитов.ЗначениеПоУмолчанию";
	
	Запрос.УстановитьПараметр("Владелец", Владелец);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Возврат РезультатЗапроса.Пустой();
	
КонецФункции

Функция ЭтотЭлементИспользуется()
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ИСТИНА КАК ЗначениеИспользуется
	|ИЗ
	|	РегистрСведений.ЗначенияДополнительныхРеквизитовОбъектов КАК ЗначенияДополнительныхРеквизитовОбъектов
	|ГДЕ
	|	ЗначенияДополнительныхРеквизитовОбъектов.Реквизит = &Реквизит
	|	И ЗначенияДополнительныхРеквизитовОбъектов.Значение = &Значение";
	
	Запрос.УстановитьПараметр("Реквизит", Владелец);
	Запрос.УстановитьПараметр("Значение", Ссылка);
	
	Возврат НЕ Запрос.Выполнить().Пустой();
	
КонецФункции

#КонецОбласти

#КонецЕсли
