///////////////////////////////////////////////////////////////////////////////////////////////////////
// Copyright (c) 2025, ООО ДНС Технологии
// SDMS (Software Development Management System) — это корпоративная система учета разработки и управления проектами 
// Все права защищены. Эта программа и сопроводительные материалы предоставляются 
// в соответствии с условиями лицензии General Public License (GNU GPL v3)
// Текст лицензии доступен по ссылке:
// https://www.gnu.org/licenses/gpl-3.0.html
///////////////////////////////////////////////////////////////////////////////////////////////////////

#Если Сервер ИЛИ ТолстыйКлиент ИЛИ ВнешнееСоединение Тогда
	
#Область ОбъявлениеПеременных

Перем АктуализироватьВидыДеятельностиУслуг;

#КонецОбласти

#Область ОбработчикиСобытий

Процедура ПередЗаписью(Отказ)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	СтарыеЗначения = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(Ссылка, "Наименование, Назначение");
	
	Если НЕ ЭтоНовый() Тогда
		Если Назначение <> СтарыеЗначения.Назначение И Назначение = Перечисления.НазначенияВидовДеятельности.Общее
			И ИспользуетсяВУслуге(Отказ) Тогда
			Возврат;
		КонецЕсли;
		
		АктуализироватьВидыДеятельностиУслуг = (Назначение = Перечисления.НазначенияВидовДеятельности.ITПодразделение
			И СтарыеЗначения.Наименование <> Наименование);
	КонецЕсли;
	
КонецПроцедуры

Процедура ПриЗаписи(Отказ)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	Если АктуализироватьВидыДеятельностиУслуг = Истина Тогда
		Справочники.ВидыДеятельностиУслуг.ОбработатьИзменениеВидаДеятельности(Ссылка);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция ИспользуетсяВУслуге(Отказ)
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	Услуги.Наименование КАК Наименование
	|ИЗ
	|	Справочник.Услуги.ВидыДеятельности КАК УслугиВидыДеятельности
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.Услуги КАК Услуги
	|		ПО УслугиВидыДеятельности.Ссылка = Услуги.Ссылка
	|ГДЕ
	|	УслугиВидыДеятельности.ВидДеятельности = &ВидДеятельности
	|	И Услуги.Используется
	|	И НЕ Услуги.ПометкаУдаления";
	Запрос.УстановитьПараметр("ВидДеятельности", Ссылка);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Если НЕ РезультатЗапроса.Пустой() Тогда
		Услуги = Новый Массив;
		
		Выборка = РезультатЗапроса.Выбрать();
		Пока Выборка.Следующий() Цикл
			Услуги.Добавить(Выборка.Наименование);
		КонецЦикла;
		
		Сообщение = СтрШаблон("Общее назначение предназначено для пользователей, которые не относятся к IT.
									|Данный вид деятельности уже используется в услугах: %1.", СтрСоединить(Услуги, ", "));
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(Сообщение, , , , Отказ);
	КонецЕсли;
	
	Возврат Отказ;
	
КонецФункции

#КонецОбласти

#КонецЕсли
