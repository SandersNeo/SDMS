///////////////////////////////////////////////////////////////////////////////////////////////////////
// Copyright (c) 2025, ООО ДНС Технологии
// SDMS (Software Development Management System) — это корпоративная система учета разработки и управления проектами 
// Все права защищены. Эта программа и сопроводительные материалы предоставляются 
// в соответствии с условиями лицензии General Public License (GNU GPL v3)
// Текст лицензии доступен по ссылке:
// https://www.gnu.org/licenses/gpl-3.0.html
///////////////////////////////////////////////////////////////////////////////////////////////////////

#Если Сервер ИЛИ ТолстыйКлиентОбычноеПриложение ИЛИ ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

// Добавляет обработчики обновления
//
// Параметры:
//  Обработчики	 - ТаблицаЗначений	 - см. ПриложениеВызовСервера.ПолучитьОбработчикиДанных
//
Процедура ДобавлениеОбработчиковОбновления(Обработчики) Экспорт     
	
	Обработчик = Обработчики.Добавить();
	Обработчик.Версия = "*";
	Обработчик.Процедура = "Справочники.ТипыОбъектовВладельцев.ПроверитьПредопределенныеЭлементы";
	
КонецПроцедуры

// Возвращает ссылку на предопределенный элемент справочника ТипыОбъектовВладельцев,
// который логически связан с объектом БД. Неопределено, если элемент не найден.
//
// Параметры:
//  Ссылка - Ссылка - ссылка на объект базы данных.
//
// Возвращаемое значение:
//  СправочникСсылка.ТипыОбъектовВладельцев, если элемент найден. В противном случае
//  Неопределено.
//
Функция НайтиСвязанныйЭлемент(Знач Ссылка) Экспорт
	
	СвязанныйЭлемент = Неопределено;
	
	ПолноеИмя = Ссылка.Метаданные().ПолноеИмя();
	ПредопределенноеИмя = СтрЗаменить(ПолноеИмя, ".", "_");
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ТипыОбъектовВладельцев.Ссылка КАК Ссылка
	|ИЗ
	|	Справочник.ТипыОбъектовВладельцев КАК ТипыОбъектовВладельцев
	|ГДЕ
	|	ТипыОбъектовВладельцев.ИмяПредопределенныхДанных = &ПредопределенноеИмя";
	
	Запрос.УстановитьПараметр("ПредопределенноеИмя", ПредопределенноеИмя);
	
	Попытка	
		РезультатЗапроса = Запрос.Выполнить();
		
		Если НЕ РезультатЗапроса.Пустой() Тогда
			Выборка = РезультатЗапроса.Выбрать();
			Выборка.Следующий();
			
			СвязанныйЭлемент = Выборка.Ссылка;
		КонецЕсли;
	Исключение
		ВызватьИсключение СтрШаблон("ru = 'Для объекта конфигурации %1 не задан предопределенный элемент в справочнике ТипыОбъектовВладельцев.'", ПолноеИмя);
	КонецПопытки;
	
	Возврат СвязанныйЭлемент;	
	
КонецФункции

// Проверяет предопределенные настройки
//
Процедура ПроверитьПредопределенныеЭлементы() Экспорт 
	
	ЗаполнитьПредопределенныйЭлемент("Документ_Задача", "ПроцессУстановкиСтатусов");	                                 	
	ЗаполнитьПредопределенныйЭлемент("Документ_ВнутреннееЗадание", "ПроцессУстановкиСтатусовВнутреннееЗадание");	                                 	
	
КонецПроцедуры     

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Процедура ЗаполнитьПредопределенныйЭлемент(ИмяЭлемента, ИмяНастройки) 
	
	Попытка
		Объект = ОбщегоНазначения.ПредопределенныйЭлемент("Справочник.ТипыОбъектовВладельцев." + ИмяЭлемента);
	Исключение
		Сообщение = НСтр("ru = 'Предопределенный элемент ""%Параметр%"" не найден.'");
		Сообщение = СтрЗаменить(Сообщение, "%Параметр%", "Справочник.ТипыОбъектовВладельцев." + ИмяЭлемента);
		ВызватьИсключение Сообщение;
	КонецПопытки; 
	
	Попытка
		ОбъектНастройка = ОбщегоНазначения.ПредопределенныйЭлемент("ПланВидовХарактеристик.ВидыНастроекФилиалов." + ИмяНастройки);
	Исключение
		Сообщение = НСтр("ru = 'Предопределенный элемент ""%Параметр%"" не найден.'");
		Сообщение = СтрЗаменить(Сообщение, "%Параметр%", "ПланВидовХарактеристик.ВидыНастроекФилиалов." + ИмяНастройки);
		ВызватьИсключение Сообщение;
	КонецПопытки;
	
	Если Объект <> Неопределено Тогда 
		Объект = Объект.ПолучитьОбъект();
		
		Если НЕ ЗначениеЗаполнено(Объект.Настройка) Тогда
			Объект.Настройка = ОбъектНастройка.Ссылка;  
			Объект.Записать();
		КонецЕсли;
	КонецЕсли;	

КонецПроцедуры

#КонецОбласти

#КонецЕсли
