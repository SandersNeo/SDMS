///////////////////////////////////////////////////////////////////////////////////////////////////////
// Copyright (c) 2025, ООО ДНС Технологии
// SDMS (Software Development Management System) — это корпоративная система учета разработки и управления проектами 
// Все права защищены. Эта программа и сопроводительные материалы предоставляются 
// в соответствии с условиями лицензии General Public License (GNU GPL v3)
// Текст лицензии доступен по ссылке:
// https://www.gnu.org/licenses/gpl-3.0.html
///////////////////////////////////////////////////////////////////////////////////////////////////////

#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ОбщегоНазначенияКлиентСервер.УстановитьПараметрДинамическогоСписка(ЭтотОбъект.Список, "Владелец", 
		ПараметрыСеанса.ТекущийПользователь);
		
	ТекущаяДата = ТекущаяДатаСеанса();
		
	ОбщегоНазначенияКлиентСервер.УстановитьПараметрДинамическогоСписка(ЭтотОбъект.Список, "ТекущаяДата", 
		ТекущаяДата);
		
	Если Параметры.Отбор.Свойство("Филиал") Тогда
		Если ТипЗнч(Параметры.Отбор.Филиал) = Тип("Массив") Тогда
			СвязанныеФилиалы = Новый ФиксированныйМассив(Параметры.Отбор.Филиал);
		Иначе
			СвязанныеФилиалы = Параметры.Отбор.Филиал;
		КонецЕсли;
		ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(ЭтотОбъект.Филиалы.Отбор, "Ссылка",
			ВидСравненияКомпоновкиДанных.ВИерархии, СвязанныеФилиалы, , Истина);
	КонецЕсли;
	
	Если НЕ ЗначениеЗаполнено(Параметры.ТекущаяСтрока) Тогда
		ФилиалПользователя = ПараметрыСеанса.Филиал;
	Иначе
		ФилиалПользователя = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Параметры.ТекущаяСтрока, "Филиал");
	КонецЕсли;
	
	Элементы.Филиалы.ТекущаяСтрока = ФилиалПользователя;
	Филиал = ПараметрыСеанса.Филиал;
	
	Если Параметры.Отбор.Свойство("СуществующиеПрофили") Тогда
		ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(ЭтотОбъект.Список.Отбор, "ПользовательСсылка",
			ВидСравненияКомпоновкиДанных.НеВСписке, Параметры.Отбор.СуществующиеПрофили, , Истина);
	КонецЕсли; 
	
	ИнтерфейсПриложения.УстановитьУсловноеОформлениеСпискаДоступныхЗначений(
		ЭтотОбъект, "Список.Избранное", "СписокНаименование");
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовФормы	
	
&НаКлиенте
Процедура СписокВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	Если Поле.Имя = "Избранное" Тогда
		СтандартнаяОбработка = Ложь;
		ТекущиеДанные = Элемент.ТекущиеДанные;
		ОбщегоНазначенияВызовСервера.ОбработатьПометкуИзбранного(ТекущиеДанные.ПользовательСсылка,
			НЕ ТекущиеДанные.Избранное, "Пользователи");
		Элементы.Список.Обновить();
	КонецЕсли;
	
КонецПроцедуры	

&НаКлиенте
Процедура ФилиалыПриАктивизацииСтроки(Элемент)
	
	Если ЗначениеЗаполнено(Элемент.ТекущаяСтрока) Тогда
		
		УдалитьИУстановитьОтборы(Элемент.ТекущаяСтрока);
	
		ВключеноВсеПользователи = Ложь;
		ВключеноИзбранное = Ложь;
		ВключеноМойФилиал = Элемент.ТекущаяСтрока = Филиал; 
	КонецЕсли;

КонецПроцедуры
	
#КонецОбласти                          

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ОткрытьВсе(Команда)
	
	Если СвязанныеФилиалы = Неопределено Тогда
		ОбщегоНазначенияКлиентСервер.УдалитьЭлементыГруппыОтбора(ЭтотОбъект.Список.Отбор, "Филиал");
	Иначе		
		ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбора(ЭтотОбъект.Список.Отбор, "Филиал", 
			СвязанныеФилиалы, ВидСравненияКомпоновкиДанных.ВИерархии);
	КонецЕсли;

	ОбщегоНазначенияКлиентСервер.УдалитьЭлементыГруппыОтбора(ЭтотОбъект.Список.Отбор, "Избранное");
	ОбщегоНазначенияКлиентСервер.УдалитьЭлементыГруппыОтбора(ЭтотОбъект.Список.Отбор, "Команда");
	ОбщегоНазначенияКлиентСервер.УдалитьЭлементыГруппыОтбора(ЭтотОбъект.Список.Отбор, "КастомнаяКоманда"); 
			
	// Сбрасываем текущую строку в списке так как после включения всех филиалов.
	// Выделение какой либо строки среди филиалов некорректно и не отражает действительности
	Элементы.Филиалы.ТекущаяСтрока = Неопределено;
	
	ВключеноВсеПользователи = Истина;
	ВключеноИзбранное = Ложь;
	ВключеноМойФилиал = Ложь;
	
	ЭтотОбъект.ТекущийЭлемент = Элементы.СписокСтрокаПоиска;
		
КонецПроцедуры

&НаКлиенте
Процедура ОткрытьИзбранное(Команда)
	
	ВключеноИзбранное = НЕ ВключеноИзбранное; 
	ВключеноМойФилиал = Ложь;
	
	ОбщегоНазначенияКлиентСервер.УдалитьЭлементыГруппыОтбора(ЭтотОбъект.Список.Отбор, "Филиал");
	ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбора(ЭтотОбъект.Список.Отбор, "Избранное", Истина, 
		ВидСравненияКомпоновкиДанных.Равно, , ВключеноИзбранное);
		
	Если ВключеноИзбранное = Истина Тогда
		// Сбрасываем текущую строку в списке так как после включения избранного
		// Фильтр по выбраному филиалу не имеет силы
		Элементы.Филиалы.ТекущаяСтрока = Неопределено;
		ВключеноВсеПользователи = Истина;
	Иначе
		ВключеноВсеПользователи = Истина;  
	КонецЕсли;
		
КонецПроцедуры

&НаКлиенте
Процедура ОткрытьМойФилиал(Команда)
		
	УдалитьИУстановитьОтборы(Филиал);
	
	Элементы.Филиалы.ТекущаяСтрока = Филиал;	
	
	ВключеноВсеПользователи = Ложь;
	ВключеноИзбранное = Ложь;
	ВключеноМойФилиал = Истина;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура УдалитьИУстановитьОтборы(Филиал)
	
	ОбщегоНазначенияКлиентСервер.УдалитьЭлементыГруппыОтбора(ЭтотОбъект.Список.Отбор, "Избранное");
	Родитель = ОбщегоНазначенияВызовСервера.ЗначениеРеквизитаОбъекта(Филиал, "Родитель");
	
	Если ЗначениеЗаполнено(Родитель) Тогда
		Если Родитель = ПредопределенноеЗначение("Справочник.Филиалы.КастомныеКоманды") Тогда
			ОбщегоНазначенияКлиентСервер.УдалитьЭлементыГруппыОтбора(ЭтотОбъект.Список.Отбор, "Филиал");  
			ОбщегоНазначенияКлиентСервер.УдалитьЭлементыГруппыОтбора(ЭтотОбъект.Список.Отбор, "Команда");  
			ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбора(ЭтотОбъект.Список.Отбор, "КастомнаяКоманда", Филиал, 
				ВидСравненияКомпоновкиДанных.Равно);	
		Иначе
			ОбщегоНазначенияКлиентСервер.УдалитьЭлементыГруппыОтбора(ЭтотОбъект.Список.Отбор, "Филиал"); 
			ОбщегоНазначенияКлиентСервер.УдалитьЭлементыГруппыОтбора(ЭтотОбъект.Список.Отбор, "КастомнаяКоманда");  
			ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбора(ЭтотОбъект.Список.Отбор, "Команда", Филиал, 
				ВидСравненияКомпоновкиДанных.Равно);
		КонецЕсли;
	Иначе 			
		ОбщегоНазначенияКлиентСервер.УдалитьЭлементыГруппыОтбора(ЭтотОбъект.Список.Отбор, "Команда");  
		ОбщегоНазначенияКлиентСервер.УдалитьЭлементыГруппыОтбора(ЭтотОбъект.Список.Отбор, "КастомнаяКоманда");  
		ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбора(ЭтотОбъект.Список.Отбор, "Филиал", Филиал, 
			ВидСравненияКомпоновкиДанных.Равно);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти
