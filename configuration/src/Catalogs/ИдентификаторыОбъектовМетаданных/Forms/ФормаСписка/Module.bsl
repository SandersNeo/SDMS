///////////////////////////////////////////////////////////////////////////////////////////////////////
// Copyright (c) 2025, ООО ДНС Технологии
// SDMS (Software Development Management System) — это корпоративная система учета разработки и управления проектами 
// Все права защищены. Эта программа и сопроводительные материалы предоставляются 
// в соответствии с условиями лицензии General Public License (GNU GPL v3)
// Текст лицензии доступен по ссылке:
// https://www.gnu.org/licenses/gpl-3.0.html
///////////////////////////////////////////////////////////////////////////////////////////////////////

#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	НовыйОтбор = Список.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	НовыйОтбор.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("ПометкаУдаления");
	НовыйОтбор.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно;
	НовыйОтбор.ПравоеЗначение = Ложь;
	НовыйОтбор.Использование = Истина;
	
	НовыйОтбор = Дерево.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	НовыйОтбор.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("ПометкаУдаления");
	НовыйОтбор.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно;
	НовыйОтбор.ПравоеЗначение = Ложь;
	НовыйОтбор.Использование = Истина;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКоманд

&НаКлиенте
Процедура ПоказыватьПомеченныеНаУдаление(Команда)
	
	Список.Отбор.Элементы[0].ПравоеЗначение = НЕ Список.Отбор.Элементы[0].ПравоеЗначение;
	Дерево.Отбор.Элементы[0].ПравоеЗначение = НЕ Дерево.Отбор.Элементы[0].ПравоеЗначение;
	
КонецПроцедуры

&НаКлиенте
Асинх Процедура Синхронизировать(Команда)
	
	ТекстВопроса = "Загрузить ""ConfigDumpInfo.xml"" вручную?";
	ОтветНаВопрос = Ждать ВопросАсинх(ТекстВопроса, РежимДиалогаВопрос.ДаНетОтмена, , КодВозвратаДиалога.Да);
	
	Если ОтветНаВопрос = КодВозвратаДиалога.Да Тогда
		ПараметрыДиалога = Новый ПараметрыДиалогаПомещенияФайлов(, Ложь, "*.xml");
		ОписаниеФайла = Ждать ПоместитьФайлНаСерверАсинх(, , , ПараметрыДиалога, УникальныйИдентификатор);
		
		Если ОписаниеФайла <> Неопределено Тогда
			СинхронизироватьНаСервере(ОписаниеФайла.Адрес)
		КонецЕсли;
		
	ИначеЕсли ОтветНаВопрос = КодВозвратаДиалога.Нет Тогда
		СинхронизироватьНаСервере();
	Иначе
		Возврат;
	КонецЕсли;
	
	Элементы.Список.Обновить();
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервереБезКонтекста
Процедура СинхронизироватьНаСервере(Знач АдресХранилища = Неопределено)
	
	СоставКонфигурации = Неопределено;
	
	Если АдресХранилища <> Неопределено Тогда
		ДвоичныеДанные = ПолучитьИзВременногоХранилища(АдресХранилища);
		
		ЧтениеДанных = Новый ЧтениеДанных(ДвоичныеДанные, КодировкаТекста.UTF8);
		ЧтениеТекста = Новый ЧтениеТекста(ЧтениеДанных.ИсходныйПоток(), КодировкаТекста.UTF8);
		СоставКонфигурации = ЧтениеТекста.Прочитать();
		ЧтениеТекста.Закрыть();
		ЧтениеДанных.Закрыть();
	КонецЕсли;
	
	Справочники.ИдентификаторыОбъектовМетаданных.СинхронизироватьИдентификаторыМетаданных(СоставКонфигурации);
	
КонецПроцедуры

#КонецОбласти
