///////////////////////////////////////////////////////////////////////////////////////////////////////
// Copyright (c) 2025, ООО ДНС Технологии
// SDMS (Software Development Management System) — это корпоративная система учета разработки и управления проектами 
// Все права защищены. Эта программа и сопроводительные материалы предоставляются 
// в соответствии с условиями лицензии General Public License (GNU GPL v3)
// Текст лицензии доступен по ссылке:
// https://www.gnu.org/licenses/gpl-3.0.html
///////////////////////////////////////////////////////////////////////////////////////////////////////

#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Пользователь = ПараметрыСеанса.ТекущийПользователь;
	
	Если Параметры.Ключ.Пустая() Тогда
		Объект.Автор = Пользователь;
		Объект.ТипОбъекта = Справочники.ТипыОбъектовВладельцев.Документ_ЗаявкаНаРазработку;

		Элементы.Филиал.Доступность = Истина;
		Элементы.Филиал.РежимВыбораИзСписка = Истина;
		ЗаполнитьСписокФилиалов(Пользователь);
	Иначе
		ЭтотОбъект.ТолькоПросмотр = ДоступностьРедактирования(Объект.Владелец, Пользователь);		
	КонецЕсли;
		
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовФормы

&НаКлиенте
Процедура АвторНажатие(Элемент, СтандартнаяОбработка)
	
	ИнтерфейсПриложенияКлиент.ОткрытьИнформациюОПользователе(ЭтотОбъект, Объект.Автор, СтандартнаяОбработка);

КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервереБезКонтекста
Функция ДоступностьРедактирования(Филиал, Пользователь)
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	Филиалы.Ссылка КАК Филиал
	|ИЗ
	|	Справочник.Филиалы КАК Филиалы
	|ГДЕ
	|	Филиалы.Ссылка = &Филиал
	|	И Филиалы.Родитель = ЗНАЧЕНИЕ(Справочник.Филиалы.ПустаяСсылка)
	|	И (Филиалы.Руководитель = &Руководитель
	|			ИЛИ Филиалы.ЗаместительРуководителя = &Руководитель)
	|
	|ОБЪЕДИНИТЬ
	|
	|ВЫБРАТЬ
	|	Филиалы.Ссылка
	|ИЗ
	|	Справочник.Филиалы КАК Филиалы
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.Филиалы.ИерархияТерриторий КАК ФилиалыИерархияТерриторий
	|		ПО (ФилиалыИерархияТерриторий.Ссылка = Филиалы.Ссылка)
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.Территории КАК Территории
	|		ПО (ФилиалыИерархияТерриторий.Территория = Территории.Ссылка)
	|			И (Территории.Руководитель = &Руководитель)
	|ГДЕ
	|	Филиалы.Ссылка = &Филиал";
	
	Запрос.УстановитьПараметр("Руководитель", Пользователь);
	Запрос.УстановитьПараметр("Филиал", Филиал);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Возврат РезультатЗапроса.Пустой();		
	
КонецФункции

&НаСервере
Процедура ЗаполнитьСписокФилиалов(Пользователь)
	
	ФилиалыПользователя = Справочники.Филиалы.ПолучитьВсеФилиалыРуководителя();
	
	СписокВыбора = Элементы.Филиал.СписокВыбора;
	
	Для Каждого ЭлементСписка Из ФилиалыПользователя Цикл
		ЗаполнитьЗначенияСвойств(СписокВыбора.Добавить(), ЭлементСписка);
	КонецЦикла;
		
КонецПроцедуры

#КонецОбласти
