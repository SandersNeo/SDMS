///////////////////////////////////////////////////////////////////////////////////////////////////////
// Copyright (c) 2025, ООО ДНС Технологии
// SDMS (Software Development Management System) — это корпоративная система учета разработки и управления проектами 
// Все права защищены. Эта программа и сопроводительные материалы предоставляются 
// в соответствии с условиями лицензии General Public License (GNU GPL v3)
// Текст лицензии доступен по ссылке:
// https://www.gnu.org/licenses/gpl-3.0.html
///////////////////////////////////////////////////////////////////////////////////////////////////////

Функция ПолучитьАссетыДляОбновления(Знач ОбновлениеИзХранилища) Экспорт
	
	НастрокиОбновленияАссетов = Новый Массив;

	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ВерсииАссетовСрезПоследних.Период КАК Период,
	|	ВерсииАссетовСрезПоследних.ТипАссетов КАК ТипАссетов,
	|	ВерсииАссетовСрезПоследних.Версия КАК Версия
	|ПОМЕСТИТЬ ВерсииДляОбновления
	|ИЗ
	|	РегистрСведений.ВерсииАссетов.СрезПоследних(&Период, ) КАК ВерсииАссетовСрезПоследних
	|ГДЕ
	|	НЕ ВерсииАссетовСрезПоследних.Обновлен
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТипыАссетов.Ссылка КАК Ссылка,
	|	ТипыАссетов.АдресРепозиторияGit КАК АдресРепозиторияGit,
	|	ТипыАссетов.ИмяПроектаGit КАК ИмяПроектаGit,
	|	ТипыАссетов.КодПроектаGit КАК КодПроектаGit,
	|	ВерсииДляОбновления.Версия КАК Версия,
	|	ВерсииДляОбновления.Период КАК Дата,
	|	ТипыАссетов.КаталогРаспаковки КАК КаталогРаспаковки,
	|	ТипыАссетов.РасположениеФайлов КАК РасположениеФайлов
	|ИЗ
	|	ВерсииДляОбновления КАК ВерсииДляОбновления
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.ТипыАссетов КАК ТипыАссетов
	|		ПО ВерсииДляОбновления.ТипАссетов = ТипыАссетов.Ссылка
	|ГДЕ
	|	ТипыАссетов.АдресРепозиторияGit <> """"";
	
	Запрос.УстановитьПараметр("Период", ТекущаяДатаСеанса());
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Если РезультатЗапроса.Пустой() Тогда
		Возврат НастрокиОбновленияАссетов;
	КонецЕсли;
	
	СписокАссетов = Новый Массив;
	Выборка = РезультатЗапроса.Выбрать();
	
	УстановитьПривилегированныйРежим(Истина);
		
	Пока Выборка.Следующий() Цикл
		Токен = РегистрыСведений.БезопасноеХранилищеДанных.ПолучитьДанные(
			Перечисления.НазначенияДанныхБезопасногоХранилища.НастройкиАвторизацииОбновленияАссетов, Выборка.Ссылка);
			
		Если Токен = Неопределено Тогда
			Продолжить;
		КонецЕсли;
		
		Если ОбновлениеИзХранилища Тогда
			ТипАссетов = XMLСтрока(Выборка.Ссылка);
			АдресРепозитория = Выборка.АдресРепозиторияGit;
		Иначе
			ТипАссетов = Выборка.Ссылка;
			АдресРепозитория = СтрЗаменить(Выборка.АдресРепозиторияGit, "https://", "");
		КонецЕсли;
				
		Ассеты = Новый Структура;
		Ассеты.Вставить("Тип", ТипАссетов);
		Ассеты.Вставить("АдресРепозитория", АдресРепозитория);
		Ассеты.Вставить("ИмяПроекта", Выборка.ИмяПроектаGit);
		Ассеты.Вставить("КодПроекта", Выборка.КодПроектаGit);
		Ассеты.Вставить("Версия", Выборка.Версия);
		Ассеты.Вставить("КаталогРаспаковки", Выборка.КаталогРаспаковки);
		Ассеты.Вставить("РасположениеФайлов", Выборка.РасположениеФайлов);
		Ассеты.Вставить("Токен", Токен);
		Ассеты.Вставить("Дата", Выборка.Дата);
		
		НастрокиОбновленияАссетов.Добавить(Ассеты);
	КонецЦикла;
	
	УстановитьПривилегированныйРежим(Ложь);
	
	Возврат НастрокиОбновленияАссетов;
	
КонецФункции
