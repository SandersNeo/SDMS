///////////////////////////////////////////////////////////////////////////////////////////////////////
// Copyright (c) 2025, ООО ДНС Технологии
// SDMS (Software Development Management System) — это корпоративная система учета разработки и управления проектами 
// Все права защищены. Эта программа и сопроводительные материалы предоставляются 
// в соответствии с условиями лицензии General Public License (GNU GPL v3)
// Текст лицензии доступен по ссылке:
// https://www.gnu.org/licenses/gpl-3.0.html
///////////////////////////////////////////////////////////////////////////////////////////////////////

#Если Сервер ИЛИ ТолстыйКлиентОбычноеПриложение ИЛИ ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

Процедура ПередЗаписью(Отказ)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	Если НЕ ЭтоНовый() Тогда
		РезультатПроверки = ВыполнитьПроверку();
		
		Если РезультатПроверки.Используется Тогда
			Если РезультатПроверки.ИспользуетсяВУдаленныхТипах Тогда
				Отказ = Истина;
				ОбщегоНазначенияКлиентСервер.СообщитьПользователю("Запрещено удаление типа, в котором уже используется этот вид реквизита");
			КонецЕсли;
			
			Если ДополнительныеСвойства.Свойство("ИзменилсяТипЗначения") Тогда
				Если ДополнительныеСвойства.ИзменилсяТипЗначения Тогда
					ОбщегоНазначенияКлиентСервер.СообщитьПользователю("Запрещено изменять тип значения дополнительного реквизита, если он уже используется.", Ссылка, "ТипЗначения", "Объект.ТипЗначения", Отказ);
				КонецЕсли;
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
		
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыФункции

// Проверяет используется ли дополнительный реквизит, отдельный поиск использования в удаленных типах,
// если они есть, а также наличие подчиненных элементов в справочнике ДоступныеЗначенияРеквизитов
// 
// Возвращаемое значение:
//   - Структура
//		* ИспользуетсяВУдаленныхТипах	- Булево	- использование данного дополнительного реквизита в объектах, тип которых удален из списка доступных типов
//		* ЕстьДоступныеЗначения			- Булево	- наличие подчиненных элементов в справочнике ДоступныеЗначенияРеквизитов
//		* Используется					- Булево	- используется ли вообще данный дополнительный реквизит
//
Функция ВыполнитьПроверку()
	
	Результат = Новый Структура;
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	&Ссылка КАК Ссылка,
	|	Таблица.Тип КАК Тип,
	|	Таблица.Филиал КАК Филиал
	|ПОМЕСТИТЬ ТипыТекущегоОбъекта
	|ИЗ
	|	&ТаблицаТипов КАК Таблица
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ИспользуемыеОбъекты.Ссылка КАК Ссылка,
	|	ИспользуемыеОбъекты.Тип КАК Тип
	|ПОМЕСТИТЬ УдаленныеТипы
	|ИЗ
	|	ПланВидовХарактеристик.ВидыДополнительныхРеквизитов.ИспользоватьВОбъектах КАК ИспользуемыеОбъекты
	|		ЛЕВОЕ СОЕДИНЕНИЕ ТипыТекущегоОбъекта КАК ТипыТекущегоОбъекта
	|		ПО ИспользуемыеОбъекты.Ссылка = ТипыТекущегоОбъекта.Ссылка
	|			И ИспользуемыеОбъекты.Тип = ТипыТекущегоОбъекта.Тип
	|			И ИспользуемыеОбъекты.Филиал = ТипыТекущегоОбъекта.Филиал
	|ГДЕ
	|	ИспользуемыеОбъекты.Ссылка = &Ссылка
	|	И ТипыТекущегоОбъекта.Ссылка ЕСТЬ NULL
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ИСТИНА КАК Используется
	|ИЗ
	|	РегистрСведений.ЗначенияДополнительныхРеквизитовОбъектов КАК ЗначенияДополнительныхРеквизитов
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ УдаленныеТипы КАК УдаленныеТипы
	|		ПО ЗначенияДополнительныхРеквизитов.Реквизит = УдаленныеТипы.Ссылка
	|			И (ТИПЗНАЧЕНИЯ(ЗначенияДополнительныхРеквизитов.Объект) = ТИПЗНАЧЕНИЯ(УдаленныеТипы.Тип))
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ИСТИНА КАК Используется
	|ИЗ
	|	РегистрСведений.ЗначенияДополнительныхРеквизитовОбъектов КАК ЗначенияДополнительныхРеквизитовОбъектов
	|ГДЕ
	|	ЗначенияДополнительныхРеквизитовОбъектов.Реквизит = &Ссылка";
	
	Запрос.УстановитьПараметр("ТаблицаТипов", ИспользоватьВОбъектах.Выгрузить());
	Запрос.УстановитьПараметр("Ссылка", Ссылка);
	
	Пакеты = Запрос.ВыполнитьПакет();
	
	Результат.Вставить("ИспользуетсяВУдаленныхТипах", (НЕ Пакеты.Получить(2).Пустой()));
	Результат.Вставить("Используется", (НЕ Пакеты.Получить(3).Пустой()));
	
	Возврат Результат;
	
КонецФункции

#КонецОбласти

#КонецЕсли
