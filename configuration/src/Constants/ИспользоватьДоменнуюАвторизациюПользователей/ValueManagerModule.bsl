///////////////////////////////////////////////////////////////////////////////////////////////////////
// Copyright (c) 2025, ООО ДНС Технологии
// SDMS (Software Development Management System) — это корпоративная система учета разработки и управления проектами 
// Все права защищены. Эта программа и сопроводительные материалы предоставляются 
// в соответствии с условиями лицензии General Public License (GNU GPL v3)
// Текст лицензии доступен по ссылке:
// https://www.gnu.org/licenses/gpl-3.0.html
///////////////////////////////////////////////////////////////////////////////////////////////////////

#Если Сервер ИЛИ ТолстыйКлиентОбычноеПриложение ИЛИ ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

Процедура ПриЗаписи(Отказ)
	
	ИзменитьНастройкуАутентификацииСредствамиОперационнойСистемы(Значение);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

// Изменяет состояние настройки АутентификацияОС у пользователей информационной базы.
//
// Параметры:
//  ВключитьАутентификацию - Булево - признак включения или отключения авторизации средствами
//                           операционной системы.
//
Процедура ИзменитьНастройкуАутентификацииСредствамиОперационнойСистемы(Знач ВключитьАутентификацию)
	
	// Формирование имени события для журнала регистрации
	Если ВключитьАутентификацию Тогда
		ИмяСобытия = НСтр("ru = 'Администрирование.Включение доменной авторизации'");
	Иначе
		ИмяСобытия = НСтр("ru = 'Администрирование.Отключение доменной авторизации'");
	КонецЕсли;
	
	ПользователиИБ = ПользователиИнформационнойБазы.ПолучитьПользователей();

	Для Каждого ПользовательБазы Из ПользователиИБ Цикл
		// Если выполняется включение доменной авторизации, но у пользователя не заполнен ПользовательОС,
		// то такому пользователю нет смысла включать авторизацию.
		Если ВключитьАутентификацию И ПустаяСтрока(ПользовательБазы.ПользовательОС) Тогда
			Продолжить;
		КонецЕсли;
		
		// Если текущее значение в настройках пользователя отличается от значения параметра
		Если ПользовательБазы.АутентификацияОС <> ВключитьАутентификацию Тогда 
			ПользовательБазы.АутентификацияОС = ВключитьАутентификацию;
			
			// Каждый пользователель пишется отдельно, чтобы ошибка при установке свойства для одного
			// пользователя не влияла на других.
			Попытка
				ПользовательБазы.Записать();
			Исключение
				ПредставлениеОшибки = ПодробноеПредставлениеОшибки(ИнформацияОбОшибке());
				
				ТекстКомментария = 
				"Не удалось изменить признак ""АутентификацияОС"" у пользователя информационной базы.
				|
				|Пользователь: %1
				|Ошибка: %2";
				
				ТекстКомментария = СтрШаблон(ТекстКомментария, ПользовательБазы.Имя, ПредставлениеОшибки);
				
				ЗаписьЖурналаРегистрации(ИмяСобытия, УровеньЖурналаРегистрации.Ошибка, , , ТекстКомментария);
			КонецПопытки;
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти

#КонецЕсли
