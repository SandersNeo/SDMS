///////////////////////////////////////////////////////////////////////////////////////////////////////
// Copyright (c) 2025, ООО ДНС Технологии
// SDMS (Software Development Management System) — это корпоративная система учета разработки и управления проектами 
// Все права защищены. Эта программа и сопроводительные материалы предоставляются 
// в соответствии с условиями лицензии General Public License (GNU GPL v3)
// Текст лицензии доступен по ссылке:
// https://www.gnu.org/licenses/gpl-3.0.html
///////////////////////////////////////////////////////////////////////////////////////////////////////

#Если Сервер ИЛИ ТолстыйКлиентОбычноеПриложение ИЛИ ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

Процедура ПередЗаписью(Отказ)
	
	// Значение константы всегда должно равняться последнему дню месяца
	Значение = КонецМесяца(Значение);
	
	ПредыдущееЗначение = ПолучитьСохраненноеЗначениеКонстанты();
	ДополнительныеСвойства.Вставить("ПредыдущееЗначение", ПредыдущееЗначение);
	
КонецПроцедуры

Процедура ПриЗаписи(Отказ)
	
	ЗаписатьСобытиеИзмененияВЖурналРегистрации();
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Процедура ЗаписатьСобытиеИзмененияВЖурналРегистрации()
	
	ТекстКомментария = СтрШаблон("Предыдущее значение: %1
		|Новое значение: %2", ДополнительныеСвойства.ПредыдущееЗначение, Формат(Значение, "ДП='не задано'"));
	
	ИмяСобытия = НСтр("ru = 'Константы.Изменение'");
	
	ЗаписьЖурналаРегистрации(ИмяСобытия,
		УровеньЖурналаРегистрации.Предупреждение, 
		Метаданные(), ,
		ТекстКомментария,
		РежимТранзакцииЗаписиЖурналаРегистрации.Транзакционная);
	
КонецПроцедуры

Функция ПолучитьСохраненноеЗначениеКонстанты()
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ДатаЗапретаИзмененияДанных.Значение КАК Значение
	|ИЗ
	|	Константа.ДатаЗапретаИзмененияДанных КАК ДатаЗапретаИзмененияДанных";
	
	Выборка = Запрос.Выполнить().Выбрать();
	Выборка.Следующий();
	
	Возврат Выборка.Значение;
	
КонецФункции

#КонецОбласти

#КонецЕсли
