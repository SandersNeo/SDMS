///////////////////////////////////////////////////////////////////////////////////////////////////////
// Copyright (c) 2025, ООО ДНС Технологии
// SDMS (Software Development Management System) — это корпоративная система учета разработки и управления проектами 
// Все права защищены. Эта программа и сопроводительные материалы предоставляются 
// в соответствии с условиями лицензии General Public License (GNU GPL v3)
// Текст лицензии доступен по ссылке:
// https://www.gnu.org/licenses/gpl-3.0.html
///////////////////////////////////////////////////////////////////////////////////////////////////////

#Область ОписаниеПеременных

// Хранилище глобальных переменных.
//
// ПараметрыПриложения - Соответствие - хранилище переменных, где:
//   * Ключ - Строка - имя переменной.
//   * Значение - Произвольный - значение переменной.
//
Перем ПараметрыПриложения Экспорт;

#КонецОбласти

#Область ОбработчикиСобытий

Процедура ПриВыбореДействияСообщенияСистемыВзаимодействия(Сообщение, Действие)
	
	ВзаимодействиеКлиент.ПриВыбореДействияСообщения(Сообщение, Действие);
	
КонецПроцедуры

Процедура ПередНачаломРаботыСистемы(Отказ)
	
	ПриложениеКлиент.ПередНачаломРаботыСистемы(Отказ);    
	ПараметрыОбновления = ПриложениеВызовСервера.ПараметрыОбновления();
	ПараметрыПриложения.Вставить("ОткрытиеМастераНастройки", ПараметрыОбновления.ОткрытиеМастераНастройки);
	
	Если Врег(ПараметрЗапуска) = "UPDATE" Тогда
		ПриложениеВызовСервера.ВыполнитьОбновлениеИнформационнойБазы();
		Отказ = Истина;
		Возврат;
	КонецЕсли;
    
	ОшибкаОбновления = Ложь;
	
	Если ПараметрыОбновления.НужноОбновление Тогда
		Выполнено = ПриложениеВызовСервера.ВыполнитьОбновлениеДанных();
		ОшибкаОбновления = НЕ Выполнено;
		ПараметрыПриложения.Вставить("ОшибкаОбновленияДанных", ОшибкаОбновления); 	
	КонецЕсли;
	
	ПриложениеВызовСервера.ЗаполнитьПараметрСеансаДоступенВнешнийСайт(ОкноВнешнегоСайта.Доступно);
	
КонецПроцедуры

Процедура ПриНачалеРаботыСистемы()
		
	ПриложениеКлиент.ПриНачалеРаботыСистемы();
	
КонецПроцедуры

Процедура ПриВыбореРезультатаГлобальногоПоиска(ЭлементРезультата, СтандартнаяОбработка)
	
	Если ЭлементРезультата.Значение = "###ПоискНаРабочемСтоле" Тогда    

		СтандартнаяОбработка = Ложь;
		Форма = Неопределено;
		ФормыРезультатыПоиска = Новый Массив; 		
		ФормыРезультатыПоиска.Добавить("Обработка.РезультатыПоиска.Форма.ФормаОбработки");
			
		ОткрытыеОкнаПриложения = ПолучитьОкна();
		
		Для Каждого Окно Из ОткрытыеОкнаПриложения Цикл
			Если Окно.Основное Тогда
				Продолжить;
			КонецЕсли;
			
			Содержимое = Окно.Содержимое;
			
			Если Содержимое.Количество() > 0 И ФормыРезультатыПоиска.Найти(Содержимое[0].ИмяФормы) <> Неопределено Тогда
				Форма = Содержимое[0]; 
				Прервать;
			КонецЕсли;
		КонецЦикла; 
		
		ОткрытьФорму = Ложь;
		
		Если Форма = Неопределено И ОкноВнешнегоСайта.Доступно Тогда
			Форма = ПолучитьФорму("Обработка.РезультатыПоиска.Форма"); 
			ОткрытьФорму = Истина;
		КонецЕсли;
			
		Если Форма <> Неопределено Тогда
			Форма.СтрокаПоиска = ЭлементРезультата.Описание;
			
			Если ОткрытьФорму Тогда
				Форма.Открыть();
			КонецЕсли;
			
			Форма.Активизировать(); 
			ПоискПоТегу = СтрНачинаетсяС(Форма.СтрокаПоиска, "#");
			ПолнотекстовыйПоискКлиент.ПриВыполненииПоиска(, Форма, Форма.КонтейнерСостоянийПоиска, 
				Форма.ПредыдущаяАктивнаяСтраница,, ПоискПоТегу);
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры 

Процедура ПриГлобальномПоиске(СтрокаПоиска, ПланПоиска)
	
	Элемент = ПланПоиска.Найти("ПоискНаРабочемСтоле", "ГлобальныйПоискКлиент");
	// для навигационной ссылки и поиска по номеру документа, при создании
	// нужно исключить поиск на рабочем столе
			
	Если СокрЛП(СтрокаПоиска) = "+" Тогда   
		
		ПланПоиска.Очистить();
		ПланПоиска.Добавить("ДобавитьКомандыСоздания", "ГлобальныйПоискКлиент", Ложь);
			
	ИначеЕсли СтрНачинаетсяС(СтрокаПоиска, "#") И СтрДлина(СтрокаПоиска) >= 4 Тогда 
		
		ПланПоиска.Очистить();
		ПланПоиска.Добавить("ДобавитьДанныеПоТегу", "ГлобальныйПоискКлиент", Ложь);
		ГлобальныйПоискКлиент.ДобавитьПоискНаРабочемСтоле(ПланПоиска);  
		
	ИначеЕсли ОбщегоНазначенияКлиент.ЭтоНавигационнаяСсылка(СтрокаПоиска) Тогда 
		
		// Возможно это навигационная ссылка во внешнем формате
		НавигационнаяСсылка = ГлобальныйПоискКлиент.ПолучитьНавигационнуюСсылкуИзВнешнейСсылки(СтрокаПоиска);
		
		Если НавигационнаяСсылка = "" Тогда 
			Если Элемент <> Неопределено Тогда
				ПланПоиска.Удалить(Элемент);
			КонецЕсли;
		Иначе		
			ПланПоиска.Очистить();
			ПланПоиска.Добавить("ДобавитьДанныеПоНавигационнойСсылке", "ГлобальныйПоискКлиент", Ложь);
		КонецЕсли;
		
	ИначеЕсли ПолнотекстовыйПоискКлиент.СтрокаПодобнаНомеруОбъекта(СтрокаПоиска) Тогда 
		
		Если Элемент <> Неопределено Тогда
			ПланПоиска.Удалить(Элемент);
		КонецЕсли;
		
	Иначе 		
		Если Элемент = Неопределено Тогда
			ГлобальныйПоискКлиент.ДобавитьПоискНаРабочемСтоле(ПланПоиска);
		КонецЕсли; 	
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти
