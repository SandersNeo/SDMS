///////////////////////////////////////////////////////////////////////////////////////////////////////
// Copyright (c) 2025, ООО ДНС Технологии
// SDMS (Software Development Management System) — это корпоративная система учета разработки и управления проектами 
// Все права защищены. Эта программа и сопроводительные материалы предоставляются 
// в соответствии с условиями лицензии General Public License (GNU GPL v3)
// Текст лицензии доступен по ссылке:
// https://www.gnu.org/licenses/gpl-3.0.html
///////////////////////////////////////////////////////////////////////////////////////////////////////

#Область ОписаниеПеременных

&НаКлиенте
Перем КоличествоЧеловекНаДоску; // Хранит количество человек, заданное пользователем для доски

#КонецОбласти

#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	РегистрыСведений.ОтслеживаниеИспользованияФункциональности.ОткрытиеФормы(ЭтотОбъект.ИмяФормы);
	
	Ключи = ПолучитьКлючиНастроек();
	КоличествоЧеловекНаДоску = ОбщегоНазначенияВызовСервера.ЗагрузитьНастройкиДанныхФормы(Ключи.Объект, Ключи.Настройка);
	
	Если КоличествоЧеловекНаДоску = Неопределено Тогда
		КоличествоЧеловекНаДоску = Новый Соответствие;
	КонецЕсли;
	
	СписокСвойств = "КоличествоЧеловек, Направление, Филиал, Период, ПоинтыНаЧеловекаВДень, ЧасРасчета";
	ЗаполнитьЗначенияСвойств(ЭтотОбъект, Параметры, СписокСвойств);
	
	Если ЗначениеЗаполнено(Направление) Тогда
		Элементы.Филиал.СписокВыбора.ЗагрузитьЗначения(ПолучитьСписокФилиалов(Направление));
	КонецЕсли;
	
	АдресХранилища = ПоместитьВоВременноеХранилище(КоличествоЧеловекНаДоску, УникальныйИдентификатор);
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	КоличествоЧеловекНаДоску = ПолучитьИзВременногоХранилища(АдресХранилища);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовФормы

&НаКлиенте
Процедура КоличествоЧеловекПриИзменении(Элемент)
	
	Если ЗначениеЗаполнено(ДоскаTrello) Тогда
		КоличествоЧеловекНаДоску.Вставить(ДоскаTrello, КоличествоЧеловек);
		СохранитьНастройкиПользователя(КоличествоЧеловекНаДоску);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура НаправлениеПриИзменении(Элемент)
	
	Элементы.Филиал.СписокВыбора.Очистить();
	
	Если ЗначениеЗаполнено(Направление) Тогда
		Элементы.Филиал.СписокВыбора.ЗагрузитьЗначения(ПолучитьСписокФилиалов(Направление));
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКоманд

&НаКлиенте
Процедура Сохранить(Команда)
	
	НастройкиОтчета = Новый Структура;
	НастройкиОтчета.Вставить("Филиал", Филиал);
	НастройкиОтчета.Вставить("КоличествоЧеловек", КоличествоЧеловек);
	НастройкиОтчета.Вставить("Направление", Направление);
	НастройкиОтчета.Вставить("Период", Период);
	НастройкиОтчета.Вставить("ПоинтыНаЧеловекаВДень", ПоинтыНаЧеловекаВДень);
	НастройкиОтчета.Вставить("ЧасРасчета", ЧасРасчета);
	
	Закрыть(НастройкиОтчета);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервереБезКонтекста
Функция ПолучитьКлючиНастроек()
	
	КлючиНастроек = Новый Структура;
	КлючиНастроек.Вставить("Объект", "Отчет.АнализРаботыКомандыSITE");
	КлючиНастроек.Вставить("Настройка", "КоличествоЧеловекНаДоску");
	
	Возврат КлючиНастроек;
	
КонецФункции

&НаСервереБезКонтекста
Функция ПолучитьСписокФилиалов(Знач Направление)

	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	Филиалы.Ссылка КАК Филиал
	|ИЗ
	|	Справочник.Филиалы КАК Филиалы
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.НастройкиФилиалов КАК НастройкиФилиалов
	|		ПО (НастройкиФилиалов.Филиал = Филиалы.Ссылка)
	|			И (НастройкиФилиалов.Настройка = ЗНАЧЕНИЕ(ПланВидовХарактеристик.ВидыНастроекФилиалов.НаправлениеРазработки))
	|			И (НастройкиФилиалов.Значение = &Направление)
	|ГДЕ
	|	НЕ Филиалы.ПометкаУдаления";
	
	Запрос.УстановитьПараметр("Направление", Направление);
	
	Возврат Запрос.Выполнить().Выгрузить().ВыгрузитьКолонку("Филиал");
	                                                                 	
КонецФункции

&НаСервереБезКонтекста
Процедура СохранитьНастройкиПользователя(Знач Настройки)
	
	Ключи = ПолучитьКлючиНастроек();
	ОбщегоНазначенияВызовСервера.СохранитьНастройкиДанныхФормы(Ключи.Объект, Ключи.Настройка, Настройки);
	
КонецПроцедуры

#КонецОбласти
