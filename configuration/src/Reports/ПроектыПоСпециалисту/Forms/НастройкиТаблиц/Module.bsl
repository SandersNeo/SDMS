///////////////////////////////////////////////////////////////////////////////////////////////////////
// Copyright (c) 2025, ООО ДНС Технологии
// SDMS (Software Development Management System) — это корпоративная система учета разработки и управления проектами 
// Все права защищены. Эта программа и сопроводительные материалы предоставляются 
// в соответствии с условиями лицензии General Public License (GNU GPL v3)
// Текст лицензии доступен по ссылке:
// https://www.gnu.org/licenses/gpl-3.0.html
///////////////////////////////////////////////////////////////////////////////////////////////////////

#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	РегистрыСведений.ОтслеживаниеИспользованияФункциональности.ОткрытиеФормы(ЭтотОбъект.ИмяФормы);
	
	ЗаполнитьНастройкиКолонок(Колонки, Параметры.НастройкиКолонок, Параметры.Колонки);
	
	Если Параметры.Свойство("ВыбранныеСтатусы") Тогда
		ЗаполнитьСписокСтатусов(Параметры.ВыбранныеСтатусы);
	Иначе
		Элементы.СтраницаСтатусы.Видимость = Ложь;
	КонецЕсли;
		
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКоманд


&НаКлиенте
Процедура НастройкиКолонокПоУмолчанию(Команда)
	
	Колонки = НастройкиКолонокПоУмолчанию;
	
КонецПроцедуры

&НаКлиенте
Процедура Сохранить(Команда)
	
	НастройкиСписка = Новый Структура;  
	НастройкиСписка.Вставить("НастройкиКолонок", Колонки);

	Если Элементы.СтраницаСтатусы.Видимость Тогда
		ВыбранныеСтатусы = ПолучитьВыбранные(Статусы);
		НастройкиСписка.Вставить("ВыбранныеСтатусы", ВыбранныеСтатусы);
	КонецЕсли;
	
	Закрыть(НастройкиСписка);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура ЗаполнитьНастройкиКолонок(СписокКолонок, Знач СохраненныеНастройки, Знач ТекущиеНастройки)
	
	НастройкиКолонокПоУмолчанию = ТекущиеНастройки.Скопировать();
	
	Если СохраненныеНастройки <> Неопределено Тогда
		Для Каждого Колонка Из СохраненныеНастройки Цикл
			ЭлементСписка = ТекущиеНастройки.НайтиПоЗначению(Колонка.Значение);
			
			Если ЭлементСписка <> Неопределено Тогда
				НоваяКолонка = СписокКолонок.Добавить(ЭлементСписка.Значение, ЭлементСписка.Представление, Колонка.Пометка);
				ТекущиеНастройки.Удалить(ЭлементСписка);
			КонецЕсли;
		КонецЦикла;
		
		Для Каждого Колонка Из ТекущиеНастройки Цикл
			ЗаполнитьЗначенияСвойств(СписокКолонок.Добавить(), Колонка);
		КонецЦикла;
	Иначе
		СписокКолонок = ТекущиеНастройки;
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьСписокСтатусов(ВыбранныеСтатусы)
	
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	СтатусыОбъектов.Ссылка КАК Значение,
	|	ВЫБОР
	|		КОГДА СтатусыОбъектов.Ссылка В (&ВыбранныеСтатусы)
	|			ТОГДА ИСТИНА
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ КАК Пометка,
	|	СтатусыОбъектов.Наименование КАК Представление,
	|	СтатусыОбъектов.ИндексКартинки КАК ИндексКартинки
	|ИЗ
	|	Справочник.СтатусыОбъектов КАК СтатусыОбъектов
	|
	|УПОРЯДОЧИТЬ ПО
	|	СтатусыОбъектов.Порядок";
	
	Запрос = Новый Запрос(ТекстЗапроса);
	Запрос.УстановитьПараметр("ВыбранныеСтатусы", ВыбранныеСтатусы);
	
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		ЗаполнитьЗначенияСвойств(Статусы.Добавить(), Выборка);
	КонецЦикла;
	
КонецПроцедуры

&НаКлиенте
Функция ПолучитьВыбранные(Список, Знач ПолучитьСписок = Ложь)
	
	Если ПолучитьСписок Тогда
		ВыбранныеЗначения = Новый СписокЗначений;
	Иначе
		ВыбранныеЗначения = Новый Массив;
	КонецЕсли; 
	
	Для Каждого Строка Из Список Цикл
		Если ПолучитьСписок Тогда
			ЗаполнитьЗначенияСвойств(ВыбранныеЗначения.Добавить(), Строка);
		ИначеЕсли Строка.Пометка Тогда
			ВыбранныеЗначения.Добавить(Строка.Значение);
		КонецЕсли;
	КонецЦикла; 
	
	Возврат ВыбранныеЗначения;
	
КонецФункции

#КонецОбласти
