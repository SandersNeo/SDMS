///////////////////////////////////////////////////////////////////////////////////////////////////////
// Copyright (c) 2025, ООО ДНС Технологии
// SDMS (Software Development Management System) — это корпоративная система учета разработки и управления проектами 
// Все права защищены. Эта программа и сопроводительные материалы предоставляются 
// в соответствии с условиями лицензии General Public License (GNU GPL v3)
// Текст лицензии доступен по ссылке:
// https://www.gnu.org/licenses/gpl-3.0.html
///////////////////////////////////////////////////////////////////////////////////////////////////////

#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.Свойство("Спринт") Тогда
		Спринт = Параметры.Спринт;
		
		ЗаполнитьДанныеСпринта();	
	Иначе
		Элементы.Спринт.Видимость = Истина;	
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовФормы

&НаКлиенте
Процедура КомандаРазработчиковПриИзменении(Элемент)
	
	СформироватьДиаграммуНаСервере();
	
КонецПроцедуры

&НаКлиенте
Процедура СпринтПриИзменении(Элемент)
	
	Если ЗначениеЗаполнено(Спринт) Тогда
		ЗаполнитьДанныеСпринта();
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура ЗаполнитьДанныеСпринта()
	
	РеквизитыСпринта = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(Спринт, "Номер, ДатаНачала, ДатаОкончания, Филиал, Команда");
	
	ДатаНачалаСпринта = РеквизитыСпринта.ДатаНачала;
	ДатаОкончанияСпринта = КонецДня(РеквизитыСпринта.ДатаОкончания);
	Филиал = РеквизитыСпринта.Филиал;
	
	КоличествоДней = (РеквизитыСпринта.ДатаОкончания - РеквизитыСпринта.ДатаНачала) / 86400;
	
	// Установка заголовка формы
	Заголовок = СтрШаблон("График выполнения задач спринта #%1 (%2 - %3)", РеквизитыСпринта.Номер, 
		Формат(ДатаНачалаСпринта, "ДФ=dd.MM.yyyy"), Формат(ДатаОкончанияСпринта, "ДФ=dd.MM.yyyy"));
				
	Если ЗначениеЗаполнено(РеквизитыСпринта.Команда) Тогда
		Элементы.КомандаРазработчиков.Видимость = Истина;
		Элементы.КомандаРазработчиков.ТолькоПросмотр = Истина;
		Элементы.КомандаРазработчиков.РежимВыбораИзСписка = Ложь;
		Элементы.КомандаРазработчиков.КнопкаВыпадающегоСписка = Ложь; 
		
		КомандаРазработчиков = РеквизитыСпринта.Команда;
	Иначе
		СписокКоманд = Справочники.Филиалы.ПолучитьКомандыРазработки(Филиал);
		
		Если СписокКоманд.Количество() = 0 Тогда
			Элементы.КомандаРазработчиков.Видимость = Ложь;
		Иначе			
			Элементы.КомандаРазработчиков.Видимость = Истина;
			Элементы.КомандаРазработчиков.ТолькоПросмотр = Ложь;
			Элементы.КомандаРазработчиков.РежимВыбораИзСписка = Истина;
			Элементы.КомандаРазработчиков.КнопкаВыпадающегоСписка = Истина;

			СписокВыбораКоманд = Элементы.КомандаРазработчиков.СписокВыбора;
			СписокВыбораКоманд.Добавить(Справочники.Филиалы.ПустаяСсылка(), "Без команды");
			
			Для Каждого ЭлементКоманда Из СписокКоманд Цикл
				СписокВыбораКоманд.Добавить(ЭлементКоманда.Значение, ЭлементКоманда.Представление);
			КонецЦикла;
		КонецЕсли;
	КонецЕсли; 
	
	СформироватьДиаграммуНаСервере();
		
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьТаблицу(Знач Дерево, Знач ИмяРеквизита, Знач Коэффициент)
	
	ЭтотОбъект[ИмяРеквизита].Очистить();
	
	Для Каждого Строка Из Дерево.Строки Цикл
		НоваяСтрока = ЭтотОбъект[ИмяРеквизита].Добавить();
		НоваяСтрока.Объект = Строка.Объект;
		
		Счетчик = 0;
		Для Каждого Подстрока Из Строка.Строки Цикл
			НоваяСтрока["День" + Счетчик + ИмяРеквизита] = Подстрока.Время;
			
			Если ИмяРеквизита = "План" Тогда
				НоваяСтрока["День" + Счетчик + "Расчет"] = Окр(Подстрока.Время * Коэффициент, 2);
			КонецЕсли;
			
			Счетчик = Счетчик + 1;
		КонецЦикла;
	КонецЦикла;
	
КонецПроцедуры

&НаСервере
Процедура СобратьТаблицу(Знач Дерево, Знач ИмяРеквизита, Знач Коэффициент = 0)
	
	Таблица = РеквизитФормыВЗначение(ИмяРеквизита);
	МассивРеквизитов = Новый Массив;
	
	Для Каждого Колонка Из Таблица.Колонки Цикл
		МассивРеквизитов.Добавить(ИмяРеквизита + "." + Колонка.Имя);
	КонецЦикла;
	
	ИзменитьРеквизиты(, МассивРеквизитов);
	
	ЭлементТаблица = Элементы.Найти(ИмяРеквизита);
	
	Если ЭлементТаблица <> Неопределено Тогда
		Элементы.Удалить(ЭлементТаблица);
	КонецЕсли;
	
	ЭлементТаблица = Элементы.Добавить(ИмяРеквизита, Тип("ТаблицаФормы"), Элементы.Найти("Группа" + ИмяРеквизита));
	ЭлементТаблица.ПутьКДанным = ИмяРеквизита;
	ЭлементТаблица.Отображение = ОтображениеТаблицы.Список;
	ЭлементТаблица.ИзменятьПорядокСтрок = Ложь;
	ЭлементТаблица.ИзменятьСоставСтрок = Ложь;
	ЭлементТаблица.ПоложениеКоманднойПанели = ПоложениеКоманднойПанелиЭлементаФормы.Нет;
	
	МассивРеквизитов.Очистить();
	
	Если ИмяРеквизита = "План" Тогда
		НоваяКолонка = Новый РеквизитФормы("Объект", Новый ОписаниеТипов("СправочникСсылка.Пользователи"), ИмяРеквизита, "Пользователь");
	Иначе
		НоваяКолонка = Новый РеквизитФормы("Объект", Новый ОписаниеТипов("ДокументСсылка.Задача"), ИмяРеквизита, "Задача");
		МассивРеквизитов.Добавить(НоваяКолонка);
		НоваяКолонка = Новый РеквизитФормы("ОбъектОценка", Новый ОписаниеТипов("Число"), ИмяРеквизита, "Оценка");
	КонецЕсли;
	
	МассивРеквизитов.Добавить(НоваяКолонка);
	ИзменитьРеквизиты(МассивРеквизитов);
	МассивРеквизитов.Очистить();
	
	НовыйЭлементФормы = Элементы.Добавить(ИмяРеквизита + "Объект", Тип("ПолеФормы"), ЭлементТаблица);
	НовыйЭлементФормы.Вид = ВидПоляФормы.ПолеНадписи;
	НовыйЭлементФормы.ПутьКДанным = ИмяРеквизита + ".Объект";
	НовыйЭлементФормы.Ширина = 20;
	
	Если ИмяРеквизита = "Факт" Тогда
		НовыйЭлементФормы = Элементы.Добавить(ИмяРеквизита + "ОбъектОценка", Тип("ПолеФормы"), ЭлементТаблица);
		НовыйЭлементФормы.Вид = ВидПоляФормы.ПолеНадписи;
		НовыйЭлементФормы.ПутьКДанным = ИмяРеквизита + ".ОбъектОценка";
		НовыйЭлементФормы.Ширина = 7;
	КонецЕсли;
	
	Для Счетчик = 0 По КоличествоДней Цикл
		НоваяКолонка = Новый РеквизитФормы("День" + Счетчик + ИмяРеквизита, Новый ОписаниеТипов("Число"), ИмяРеквизита);
		МассивРеквизитов.Добавить(НоваяКолонка);
		
		Если ИмяРеквизита = "План" Тогда
			НоваяКолонка = Новый РеквизитФормы("День" + Счетчик + "Расчет", Новый ОписаниеТипов("Число"), ИмяРеквизита);
			МассивРеквизитов.Добавить(НоваяКолонка);
		КонецЕсли;
		
		ИзменитьРеквизиты(МассивРеквизитов);
		
		НоваяГруппа = Элементы.Добавить(ИмяРеквизита + "День" + Счетчик, Тип("ГруппаФормы"), ЭлементТаблица);
		НоваяГруппа.Вид = ВидГруппыФормы.ГруппаКолонок;
		НоваяГруппа.Группировка = ГруппировкаКолонок.Горизонтальная;
		НоваяГруппа.Заголовок = Формат(ДатаНачалаСпринта + Счетчик * 86400, "ДФ=dd.MM");
		НоваяГруппа.ОтображатьВШапке = Истина;
		НоваяГруппа.ГоризонтальноеПоложениеВШапке = ГоризонтальноеПоложениеЭлемента.Центр;
		
		Для Каждого Колонка Из МассивРеквизитов Цикл
			НовыйЭлементФормы = Элементы.Добавить(ИмяРеквизита + Колонка.Имя, Тип("ПолеФормы"), НоваяГруппа);
			НовыйЭлементФормы.ОтображатьВШапке = Ложь;
			НовыйЭлементФормы.Вид = ВидПоляФормы.ПолеНадписи;
			НовыйЭлементФормы.ПутьКДанным = ИмяРеквизита + "." + Колонка.Имя;
			НовыйЭлементФормы.Ширина = 5;
		КонецЦикла;
		
		МассивРеквизитов.Очистить();
	КонецЦикла;
	
	ЗаполнитьТаблицу(Дерево, ИмяРеквизита, Коэффициент);
	
КонецПроцедуры

&НаСервере
Процедура СформироватьДиаграммуНаСервере()
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	#Область ТекстЗапроса
	"ВЫБРАТЬ
	|	ПроизводственныйКалендарь.ДатаКалендаря КАК ДатаКалендаря,
	|	ПроизводственныйКалендарь.КоличествоРабочихЧасов КАК КоличествоРабочихЧасов
	|ПОМЕСТИТЬ ДниКалендаряСпринта
	|ИЗ
	|	РегистрСведений.ПроизводственныйКалендарь КАК ПроизводственныйКалендарь
	|ГДЕ
	|	ПроизводственныйКалендарь.ДатаКалендаря МЕЖДУ &ДатаНачалаСпринта И &ДатаОкончанияСпринта
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Пользователи.Ссылка КАК Пользователь
	|ПОМЕСТИТЬ ПользователиФилиала
	|ИЗ
	|	Справочник.Филиалы КАК Филиалы
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.ЛичныеДела.СрезПоследних(&ТекущаяДата, Событие = ЗНАЧЕНИЕ(Перечисление.СобытияПоЛичнымДелам.ПереведенВДругоеПодразделение)) КАК ЛичныеДелаСрезПоследних
	|		ПО (Филиалы.Ссылка = (ВЫРАЗИТЬ(ЛичныеДелаСрезПоследних.Данные КАК Справочник.Филиалы)))
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.Пользователи КАК Пользователи
	|		ПО (ЛичныеДелаСрезПоследних.Сотрудник = Пользователи.Ссылка)
	|			И (НЕ Пользователи.Недействителен)
	|			И (НЕ Пользователи.ПометкаУдаления)
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ЛичныеДела.СрезПоследних(&ТекущаяДата, Событие = ЗНАЧЕНИЕ(Перечисление.СобытияПоЛичнымДелам.ПереведенВКоманду)) КАК ЛичныеДелаСрезПоследних_Команды
	|		ПО (Пользователи.Ссылка = ЛичныеДелаСрезПоследних_Команды.Сотрудник)
	|			И ((ВЫРАЗИТЬ(ЛичныеДелаСрезПоследних_Команды.Данные КАК Справочник.Филиалы)) = &КомандаРазработчиков)
	|ГДЕ
	|	Филиалы.Ссылка = &Филиал
	|	И ВЫБОР
	|			КОГДА &КомандаЗаполнена
	|				ТОГДА ЛичныеДелаСрезПоследних_Команды.Данные ЕСТЬ НЕ NULL 
	|			ИНАЧЕ ИСТИНА
	|		КОНЕЦ
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ПользователиФилиала.Пользователь КАК Пользователь,
	|	ДниКалендаряСпринта.ДатаКалендаря КАК ДатаКалендаря,
	|	ДниКалендаряСпринта.КоличествоРабочихЧасов КАК КоличествоРабочихЧасов
	|ПОМЕСТИТЬ ПользователиСДнямиКалендаря
	|ИЗ
	|	ДниКалендаряСпринта КАК ДниКалендаряСпринта,
	|	ПользователиФилиала КАК ПользователиФилиала
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТабельУчетаРабочегоВремени.Дата КАК Дата,
	|	ТабельУчетаРабочегоВремени.Сотрудник КАК Сотрудник,
	|	МАКСИМУМ(ТабельУчетаРабочегоВремени.ПриоритетДляТабеля) КАК ПриоритетДляТабеля
	|ПОМЕСТИТЬ АктуальныеЗаписиТабеля
	|ИЗ
	|	РегистрСведений.ТабельУчетаРабочегоВремени КАК ТабельУчетаРабочегоВремени
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ПользователиСДнямиКалендаря КАК ПользователиСДнямиКалендаря
	|		ПО ТабельУчетаРабочегоВремени.Дата = ПользователиСДнямиКалендаря.ДатаКалендаря
	|			И ТабельУчетаРабочегоВремени.Сотрудник = ПользователиСДнямиКалендаря.Пользователь
	|
	|СГРУППИРОВАТЬ ПО
	|	ТабельУчетаРабочегоВремени.Дата,
	|	ТабельУчетаРабочегоВремени.Сотрудник
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТабельУчетаРабочегоВремени.Дата КАК Дата,
	|	ТабельУчетаРабочегоВремени.Сотрудник КАК Сотрудник,
	|	ТабельУчетаРабочегоВремени.ИдентификаторДокумента КАК ИдентификаторДокумента,
	|	ТабельУчетаРабочегоВремени.ОтработаноДней КАК ОтработаноДней,
	|	ТабельУчетаРабочегоВремени.ОтработаноЧасов КАК ОтработаноЧасов,
	|	ТабельУчетаРабочегоВремени.ОтработаноЧасовНочь КАК ОтработаноЧасовНочь,
	|	ТабельУчетаРабочегоВремени.ВидВремени КАК ВидВремени,
	|	ТабельУчетаРабочегоВремени.ДатаДобавления КАК ДатаДобавления,
	|	ТабельУчетаРабочегоВремени.ПриоритетДляТабеля КАК ПриоритетДляТабеля
	|ПОМЕСТИТЬ ТабельУчетаРабочегоВремени
	|ИЗ
	|	РегистрСведений.ТабельУчетаРабочегоВремени КАК ТабельУчетаРабочегоВремени
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ АктуальныеЗаписиТабеля КАК АктуальныеЗаписиТабеля
	|		ПО ТабельУчетаРабочегоВремени.Дата = АктуальныеЗаписиТабеля.Дата
	|			И ТабельУчетаРабочегоВремени.Сотрудник = АктуальныеЗаписиТабеля.Сотрудник
	|			И ТабельУчетаРабочегоВремени.ПриоритетДляТабеля = АктуальныеЗаписиТабеля.ПриоритетДляТабеля
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ПользователиСДнямиКалендаря.ДатаКалендаря КАК ДатаКалендаря,
	|	ВЫБОР
	|		КОГДА НЕ ОтсутствияСотрудниковНаРабочемМесте.Сотрудник ЕСТЬ NULL
	|			ТОГДА 0
	|		КОГДА НЕ ТабельУчетаРабочегоВремени.Сотрудник ЕСТЬ NULL
	|			ТОГДА ВЫБОР
	|					КОГДА КлассификаторИспользованияРабочегоВремени.РабочееВремя
	|						ТОГДА ТабельУчетаРабочегоВремени.ОтработаноЧасов
	|					ИНАЧЕ 0
	|				КОНЕЦ
	|		ИНАЧЕ ПользователиСДнямиКалендаря.КоличествоРабочихЧасов
	|	КОНЕЦ КАК Время,
	|	ПользователиСДнямиКалендаря.Пользователь КАК Пользователь
	|ПОМЕСТИТЬ ПланируемоеВремяПоСотрудникамДатам
	|ИЗ
	|	ПользователиСДнямиКалендаря КАК ПользователиСДнямиКалендаря
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ОтсутствияСотрудниковНаРабочемМесте КАК ОтсутствияСотрудниковНаРабочемМесте
	|		ПО ПользователиСДнямиКалендаря.ДатаКалендаря = ОтсутствияСотрудниковНаРабочемМесте.ДатаОтсутствия
	|			И (ОтсутствияСотрудниковНаРабочемМесте.Филиал = &Филиал)
	|			И ПользователиСДнямиКалендаря.Пользователь = ОтсутствияСотрудниковНаРабочемМесте.Сотрудник
	|		ЛЕВОЕ СОЕДИНЕНИЕ ТабельУчетаРабочегоВремени КАК ТабельУчетаРабочегоВремени
	|		ПО (ТабельУчетаРабочегоВремени.Дата = ПользователиСДнямиКалендаря.ДатаКалендаря)
	|			И (ТабельУчетаРабочегоВремени.Сотрудник = ПользователиСДнямиКалендаря.Пользователь)
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.КлассификаторИспользованияРабочегоВремени КАК КлассификаторИспользованияРабочегоВремени
	|		ПО (ТабельУчетаРабочегоВремени.ВидВремени = КлассификаторИспользованияРабочегоВремени.Ссылка)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ПланируемоеВремяПоСотрудникамДатам.ДатаКалендаря КАК ДатаКалендаря,
	|	СУММА(ПланируемоеВремяПоСотрудникамДатам.Время) КАК Время
	|ПОМЕСТИТЬ ПланируемоеВремяПоДатам
	|ИЗ
	|	ПланируемоеВремяПоСотрудникамДатам КАК ПланируемоеВремяПоСотрудникамДатам
	|
	|СГРУППИРОВАТЬ ПО
	|	ПланируемоеВремяПоСотрудникамДатам.ДатаКалендаря
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ДокументЗадача.Ссылка КАК Задача
	|ПОМЕСТИТЬ ОтобранныеЗадачи
	|ИЗ
	|	РегистрСведений.СоставСпринтов КАК СоставСпринтов
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.Задача КАК ДокументЗадача
	|		ПО СоставСпринтов.Объект = ДокументЗадача.Ссылка
	|			И (ВЫБОР
	|				КОГДА &КомандаЗаполнена
	|					ТОГДА ДокументЗадача.КомандаРазработчиков = &КомандаРазработчиков
	|				ИНАЧЕ ИСТИНА
	|			КОНЕЦ)
	|ГДЕ
	|	СоставСпринтов.Спринт = &Спринт
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	СвойстваЗадачСрезПоследних.Объект КАК ЗадачаСсылка,
	|	ЕСТЬNULL(ПланируемыеТрудозатраты.Трудозатраты, 0) КАК ПланируемыеТрудозатраты
	|ПОМЕСТИТЬ ЗадачиСпринта
	|ИЗ
	|	РегистрСведений.СвойстваЗадач.СрезПоследних(
	|			&ДатаОкончанияСпринта,
	|			Объект В
	|				(ВЫБРАТЬ
	|					ОтобранныеЗадачи.Задача
	|				ИЗ
	|					ОтобранныеЗадачи)) КАК СвойстваЗадачСрезПоследних
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ПланируемыеТрудозатраты КАК ПланируемыеТрудозатраты
	|		ПО СвойстваЗадачСрезПоследних.Объект = ПланируемыеТрудозатраты.Объект
	|			И (ПланируемыеТрудозатраты.ТипТрудозатрат = ЗНАЧЕНИЕ(Перечисление.ТипыТрудозатрат.Разработка))
	|ГДЕ
	|	СвойстваЗадачСрезПоследних.Статус <> ЗНАЧЕНИЕ(Справочник.СтатусыОбъектов.Отклонен)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	НАЧАЛОПЕРИОДА(ЕСТЬNULL(Трудозатраты.Период, ДАТАВРЕМЯ(1, 1, 1)), ДЕНЬ) КАК Период,
	|	ЗадачиСпринта.ЗадачаСсылка КАК Задача,
	|	СУММА(Трудозатраты.Затрата) КАК Затрата,
	|	ЗадачиСпринта.ПланируемыеТрудозатраты КАК ПланируемыеТрудозатраты
	|ПОМЕСТИТЬ ФактическиеТрудозатратыЗадачПоДатам
	|ИЗ
	|	ЗадачиСпринта КАК ЗадачиСпринта
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.Трудозатраты КАК Трудозатраты
	|		ПО (Трудозатраты.Период МЕЖДУ &ДатаНачалаСпринта И &ДатаОкончанияСпринта)
	|			И ЗадачиСпринта.ЗадачаСсылка = Трудозатраты.Объект
	|			И (Трудозатраты.Пользователь В
	|				(ВЫБРАТЬ
	|					ПользователиФилиала.Пользователь
	|				ИЗ
	|					ПользователиФилиала КАК ПользователиФилиала))
	|
	|СГРУППИРОВАТЬ ПО
	|	НАЧАЛОПЕРИОДА(ЕСТЬNULL(Трудозатраты.Период, ДАТАВРЕМЯ(1, 1, 1)), ДЕНЬ),
	|	ЗадачиСпринта.ЗадачаСсылка,
	|	ЗадачиСпринта.ПланируемыеТрудозатраты
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ДниКалендаряСпринта.ДатаКалендаря КАК ДатаКалендаря,
	|	ФактическиеТрудозатратыЗадачПоДатам.Задача КАК Задача,
	|	ВЫБОР
	|		КОГДА ФактическиеТрудозатратыЗадачПоДатам.ПланируемыеТрудозатраты = 0
	|			ТОГДА 0
	|		ИНАЧЕ СУММА(ФактическиеТрудозатратыЗадачПоДатам.Затрата) / ФактическиеТрудозатратыЗадачПоДатам.ПланируемыеТрудозатраты
	|	КОНЕЦ КАК ПроцентПоФакту,
	|	ФактическиеТрудозатратыЗадачПоДатам.ПланируемыеТрудозатраты КАК ПланируемыеТрудозатраты
	|ПОМЕСТИТЬ ОбщиеФактическиеТрудозатратыПоЗадачамПоДатам
	|ИЗ
	|	ДниКалендаряСпринта КАК ДниКалендаряСпринта
	|		ЛЕВОЕ СОЕДИНЕНИЕ ФактическиеТрудозатратыЗадачПоДатам КАК ФактическиеТрудозатратыЗадачПоДатам
	|		ПО (КОНЕЦПЕРИОДА(ДниКалендаряСпринта.ДатаКалендаря, ДЕНЬ) >= ФактическиеТрудозатратыЗадачПоДатам.Период)
	|
	|СГРУППИРОВАТЬ ПО
	|	ДниКалендаряСпринта.ДатаКалендаря,
	|	ФактическиеТрудозатратыЗадачПоДатам.Задача,
	|	ФактическиеТрудозатратыЗадачПоДатам.ПланируемыеТрудозатраты
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ОбщиеФактическиеТрудозатратыПоЗадачамПоДатам.ДатаКалендаря КАК ДатаКалендаря,
	|	ОбщиеФактическиеТрудозатратыПоЗадачамПоДатам.Задача КАК Задача,
	|	ВЫБОР
	|		КОГДА ОбщиеФактическиеТрудозатратыПоЗадачамПоДатам.ПроцентПоФакту > 0.9
	|			ТОГДА 0.9
	|		ИНАЧЕ ОбщиеФактическиеТрудозатратыПоЗадачамПоДатам.ПроцентПоФакту
	|	КОНЕЦ КАК ПроцентПоФакту,
	|	МАКСИМУМ(СвойстваЗадач.ПроцентВыполнения) / 100 КАК ПроцентВыполнения,
	|	МАКСИМУМ(ВЫБОР
	|			КОГДА ЕСТЬNULL(СвойстваЗадач.Статус, ЗНАЧЕНИЕ(Справочник.СтатусыОбъектов.ПустаяСсылка)) В (ЗНАЧЕНИЕ(Справочник.СтатусыОбъектов.Решен), ЗНАЧЕНИЕ(Справочник.СтатусыОбъектов.Протестирован), ЗНАЧЕНИЕ(Справочник.СтатусыОбъектов.Публикация))
	|				ТОГДА 1
	|			КОГДА ЕСТЬNULL(СвойстваЗадач.ПользовательскийСтатус, ЗНАЧЕНИЕ(Справочник.СтатусыОбъектов.ПустаяСсылка)) В (ЗНАЧЕНИЕ(Справочник.СтатусыОбъектов.Решен), ЗНАЧЕНИЕ(Справочник.СтатусыОбъектов.Протестирован), ЗНАЧЕНИЕ(Справочник.СтатусыОбъектов.Публикация))
	|				ТОГДА 1
	|			ИНАЧЕ 0
	|		КОНЕЦ) КАК ПроцентПоСтатусу,
	|	ОбщиеФактическиеТрудозатратыПоЗадачамПоДатам.ПланируемыеТрудозатраты КАК ПланируемыеТрудозатраты
	|ПОМЕСТИТЬ ПроцентыВыполненияЗадачПоДатам
	|ИЗ
	|	ОбщиеФактическиеТрудозатратыПоЗадачамПоДатам КАК ОбщиеФактическиеТрудозатратыПоЗадачамПоДатам
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.СвойстваЗадач КАК СвойстваЗадач
	|		ПО (КОНЕЦПЕРИОДА(ОбщиеФактическиеТрудозатратыПоЗадачамПоДатам.ДатаКалендаря, ДЕНЬ) >= СвойстваЗадач.Период)
	|			И ОбщиеФактическиеТрудозатратыПоЗадачамПоДатам.Задача = СвойстваЗадач.Объект
	|
	|СГРУППИРОВАТЬ ПО
	|	ОбщиеФактическиеТрудозатратыПоЗадачамПоДатам.ДатаКалендаря,
	|	ОбщиеФактическиеТрудозатратыПоЗадачамПоДатам.Задача,
	|	ОбщиеФактическиеТрудозатратыПоЗадачамПоДатам.ПроцентПоФакту,
	|	ОбщиеФактическиеТрудозатратыПоЗадачамПоДатам.ПланируемыеТрудозатраты
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ПроцентыВыполненияЗадачПоДатам.ДатаКалендаря КАК ДатаКалендаря,
	|	ПроцентыВыполненияЗадачПоДатам.Задача КАК Задача,
	|	ВЫРАЗИТЬ(ВЫБОР
	|			КОГДА ПроцентыВыполненияЗадачПоДатам.ПроцентПоСтатусу = 1
	|				ТОГДА 1
	|			КОГДА ПроцентыВыполненияЗадачПоДатам.ПроцентВыполнения > 0
	|				ТОГДА ПроцентыВыполненияЗадачПоДатам.ПроцентВыполнения
	|			ИНАЧЕ ПроцентыВыполненияЗадачПоДатам.ПроцентПоФакту
	|		КОНЕЦ * ПроцентыВыполненияЗадачПоДатам.ПланируемыеТрудозатраты КАК ЧИСЛО(10, 2)) КАК Факт
	|ПОМЕСТИТЬ ФактическиеТрудозатраты
	|ИЗ
	|	ПроцентыВыполненияЗадачПоДатам КАК ПроцентыВыполненияЗадачПоДатам
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ДниКалендаряСпринта.ДатаКалендаря КАК ДатаКалендаря,
	|	ЗадачиСпринта.ЗадачаСсылка КАК Задача,
	|	МАКСИМУМ(ЕСТЬNULL(ФактическиеТрудозатраты.Факт, 0)) КАК Факт
	|ПОМЕСТИТЬ КалендарьЗадачаФакт
	|ИЗ
	|	ДниКалендаряСпринта КАК ДниКалендаряСпринта
	|		ПОЛНОЕ СОЕДИНЕНИЕ ЗадачиСпринта КАК ЗадачиСпринта
	|		ПО (ИСТИНА)
	|		ЛЕВОЕ СОЕДИНЕНИЕ ФактическиеТрудозатраты КАК ФактическиеТрудозатраты
	|		ПО (ЗадачиСпринта.ЗадачаСсылка = ФактическиеТрудозатраты.Задача)
	|			И ДниКалендаряСпринта.ДатаКалендаря >= ФактическиеТрудозатраты.ДатаКалендаря
	|
	|СГРУППИРОВАТЬ ПО
	|	ДниКалендаряСпринта.ДатаКалендаря,
	|	ЗадачиСпринта.ЗадачаСсылка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	КалендарьЗадачаФакт.ДатаКалендаря КАК ДатаКалендаря,
	|	СУММА(КалендарьЗадачаФакт.Факт) КАК Факт
	|ПОМЕСТИТЬ ФактическиеТрудозатратыПоДатам
	|ИЗ
	|	КалендарьЗадачаФакт КАК КалендарьЗадачаФакт
	|
	|СГРУППИРОВАТЬ ПО
	|	КалендарьЗадачаФакт.ДатаКалендаря
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ФактическиеТрудозатратыПоДатам.ДатаКалендаря КАК Дата,
	|	СУММА(ПланируемоеВремяПоДатам.Время) КАК План,
	|	ФактическиеТрудозатратыПоДатам.Факт КАК Факт
	|ИЗ
	|	ПланируемоеВремяПоДатам КАК ПланируемоеВремяПоДатам
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ФактическиеТрудозатратыПоДатам КАК ФактическиеТрудозатратыПоДатам
	|		ПО ПланируемоеВремяПоДатам.ДатаКалендаря <= ФактическиеТрудозатратыПоДатам.ДатаКалендаря
	|
	|СГРУППИРОВАТЬ ПО
	|	ФактическиеТрудозатратыПоДатам.ДатаКалендаря,
	|	ФактическиеТрудозатратыПоДатам.Факт
	|
	|УПОРЯДОЧИТЬ ПО
	|	Дата
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	КалендарьЗадачаФакт.ДатаКалендаря КАК ДатаКалендаря,
	|	КалендарьЗадачаФакт.Задача КАК Объект,
	|	КалендарьЗадачаФакт.Факт КАК Время
	|ИЗ
	|	КалендарьЗадачаФакт КАК КалендарьЗадачаФакт
	|
	|УПОРЯДОЧИТЬ ПО
	|	Объект,
	|	ДатаКалендаря
	|ИТОГИ ПО
	|	Объект
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ПланируемоеВремяПоСотрудникамДатам.Пользователь КАК Объект,
	|	ПланируемоеВремяПоСотрудникамДатам.ДатаКалендаря КАК ДатаКалендаря,
	|	ПланируемоеВремяПоСотрудникамДатам.Время КАК Время
	|ИЗ
	|	ПланируемоеВремяПоСотрудникамДатам КАК ПланируемоеВремяПоСотрудникамДатам
	|
	|УПОРЯДОЧИТЬ ПО
	|	Объект,
	|	ДатаКалендаря
	|ИТОГИ ПО
	|	Объект
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ЗадачиСпринта.ПланируемыеТрудозатраты КАК ПланируемыеТрудозатраты,
	|	ЗадачиСпринта.ЗадачаСсылка КАК ЗадачаСсылка
	|ИЗ
	|	ЗадачиСпринта КАК ЗадачиСпринта
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	СУММА(ПланируемоеВремяПоДатам.Время) КАК Время
	|ИЗ
	|	ПланируемоеВремяПоДатам КАК ПланируемоеВремяПоДатам";
	#КонецОбласти	

	Запрос.УстановитьПараметр("Спринт", Спринт);
	Запрос.УстановитьПараметр("ДатаНачалаСпринта", Спринт.ДатаНачала);
	Запрос.УстановитьПараметр("ДатаОкончанияСпринта", КонецДня(Спринт.ДатаОкончания));
	Запрос.УстановитьПараметр("ТекущаяДата", ТекущаяДатаСеанса());
	Запрос.УстановитьПараметр("Филиал", Филиал);
	Запрос.УстановитьПараметр("КомандаЗаполнена", ЗначениеЗаполнено(КомандаРазработчиков));
	Запрос.УстановитьПараметр("КомандаРазработчиков", КомандаРазработчиков);
	
	РезультатЗапроса = Запрос.ВыполнитьПакет();
	
	КоличествоПакетов = РезультатЗапроса.Количество();
	
	ПакетГрафик = РезультатЗапроса[КоличествоПакетов - 5];
	Если ПакетГрафик.Пустой() Тогда
		Возврат;
	КонецЕсли;
	
	Выборка = ПакетГрафик.Выбрать();
	ВыгрузкаФакт = РезультатЗапроса[КоличествоПакетов - 4].Выгрузить(ОбходРезультатаЗапроса.ПоГруппировкам);
	ВыгрузкаПлан = РезультатЗапроса[КоличествоПакетов - 3].Выгрузить(ОбходРезультатаЗапроса.ПоГруппировкам);
	
	ВыгрузкаЗадач = РезультатЗапроса[КоличествоПакетов - 2].Выгрузить();
	ПланЗадачи = ВыгрузкаЗадач.Итог("ПланируемыеТрудозатраты");
	
	ВыборкаВремени = РезультатЗапроса[КоличествоПакетов - 1].Выбрать();
	ВыборкаВремени.Следующий();
	ПланСотрудники = ВыборкаВремени.Время;
	
	Если ПланСотрудники > 0 Тогда
		Коэффициент = ПланЗадачи / ПланСотрудники;
	Иначе
		Коэффициент = 0;
	КонецЕсли;
	
	ГрафикСгоранияЗадач.Очистить();
	
	СерияПлановоеВремя = ГрафикСгоранияЗадач.УстановитьСерию("План");
	СерияПлановоеВремя.Маркер = ТипМаркераДиаграммы.Нет;
	
	СерияФактическоеВремя = ГрафикСгоранияЗадач.УстановитьСерию("Факт");
	СерияФактическоеВремя.Маркер = ТипМаркераДиаграммы.Ромб;
	
	Неточность = 0.01;
	РазрядностьОкругления = 2;
	
	Пока Выборка.Следующий() Цикл
		Точка = ГрафикСгоранияЗадач.УстановитьТочку(Выборка.Дата);
		Точка.Текст = Формат(Выборка.Дата, "ДФ=dd.MM.yy");
		
		ЗначениеПлана = Выборка.План * Коэффициент + Неточность;
		ПодсказкаПлана = Окр(Выборка.План * Коэффициент, РазрядностьОкругления);
		ГрафикСгоранияЗадач.УстановитьЗначение(Точка, СерияПлановоеВремя, ЗначениеПлана, , ПодсказкаПлана);
		
		ЗначениеФакта = Выборка.Факт + Неточность;
		ПодсказкаФакта = Выборка.Факт;
		ГрафикСгоранияЗадач.УстановитьЗначение(Точка, СерияФактическоеВремя, ЗначениеФакта, , ПодсказкаФакта);
	КонецЦикла;
	
	СобратьТаблицу(ВыгрузкаФакт, "Факт");
	
	Для Каждого Задача Из ВыгрузкаЗадач Цикл
		Строка = Факт.НайтиСтроки(Новый Структура("Объект", Задача.ЗадачаСсылка))[0];
		Строка.ОбъектОценка = Задача.ПланируемыеТрудозатраты;
	КонецЦикла;
	
	СобратьТаблицу(ВыгрузкаПлан, "План", Коэффициент);
		
КонецПроцедуры

#КонецОбласти
