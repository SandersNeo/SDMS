///////////////////////////////////////////////////////////////////////////////////////////////////////
// Copyright (c) 2025, ООО ДНС Технологии
// SDMS (Software Development Management System) — это корпоративная система учета разработки и управления проектами 
// Все права защищены. Эта программа и сопроводительные материалы предоставляются 
// в соответствии с условиями лицензии General Public License (GNU GPL v3)
// Текст лицензии доступен по ссылке:
// https://www.gnu.org/licenses/gpl-3.0.html
///////////////////////////////////////////////////////////////////////////////////////////////////////

#Область ПрограммныйИнтерфейс

Функция ПолучитьТаблицуОтсутствий(Знач ДатаНачала) Экспорт
			
	ТаблицаОтсутствий = Новый ТаблицаЗначений;
	ТаблицаОтсутствий.Колонки.Добавить("Пользователь", Новый ОписаниеТипов("СправочникСсылка.Пользователи"));
	ТаблицаОтсутствий.Колонки.Добавить("ДатаКалендаря", Новый ОписаниеТипов("Дата"));
	ТаблицаОтсутствий.Колонки.Добавить("ВидВремениОтсутствие", Новый ОписаниеТипов("СправочникСсылка.КлассификаторИспользованияРабочегоВремени"));
	ТаблицаОтсутствий.Колонки.Добавить("ОтработаноЧасов", Новый ОписаниеТипов("Число"));
	ТаблицаОтсутствий.Колонки.Добавить("ПриоритетОтсутствия", Новый ОписаниеТипов("Число"));
	ТаблицаОтсутствий.Колонки.Добавить("БуквенныйКодВидВремени", Новый ОписаниеТипов("Строка"));
	
	// Все даты вместе и отсортирует по приориетам
	// 1 - табель
	// 2 - документ отсутствия
	// 3 - плановые отпуска (только будущие после текущей даты, т.к. до текущей даты они уже в табеле как фактические)  
	// 4 - коммандировки
	Запрос = Новый Запрос;
	Запрос.Текст =
	#Область ТекстЗапроса
	"ВЫБРАТЬ
	|	ПроизводственныйКалендарь.ДатаКалендаря КАК ДатаКалендаря,
	|	ПроизводственныйКалендарь.ВидДня КАК ВидДня
	|ПОМЕСТИТЬ КалендарьБудущихДат
	|ИЗ
	|	РегистрСведений.ПроизводственныйКалендарь КАК ПроизводственныйКалендарь
	|ГДЕ
	|	ПроизводственныйКалендарь.ДатаКалендаря МЕЖДУ &ТекущаяДата И &ДатаОкончания
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ГрафикОтпусков.Сотрудник КАК Сотрудник,
	|	ГрафикОтпусков.Период КАК ДатаНачала,
	|	ГрафикОтпусков.ДатаОкончания КАК ДатаОкончания
	|ПОМЕСТИТЬ ГрафикОтпусков
	|ИЗ
	|	РегистрСведений.ГрафикОтпусков КАК ГрафикОтпусков
	|ГДЕ
	|	ГрафикОтпусков.Период <= &ДатаОкончания
	|	И ГрафикОтпусков.ДатаОкончания >= &ДатаНачала
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ОтсутствияНаРабочемМесте.ДатаОтсутствия КАК Дата,
	|	ОтсутствияНаРабочемМесте.Сотрудник КАК Сотрудник,
	|	ЗНАЧЕНИЕ(Справочник.КлассификаторИспользованияРабочегоВремени.ПустаяСсылка) КАК ВидВремени,
	|	0 КАК ОтработаноЧасов,
	|	""Б/О"" КАК БуквенныйКод
	|ПОМЕСТИТЬ ОтсутствияНаРабочемМесте
	|ИЗ
	|	Документ.ОтсутствияНаРабочемМесте.ДатыОтсутствияСотрудников КАК ОтсутствияНаРабочемМесте
	|ГДЕ
	|	ОтсутствияНаРабочемМесте.ДатаОтсутствия МЕЖДУ &ДатаНачала И &ДатаОкончания
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТабельУчетаРабочегоВремени.Дата КАК Дата,
	|	ТабельУчетаРабочегоВремени.Сотрудник КАК Сотрудник,
	|	МАКСИМУМ(ТабельУчетаРабочегоВремени.ПриоритетДляТабеля) КАК ПриоритетДляТабеля
	|ПОМЕСТИТЬ ТабельМаксимальныйПриоритет
	|ИЗ
	|	РегистрСведений.ТабельУчетаРабочегоВремени КАК ТабельУчетаРабочегоВремени
	|ГДЕ
	|	ТабельУчетаРабочегоВремени.Дата МЕЖДУ &ДатаНачала И &ДатаОкончания
	|
	|СГРУППИРОВАТЬ ПО
	|	ТабельУчетаРабочегоВремени.Дата,
	|	ТабельУчетаРабочегоВремени.Сотрудник
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТабельУчетаРабочегоВремени.Дата КАК Дата,
	|	ТабельУчетаРабочегоВремени.Сотрудник КАК Сотрудник,
	|	ВЫБОР
	|		КОГДА ТабельУчетаРабочегоВремени.ВидВремени = &Явка
	|				И НЕ ОтсутствияНаРабочемМесте.Сотрудник ЕСТЬ NULL
	|			ТОГДА ОтсутствияНаРабочемМесте.ВидВремени
	|		ИНАЧЕ ТабельУчетаРабочегоВремени.ВидВремени
	|	КОНЕЦ КАК ВидВремени,
	|	ТабельУчетаРабочегоВремени.ОтработаноЧасов КАК ОтработаноЧасов,
	|	ВЫБОР
	|		КОГДА ТабельУчетаРабочегоВремени.ВидВремени = &Явка
	|				И НЕ ОтсутствияНаРабочемМесте.Сотрудник ЕСТЬ NULL
	|			ТОГДА ОтсутствияНаРабочемМесте.БуквенныйКод
	|		ИНАЧЕ КлассификаторИспользованияРабочегоВремени.БуквенныйКод
	|	КОНЕЦ КАК БуквенныйКод,
	|	ВЫБОР
	|		КОГДА ТабельУчетаРабочегоВремени.ВидВремени = &Командировка
	|			ТОГДА 4
	|		ИНАЧЕ 1
	|	КОНЕЦ КАК ПриоритетЗаписи
	|ПОМЕСТИТЬ ПолнаяТаблицаОтсутствияСотрудников
	|ИЗ
	|	ТабельМаксимальныйПриоритет КАК ТабельМаксимальныйПриоритет
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.ТабельУчетаРабочегоВремени КАК ТабельУчетаРабочегоВремени
	|		ПО ТабельМаксимальныйПриоритет.Сотрудник = ТабельУчетаРабочегоВремени.Сотрудник
	|			И ТабельМаксимальныйПриоритет.Дата = ТабельУчетаРабочегоВремени.Дата
	|			И ТабельМаксимальныйПриоритет.ПриоритетДляТабеля = ТабельУчетаРабочегоВремени.ПриоритетДляТабеля
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.КлассификаторИспользованияРабочегоВремени КАК КлассификаторИспользованияРабочегоВремени
	|		ПО (ТабельУчетаРабочегоВремени.ВидВремени = КлассификаторИспользованияРабочегоВремени.Ссылка)
	|		ЛЕВОЕ СОЕДИНЕНИЕ ОтсутствияНаРабочемМесте КАК ОтсутствияНаРабочемМесте
	|		ПО (ОтсутствияНаРабочемМесте.Дата = ТабельУчетаРабочегоВремени.Дата)
	|			И (ОтсутствияНаРабочемМесте.Сотрудник = ТабельУчетаРабочегоВремени.Сотрудник)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ОтсутствияНаРабочемМесте.Дата,
	|	ОтсутствияНаРабочемМесте.Сотрудник,
	|	ОтсутствияНаРабочемМесте.ВидВремени,
	|	ОтсутствияНаРабочемМесте.ОтработаноЧасов,
	|	ОтсутствияНаРабочемМесте.БуквенныйКод,
	|	2
	|ИЗ
	|	ОтсутствияНаРабочемМесте КАК ОтсутствияНаРабочемМесте
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	КалендарьБудущихДат.ДатаКалендаря,
	|	ГрафикОтпусков.Сотрудник,
	|	ЗНАЧЕНИЕ(Справочник.КлассификаторИспользованияРабочегоВремени.ПустаяСсылка),
	|	0,
	|	"""",
	|	3
	|ИЗ
	|	КалендарьБудущихДат КАК КалендарьБудущихДат
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ГрафикОтпусков КАК ГрафикОтпусков
	|		ПО (КалендарьБудущихДат.ДатаКалендаря МЕЖДУ ГрафикОтпусков.ДатаНачала И ГрафикОтпусков.ДатаОкончания)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ПолнаяТаблицаОтсутствияСотрудников.Дата КАК Дата,
	|	ПолнаяТаблицаОтсутствияСотрудников.Сотрудник КАК Сотрудник,
	|	МИНИМУМ(ПолнаяТаблицаОтсутствияСотрудников.ПриоритетЗаписи) КАК ПриоритетЗаписи
	|ПОМЕСТИТЬ ОтобранныеЗаписиПоПриоритетам
	|ИЗ
	|	ПолнаяТаблицаОтсутствияСотрудников КАК ПолнаяТаблицаОтсутствияСотрудников
	|
	|СГРУППИРОВАТЬ ПО
	|	ПолнаяТаблицаОтсутствияСотрудников.Дата,
	|	ПолнаяТаблицаОтсутствияСотрудников.Сотрудник
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ПолнаяТаблицаОтсутствияСотрудников.Дата КАК Дата,
	|	ПолнаяТаблицаОтсутствияСотрудников.Сотрудник КАК Сотрудник,
	|	ПолнаяТаблицаОтсутствияСотрудников.ВидВремени КАК ВидВремени,
	|	ПолнаяТаблицаОтсутствияСотрудников.ОтработаноЧасов КАК ОтработаноЧасов,
	|	ПолнаяТаблицаОтсутствияСотрудников.ПриоритетЗаписи КАК ПриоритетЗаписи,
	|	ПолнаяТаблицаОтсутствияСотрудников.БуквенныйКод КАК БуквенныйКодВидВремени
	|ИЗ
	|	ПолнаяТаблицаОтсутствияСотрудников КАК ПолнаяТаблицаОтсутствияСотрудников
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ОтобранныеЗаписиПоПриоритетам КАК ОтобранныеЗаписиПоПриоритетам
	|		ПО ПолнаяТаблицаОтсутствияСотрудников.Дата = ОтобранныеЗаписиПоПриоритетам.Дата
	|			И ПолнаяТаблицаОтсутствияСотрудников.Сотрудник = ОтобранныеЗаписиПоПриоритетам.Сотрудник
	|			И ПолнаяТаблицаОтсутствияСотрудников.ПриоритетЗаписи = ОтобранныеЗаписиПоПриоритетам.ПриоритетЗаписи";
	#КонецОбласти
	
	Явка = Справочники.КлассификаторИспользованияРабочегоВремени.ПолучитьЭлементПоБуквенномуКоду("Я");
	Командировка = Справочники.КлассификаторИспользованияРабочегоВремени.ПолучитьЭлементПоБуквенномуКоду("К");
	
	Запрос.УстановитьПараметр("Явка", Явка);
	Запрос.УстановитьПараметр("Командировка", Командировка);
	Запрос.УстановитьПараметр("ДатаНачала", ДатаНачала);
	Запрос.УстановитьПараметр("ДатаОкончания", КонецМесяца(ДатаНачала));
	Запрос.УстановитьПараметр("ТекущаяДата", ТекущаяДатаСеанса());
	
	УстановитьПривилегированныйРежим(Истина);
	ВыборкаДатыОтсутствия = Запрос.Выполнить().Выбрать();
	УстановитьПривилегированныйРежим(Ложь);
	
	Пока ВыборкаДатыОтсутствия.Следующий() Цикл
		СтрокаТаблицы = ТаблицаОтсутствий.Добавить();
		СтрокаТаблицы.Пользователь = ВыборкаДатыОтсутствия.Сотрудник;
		СтрокаТаблицы.ДатаКалендаря = ВыборкаДатыОтсутствия.Дата;
		СтрокаТаблицы.ВидВремениОтсутствие = ВыборкаДатыОтсутствия.ВидВремени;
		СтрокаТаблицы.ПриоритетОтсутствия = ВыборкаДатыОтсутствия.ПриоритетЗаписи;
		СтрокаТаблицы.ОтработаноЧасов = ВыборкаДатыОтсутствия.ОтработаноЧасов;
		СтрокаТаблицы.БуквенныйКодВидВремени = ВыборкаДатыОтсутствия.БуквенныйКодВидВремени;
	КонецЦикла;
	
	Возврат ТаблицаОтсутствий;
	
КонецФункции

#КонецОбласти
