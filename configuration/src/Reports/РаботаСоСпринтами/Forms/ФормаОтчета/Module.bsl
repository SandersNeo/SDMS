///////////////////////////////////////////////////////////////////////////////////////////////////////
// Copyright (c) 2025, ООО ДНС Технологии
// SDMS (Software Development Management System) — это корпоративная система учета разработки и управления проектами 
// Все права защищены. Эта программа и сопроводительные материалы предоставляются 
// в соответствии с условиями лицензии General Public License (GNU GPL v3)
// Текст лицензии доступен по ссылке:
// https://www.gnu.org/licenses/gpl-3.0.html
///////////////////////////////////////////////////////////////////////////////////////////////////////

#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ЗагрузитьПользовательскиеНастройки();
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовФормы

&НаКлиенте
Процедура РезультатОбработкаРасшифровки(Элемент, Расшифровка, СтандартнаяОбработка, ДополнительныеПараметры)
	
	Если ТипЗнч(Расшифровка) = Тип("Структура") И Расшифровка.Свойство("Колонка") Тогда
		СтандартнаяОбработка = Ложь;
		Колонка = Расшифровка.Колонка;
		
		Если Колонка = "КоличествоВозвратов" ИЛИ Колонка = "ВозвратВРаботу" Тогда
			ОткрытьФорму("Отчет.МетрикиЭффективностиГруппРазработки.ФормаОбъекта");
			
		ИначеЕсли Колонка = "ДоступноеВремя" Тогда		
			ПараметрыОткрытия = Новый Структура;
			ПараметрыОткрытия.Вставить("Филиал", Расшифровка.Филиал);
			ПараметрыОткрытия.Вставить("Команда", Расшифровка.Команда);
			ПараметрыОткрытия.Вставить("Спринт", Расшифровка.Спринт);
			ПараметрыОткрытия.Вставить("ДатаНачала", Расшифровка.ДатаНачала);
			ПараметрыОткрытия.Вставить("ДатаОкончания", Расшифровка.ДатаОкончания);
			ПараметрыОткрытия.Вставить("НомерСпринта", Расшифровка.НомерСпринта);
			
			ОткрытьФорму("Отчет.ДоступноеВремяСпринта.ФормаОбъекта", ПараметрыОткрытия, 
				ЭтотОбъект, Новый УникальныйИдентификатор);
			
		ИначеЕсли Колонка = "ПлановоеВремя" 
			ИЛИ Колонка = "ФактическоеВремя"
			ИЛИ Колонка = "КоличествоЗаявок" 
			ИЛИ Колонка = "КоличествоЗадач" 
			ИЛИ Колонка = "ЗадачЗакрыто"
			ИЛИ Колонка = "ЗадачЗакрытоВовремя"
			ИЛИ Колонка = "ЗадачВРаботе"
			ИЛИ Колонка = "ЗадачВТестировании" 
			ИЛИ Колонка = "ВозвратовПоЗадачам" Тогда
			ОткрытьФорму("Отчет.РаботаСоСпринтами.Форма.ФормаРасшифровки", Расшифровка, 
				ЭтотОбъект, Новый УникальныйИдентификатор);			
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКоманд

&НаКлиенте
Процедура Сформировать(Команда)
	
	Если ПараметрыЗаполнены() Тогда
		СформироватьНаСервере();
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура ЗагрузитьПользовательскиеНастройки()
	
	Ключи = ПолучитьКлючиНастроек();
	
	СохраненныеНастройки = ОбщегоНазначенияВызовСервера.ЗагрузитьНастройкиДанныхФормы(Ключи.КлючОбъекта, 
		Ключи.КлючНастроек);
	
	Если СохраненныеНастройки <> Неопределено Тогда 
			
		Если СохраненныеНастройки.Свойство("Филиал") Тогда 
			Если ТипЗнч(СохраненныеНастройки.Филиал) = Тип("СписокЗначений") Тогда 
				Филиалы.ЗагрузитьЗначения(СохраненныеНастройки.Филиал.ВыгрузитьЗначения());
			Иначе
				Филиалы.Добавить(СохраненныеНастройки.Филиал);
			КонецЕсли;
		КонецЕсли; 
		
		СохраненныеНастройки.Свойство("ДатаНачала", ДатаНачала);
		СохраненныеНастройки.Свойство("ДатаОкончания", ДатаОкончания);
		
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьПараметрыРасшифровки(Строка, Знач Спринт, Знач Филиал = Неопределено, Знач КомандаРазработчиков = Неопределено)
	
	ДанныеСпринта = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(Спринт, "Номер, ДатаНачала, ДатаОкончания");
		
	КоличествоЗаявок = Новый Структура;
	КоличествоЗаявок.Вставить("Колонка", "КоличествоЗаявок");
	КоличествоЗаявок.Вставить("Спринт", Спринт);
	КоличествоЗаявок.Вставить("Филиал", Филиал);
	КоличествоЗаявок.Вставить("КомандаРазработчиков", КомандаРазработчиков);
	Строка.Параметры.РасшифровкаКоличествоЗаявок = КоличествоЗаявок;
	
	КоличествоЗадач = Новый Структура;
	КоличествоЗадач.Вставить("Колонка", "КоличествоЗадач");
	КоличествоЗадач.Вставить("Спринт", Спринт);
	КоличествоЗадач.Вставить("Филиал", Филиал);
	КоличествоЗадач.Вставить("КомандаРазработчиков", КомандаРазработчиков);
	Строка.Параметры.РасшифровкаКоличествоЗадач = КоличествоЗадач;

	ЗадачЗакрыто = Новый Структура;
	ЗадачЗакрыто.Вставить("Колонка", "ЗадачЗакрыто");
	ЗадачЗакрыто.Вставить("Спринт", Спринт);
	ЗадачЗакрыто.Вставить("Филиал", Филиал);
	ЗадачЗакрыто.Вставить("КомандаРазработчиков", КомандаРазработчиков);
	Строка.Параметры.РасшифровкаЗадачЗакрыто = ЗадачЗакрыто;

	ЗадачЗакрытоВовремя = Новый Структура;
	ЗадачЗакрытоВовремя.Вставить("Колонка", "ЗадачЗакрытоВовремя");
	ЗадачЗакрытоВовремя.Вставить("Спринт", Спринт);
	ЗадачЗакрытоВовремя.Вставить("Филиал", Филиал);
	ЗадачЗакрытоВовремя.Вставить("КомандаРазработчиков", КомандаРазработчиков);
	Строка.Параметры.РасшифровкаЗадачЗакрытоВовремя = ЗадачЗакрытоВовремя;	
	
	ЗадачВРаботе = Новый Структура;
	ЗадачВРаботе.Вставить("Колонка", "ЗадачВРаботе");
	ЗадачВРаботе.Вставить("Спринт", Спринт);
	ЗадачВРаботе.Вставить("Филиал", Филиал);
	ЗадачВРаботе.Вставить("КомандаРазработчиков", КомандаРазработчиков);
	Строка.Параметры.РасшифровкаЗадачВРаботе = ЗадачВРаботе;

	ЗадачВТестировании = Новый Структура;
	ЗадачВТестировании.Вставить("Колонка", "ЗадачВТестировании");
	ЗадачВТестировании.Вставить("Спринт", Спринт);
	ЗадачВТестировании.Вставить("Филиал", Филиал);
	ЗадачВТестировании.Вставить("КомандаРазработчиков", КомандаРазработчиков);
	Строка.Параметры.РасшифровкаЗадачВТестировании = ЗадачВТестировании;
		
	ВозвратыПоЗадачам = Новый Структура;
	ВозвратыПоЗадачам.Вставить("Колонка", "ВозвратовПоЗадачам");
	ВозвратыПоЗадачам.Вставить("Спринт", Спринт);
	ВозвратыПоЗадачам.Вставить("Филиал", Филиал);
	ВозвратыПоЗадачам.Вставить("КомандаРазработчиков", КомандаРазработчиков);
	ВозвратыПоЗадачам.Вставить("ДатаНачала", ДатаНачала);
	ВозвратыПоЗадачам.Вставить("ДатаОкончания", ДатаОкончания);	
	Строка.Параметры.РасшифровкаВозвратовПоЗадачам = ВозвратыПоЗадачам;

	ВозвратВРаботу = Новый Структура;
	ВозвратВРаботу.Вставить("Колонка", "ВозвратВРаботу");
	Строка.Параметры.РасшифровкаВозвратВРаботу = ВозвратВРаботу;
	
	ДоступноеВремя = Новый Структура;
	ДоступноеВремя.Вставить("Колонка", "ДоступноеВремя");
	ДоступноеВремя.Вставить("НомерСпринта", ДанныеСпринта.Номер);
	ДоступноеВремя.Вставить("Филиал", Филиал);
	ДоступноеВремя.Вставить("Команда", КомандаРазработчиков);
	ДоступноеВремя.Вставить("ДатаНачала", ДанныеСпринта.ДатаНачала);
	ДоступноеВремя.Вставить("ДатаОкончания", ДанныеСпринта.ДатаОкончания);	
	ДоступноеВремя.Вставить("Спринт", Спринт);	
	Строка.Параметры.РасшифровкаДоступноеВремя = ДоступноеВремя;
	
	ПлановоеВремя = Новый Структура;
	ПлановоеВремя.Вставить("Колонка", "ПлановоеВремя");
	ПлановоеВремя.Вставить("Филиал", Филиал);
	ПлановоеВремя.Вставить("Спринт", Спринт);
	ПлановоеВремя.Вставить("ДатаНачала", ДатаНачала);
	ПлановоеВремя.Вставить("ДатаОкончания", ДатаОкончания);	
	Строка.Параметры.РасшифровкаПлановоеВремя = ПлановоеВремя;
	
	ФактическоеВремя = Новый Структура;
	ФактическоеВремя.Вставить("Колонка", "ФактическоеВремя");
	ФактическоеВремя.Вставить("Филиал", Филиал);
	ФактическоеВремя.Вставить("Спринт", Спринт);
	ФактическоеВремя.Вставить("ДатаНачала", ДатаНачала);
	ФактическоеВремя.Вставить("ДатаОкончания", ДатаОкончания);	
	Строка.Параметры.РасшифровкаФактическоеВремя = ФактическоеВремя;
			
КонецПроцедуры

&НаКлиенте
Функция ПараметрыЗаполнены()
	
	Ошибка = Ложь;
	
	Если Филиалы.Количество() = 0 Тогда
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю("Филиал не заполнен", , "Филиалы", , Ошибка);
	КонецЕсли;
	
	Если НЕ ЗначениеЗаполнено(ДатаНачала) Тогда
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю("Дата начала не заполнена", , "ДатаНачала", , Ошибка);
	КонецЕсли;
	
	Если НЕ ЗначениеЗаполнено(ДатаОкончания) Тогда
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю("Дата окончания не заполнена", , "ДатаОкончания", , Ошибка);
	КонецЕсли;
	
	Возврат НЕ Ошибка;
	
КонецФункции

&НаСервереБезКонтекста
Функция ПолучитьКлючиНастроек()
	
	КлючиНастроек = Новый Структура("КлючОбъекта, КлючНастроек");
	КлючиНастроек.КлючОбъекта = "Отчет.РаботаСоСпринтами";
	КлючиНастроек.КлючНастроек = "НастройкиОтчета";
	
	Возврат КлючиНастроек;
	
КонецФункции

&НаСервере
Процедура СохранитьПользовательскиеНастройки()
	
	СтруктураНастроек = Новый Структура;
	СтруктураНастроек.Вставить("Филиал", Филиалы);
	СтруктураНастроек.Вставить("ДатаНачала", ДатаНачала);
	СтруктураНастроек.Вставить("ДатаОкончания", ДатаОкончания);
	
	Ключи = ПолучитьКлючиНастроек();
	
	ОбщегоНазначенияВызовСервера.СохранитьНастройкиДанныхФормы(Ключи.КлючОбъекта, Ключи.КлючНастроек, СтруктураНастроек);
	
КонецПроцедуры

&НаСервере
Процедура СформироватьНаСервере()
	
	Результат.Очистить();
	
	Запрос = Новый Запрос;	
	Запрос.Текст =
	#Область ТекстЗапроса
	"ВЫБРАТЬ
	|	Спринт.Ссылка КАК Ссылка,
	|	Спринт.ДатаНачала КАК ДатаНачала,
	|	Спринт.ДатаОкончания КАК ДатаОкончания,
	|	Спринт.Филиал КАК Филиал,
	|	Спринт.Команда КАК Команда
	|ПОМЕСТИТЬ ОтобранныеСпринты
	|ИЗ
	|	Документ.Спринт КАК Спринт
	|ГДЕ
	|	Спринт.Филиал В(&Филиалы)
	|	И Спринт.ДатаНачала >= &ДатаНачала
	|	И Спринт.ДатаОкончания <= &ДатаОкончания
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ДоступноеВремяСпринтов.Спринт КАК Спринт,
	|	СУММА(ДоступноеВремяСпринтов.Время) КАК Время,
	|	ДоступноеВремяСпринтов.Команда КАК Команда,
	|	ОтобранныеСпринты.Филиал КАК Филиал
	|ПОМЕСТИТЬ ДоступноеВремяСпринтов
	|ИЗ
	|	РегистрСведений.ДоступноеВремяСпринтов КАК ДоступноеВремяСпринтов
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ОтобранныеСпринты КАК ОтобранныеСпринты
	|		ПО (ОтобранныеСпринты.Ссылка = ДоступноеВремяСпринтов.Спринт)
	|
	|СГРУППИРОВАТЬ ПО
	|	ДоступноеВремяСпринтов.Спринт,
	|	ДоступноеВремяСпринтов.Команда,
	|	ОтобранныеСпринты.Филиал
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ЛичныеДелаСрезПоследних.Сотрудник КАК Пользователь,
	|	&ДатаНачала КАК Период,
	|	ВЫРАЗИТЬ(ЛичныеДелаСрезПоследних.Данные КАК Справочник.Филиалы) КАК Филиал
	|ПОМЕСТИТЬ ИсторияРаботыПользователейВФилиалах
	|ИЗ
	|	РегистрСведений.ЛичныеДела.СрезПоследних(&ДатаНачала, Событие = ЗНАЧЕНИЕ(Перечисление.СобытияПоЛичнымДелам.ПереведенВДругоеПодразделение)) КАК ЛичныеДелаСрезПоследних
	|ГДЕ
	|	ВЫРАЗИТЬ(ЛичныеДелаСрезПоследних.Данные КАК Справочник.Филиалы) В (&Филиалы)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ЛичныеДела.Сотрудник,
	|	ЛичныеДела.Период,
	|	ВЫРАЗИТЬ(ЛичныеДела.Данные КАК Справочник.Филиалы)
	|ИЗ
	|	РегистрСведений.ЛичныеДела КАК ЛичныеДела
	|ГДЕ
	|	ЛичныеДела.Период МЕЖДУ &ДатаНачала И &ДатаОкончания
	|	И ЛичныеДела.Событие = ЗНАЧЕНИЕ(Перечисление.СобытияПоЛичнымДелам.ПереведенВДругоеПодразделение)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ИсторияРаботыПользователейВФилиалах.Пользователь КАК Пользователь,
	|	ИсторияРаботыПользователейВФилиалах.Период КАК ДатаНачала,
	|	ЕСТЬNULL(МИНИМУМ(ДОБАВИТЬКДАТЕ(ИсторияРаботыПользователейВФилиалах1.Период, ДЕНЬ, -1)), &ДатаОкончания) КАК ДатаОкончания,
	|	ИсторияРаботыПользователейВФилиалах.Филиал КАК Филиал
	|ПОМЕСТИТЬ ПериодыРаботыНаФилиале
	|ИЗ
	|	ИсторияРаботыПользователейВФилиалах КАК ИсторияРаботыПользователейВФилиалах
	|		ЛЕВОЕ СОЕДИНЕНИЕ ИсторияРаботыПользователейВФилиалах КАК ИсторияРаботыПользователейВФилиалах1
	|		ПО ИсторияРаботыПользователейВФилиалах.Пользователь = ИсторияРаботыПользователейВФилиалах1.Пользователь
	|			И ИсторияРаботыПользователейВФилиалах.Период < ИсторияРаботыПользователейВФилиалах1.Период
	|ГДЕ
	|	ИсторияРаботыПользователейВФилиалах.Филиал В(&Филиалы)
	|
	|СГРУППИРОВАТЬ ПО
	|	ИсторияРаботыПользователейВФилиалах.Период,
	|	ИсторияРаботыПользователейВФилиалах.Пользователь,
	|	ИсторияРаботыПользователейВФилиалах.Филиал
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ПериодыРаботыНаФилиале.Пользователь КАК Пользователь,
	|	ПериодыРаботыНаФилиале.Филиал КАК Филиал
	|ПОМЕСТИТЬ СотрудникиФилиала
	|ИЗ
	|	ПериодыРаботыНаФилиале КАК ПериодыРаботыНаФилиале
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	КомандыСрезПоследних.Сотрудник КАК Сотрудник,
	|	ВЫРАЗИТЬ(КомандыСрезПоследних.Данные КАК Справочник.Филиалы) КАК Команда,
	|	КомандыСрезПоследних.Период КАК Период,
	|	ВЫРАЗИТЬ(КомандыСрезПоследних.Данные КАК Справочник.Филиалы).Родитель КАК Филиал
	|ПОМЕСТИТЬ КомандыСотрудников
	|ИЗ
	|	РегистрСведений.ЛичныеДела.СрезПоследних(
	|			&ДатаНачала,
	|			Событие = ЗНАЧЕНИЕ(Перечисление.СобытияПоЛичнымДелам.ПереведенВКоманду)
	|				И Сотрудник В
	|					(ВЫБРАТЬ
	|						СотрудникиФилиала.Пользователь
	|					ИЗ
	|						СотрудникиФилиала)) КАК КомандыСрезПоследних
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	Команды.Сотрудник,
	|	ВЫРАЗИТЬ(Команды.Данные КАК Справочник.Филиалы),
	|	Команды.Период,
	|	СотрудникиФилиала.Филиал
	|ИЗ
	|	РегистрСведений.ЛичныеДела КАК Команды
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ СотрудникиФилиала КАК СотрудникиФилиала
	|		ПО Команды.Сотрудник = СотрудникиФилиала.Пользователь
	|ГДЕ
	|	Команды.Период МЕЖДУ &ДатаНачала И &ДатаОкончания
	|	И Команды.Событие = ЗНАЧЕНИЕ(Перечисление.СобытияПоЛичнымДелам.ПереведенВКоманду)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	КомандыСотрудников.Сотрудник КАК Сотрудник,
	|	КомандыСотрудников.Команда КАК Команда,
	|	КомандыСотрудников.Филиал КАК Филиал,
	|	КомандыСотрудников.Период КАК ДатаНачала,
	|	ЕСТЬNULL(МИНИМУМ(ДОБАВИТЬКДАТЕ(КомандыСотрудников1.Период, ДЕНЬ, -1)), &ДатаОкончания) КАК ДатаОкончания
	|ПОМЕСТИТЬ ПериодыНахожденияВКоманде
	|ИЗ
	|	КомандыСотрудников КАК КомандыСотрудников
	|		ЛЕВОЕ СОЕДИНЕНИЕ КомандыСотрудников КАК КомандыСотрудников1
	|		ПО КомандыСотрудников.Сотрудник = КомандыСотрудников1.Сотрудник
	|			И КомандыСотрудников.Период < КомандыСотрудников1.Период
	|
	|СГРУППИРОВАТЬ ПО
	|	КомандыСотрудников.Сотрудник,
	|	КомандыСотрудников.Команда,
	|	КомандыСотрудников.Филиал,
	|	КомандыСотрудников.Период
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ПроизводственныйКалендарь.ДатаКалендаря КАК ДатаКалендаря,
	|	ПроизводственныйКалендарь.КоличествоРабочихЧасов КАК КоличествоРабочихЧасов,
	|	ПериодыНахожденияВФилиале.Филиал КАК Филиал,
	|	ПериодыНахожденияВФилиале.Пользователь КАК Пользователь,
	|	ЕСТЬNULL(ПериодыНахожденияВКоманде.Команда, ЗНАЧЕНИЕ(Справочник.Филиалы.ПустаяСсылка)) КАК Команда
	|ПОМЕСТИТЬ ДниКалендаряСпринта
	|ИЗ
	|	РегистрСведений.ПроизводственныйКалендарь КАК ПроизводственныйКалендарь
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ПериодыРаботыНаФилиале КАК ПериодыНахожденияВФилиале
	|		ПО (ПроизводственныйКалендарь.ДатаКалендаря МЕЖДУ ПериодыНахожденияВФилиале.ДатаНачала И ПериодыНахожденияВФилиале.ДатаОкончания)
	|		ЛЕВОЕ СОЕДИНЕНИЕ ПериодыНахожденияВКоманде КАК ПериодыНахожденияВКоманде
	|		ПО (ПериодыНахожденияВФилиале.Пользователь = ПериодыНахожденияВКоманде.Сотрудник)
	|			И (ПроизводственныйКалендарь.ДатаКалендаря МЕЖДУ ПериодыНахожденияВКоманде.ДатаНачала И ПериодыНахожденияВКоманде.ДатаОкончания)
	|			И (ПериодыНахожденияВФилиале.Филиал = ПериодыНахожденияВКоманде.Филиал)
	|ГДЕ
	|	ПроизводственныйКалендарь.ДатаКалендаря МЕЖДУ &ДатаНачала И &ДатаОкончания
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТабельУчетаРабочегоВремени.Дата КАК Дата,
	|	ТабельУчетаРабочегоВремени.Сотрудник КАК Сотрудник,
	|	МАКСИМУМ(ТабельУчетаРабочегоВремени.ПриоритетДляТабеля) КАК ПриоритетДляТабеля,
	|	СотрудникиФилиала.Филиал КАК Филиал
	|ПОМЕСТИТЬ АктуальныеЗаписиТабеля
	|ИЗ
	|	РегистрСведений.ТабельУчетаРабочегоВремени КАК ТабельУчетаРабочегоВремени
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ СотрудникиФилиала КАК СотрудникиФилиала
	|		ПО ТабельУчетаРабочегоВремени.Сотрудник = СотрудникиФилиала.Пользователь
	|			И ТабельУчетаРабочегоВремени.Филиал = СотрудникиФилиала.Филиал
	|ГДЕ
	|	ТабельУчетаРабочегоВремени.Дата МЕЖДУ &ДатаНачала И &ДатаОкончания
	|
	|СГРУППИРОВАТЬ ПО
	|	ТабельУчетаРабочегоВремени.Дата,
	|	ТабельУчетаРабочегоВремени.Сотрудник,
	|	СотрудникиФилиала.Филиал
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТабельУчетаРабочегоВремени.Дата КАК Дата,
	|	ТабельУчетаРабочегоВремени.Сотрудник КАК Сотрудник,
	|	ТабельУчетаРабочегоВремени.Филиал КАК Филиал,
	|	ТабельУчетаРабочегоВремени.ИдентификаторДокумента КАК ИдентификаторДокумента,
	|	ТабельУчетаРабочегоВремени.ОтработаноДней КАК ОтработаноДней,
	|	ТабельУчетаРабочегоВремени.ОтработаноЧасов КАК ОтработаноЧасов,
	|	ТабельУчетаРабочегоВремени.ОтработаноЧасовНочь КАК ОтработаноЧасовНочь,
	|	ТабельУчетаРабочегоВремени.ВидВремени КАК ВидВремени,
	|	ТабельУчетаРабочегоВремени.ДатаДобавления КАК ДатаДобавления,
	|	ТабельУчетаРабочегоВремени.ПриоритетДляТабеля КАК ПриоритетДляТабеля
	|ПОМЕСТИТЬ ПриведенныйТабельУчетаРабочегоВремени
	|ИЗ
	|	РегистрСведений.ТабельУчетаРабочегоВремени КАК ТабельУчетаРабочегоВремени
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ АктуальныеЗаписиТабеля КАК АктуальныеЗаписиТабеля
	|		ПО ТабельУчетаРабочегоВремени.Дата = АктуальныеЗаписиТабеля.Дата
	|			И ТабельУчетаРабочегоВремени.Сотрудник = АктуальныеЗаписиТабеля.Сотрудник
	|			И ТабельУчетаРабочегоВремени.ПриоритетДляТабеля = АктуальныеЗаписиТабеля.ПриоритетДляТабеля
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ДниКалендаряСпринта.Команда КАК КомандаРазработчиков,
	|	ДниКалендаряСпринта.Филиал КАК Филиал,
	|	ДниКалендаряСпринта.Пользователь КАК Пользователь,
	|	ДниКалендаряСпринта.ДатаКалендаря КАК ДатаКалендаря,
	|	ВЫБОР
	|		КОГДА ОтсутствияСотрудниковНаРабочемМесте.Сотрудник ЕСТЬ НЕ NULL 
	|			ТОГДА 0
	|		КОГДА ТабельУчетаРабочегоВремени.Сотрудник ЕСТЬ НЕ NULL 
	|			ТОГДА ВЫБОР
	|					КОГДА КлассификаторИспользованияРабочегоВремени.РабочееВремя
	|						ТОГДА ТабельУчетаРабочегоВремени.ОтработаноЧасов
	|					ИНАЧЕ 0
	|				КОНЕЦ
	|		ИНАЧЕ ДниКалендаряСпринта.КоличествоРабочихЧасов
	|	КОНЕЦ КАК ВремяНаРазработку
	|ПОМЕСТИТЬ ПользователиСДнямиКалендаря
	|ИЗ
	|	ДниКалендаряСпринта КАК ДниКалендаряСпринта
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ОтсутствияСотрудниковНаРабочемМесте КАК ОтсутствияСотрудниковНаРабочемМесте
	|		ПО ДниКалендаряСпринта.ДатаКалендаря = ОтсутствияСотрудниковНаРабочемМесте.ДатаОтсутствия
	|			И ДниКалендаряСпринта.Филиал = ОтсутствияСотрудниковНаРабочемМесте.Филиал
	|			И ДниКалендаряСпринта.Пользователь = ОтсутствияСотрудниковНаРабочемМесте.Сотрудник
	|		ЛЕВОЕ СОЕДИНЕНИЕ ПриведенныйТабельУчетаРабочегоВремени КАК ТабельУчетаРабочегоВремени
	|		ПО (ТабельУчетаРабочегоВремени.Дата = ДниКалендаряСпринта.ДатаКалендаря)
	|			И (ТабельУчетаРабочегоВремени.Сотрудник = ДниКалендаряСпринта.Пользователь)
	|			И (ТабельУчетаРабочегоВремени.Филиал = ДниКалендаряСпринта.Филиал)
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.КлассификаторИспользованияРабочегоВремени КАК КлассификаторИспользованияРабочегоВремени
	|		ПО (ТабельУчетаРабочегоВремени.ВидВремени = КлассификаторИспользованияРабочегоВремени.Ссылка)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ОтобранныеСпринты.Ссылка КАК Спринт,
	|	СоставСпринтов.Объект КАК Задача,
	|	ДокументЗадача.КомандаРазработчиков КАК КомандаРазработчиков,
	|	ДокументЗадача.ОбъектОснование КАК ОбъектОснование,
	|	ДокументЗадача.Статус КАК Статус,
	|	СтатусыОбъектов.Порядок КАК ПорядокСтатуса,
	|	ОтобранныеСпринты.ДатаНачала КАК ДатаНачала,
	|	ОтобранныеСпринты.ДатаОкончания КАК ДатаОкончания,
	|	ПланируемыеТрудозатратыПоСпринтам.Трудозатраты КАК ПланируемоеВремя,
	|	ОтобранныеСпринты.Филиал КАК Филиал
	|ПОМЕСТИТЬ ЗадачиСпринтовЗаПериод
	|ИЗ
	|	ОтобранныеСпринты КАК ОтобранныеСпринты
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.СоставСпринтов КАК СоставСпринтов
	|		ПО ОтобранныеСпринты.Ссылка = СоставСпринтов.Спринт
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ПланируемыеТрудозатратыПоСпринтам КАК ПланируемыеТрудозатратыПоСпринтам
	|		ПО ОтобранныеСпринты.Ссылка = ПланируемыеТрудозатратыПоСпринтам.Спринт
	|			И (СоставСпринтов.Объект = ПланируемыеТрудозатратыПоСпринтам.Объект)
	|			И (ПланируемыеТрудозатратыПоСпринтам.ТипТрудозатрат = ЗНАЧЕНИЕ(Перечисление.ТипыТрудозатрат.Разработка))
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.Задача КАК ДокументЗадача
	|		ПО (СоставСпринтов.Объект = ДокументЗадача.Ссылка)
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.СтатусыОбъектов КАК СтатусыОбъектов
	|		ПО (ДокументЗадача.Статус = СтатусыОбъектов.Ссылка)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ЗадачиСпринтовЗаПериод.Спринт КАК Спринт,
	|	ЗадачиСпринтовЗаПериод.КомандаРазработчиков КАК КомандаРазработчиков,
	|	ЗадачиСпринтовЗаПериод.Задача КАК Задача,
	|	Трудозатраты.Пользователь КАК Пользователь,
	|	Трудозатраты.Затрата КАК Затрата,
	|	ЗадачиСпринтовЗаПериод.Филиал КАК Филиал
	|ПОМЕСТИТЬ ФактическиеТрудозатратыФилиала
	|ИЗ
	|	ЗадачиСпринтовЗаПериод КАК ЗадачиСпринтовЗаПериод
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.Трудозатраты КАК Трудозатраты
	|		ПО ЗадачиСпринтовЗаПериод.Задача = Трудозатраты.Объект
	|			И (Трудозатраты.Период МЕЖДУ ЗадачиСпринтовЗаПериод.ДатаНачала И ЗадачиСпринтовЗаПериод.ДатаОкончания)
	|			И (Трудозатраты.ITСтруктура)
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ПериодыРаботыНаФилиале КАК ПериодыРаботыНаФилиале
	|		ПО (Трудозатраты.Пользователь = ПериодыРаботыНаФилиале.Пользователь)
	|			И (Трудозатраты.Период МЕЖДУ ПериодыРаботыНаФилиале.ДатаНачала И ПериодыРаботыНаФилиале.ДатаОкончания)
	|			И ЗадачиСпринтовЗаПериод.Филиал = ПериодыРаботыНаФилиале.Филиал
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ЗадачиСпринтовЗаПериод.Задача КАК Задача,
	|	ЗадачиСпринтовЗаПериод.Спринт КАК Спринт,
	|	ЗадачиСпринтовЗаПериод.КомандаРазработчиков КАК КомандаРазработчиков,
	|	ЗадачиСпринтовЗаПериод.Филиал КАК Филиал,
	|	СУММА(ЗадачиСпринтовЗаПериод.Затрата) КАК Затрата
	|ПОМЕСТИТЬ ФактическиеТрудозатраты
	|ИЗ
	|	(ВЫБРАТЬ
	|		ФактическиеТрудозатратыФилиала.Задача КАК Задача,
	|		ФактическиеТрудозатратыФилиала.Спринт КАК Спринт,
	|		ФактическиеТрудозатратыФилиала.КомандаРазработчиков КАК КомандаРазработчиков,
	|		ФактическиеТрудозатратыФилиала.Филиал КАК Филиал,
	|		ФактическиеТрудозатратыФилиала.Затрата КАК Затрата
	|	ИЗ
	|		ФактическиеТрудозатратыФилиала КАК ФактическиеТрудозатратыФилиала
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		ЗадачиСпринтовЗаПериод.Задача,
	|		ЗадачиСпринтовЗаПериод.Спринт,
	|		ЗадачиСпринтовЗаПериод.КомандаРазработчиков,
	|		ЗадачиСпринтовЗаПериод.Филиал,
	|		Трудозатраты.Затрата
	|	ИЗ
	|		ЗадачиСпринтовЗаПериод КАК ЗадачиСпринтовЗаПериод
	|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.ДоступноеВремяСпринтов КАК ДоступноеВремяСпринтов
	|			ПО (ДоступноеВремяСпринтов.Спринт = ЗадачиСпринтовЗаПериод.Спринт)
	|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.Трудозатраты КАК Трудозатраты
	|			ПО ЗадачиСпринтовЗаПериод.Задача = Трудозатраты.Объект
	|				И (ДоступноеВремяСпринтов.Пользователь = Трудозатраты.Пользователь)
	|				И (Трудозатраты.Период МЕЖДУ ЗадачиСпринтовЗаПериод.ДатаНачала И ЗадачиСпринтовЗаПериод.ДатаОкончания)
	|	ГДЕ
	|		НЕ Трудозатраты.Пользователь В
	|					(ВЫБРАТЬ РАЗЛИЧНЫЕ
	|						ФактическиеТрудозатратыФилиала.Пользователь
	|					ИЗ
	|						ФактическиеТрудозатратыФилиала)) КАК ЗадачиСпринтовЗаПериод
	|
	|СГРУППИРОВАТЬ ПО
	|	ЗадачиСпринтовЗаПериод.Задача,
	|	ЗадачиСпринтовЗаПериод.Спринт,
	|	ЗадачиСпринтовЗаПериод.КомандаРазработчиков,
	|	ЗадачиСпринтовЗаПериод.Филиал
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ЗадачиСпринтовЗаПериод.Задача КАК Задача,
	|	ЗадачиСпринтовЗаПериод.Спринт КАК Спринт,
	|	ЗадачиСпринтовЗаПериод.КомандаРазработчиков КАК КомандаРазработчиков
	|ПОМЕСТИТЬ ВсеЗадачиСпринтовЗаПериод
	|ИЗ
	|	ЗадачиСпринтовЗаПериод КАК ЗадачиСпринтовЗаПериод
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.ИсторияСтатусовЗаявокПоСистемам КАК ИсторияСтатусовЗаявокПоСистемам
	|		ПО ((ВЫРАЗИТЬ(ЗадачиСпринтовЗаПериод.ОбъектОснование КАК Документ.ЗаявкаНаРазработку)) = ИсторияСтатусовЗаявокПоСистемам.Заявка)
	|ГДЕ
	|	ИсторияСтатусовЗаявокПоСистемам.Период МЕЖДУ &ДатаНачала И &ДатаОкончания
	|	И ИсторияСтатусовЗаявокПоСистемам.Статус = ЗНАЧЕНИЕ(Справочник.СтатусыОбъектов.Решен)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	СвойстваЗадач.Период КАК Период,
	|	СвойстваЗадач.Объект КАК Объект,
	|	СвойстваЗадач.Статус КАК Статус
	|ПОМЕСТИТЬ ИзмененияПоВыбраннымЗадачам
	|ИЗ
	|	ВсеЗадачиСпринтовЗаПериод КАК ВсеЗадачиСпринтовЗаПериод
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.СвойстваЗадач КАК СвойстваЗадач
	|		ПО ВсеЗадачиСпринтовЗаПериод.Задача = СвойстваЗадач.Объект
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ИзмененияПоВыбраннымЗадачам.Объект КАК Задача,
	|	МИНИМУМ(ИзмененияПоВыбраннымЗадачамСледующая.Период) КАК ПериодСледующейЗаписи
	|ПОМЕСТИТЬ СледующиеЗаписиРегистраПослеТестирования
	|ИЗ
	|	ИзмененияПоВыбраннымЗадачам КАК ИзмененияПоВыбраннымЗадачам
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ИзмененияПоВыбраннымЗадачам КАК ИзмененияПоВыбраннымЗадачамСледующая
	|		ПО ИзмененияПоВыбраннымЗадачам.Объект = ИзмененияПоВыбраннымЗадачамСледующая.Объект
	|			И ИзмененияПоВыбраннымЗадачам.Период < ИзмененияПоВыбраннымЗадачамСледующая.Период
	|ГДЕ
	|	ИзмененияПоВыбраннымЗадачам.Статус = ЗНАЧЕНИЕ(Справочник.СтатусыОбъектов.Тестирование)
	|
	|СГРУППИРОВАТЬ ПО
	|	ИзмененияПоВыбраннымЗадачам.Объект,
	|	ИзмененияПоВыбраннымЗадачам.Период
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ЗадачиСпринтовЗаПериод.Спринт КАК Спринт,
	|	ЗадачиСпринтовЗаПериод.КомандаРазработчиков КАК КомандаРазработчиков,
	|	ЗадачиСпринтовЗаПериод.Задача КАК Задача,
	|	СУММА(ВЫБОР
	|			КОГДА ИзмененияПоВыбраннымЗадачам.Объект ЕСТЬ NULL
	|				ТОГДА 0
	|			ИНАЧЕ 1
	|		КОНЕЦ) КАК Возвраты,
	|	ЗадачиСпринтовЗаПериод.Филиал КАК Филиал
	|ПОМЕСТИТЬ КоличествоВозвратов
	|ИЗ
	|	ЗадачиСпринтовЗаПериод КАК ЗадачиСпринтовЗаПериод
	|		ЛЕВОЕ СОЕДИНЕНИЕ СледующиеЗаписиРегистраПослеТестирования КАК СледующиеЗаписиРегистраПослеТестирования
	|		ПО ЗадачиСпринтовЗаПериод.Задача = СледующиеЗаписиРегистраПослеТестирования.Задача
	|		ЛЕВОЕ СОЕДИНЕНИЕ ИзмененияПоВыбраннымЗадачам КАК ИзмененияПоВыбраннымЗадачам
	|		ПО (СледующиеЗаписиРегистраПослеТестирования.ПериодСледующейЗаписи = ИзмененияПоВыбраннымЗадачам.Период)
	|			И (СледующиеЗаписиРегистраПослеТестирования.Задача = ИзмененияПоВыбраннымЗадачам.Объект)
	|			И (ИзмененияПоВыбраннымЗадачам.Статус = ЗНАЧЕНИЕ(Справочник.СтатусыОбъектов.ВРаботе))
	|
	|СГРУППИРОВАТЬ ПО
	|	ЗадачиСпринтовЗаПериод.Спринт,
	|	ЗадачиСпринтовЗаПериод.КомандаРазработчиков,
	|	ЗадачиСпринтовЗаПериод.Задача,
	|	ЗадачиСпринтовЗаПериод.Филиал
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ОтобранныеСпринты.Ссылка КАК Спринт,
	|	ЕСТЬNULL(ПользователиСДнямиКалендаря.КомандаРазработчиков, ОтобранныеСпринты.Команда) КАК КомандаРазработчиков,
	|	ВЫРАЗИТЬ(СУММА(ПользователиСДнямиКалендаря.ВремяНаРазработку) * 0.8 КАК ЧИСЛО(10, 1)) КАК ВремяНаРазработку,
	|	ОтобранныеСпринты.Филиал КАК Филиал
	|ПОМЕСТИТЬ ПланТрудозатрат
	|ИЗ
	|	ОтобранныеСпринты КАК ОтобранныеСпринты
	|		ЛЕВОЕ СОЕДИНЕНИЕ ПользователиСДнямиКалендаря КАК ПользователиСДнямиКалендаря
	|		ПО (ПользователиСДнямиКалендаря.ДатаКалендаря МЕЖДУ ОтобранныеСпринты.ДатаНачала И ОтобранныеСпринты.ДатаОкончания)
	|			И ОтобранныеСпринты.Филиал = ПользователиСДнямиКалендаря.Филиал
	|		ЛЕВОЕ СОЕДИНЕНИЕ ДоступноеВремяСпринтов КАК ДоступноеВремяСпринтов
	|		ПО (ДоступноеВремяСпринтов.Спринт = ОтобранныеСпринты.Ссылка)
	|ГДЕ
	|	ДоступноеВремяСпринтов.Спринт ЕСТЬ NULL
	|
	|СГРУППИРОВАТЬ ПО
	|	ОтобранныеСпринты.Ссылка,
	|	ЕСТЬNULL(ПользователиСДнямиКалендаря.КомандаРазработчиков, ОтобранныеСпринты.Команда),
	|	ОтобранныеСпринты.Филиал
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ДоступноеВремяСпринтов.Спринт,
	|	ДоступноеВремяСпринтов.Команда,
	|	ДоступноеВремяСпринтов.Время,
	|	ДоступноеВремяСпринтов.Филиал
	|ИЗ
	|	ДоступноеВремяСпринтов КАК ДоступноеВремяСпринтов
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ЗадачиСпринтовЗаПериод.Задача КАК Задача,
	|	МАКСИМУМ(СвойстваЗадач.Период) КАК Период
	|ПОМЕСТИТЬ ПоследнееИзменениеСвойстваОбъекта
	|ИЗ
	|	ЗадачиСпринтовЗаПериод КАК ЗадачиСпринтовЗаПериод
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.СвойстваЗадач КАК СвойстваЗадач
	|		ПО ЗадачиСпринтовЗаПериод.Задача = СвойстваЗадач.Объект
	|			И (СвойстваЗадач.Период <= ДОБАВИТЬКДАТЕ(ЗадачиСпринтовЗаПериод.ДатаОкончания, ДЕНЬ, 7))
	|
	|СГРУППИРОВАТЬ ПО
	|	ЗадачиСпринтовЗаПериод.Задача
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ПоследнееИзменениеСвойстваОбъекта.Задача КАК Задача,
	|	СвойстваЗадач.Статус КАК Статус
	|ПОМЕСТИТЬ ПоследнийСтатусОбъектаНаМоментОкончанияСпринта
	|ИЗ
	|	РегистрСведений.СвойстваЗадач КАК СвойстваЗадач
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ПоследнееИзменениеСвойстваОбъекта КАК ПоследнееИзменениеСвойстваОбъекта
	|		ПО (ПоследнееИзменениеСвойстваОбъекта.Задача = СвойстваЗадач.Объект)
	|			И (ПоследнееИзменениеСвойстваОбъекта.Период = СвойстваЗадач.Период)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ЗадачиСпринтовЗаПериод.Спринт КАК Спринт,
	|	ЗадачиСпринтовЗаПериод.КомандаРазработчиков КАК КомандаРазработчиков,
	|	ВЫБОР
	|		КОГДА ЗадачиСпринтовЗаПериод.ДатаНачала <= &ТекущаяДата
	|				И ЗадачиСпринтовЗаПериод.ДатаОкончания >= &ТекущаяДата
	|			ТОГДА ИСТИНА
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ КАК ТекущийСпринт,
	|	КОЛИЧЕСТВО(РАЗЛИЧНЫЕ ЗадачиСпринтовЗаПериод.Задача) КАК КоличествоЗадач,
	|	КОЛИЧЕСТВО(РАЗЛИЧНЫЕ ЗадачиСпринтовЗаПериод.ОбъектОснование) КАК КоличествоЗаявок,
	|	СУММА(КоличествоВозвратов.Возвраты) КАК ВозвратовПоЗадачам,
	|	СУММА(ФактическиеТрудозатраты.Затрата) КАК ФактическоеВремя,
	|	СУММА(ЗадачиСпринтовЗаПериод.ПланируемоеВремя) КАК ПлановоеВремя,
	|	СУММА(ВЫБОР
	|			КОГДА ЗадачиСпринтовЗаПериод.Статус В (ЗНАЧЕНИЕ(Справочник.СтатусыОбъектов.Решен), ЗНАЧЕНИЕ(Справочник.СтатусыОбъектов.Отклонен))
	|				ТОГДА 1
	|			ИНАЧЕ 0
	|		КОНЕЦ) КАК ЗадачЗакрыто,
	|	СУММА(ВЫБОР
	|			КОГДА НЕ ЗадачиСпринтовЗаПериод.Статус В (ЗНАЧЕНИЕ(Справочник.СтатусыОбъектов.Тестирование), ЗНАЧЕНИЕ(Справочник.СтатусыОбъектов.Решен), ЗНАЧЕНИЕ(Справочник.СтатусыОбъектов.Отклонен))
	|				ТОГДА 1
	|			ИНАЧЕ 0
	|		КОНЕЦ) КАК ЗадачВРаботе,
	|	СУММА(ВЫБОР
	|			КОГДА ЗадачиСпринтовЗаПериод.Статус = ЗНАЧЕНИЕ(Справочник.СтатусыОбъектов.Тестирование)
	|				ТОГДА 1
	|			ИНАЧЕ 0
	|		КОНЕЦ) КАК ЗадачВТестировании,
	|	СУММА(ВЫБОР
	|			КОГДА ПоследнийСтатусОбъектаНаМоментОкончанияСпринта.Статус В (ЗНАЧЕНИЕ(Справочник.СтатусыОбъектов.Решен), ЗНАЧЕНИЕ(Справочник.СтатусыОбъектов.Отклонен))
	|				ТОГДА 1
	|			ИНАЧЕ 0
	|		КОНЕЦ) КАК ЗадачЗакрытоВовремя,
	|	ЗадачиСпринтовЗаПериод.Филиал КАК Филиал
	|ПОМЕСТИТЬ ПоказателиСпринтовКоманд
	|ИЗ
	|	ЗадачиСпринтовЗаПериод КАК ЗадачиСпринтовЗаПериод
	|		ЛЕВОЕ СОЕДИНЕНИЕ КоличествоВозвратов КАК КоличествоВозвратов
	|		ПО ЗадачиСпринтовЗаПериод.Спринт = КоличествоВозвратов.Спринт
	|			И ЗадачиСпринтовЗаПериод.КомандаРазработчиков = КоличествоВозвратов.КомандаРазработчиков
	|			И ЗадачиСпринтовЗаПериод.Задача = КоличествоВозвратов.Задача
	|			И ЗадачиСпринтовЗаПериод.Филиал = КоличествоВозвратов.Филиал
	|		ЛЕВОЕ СОЕДИНЕНИЕ ФактическиеТрудозатраты КАК ФактическиеТрудозатраты
	|		ПО ЗадачиСпринтовЗаПериод.Спринт = ФактическиеТрудозатраты.Спринт
	|			И ЗадачиСпринтовЗаПериод.КомандаРазработчиков = ФактическиеТрудозатраты.КомандаРазработчиков
	|			И ЗадачиСпринтовЗаПериод.Задача = ФактическиеТрудозатраты.Задача
	|			И ЗадачиСпринтовЗаПериод.Филиал = ФактическиеТрудозатраты.Филиал
	|		ЛЕВОЕ СОЕДИНЕНИЕ ПоследнийСтатусОбъектаНаМоментОкончанияСпринта КАК ПоследнийСтатусОбъектаНаМоментОкончанияСпринта
	|		ПО ЗадачиСпринтовЗаПериод.Задача = ПоследнийСтатусОбъектаНаМоментОкончанияСпринта.Задача
	|
	|СГРУППИРОВАТЬ ПО
	|	ЗадачиСпринтовЗаПериод.Спринт,
	|	ВЫБОР
	|		КОГДА ЗадачиСпринтовЗаПериод.ДатаНачала <= &ТекущаяДата
	|				И ЗадачиСпринтовЗаПериод.ДатаОкончания >= &ТекущаяДата
	|			ТОГДА ИСТИНА
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ,
	|	ЗадачиСпринтовЗаПериод.КомандаРазработчиков,
	|	ЗадачиСпринтовЗаПериод.Филиал
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ПоказателиСпринтовКоманд.Спринт КАК Спринт,
	|	ПоказателиСпринтовКоманд.ТекущийСпринт КАК ТекущийСпринт,
	|	ПРЕДСТАВЛЕНИЕ(ПоказателиСпринтовКоманд.Спринт) КАК СпринтПредставление,
	|	ПоказателиСпринтовКоманд.КомандаРазработчиков КАК КомандаРазработчиков,
	|	ВЫБОР
	|		КОГДА ПоказателиСпринтовКоманд.КомандаРазработчиков = ЗНАЧЕНИЕ(Справочник.Филиалы.ПустаяСсылка)
	|			ТОГДА ""Нет команды""
	|		ИНАЧЕ ПРЕДСТАВЛЕНИЕ(ПоказателиСпринтовКоманд.КомандаРазработчиков)
	|	КОНЕЦ КАК КомандаРазработчиковПредставление,
	|	ПоказателиСпринтовКоманд.КоличествоЗадач КАК КоличествоЗадач,
	|	ЕСТЬNULL(ПланТрудозатрат.ВремяНаРазработку, 0) КАК ДоступноеВремя,
	|	ПоказателиСпринтовКоманд.КоличествоЗаявок КАК КоличествоЗаявок,
	|	ПоказателиСпринтовКоманд.ВозвратовПоЗадачам КАК ВозвратовПоЗадачам,
	|	ПоказателиСпринтовКоманд.ФактическоеВремя КАК ФактическоеВремя,
	|	ПоказателиСпринтовКоманд.ПлановоеВремя КАК ПлановоеВремя,
	|	ПоказателиСпринтовКоманд.ЗадачЗакрыто КАК ЗадачЗакрыто,
	|	ПоказателиСпринтовКоманд.ЗадачВРаботе КАК ЗадачВРаботе,
	|	ПоказателиСпринтовКоманд.ЗадачВТестировании КАК ЗадачВТестировании,
	|	ПоказателиСпринтовКоманд.ЗадачЗакрытоВовремя КАК ЗадачЗакрытоВовремя,
	|	ВЫБОР
	|		КОГДА ЕСТЬNULL(ПланТрудозатрат.ВремяНаРазработку, 0) <> 0
	|			ТОГДА ВЫРАЗИТЬ(ПоказателиСпринтовКоманд.ПлановоеВремя / ЕСТЬNULL(ПланТрудозатрат.ВремяНаРазработку, 0) * 100 КАК ЧИСЛО(10, 2))
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК ПроцентНаполненности,
	|	ПРЕДСТАВЛЕНИЕ(ПоказателиСпринтовКоманд.Филиал) КАК ФилиалПредставление,
	|	ПоказателиСпринтовКоманд.Филиал КАК Филиал
	|ИЗ
	|	ПоказателиСпринтовКоманд КАК ПоказателиСпринтовКоманд
	|		ЛЕВОЕ СОЕДИНЕНИЕ ПланТрудозатрат КАК ПланТрудозатрат
	|		ПО ПоказателиСпринтовКоманд.Спринт = ПланТрудозатрат.Спринт
	|			И ПоказателиСпринтовКоманд.Филиал = ПланТрудозатрат.Филиал
	|			И ПоказателиСпринтовКоманд.КомандаРазработчиков = ПланТрудозатрат.КомандаРазработчиков
	|ИТОГИ
	|	МАКСИМУМ(ТекущийСпринт),
	|	СУММА(КоличествоЗадач),
	|	СУММА(ДоступноеВремя),
	|	СУММА(КоличествоЗаявок),
	|	СУММА(ВозвратовПоЗадачам),
	|	СУММА(ФактическоеВремя),
	|	СУММА(ПлановоеВремя),
	|	СУММА(ЗадачЗакрыто),
	|	СУММА(ЗадачВРаботе),
	|	СУММА(ЗадачВТестировании),
	|	СУММА(ЗадачЗакрытоВовремя),
	|	СРЕДНЕЕ(ПроцентНаполненности),
	|	МАКСИМУМ(Филиал)
	|ПО
	|	Спринт";	
#КонецОбласти
	
	Запрос.УстановитьПараметр("Филиалы", Филиалы);
	Запрос.УстановитьПараметр("ТекущаяДата", ТекущаяДатаСеанса());
	Запрос.УстановитьПараметр("ДатаНачала", ДатаНачала);
	Запрос.УстановитьПараметр("ДатаОкончания", ДатаОкончания);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	ОбъектОтчет = РеквизитФормыВЗначение("Отчет");
	Макет = ОбъектОтчет.ПолучитьМакет("Макет");
	Результат.Вывести(Макет.ПолучитьОбласть("Шапка|Данные"));
		
	Если НЕ РезультатЗапроса.Пустой() Тогда
		Результат.НачатьАвтогруппировкуСтрок();
		ВыборкаСпринтов = РезультатЗапроса.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
		
		Пока ВыборкаСпринтов.Следующий() Цикл
			
			Если ВыборкаСпринтов.ТекущийСпринт Тогда 
				ОбластьСтрокаСпринт = Макет.ПолучитьОбласть("СтрокаСпринтТекущий|Данные");
			Иначе 
				ОбластьСтрокаСпринт = Макет.ПолучитьОбласть("СтрокаСпринт|Данные");
			КонецЕсли;
			
			ЗаполнитьЗначенияСвойств(ОбластьСтрокаСпринт.Параметры, ВыборкаСпринтов);
			ОбластьСтрокаСпринт.Параметры.ВозвратовВРаботу = ВыборкаСпринтов.ВозвратовПоЗадачам / ВыборкаСпринтов.КоличествоЗадач;
			
			ЗаполнитьПараметрыРасшифровки(ОбластьСтрокаСпринт, ВыборкаСпринтов.Спринт, ВыборкаСпринтов.Филиал);	
			Результат.Вывести(ОбластьСтрокаСпринт, 1);
			ВыборкаКоманд = ВыборкаСпринтов.Выбрать();
			
			Пока ВыборкаКоманд.Следующий() Цикл
				ОбластьСтрокаКоманда = Макет.ПолучитьОбласть("СтрокаКоманда|Данные");
				ЗаполнитьЗначенияСвойств(ОбластьСтрокаКоманда.Параметры, ВыборкаКоманд);
				ОбластьСтрокаКоманда.Параметры.ВозвратовВРаботу = ВыборкаКоманд.ВозвратовПоЗадачам / ВыборкаСпринтов.КоличествоЗадач;
				
				ЗаполнитьПараметрыРасшифровки(ОбластьСтрокаКоманда, ВыборкаКоманд.Спринт, ВыборкаКоманд.Филиал,
					ВыборкаКоманд.КомандаРазработчиков);
				Результат.Вывести(ОбластьСтрокаКоманда, 2);
			КонецЦикла;
		КонецЦикла;
		
		Результат.ЗакончитьАвтогруппировкуСтрок();
	КонецЕсли;
	
	Результат.ФиксацияСверху = 2;
	Результат.ФиксацияСлева = 1;
	
	СохранитьПользовательскиеНастройки();

КонецПроцедуры

#КонецОбласти
