///////////////////////////////////////////////////////////////////////////////////////////////////////
// Copyright (c) 2025, ООО ДНС Технологии
// SDMS (Software Development Management System) — это корпоративная система учета разработки и управления проектами 
// Все права защищены. Эта программа и сопроводительные материалы предоставляются 
// в соответствии с условиями лицензии General Public License (GNU GPL v3)
// Текст лицензии доступен по ссылке:
// https://www.gnu.org/licenses/gpl-3.0.html
///////////////////////////////////////////////////////////////////////////////////////////////////////

#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	РегистрыСведений.ОтслеживаниеИспользованияФункциональности.ОткрытиеФормы(ЭтотОбъект.ИмяФормы);
		
	Если Параметры.Свойство("Колонка") Тогда
		УстановитьЗаголовокФормы(Параметры);			
		СформироватьОтчет(Параметры);		
	Иначе
		Отказ = Истина;
		Сообщение = Новый СообщениеПользователю;
		Сообщение.Текст = "Форма не предназначена для непосредственного открытия.";
		Сообщение.Сообщить();
	КонецЕсли;

КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции
	
&НаСервере
Функция ПолучитьТекстЗапроса(Знач Колонка)
		
	Если Колонка = "КоличествоЗаявок" Тогда
		ТекстЗапроса = ТекстЗапросаКоличествоЗаявок();
	ИначеЕсли Колонка = "КоличествоЗадач" Тогда
		ТекстЗапроса = ТекстЗапросаКоличествоЗадач();
	ИначеЕсли Колонка = "ЗадачЗакрыто" Тогда
		ТекстЗапроса = ТекстЗапросаЗадачЗакрыто();
	ИначеЕсли Колонка = "ЗадачЗакрытоВовремя" Тогда
		ТекстЗапроса = ТекстЗапросаЗадачЗакрытоВовремя();
	ИначеЕсли Колонка = "ЗадачВРаботе" Тогда
		ТекстЗапроса = ТекстЗапросаЗадачВРаботе();
	ИначеЕсли Колонка = "ЗадачВТестировании" Тогда              
		ТекстЗапроса = ТекстЗапросаЗадачВТестировании();
	ИначеЕсли Колонка = "ВозвратовПоЗадачам" Тогда              
		ТекстЗапроса = ТекстЗапросаВозвратовПоЗадачам();
	Иначе
		ТекстЗапроса = "";
	КонецЕсли;
	
	Возврат ТекстЗапроса;
	
КонецФункции

&НаСервере
Процедура СформироватьОтчет(Знач Параметры)
	
	Колонка = Параметры.Колонка;
	ЭтоОтчетПоКоличествуОбъектов = (Колонка = "КоличествоЗаявок" 
		ИЛИ Колонка = "КоличествоЗадач"
		ИЛИ Колонка = "ЗадачЗакрыто"
		ИЛИ Колонка = "ЗадачЗакрытоВовремя" 
		ИЛИ Колонка = "ЗадачВРаботе"
		ИЛИ Колонка = "ЗадачВТестировании" 
		ИЛИ Колонка = "ВозвратовПоЗадачам");
	
	Если Колонка = "ПлановоеВремя" ИЛИ Колонка = "ФактическоеВремя" Тогда			
		СформироватьОтчетПланФактВремя(Параметры);
		
	ИначеЕсли Колонка = "ПревышениеПлана" Тогда
		СформироватьОтчетПревышениеПлана(Параметры);
		
	ИначеЕсли ЭтоОтчетПоКоличествуОбъектов Тогда
		СформироватьОтчетКоличествоОбъектов(Параметры);	
	КонецЕсли;
		
КонецПроцедуры

&НаСервере
Процедура СформироватьОтчетПревышениеПлана(Знач Параметры)
	
	ТабДок.Очистить();
		
	Запрос = Новый Запрос;
	Запрос.Текст =
	#Область ТекстЗапроса
	"ВЫБРАТЬ
	|	ЛичныеДелаСрезПоследних.Сотрудник КАК Сотрудник
	|ПОМЕСТИТЬ СотрудникиФилиала
	|ИЗ
	|	РегистрСведений.ЛичныеДела.СрезПоследних(&ДатаНачала, Событие = ЗНАЧЕНИЕ(Перечисление.СобытияПоЛичнымДелам.ПереведенВДругоеПодразделение)) КАК ЛичныеДелаСрезПоследних
	|ГДЕ
	|	(ВЫРАЗИТЬ(ЛичныеДелаСрезПоследних.Данные КАК Справочник.Филиалы)) = &Филиал
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ДокументСпринт.ДатаОкончания КАК ДатаОкончания,
	|	СоставСпринтов.Объект КАК Задача,
	|	ДокументЗадача.КомандаРазработчиков КАК КомандаРазработчиков,
	|	ВЫБОР
	|		КОГДА ДокументЗадача.КомандаРазработчиков = ЗНАЧЕНИЕ(Справочник.Филиалы.ПустаяСсылка)
	|			ТОГДА 0
	|		ИНАЧЕ 1
	|	КОНЕЦ КАК СортировкаКоманд,
	|	ЕСТЬNULL(ПланируемыеТрудозатраты.Трудозатраты, 0) КАК ПланируемоеВремя
	|ПОМЕСТИТЬ ЗадачиСпринтаЗаПериод
	|ИЗ
	|	Документ.Спринт КАК ДокументСпринт
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.СоставСпринтов КАК СоставСпринтов
	|		ПО ДокументСпринт.Ссылка = СоставСпринтов.Спринт
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.Задача КАК ДокументЗадача
	|		ПО (СоставСпринтов.Объект = ДокументЗадача.Ссылка)
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ПланируемыеТрудозатраты КАК ПланируемыеТрудозатраты
	|		ПО (СоставСпринтов.Объект = ПланируемыеТрудозатраты.Объект)
	|			И (ПланируемыеТрудозатраты.ТипТрудозатрат = ЗНАЧЕНИЕ(Перечисление.ТипыТрудозатрат.Разработка))
	|ГДЕ
	|	ДокументСпринт.Ссылка = &Спринт
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ЗадачиСпринтаЗаПериод.Задача КАК Задача,
	|	Трудозатраты.Пользователь КАК Пользователь,
	|	СУММА(Трудозатраты.Затрата) КАК Затрата,
	|	СУММА(ВЫБОР
	|			КОГДА Трудозатраты.Период <= ЗадачиСпринтаЗаПериод.ДатаОкончания
	|				ТОГДА Трудозатраты.Затрата
	|			ИНАЧЕ 0
	|		КОНЕЦ) КАК ТрудозатратыВоВремяСпринта,
	|	СУММА(ВЫБОР
	|			КОГДА Трудозатраты.Период > ЗадачиСпринтаЗаПериод.ДатаОкончания
	|				ТОГДА Трудозатраты.Затрата
	|			ИНАЧЕ 0
	|		КОНЕЦ) КАК ТрудозатратыПослеОкончанияСпринта
	|ПОМЕСТИТЬ ФактическиеТрудозатраты
	|ИЗ
	|	ЗадачиСпринтаЗаПериод КАК ЗадачиСпринтаЗаПериод
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.Трудозатраты КАК Трудозатраты
	|		ПО (ЗадачиСпринтаЗаПериод.Задача = (ВЫРАЗИТЬ(Трудозатраты.Объект КАК Документ.Задача)))
	|			И (Трудозатраты.ITСтруктура)
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ СотрудникиФилиала КАК СотрудникиФилиала
	|		ПО (Трудозатраты.Пользователь = СотрудникиФилиала.Сотрудник)
	|
	|СГРУППИРОВАТЬ ПО
	|	ЗадачиСпринтаЗаПериод.Задача,
	|	Трудозатраты.Пользователь
	|
	|ИМЕЮЩИЕ
	|	СУММА(ВЫБОР
	|			КОГДА Трудозатраты.Период > ЗадачиСпринтаЗаПериод.ДатаОкончания
	|				ТОГДА Трудозатраты.Затрата
	|			ИНАЧЕ 0
	|		КОНЕЦ) > 0
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ФактическиеТрудозатраты.Задача КАК Задача,
	|	КОЛИЧЕСТВО(ФактическиеТрудозатраты.Пользователь) КАК Количество
	|ПОМЕСТИТЬ КоличествоПользователей
	|ИЗ
	|	ФактическиеТрудозатраты КАК ФактическиеТрудозатраты
	|
	|СГРУППИРОВАТЬ ПО
	|	ФактическиеТрудозатраты.Задача
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВЫБОР
	|		КОГДА ЗадачиСпринтаЗаПериод.КомандаРазработчиков = ЗНАЧЕНИЕ(Справочник.Филиалы.ПустаяСсылка)
	|			ТОГДА ""Нет команды""
	|		ИНАЧЕ ПРЕДСТАВЛЕНИЕ(ЗадачиСпринтаЗаПериод.КомандаРазработчиков)
	|	КОНЕЦ КАК КомандаРазработчиковПредставление,
	|	ЗадачиСпринтаЗаПериод.Задача КАК Задача,
	|	ПРЕДСТАВЛЕНИЕ(ЗадачиСпринтаЗаПериод.Задача) КАК ЗадачаПредставление,
	|	ЗадачиСпринтаЗаПериод.ПланируемоеВремя КАК ПлановоеВремя,
	|	ФактическиеТрудозатраты.Пользователь КАК Пользователь,
	|	ПРЕДСТАВЛЕНИЕ(ФактическиеТрудозатраты.Пользователь) КАК ПользовательПредставление,
	|	ФактическиеТрудозатраты.Затрата КАК ФактическоеВремя,
	|	ФактическиеТрудозатраты.ТрудозатратыВоВремяСпринта КАК ТрудозатратыВоВремяСпринта,
	|	ФактическиеТрудозатраты.ТрудозатратыПослеОкончанияСпринта КАК ТрудозатратыПослеОкончанияСпринта,
	|	ВЫБОР
	|		КОГДА ЗадачиСпринтаЗаПериод.ПланируемоеВремя > 0
	|			ТОГДА ВЫРАЗИТЬ(ФактическиеТрудозатраты.ТрудозатратыПослеОкончанияСпринта / ЗадачиСпринтаЗаПериод.ПланируемоеВремя * 100 КАК ЧИСЛО(6, 2))
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК ПревышениеПлана
	|ИЗ
	|	ЗадачиСпринтаЗаПериод КАК ЗадачиСпринтаЗаПериод
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ФактическиеТрудозатраты КАК ФактическиеТрудозатраты
	|		ПО ЗадачиСпринтаЗаПериод.Задача = ФактическиеТрудозатраты.Задача
	|		ЛЕВОЕ СОЕДИНЕНИЕ КоличествоПользователей КАК КоличествоПользователей
	|		ПО ЗадачиСпринтаЗаПериод.Задача = КоличествоПользователей.Задача
	|
	|УПОРЯДОЧИТЬ ПО
	|	ЗадачиСпринтаЗаПериод.СортировкаКоманд
	|ИТОГИ
	|	СУММА(ПлановоеВремя),
	|	СУММА(ФактическоеВремя),
	|	СУММА(ТрудозатратыВоВремяСпринта),
	|	СУММА(ТрудозатратыПослеОкончанияСпринта),
	|	СРЕДНЕЕ(ПревышениеПлана)
	|ПО
	|	ЗадачиСпринтаЗаПериод.КомандаРазработчиков,
	|	Задача";
	#КонецОбласти
	
	Запрос.УстановитьПараметр("Спринт", Параметры.Спринт);
	Запрос.УстановитьПараметр("Филиал", Параметры.Филиал);
	Запрос.УстановитьПараметр("ДатаНачала", Параметры.ДатаНачала);
	Запрос.УстановитьПараметр("ДатаОкончания", Параметры.ДатаОкончания);
	
	РезультатЗапроса = Запрос.Выполнить();

	Макет = Отчеты.РаботаСоСпринтами.ПолучитьМакет("РасшифровкаПревышениеПлана");
	
	ОбластьШапка = Макет.ПолучитьОбласть("Шапка");
	ОбластьСтрокаГруппировка = Макет.ПолучитьОбласть("СтрокаГруппировка");
	ОбластьСтрокаДетальныеЗаписи = Макет.ПолучитьОбласть("СтрокаДетальныеЗаписи");

	ТабДок.Вывести(ОбластьШапка);	
	
	Если РезультатЗапроса.Пустой() Тогда
		Возврат;
	КонецЕсли;
		
	ТабДок.НачатьАвтогруппировкуСтрок();
	
	ВыборкаКоманда = РезультатЗапроса.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	
	Пока ВыборкаКоманда.Следующий() Цикл
		ЗаполнитьЗначенияСвойств(ОбластьСтрокаГруппировка.Параметры, ВыборкаКоманда);
		ОбластьСтрокаГруппировка.Параметры.ОбъектПредставление = ВыборкаКоманда.КомандаРазработчиковПредставление;
		
		ТабДок.Вывести(ОбластьСтрокаГруппировка, ВыборкаКоманда.Уровень());
		ВыборкаЗадача = ВыборкаКоманда.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
		
		Пока ВыборкаЗадача.Следующий() Цикл
			ЗаполнитьЗначенияСвойств(ОбластьСтрокаДетальныеЗаписи.Параметры, ВыборкаЗадача);
			ОбластьСтрокаДетальныеЗаписи.Параметры.ОбъектПредставление = ВыборкаЗадача.ЗадачаПредставление;
			ОбластьСтрокаДетальныеЗаписи.Параметры.Объект = ВыборкаЗадача.Задача;
			
			ТабДок.Вывести(ОбластьСтрокаДетальныеЗаписи, ВыборкаЗадача.Уровень());
			
			ВыборкаПользователь = ВыборкаЗадача.Выбрать();
			Пока ВыборкаПользователь.Следующий() Цикл
				ЗаполнитьЗначенияСвойств(ОбластьСтрокаДетальныеЗаписи.Параметры, ВыборкаПользователь);
				ОбластьСтрокаДетальныеЗаписи.Параметры.ОбъектПредставление = ВыборкаПользователь.ПользовательПредставление;
				ОбластьСтрокаДетальныеЗаписи.Параметры.ПлановоеВремя = 0;
				ОбластьСтрокаДетальныеЗаписи.Параметры.Объект = Неопределено;
				
				ТабДок.Вывести(ОбластьСтрокаДетальныеЗаписи, ВыборкаПользователь.Уровень());
			КонецЦикла;
		КонецЦикла;
	КонецЦикла;
	
	ТабДок.ЗакончитьАвтогруппировкуСтрок();
	
КонецПроцедуры

&НаСервере
Процедура СформироватьОтчетПланФактВремя(Знач Параметры)
	
	ТабДок.Очистить();
	
	Запрос = Новый Запрос;
	Запрос.Текст =	
	#Область ТекстЗапроса
	"ВЫБРАТЬ
	|	Спринт.Ссылка КАК Ссылка,
	|	Спринт.Номер КАК НомерСпринта,
	|	Спринт.ДатаНачала КАК ДатаНачала,
	|	Спринт.ДатаОкончания КАК ДатаОкончания,
	|	Спринт.Филиал КАК Филиал
	|ПОМЕСТИТЬ ОтобранныеСпринты
	|ИЗ
	|	Документ.Спринт КАК Спринт
	|ГДЕ
	|	Спринт.Филиал = &Филиал
	|	И Спринт.ДатаНачала >= &ДатаНачала
	|	И Спринт.ДатаОкончания <= &ДатаОкончания
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ОтобранныеСпринты.Ссылка КАК Спринт,
	|	ОтобранныеСпринты.НомерСпринта КАК НомерСпринта,
	|	СоставСпринтов.Объект КАК Задача,
	|	ДокументЗадача.КомандаРазработчиков КАК КомандаРазработчиков,
	|	ВЫБОР
	|		КОГДА ДокументЗадача.КомандаРазработчиков = ЗНАЧЕНИЕ(Справочник.Филиалы.ПустаяСсылка)
	|			ТОГДА 0
	|		ИНАЧЕ 1
	|	КОНЕЦ КАК СортировкаКоманд,
	|	ЕСТЬNULL(ПланируемыеТрудозатратыПоСпринтам.Трудозатраты, 0) КАК ПланируемоеВремя,
	|	ОтобранныеСпринты.ДатаНачала КАК ДатаНачала,
	|	ОтобранныеСпринты.ДатаОкончания КАК ДатаОкончания,
	|	ОтобранныеСпринты.Филиал КАК Филиал
	|ПОМЕСТИТЬ ЗадачиСпринтовЗаПериод
	|ИЗ
	|	ОтобранныеСпринты КАК ОтобранныеСпринты
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.СоставСпринтов КАК СоставСпринтов
	|		ПО ОтобранныеСпринты.Ссылка = СоставСпринтов.Спринт
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.Задача КАК ДокументЗадача
	|		ПО (СоставСпринтов.Объект = ДокументЗадача.Ссылка)
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ПланируемыеТрудозатратыПоСпринтам КАК ПланируемыеТрудозатратыПоСпринтам
	|		ПО ОтобранныеСпринты.Ссылка = ПланируемыеТрудозатратыПоСпринтам.Спринт
	|			И (СоставСпринтов.Объект = ПланируемыеТрудозатратыПоСпринтам.Объект)
	|			И (ПланируемыеТрудозатратыПоСпринтам.ТипТрудозатрат = ЗНАЧЕНИЕ(Перечисление.ТипыТрудозатрат.Разработка))
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ЗадачиСпринтовЗаПериод.Задача КАК Задача,
	|	ЗадачиСпринтовЗаПериод.Спринт КАК Спринт,
	|	Трудозатраты.Пользователь КАК Пользователь,
	|	СУММА(Трудозатраты.Затрата) КАК Затрата
	|ПОМЕСТИТЬ ФактическиеТрудозатратыФилиала
	|ИЗ
	|	ЗадачиСпринтовЗаПериод КАК ЗадачиСпринтовЗаПериод
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.Трудозатраты КАК Трудозатраты
	|		ПО ЗадачиСпринтовЗаПериод.Задача = Трудозатраты.Объект
	|			И (Трудозатраты.Период МЕЖДУ ЗадачиСпринтовЗаПериод.ДатаНачала И ЗадачиСпринтовЗаПериод.ДатаОкончания)
	|			И (Трудозатраты.Филиал = ЗадачиСпринтовЗаПериод.Филиал)
	|
	|СГРУППИРОВАТЬ ПО
	|	ЗадачиСпринтовЗаПериод.Спринт,
	|	ЗадачиСпринтовЗаПериод.Задача,
	|	Трудозатраты.Пользователь
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ДанныеТрудозатраты.Задача КАК Задача,
	|	ДанныеТрудозатраты.Спринт КАК Спринт,
	|	ДанныеТрудозатраты.Пользователь КАК Пользователь,
	|	СУММА(ДанныеТрудозатраты.Затрата) КАК Затрата
	|ПОМЕСТИТЬ ФактическиеТрудозатраты
	|ИЗ
	|	(ВЫБРАТЬ
	|		ФактическиеТрудозатратыФилиала.Задача КАК Задача,
	|		ФактическиеТрудозатратыФилиала.Спринт КАК Спринт,
	|		ФактическиеТрудозатратыФилиала.Пользователь КАК Пользователь,
	|		ФактическиеТрудозатратыФилиала.Затрата КАК Затрата
	|	ИЗ
	|		ФактическиеТрудозатратыФилиала КАК ФактическиеТрудозатратыФилиала
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		ЗадачиСпринтовЗаПериод.Задача,
	|		ЗадачиСпринтовЗаПериод.Спринт,
	|		Трудозатраты.Пользователь,
	|		Трудозатраты.Затрата
	|	ИЗ
	|		ЗадачиСпринтовЗаПериод КАК ЗадачиСпринтовЗаПериод
	|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.ДоступноеВремяСпринтов КАК ДоступноеВремяСпринтов
	|			ПО (ДоступноеВремяСпринтов.Спринт = ЗадачиСпринтовЗаПериод.Спринт)
	|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.Трудозатраты КАК Трудозатраты
	|			ПО ЗадачиСпринтовЗаПериод.Задача = Трудозатраты.Объект
	|				И (ДоступноеВремяСпринтов.Пользователь = Трудозатраты.Пользователь)
	|				И (Трудозатраты.Период МЕЖДУ ЗадачиСпринтовЗаПериод.ДатаНачала И ЗадачиСпринтовЗаПериод.ДатаОкончания)
	|	ГДЕ
	|		НЕ Трудозатраты.Пользователь В
	|					(ВЫБРАТЬ РАЗЛИЧНЫЕ
	|						ФактическиеТрудозатратыФилиала.Пользователь
	|					ИЗ
	|						ФактическиеТрудозатратыФилиала)) КАК ДанныеТрудозатраты
	|
	|СГРУППИРОВАТЬ ПО
	|	ДанныеТрудозатраты.Спринт,
	|	ДанныеТрудозатраты.Задача,
	|	ДанныеТрудозатраты.Пользователь
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ФактическиеТрудозатраты.Задача КАК Задача,
	|	ФактическиеТрудозатраты.Спринт КАК Спринт,
	|	КОЛИЧЕСТВО(ФактическиеТрудозатраты.Пользователь) КАК Количество
	|ПОМЕСТИТЬ КоличествоПользователей
	|ИЗ
	|	ФактическиеТрудозатраты КАК ФактическиеТрудозатраты
	|
	|СГРУППИРОВАТЬ ПО
	|	ФактическиеТрудозатраты.Спринт,
	|	ФактическиеТрудозатраты.Задача
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ЗадачиСпринтовЗаПериод.Спринт КАК Спринт,
	|	ПРЕДСТАВЛЕНИЕ(ЗадачиСпринтовЗаПериод.Спринт) КАК СпринтПредставление,
	|	ВЫБОР
	|		КОГДА ЗадачиСпринтовЗаПериод.КомандаРазработчиков = ЗНАЧЕНИЕ(Справочник.Филиалы.ПустаяСсылка)
	|			ТОГДА ""Нет команды""
	|		ИНАЧЕ ПРЕДСТАВЛЕНИЕ(ЗадачиСпринтовЗаПериод.КомандаРазработчиков)
	|	КОНЕЦ КАК КомандаРазработчиковПредставление,
	|	ЗадачиСпринтовЗаПериод.Задача КАК Задача,
	|	ПРЕДСТАВЛЕНИЕ(ЗадачиСпринтовЗаПериод.Задача) КАК ЗадачаПредставление,
	|	ЗадачиСпринтовЗаПериод.ПланируемоеВремя / ЕСТЬNULL(КоличествоПользователей.Количество, 1) КАК ПлановоеВремя,
	|	ЕСТЬNULL(ФактическиеТрудозатраты.Пользователь, ЗНАЧЕНИЕ(Справочник.Пользователи.ПустаяСсылка)) КАК Пользователь,
	|	ЕСТЬNULL(ПРЕДСТАВЛЕНИЕ(ФактическиеТрудозатраты.Пользователь), """") КАК ПользовательПредставление,
	|	ЕСТЬNULL(ФактическиеТрудозатраты.Затрата, 0) КАК ФактическоеВремя
	|ИЗ
	|	ЗадачиСпринтовЗаПериод КАК ЗадачиСпринтовЗаПериод
	|		ЛЕВОЕ СОЕДИНЕНИЕ ФактическиеТрудозатраты КАК ФактическиеТрудозатраты
	|		ПО ЗадачиСпринтовЗаПериод.Задача = ФактическиеТрудозатраты.Задача
	|			И ЗадачиСпринтовЗаПериод.Спринт = ФактическиеТрудозатраты.Спринт
	|		ЛЕВОЕ СОЕДИНЕНИЕ КоличествоПользователей КАК КоличествоПользователей
	|		ПО ЗадачиСпринтовЗаПериод.Задача = КоличествоПользователей.Задача
	|			И ЗадачиСпринтовЗаПериод.Спринт = КоличествоПользователей.Спринт
	|
	|УПОРЯДОЧИТЬ ПО
	|	ЗадачиСпринтовЗаПериод.НомерСпринта,
	|	ЗадачиСпринтовЗаПериод.СортировкаКоманд
	|ИТОГИ
	|	СУММА(ПлановоеВремя),
	|	СУММА(ФактическоеВремя)
	|ПО
	|	Спринт,
	|	ЗадачиСпринтовЗаПериод.КомандаРазработчиков,
	|	Задача";
	#КонецОбласти

	Запрос.УстановитьПараметр("Филиал", Параметры.Филиал);
	Запрос.УстановитьПараметр("ДатаНачала", Параметры.ДатаНачала);
	Запрос.УстановитьПараметр("ДатаОкончания", Параметры.ДатаОкончания);
	
	РезультатЗапроса = Запрос.Выполнить();

	Макет = Отчеты.РаботаСоСпринтами.ПолучитьМакет("РасшифровкаПланФактВремя");
	
	ОбластьШапка = Макет.ПолучитьОбласть("Шапка");
	ОбластьСтрокаСпринт = Макет.ПолучитьОбласть("СтрокаСпринт");
	ОбластьСтрокаГруппировка = Макет.ПолучитьОбласть("СтрокаГруппировка");
	ОбластьСтрокаДетальныеЗаписи = Макет.ПолучитьОбласть("СтрокаДетальныеЗаписи");

	ТабДок.Вывести(ОбластьШапка);
		
	Если РезультатЗапроса.Пустой() Тогда
		Возврат;
	КонецЕсли;
	
	ТабДок.НачатьАвтогруппировкуСтрок();
	
	ВыборкаСпринт = РезультатЗапроса.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	
	Пока ВыборкаСпринт.Следующий() Цикл
		ЗаполнитьЗначенияСвойств(ОбластьСтрокаСпринт.Параметры, ВыборкаСпринт);	
		
		ТабДок.Вывести(ОбластьСтрокаСпринт, ВыборкаСпринт.Уровень());
		ВыборкаКоманда = ВыборкаСпринт.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
		
		Пока ВыборкаКоманда.Следующий() Цикл
			ЗаполнитьЗначенияСвойств(ОбластьСтрокаГруппировка.Параметры, ВыборкаКоманда);
			ОбластьСтрокаГруппировка.Параметры.ОбъектПредставление = ВыборкаКоманда.КомандаРазработчиковПредставление;
			
			ТабДок.Вывести(ОбластьСтрокаГруппировка, ВыборкаКоманда.Уровень());
			ВыборкаЗадача = ВыборкаКоманда.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
			
			Пока ВыборкаЗадача.Следующий() Цикл
				ЗаполнитьЗначенияСвойств(ОбластьСтрокаДетальныеЗаписи.Параметры, ВыборкаЗадача);
				ОбластьСтрокаДетальныеЗаписи.Параметры.ОбъектПредставление = ВыборкаЗадача.ЗадачаПредставление;
				ОбластьСтрокаДетальныеЗаписи.Параметры.Объект = ВыборкаЗадача.Задача;
				
				ТабДок.Вывести(ОбластьСтрокаДетальныеЗаписи, ВыборкаЗадача.Уровень());
				ВыборкаПользователь = ВыборкаЗадача.Выбрать();
				
				Пока ВыборкаПользователь.Следующий() Цикл
					ЗаполнитьЗначенияСвойств(ОбластьСтрокаДетальныеЗаписи.Параметры, ВыборкаПользователь);
					ОбластьСтрокаДетальныеЗаписи.Параметры.ОбъектПредставление = ВыборкаПользователь.ПользовательПредставление;
					ОбластьСтрокаДетальныеЗаписи.Параметры.ПлановоеВремя = 0;
					ОбластьСтрокаДетальныеЗаписи.Параметры.Объект = Неопределено;
					
					ТабДок.Вывести(ОбластьСтрокаДетальныеЗаписи, ВыборкаПользователь.Уровень());
				КонецЦикла;
			КонецЦикла;
		КонецЦикла;
	КонецЦикла;
	
	ТабДок.ЗакончитьАвтогруппировкуСтрок();
	
КонецПроцедуры

&НаСервере
Процедура СформироватьОтчетКоличествоОбъектов(Знач Параметры)
	
	ТабДок.Очистить();
	
	ПостроительЗапроса = Новый ПостроительЗапроса;
	
	Если Параметры.Колонка = "ВозвратовПоЗадачам" Тогда
		Макет = Отчеты.РаботаСоСпринтами.ПолучитьМакет("РасшифровкаВозвраты");
		ПостроительЗапроса.Параметры.Вставить("ДатаНачала", Параметры.ДатаНачала);
		ПостроительЗапроса.Параметры.Вставить("ДатаОкончания", Параметры.ДатаОкончания);
	ИначеЕсли Параметры.Колонка = "ЗадачВРаботе" Тогда
		Макет = Отчеты.РаботаСоСпринтами.ПолучитьМакет("РасшифровкаКоличествоСоСтатусом");
	Иначе
		Макет = Отчеты.РаботаСоСпринтами.ПолучитьМакет("РасшифровкаКоличество");
	КонецЕсли;
	
	Если Параметры.Колонка = "КоличествоЗаявок" Тогда
		ОбластьШапка = Макет.ПолучитьОбласть("ШапкаЗаявка");
	Иначе
		ОбластьШапка = Макет.ПолучитьОбласть("ШапкаЗадача");
	КонецЕсли;
	
	ОбластьСтрока = Макет.ПолучитьОбласть("Строка");
	ТабДок.Вывести(ОбластьШапка);
	
	ТекстЗапроса = ПолучитьТекстЗапроса(Параметры.Колонка);
	
	Если ПустаяСтрока(ТекстЗапроса) Тогда
		Возврат;
	КонецЕсли;
	
	ПостроительЗапроса.Текст = ТекстЗапроса;	
	ПостроительЗапроса.Параметры.Вставить("Спринт", Параметры.Спринт);
	
	Если Параметры.КомандаРазработчиков <> Неопределено Тогда
		ЭлементОтбора = ПостроительЗапроса.Отбор.Добавить("КомандаРазработчиков");
		ЭлементОтбора.ВидСравнения  = ВидСравнения.Равно;
		ЭлементОтбора.Значение      = Параметры.КомандаРазработчиков;
		ЭлементОтбора.Использование = Истина;
	КонецЕсли;
	
	Текст = ПостроительЗапроса.Текст;
	РезультатЗапроса = ПостроительЗапроса.Результат;
	
	Если РезультатЗапроса.Пустой() Тогда
		Возврат;
	КонецЕсли;
	
	Выборка = РезультатЗапроса.Выбрать();
	
	Пока Выборка.Следующий() Цикл
		ЗаполнитьЗначенияСвойств(ОбластьСтрока.Параметры, Выборка);			
		ТабДок.Вывести(ОбластьСтрока);
	КонецЦикла;
	
КонецПроцедуры

&НаСервере
Функция ТекстЗапросаВозвратовПоЗадачам()  
	
	ТекстЗапроса = 
	"ВЫБРАТЬ
	|	ДокументСпринт.Ссылка КАК Спринт,
	|	СоставСпринтов.Объект КАК Задача,
	|	ДокументЗадача.Статус КАК Статус,
	|	ДокументЗадача.КомандаРазработчиков КАК КомандаРазработчиков,
	|	ДокументСпринт.ДатаОкончания КАК ДатаОкончания,
	|	ДокументСпринт.ДатаНачала КАК ДатаНачала,
	|	ДокументЗадача.ОбъектОснование КАК ОбъектОснование
	|ПОМЕСТИТЬ ЗадачиСпринтовЗаПериод
	|ИЗ
	|	Документ.Спринт КАК ДокументСпринт
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.СоставСпринтов КАК СоставСпринтов
	|		ПО (ДокументСпринт.Ссылка = &Спринт)
	|			И ДокументСпринт.Ссылка = СоставСпринтов.Спринт
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.Задача КАК ДокументЗадача
	|		ПО (СоставСпринтов.Объект = ДокументЗадача.Ссылка)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ЗадачиСпринтовЗаПериод.Задача КАК Задача,
	|	ЗадачиСпринтовЗаПериод.Спринт КАК Спринт,
	|	ЗадачиСпринтовЗаПериод.КомандаРазработчиков КАК КомандаРазработчиков
	|ПОМЕСТИТЬ ВсеЗадачиСпринтовЗаПериод
	|ИЗ
	|	ЗадачиСпринтовЗаПериод КАК ЗадачиСпринтовЗаПериод
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.ИсторияСтатусовЗаявокПоСистемам КАК ИсторияСтатусовЗаявокПоСистемам
	|		ПО ((ВЫРАЗИТЬ(ЗадачиСпринтовЗаПериод.ОбъектОснование КАК Документ.ЗаявкаНаРазработку)) = ИсторияСтатусовЗаявокПоСистемам.Заявка)
	|ГДЕ
	|	ИсторияСтатусовЗаявокПоСистемам.Период МЕЖДУ &ДатаНачала И &ДатаОкончания
	|	И ИсторияСтатусовЗаявокПоСистемам.Статус = ЗНАЧЕНИЕ(Справочник.СтатусыОбъектов.Решен)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	СвойстваЗадач.Период КАК Период,
	|	СвойстваЗадач.Объект КАК Объект,
	|	СвойстваЗадач.Статус КАК Статус
	|ПОМЕСТИТЬ ИзмененияПоВыбраннымЗадачам
	|ИЗ
	|	ВсеЗадачиСпринтовЗаПериод КАК ВсеЗадачиСпринтовЗаПериод
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.СвойстваЗадач КАК СвойстваЗадач
	|		ПО ВсеЗадачиСпринтовЗаПериод.Задача = СвойстваЗадач.Объект
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ИзмененияПоВыбраннымЗадачам.Объект КАК Задача,
	|	МИНИМУМ(ИзмененияПоВыбраннымЗадачамСледующая.Период) КАК ПериодСледующейЗаписи
	|ПОМЕСТИТЬ СледующиеЗаписиРегистраПослеТестирования
	|ИЗ
	|	ИзмененияПоВыбраннымЗадачам КАК ИзмененияПоВыбраннымЗадачам
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ИзмененияПоВыбраннымЗадачам КАК ИзмененияПоВыбраннымЗадачамСледующая
	|		ПО ИзмененияПоВыбраннымЗадачам.Объект = ИзмененияПоВыбраннымЗадачамСледующая.Объект
	|			И ИзмененияПоВыбраннымЗадачам.Период < ИзмененияПоВыбраннымЗадачамСледующая.Период
	|ГДЕ
	|	ИзмененияПоВыбраннымЗадачам.Статус = ЗНАЧЕНИЕ(Справочник.СтатусыОбъектов.Тестирование)
	|
	|СГРУППИРОВАТЬ ПО
	|	ИзмененияПоВыбраннымЗадачам.Объект,
	|	ИзмененияПоВыбраннымЗадачам.Период
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ЗадачиСпринтовЗаПериод.Задача КАК Объект,
	|	ПРЕДСТАВЛЕНИЕ(ЗадачиСпринтовЗаПериод.Задача) КАК ОбъектПредставление,
	|	СУММА(1) КАК Возвраты
	|ИЗ
	|	ЗадачиСпринтовЗаПериод КАК ЗадачиСпринтовЗаПериод
	|		ЛЕВОЕ СОЕДИНЕНИЕ СледующиеЗаписиРегистраПослеТестирования КАК СледующиеЗаписиРегистраПослеТестирования
	|		ПО ЗадачиСпринтовЗаПериод.Задача = СледующиеЗаписиРегистраПослеТестирования.Задача
	|		ЛЕВОЕ СОЕДИНЕНИЕ ИзмененияПоВыбраннымЗадачам КАК ИзмененияПоВыбраннымЗадачам
	|		ПО (СледующиеЗаписиРегистраПослеТестирования.ПериодСледующейЗаписи = ИзмененияПоВыбраннымЗадачам.Период)
	|			И (СледующиеЗаписиРегистраПослеТестирования.Задача = ИзмененияПоВыбраннымЗадачам.Объект)
	|			И (ИзмененияПоВыбраннымЗадачам.Статус = ЗНАЧЕНИЕ(Справочник.СтатусыОбъектов.ВРаботе))
	|ГДЕ
	|	ИзмененияПоВыбраннымЗадачам.Объект ЕСТЬ НЕ NULL 
	|{ГДЕ
	|	ЗадачиСпринтовЗаПериод.КомандаРазработчиков КАК КомандаРазработчиков}
	|
	|СГРУППИРОВАТЬ ПО
	|	ЗадачиСпринтовЗаПериод.Спринт,
	|	ЗадачиСпринтовЗаПериод.КомандаРазработчиков,
	|	ЗадачиСпринтовЗаПериод.Задача";
	              	
	Возврат ТекстЗапроса;
	
КонецФункции

&НаСервере
Функция ТекстЗапросаЗадачВРаботе() 
	
	ТекстЗапроса =  
	"ВЫБРАТЬ
	|	СоставСпринтов.Объект КАК Объект,
	|	ДокументЗадача.Статус КАК Статус,
	|	ПРЕДСТАВЛЕНИЕ(СоставСпринтов.Объект) КАК ОбъектПредставление
	|ПОМЕСТИТЬ ОтобранныеЗадачи
	|ИЗ
	|	Документ.Спринт КАК ДокументСпринт
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.СоставСпринтов КАК СоставСпринтов
	|		ПО (ДокументСпринт.Ссылка = &Спринт)
	|			И ДокументСпринт.Ссылка = СоставСпринтов.Спринт
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.Задача КАК ДокументЗадача
	|		ПО (СоставСпринтов.Объект = ДокументЗадача.Ссылка)
	|{ГДЕ
	|	ДокументЗадача.КомандаРазработчиков КАК КомандаРазработчиков}
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ОтобранныеЗадачи.Объект КАК Объект,
	|	ОтобранныеЗадачи.Статус КАК Статус,
	|	ОтобранныеЗадачи.ОбъектПредставление КАК ОбъектПредставление
	|ИЗ
	|	ОтобранныеЗадачи КАК ОтобранныеЗадачи
	|ГДЕ
	|	НЕ ОтобранныеЗадачи.Статус В (ЗНАЧЕНИЕ(Справочник.СтатусыОбъектов.Тестирование), ЗНАЧЕНИЕ(Справочник.СтатусыОбъектов.Решен), ЗНАЧЕНИЕ(Справочник.СтатусыОбъектов.Отклонен))
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ОтобранныеЗадачи.Объект,
	|	ОтобранныеЗадачи.Статус,
	|	ОтобранныеЗадачи.ОбъектПредставление
	|ИЗ
	|	ОтобранныеЗадачи КАК ОтобранныеЗадачи
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.СтатусыОбъектов КАК СтатусыОбъектов
	|		ПО ОтобранныеЗадачи.Статус = СтатусыОбъектов.Ссылка
	|ГДЕ
	|	НЕ СтатусыОбъектов.Родитель В (ЗНАЧЕНИЕ(Справочник.СтатусыОбъектов.ПустаяСсылка), ЗНАЧЕНИЕ(Справочник.СтатусыОбъектов.Тестирование), ЗНАЧЕНИЕ(Справочник.СтатусыОбъектов.Решен), ЗНАЧЕНИЕ(Справочник.СтатусыОбъектов.Отклонен))";
	
	Возврат ТекстЗапроса;
	
КонецФункции

&НаСервере
Функция ТекстЗапросаЗадачВТестировании()
	
	ТекстЗапроса =  
	"ВЫБРАТЬ
	|	СоставСпринтов.Объект КАК Объект,
	|	ДокументЗадача.Статус КАК Статус,
	|	ПРЕДСТАВЛЕНИЕ(СоставСпринтов.Объект) КАК ОбъектПредставление
	|ПОМЕСТИТЬ ОтобранныеЗадачи
	|ИЗ
	|	Документ.Спринт КАК ДокументСпринт
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.СоставСпринтов КАК СоставСпринтов
	|		ПО (ДокументСпринт.Ссылка = &Спринт)
	|			И ДокументСпринт.Ссылка = СоставСпринтов.Спринт
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.Задача КАК ДокументЗадача
	|		ПО (СоставСпринтов.Объект = ДокументЗадача.Ссылка)
	|{ГДЕ
	|	ДокументЗадача.КомандаРазработчиков КАК КомандаРазработчиков}
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ОтобранныеЗадачи.Объект КАК Объект,
	|	ОтобранныеЗадачи.ОбъектПредставление КАК ОбъектПредставление
	|ИЗ
	|	ОтобранныеЗадачи КАК ОтобранныеЗадачи
	|ГДЕ
	|	ОтобранныеЗадачи.Статус = ЗНАЧЕНИЕ(Справочник.СтатусыОбъектов.Тестирование)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ОтобранныеЗадачи.Объект,
	|	ОтобранныеЗадачи.ОбъектПредставление
	|ИЗ
	|	ОтобранныеЗадачи КАК ОтобранныеЗадачи
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.СтатусыОбъектов КАК СтатусыОбъектов
	|		ПО ОтобранныеЗадачи.Статус = СтатусыОбъектов.Ссылка
	|ГДЕ
	|	СтатусыОбъектов.Родитель = ЗНАЧЕНИЕ(Справочник.СтатусыОбъектов.Тестирование)";
	
	Возврат ТекстЗапроса;
	
КонецФункции

&НаСервере
Функция ТекстЗапросаЗадачЗакрыто()
	
	ТекстЗапроса =  
	"ВЫБРАТЬ
	|	СоставСпринтов.Объект КАК Объект,
	|	ДокументЗадача.Статус КАК Статус,
	|	ПРЕДСТАВЛЕНИЕ(СоставСпринтов.Объект) КАК ОбъектПредставление
	|ПОМЕСТИТЬ ОтобранныеЗадачи
	|ИЗ
	|	Документ.Спринт КАК ДокументСпринт
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.СоставСпринтов КАК СоставСпринтов
	|		ПО (ДокументСпринт.Ссылка = &Спринт)
	|			И ДокументСпринт.Ссылка = СоставСпринтов.Спринт
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.Задача КАК ДокументЗадача
	|		ПО (СоставСпринтов.Объект = ДокументЗадача.Ссылка)
	|{ГДЕ
	|	ДокументЗадача.КомандаРазработчиков КАК КомандаРазработчиков}
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ОтобранныеЗадачи.Объект КАК Объект,
	|	ОтобранныеЗадачи.ОбъектПредставление КАК ОбъектПредставление
	|ИЗ
	|	ОтобранныеЗадачи КАК ОтобранныеЗадачи
	|ГДЕ
	|	ОтобранныеЗадачи.Статус В (ЗНАЧЕНИЕ(Справочник.СтатусыОбъектов.Решен), ЗНАЧЕНИЕ(Справочник.СтатусыОбъектов.Отклонен))
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ОтобранныеЗадачи.Объект,
	|	ОтобранныеЗадачи.ОбъектПредставление
	|ИЗ
	|	ОтобранныеЗадачи КАК ОтобранныеЗадачи
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.СтатусыОбъектов КАК СтатусыОбъектов
	|		ПО ОтобранныеЗадачи.Статус = СтатусыОбъектов.Ссылка
	|ГДЕ
	|	СтатусыОбъектов.Родитель В (ЗНАЧЕНИЕ(Справочник.СтатусыОбъектов.Решен), ЗНАЧЕНИЕ(Справочник.СтатусыОбъектов.Отклонен))";
	
	Возврат ТекстЗапроса;
	
КонецФункции

&НаСервере
Функция ТекстЗапросаЗадачЗакрытоВовремя()
	
	ТекстЗапроса =  
	"ВЫБРАТЬ
	|	ДокументСпринт.Ссылка КАК Спринт,
	|	СоставСпринтов.Объект КАК Задача,
	|	ДокументЗадача.Статус КАК Статус,
	|	ДокументЗадача.КомандаРазработчиков КАК КомандаРазработчиков,
	|	ДокументСпринт.ДатаОкончания КАК ДатаОкончания
	|ПОМЕСТИТЬ ЗадачиСпринтовЗаПериод
	|ИЗ
	|	Документ.Спринт КАК ДокументСпринт
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.СоставСпринтов КАК СоставСпринтов
	|		ПО (ДокументСпринт.Ссылка = &Спринт)
	|			И ДокументСпринт.Ссылка = СоставСпринтов.Спринт
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.Задача КАК ДокументЗадача
	|		ПО (СоставСпринтов.Объект = ДокументЗадача.Ссылка)
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.СтатусыОбъектов КАК СтатусыОбъектов
	|		ПО (ДокументЗадача.Статус = СтатусыОбъектов.Ссылка)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ЗадачиСпринтовЗаПериод.Задача КАК Задача,
	|	МАКСИМУМ(СвойстваЗадач.Период) КАК Период,
	|	ЗадачиСпринтовЗаПериод.КомандаРазработчиков КАК КомандаРазработчиков
	|ПОМЕСТИТЬ ПоследнееИзменениеСвойстваОбъекта
	|ИЗ
	|	ЗадачиСпринтовЗаПериод КАК ЗадачиСпринтовЗаПериод
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.СвойстваЗадач КАК СвойстваЗадач
	|		ПО ЗадачиСпринтовЗаПериод.Задача = СвойстваЗадач.Объект
	|			И (СвойстваЗадач.Период <= ДОБАВИТЬКДАТЕ(ЗадачиСпринтовЗаПериод.ДатаОкончания, ДЕНЬ, 7))
	|
	|СГРУППИРОВАТЬ ПО
	|	ЗадачиСпринтовЗаПериод.Задача,
	|	ЗадачиСпринтовЗаПериод.КомандаРазработчиков
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ПоследнееИзменениеСвойстваОбъекта.Задача КАК Объект,
	|	ПРЕДСТАВЛЕНИЕ(ПоследнееИзменениеСвойстваОбъекта.Задача) КАК ОбъектПредставление
	|ИЗ
	|	РегистрСведений.СвойстваЗадач КАК СвойстваЗадач
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ПоследнееИзменениеСвойстваОбъекта КАК ПоследнееИзменениеСвойстваОбъекта
	|		ПО (ПоследнееИзменениеСвойстваОбъекта.Задача = СвойстваЗадач.Объект)
	|			И (ПоследнееИзменениеСвойстваОбъекта.Период = СвойстваЗадач.Период)
	|			И (СвойстваЗадач.Статус В (ЗНАЧЕНИЕ(Справочник.СтатусыОбъектов.Решен), ЗНАЧЕНИЕ(Справочник.СтатусыОбъектов.Отклонен)))
	|{ГДЕ
	|	ПоследнееИзменениеСвойстваОбъекта.КомандаРазработчиков КАК КомандаРазработчиков}";
	
	Возврат ТекстЗапроса;
	
КонецФункции

&НаСервере
Функция ТекстЗапросаКоличествоЗадач()
	
	ТекстЗапроса =  
	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ДокументЗадача.Ссылка КАК Объект,
	|	ПРЕДСТАВЛЕНИЕ(ДокументЗадача.Ссылка) КАК ОбъектПредставление
	|ИЗ
	|	Документ.Спринт КАК ДокументСпринт
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.СоставСпринтов КАК СоставСпринтов
	|		ПО ДокументСпринт.Ссылка = СоставСпринтов.Спринт
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.Задача КАК ДокументЗадача
	|		ПО (СоставСпринтов.Объект = ДокументЗадача.Ссылка)
	|ГДЕ
	|	ДокументСпринт.Ссылка = &Спринт
	|{ГДЕ
	|	ДокументЗадача.КомандаРазработчиков КАК КомандаРазработчиков}";
	
	Возврат ТекстЗапроса;

КонецФункции

&НаСервере
Функция ТекстЗапросаКоличествоЗаявок()
	
	ТекстЗапроса =  
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ РАЗЛИЧНЫЕ
	|	ДокументЗадача.ОбъектОснование КАК Объект,
	|	ПРЕДСТАВЛЕНИЕ(ДокументЗадача.ОбъектОснование) КАК ОбъектПредставление
	|ИЗ
	|	Документ.Спринт КАК ДокументСпринт
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.СоставСпринтов КАК СоставСпринтов
	|		ПО ДокументСпринт.Ссылка = СоставСпринтов.Спринт
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.Задача КАК ДокументЗадача
	|		ПО (СоставСпринтов.Объект = ДокументЗадача.Ссылка)
	|			И (ДокументЗадача.ОбъектОснование ССЫЛКА Документ.ЗаявкаНаРазработку)
	|ГДЕ
	|	ДокументСпринт.Ссылка = &Спринт
	|{ГДЕ
	|	ДокументЗадача.КомандаРазработчиков КАК КомандаРазработчиков}";
	
	Возврат ТекстЗапроса;
	
КонецФункции

&НаСервере
Процедура УстановитьЗаголовокФормы(Знач Параметры)
	
	Колонка = Параметры.Колонка;
	ВПараметрахЕстьФилиал = Параметры.Свойство("Филиал") И НЕ Параметры.Филиал = Неопределено;
	ВПараметрахЕстьСпринт = Параметры.Свойство("Спринт");
	
	Если ВПараметрахЕстьФилиал И ВПараметрахЕстьСпринт И Колонка = "ПревышениеПлана" Тогда
		ОбъектПредставления = Параметры.Спринт;
		Шаблон = "%1: Превышение плана.";
		
	ИначеЕсли ВПараметрахЕстьФилиал И (Колонка = "ПлановоеВремя" ИЛИ Колонка = "ФактическоеВремя") Тогда
		ОбъектПредставления = Параметры.Филиал;
		Шаблон = "%1: Плановое и фактическое время.";
		
	ИначеЕсли Параметры.Свойство("Спринт") Тогда
		
		ОбъектПредставления = Параметры.Спринт;
		
		Если Колонка = "КоличествоЗаявок" Тогда
			Шаблон = "%1: Количество заявок.";
		ИначеЕсли Колонка = "КоличествоЗадач" Тогда
			Шаблон = "%1: Количество задач.";
		ИначеЕсли Колонка = "ЗадачЗакрыто" Тогда
			Шаблон = "%1: Закрыто задач на текущий момент.";
		ИначеЕсли Колонка = "ЗадачЗакрытоВовремя" Тогда
			Шаблон = "%1: Закрыто задач на момент окончания спринта.";
		ИначеЕсли Колонка = "ЗадачВРаботе" Тогда
			Шаблон = "%1: Задачи в работе.";
		ИначеЕсли Колонка = "ЗадачВТестировании" Тогда
			Шаблон = "%1: Задачи в тестировании.";
		ИначеЕсли Колонка = "ВозвратовПоЗадачам" Тогда
			Шаблон = "%1: Количество возвратов.";
		Иначе
			Шаблон = "";
		КонецЕсли;
	Иначе
		Шаблон = "";
	КонецЕсли;
	
	Если ЗначениеЗаполнено(Шаблон) Тогда
		ПредставлениеОбъекта = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ОбъектПредставления, "Представление");
		ЭтотОбъект.Заголовок = СтрШаблон(Шаблон, ПредставлениеОбъекта);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти
