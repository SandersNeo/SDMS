///////////////////////////////////////////////////////////////////////////////////////////////////////
// Copyright (c) 2025, ООО ДНС Технологии
// SDMS (Software Development Management System) — это корпоративная система учета разработки и управления проектами 
// Все права защищены. Эта программа и сопроводительные материалы предоставляются 
// в соответствии с условиями лицензии General Public License (GNU GPL v3)
// Текст лицензии доступен по ссылке:
// https://www.gnu.org/licenses/gpl-3.0.html
///////////////////////////////////////////////////////////////////////////////////////////////////////

#Область ОбработчикиСобытийЭлементовФормы

&НаКлиенте
Процедура ГруппаЗаказчиковНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	ПараметрыОткрытия = ИнтерфейсПриложенияКлиент.СформироватьПараметрыОткрытия();
	ПараметрыОткрытия.ГруппыЗаказчиков.Значение = ГруппаЗаказчиков.ВыгрузитьЗначения();
	ПараметрыОткрытия.ГруппыЗаказчиков.Использование = Истина;
	
	ДополнительныеПараметры = Новый Структура("ИмяРеквизита, КлючСтруктуры", "ГруппаЗаказчиков", "ВыбранныеГруппыЗаказчиков");
	
	ИнтерфейсПриложенияКлиент.ОткрытьФормуНастройкиИнструментов(ЭтотОбъект, ПараметрыОткрытия, , ДополнительныеПараметры);
	
КонецПроцедуры

&НаКлиенте
Процедура ПродуктыНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	ПараметрыОткрытия = ИнтерфейсПриложенияКлиент.СформироватьПараметрыОткрытия();
	ПараметрыОткрытия.Продукты.Значение = Продукты;
	ПараметрыОткрытия.Продукты.Использование = Истина;
	
	ДополнительныеПараметры = Новый Структура("ИмяРеквизита, КлючСтруктуры", "Продукты", "ВыбранныеПродукты");
	
	ИнтерфейсПриложенияКлиент.ОткрытьФормуНастройкиИнструментов(ЭтотОбъект, ПараметрыОткрытия, , ДополнительныеПараметры);
	
КонецПроцедуры

&НаКлиенте
Процедура СистемыРазработкиНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	ПараметрыОткрытия = ИнтерфейсПриложенияКлиент.СформироватьПараметрыОткрытия();
	ПараметрыОткрытия.Системы.Значение = СистемыРазработки;
	ПараметрыОткрытия.Системы.Использование = Истина;
	
	ДополнительныеПараметры = Новый Структура("ИмяРеквизита, КлючСтруктуры", "СистемыРазработки", "ВыбранныеСистемы");
	
	ИнтерфейсПриложенияКлиент.ОткрытьФормуНастройкиИнструментов(ЭтотОбъект, ПараметрыОткрытия, , ДополнительныеПараметры);
	
КонецПроцедуры

&НаКлиенте
Процедура СтатусыНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	ПараметрыОткрытия = ИнтерфейсПриложенияКлиент.СформироватьПараметрыОткрытия();
	ПараметрыОткрытия.Статусы.Значение = Статусы;
	ПараметрыОткрытия.Статусы.Использование = Истина;
	ПараметрыОткрытия.Статусы.Вставить("ДоступныеСтатусы", ДоступныеСтатусыОтбора());
	
	ДополнительныеПараметры = Новый Структура("ИмяРеквизита, КлючСтруктуры", "Статусы", "ВыбранныеСтатусы");
	
	ИнтерфейсПриложенияКлиент.ОткрытьФормуНастройкиИнструментов(ЭтотОбъект, ПараметрыОткрытия, , ДополнительныеПараметры);
	
КонецПроцедуры

&НаКлиенте
Процедура ТаблицаЗаявокВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	НайденнаяСтрока = ДеревоЗаявок.НайтиПоИдентификатору(ВыбраннаяСтрока);
	
	Если НайденнаяСтрока <> Неопределено Тогда
		Если Поле = Элементы.ДеревоЗаявокКомментарийКИзменениюСтатуса Тогда
			ПоказатьЗначение(, НайденнаяСтрока.КомментарийКИзменениюСтатуса);
		Иначе
			ПоказатьЗначение(, НайденнаяСтрока.Заявка);
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ФилиалыНачалоВыбора(Элемент, ДанныеВыбора, ВыборДобавлением, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	ОписаниеОповещения = Новый ОписаниеОповещения("ПослеВыбораФилиалов", ЭтотОбъект);
			
	ПараметрыОткрытия = Новый Структура;
	ПараметрыОткрытия.Вставить("ТолькоИТФилиалы", Истина);
	ПараметрыОткрытия.Вставить("ВыбранныеФилиалы", Филиалы);

	ОткрытьФорму("Справочник.Филиалы.Форма.МножественныйВыбор", ПараметрыОткрытия, ЭтотОбъект, ЭтотОбъект.УникальныйИдентификатор,,, ОписаниеОповещения,
		РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);	
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура Настройки(Команда)
	
	НастройкиОткрыты = Элементы.Настройки.Пометка; 
		
	Элементы.Настройки.Пометка = НЕ НастройкиОткрыты;
	Элементы.ГруппаНастройки.Видимость = НЕ НастройкиОткрыты;
	
	СкрыватьНастройкиПослеФормированияОтчета = НЕ СкрыватьНастройкиПослеФормированияОтчета;
	
КонецПроцедуры

&НаКлиенте
Процедура Сформировать(Команда)
	
	Если Не ЗначениеЗаполнено(Период) ИЛИ Направление.Пустая() ИЛИ СистемыРазработки.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;
	
	СформироватьТаблицуЗаявок();
			
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Функция ДоступныеСтатусыОтбора()
	
	Результат = Новый Массив;
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	СтатусыОбъектов.Ссылка КАК Ссылка
	|ИЗ
	|	Справочник.СтатусыОбъектов КАК СтатусыОбъектов
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.СтатусыОбъектов.ПричиныИзмененияСтатуса КАК СтатусыОбъектовСПричинойИзменения
	|		ПО СтатусыОбъектов.Ссылка = СтатусыОбъектовСПричинойИзменения.Ссылка";
	
	ВыборкаСтатусыСПричинойИзменения = Запрос.Выполнить().Выбрать();
	
	Пока ВыборкаСтатусыСПричинойИзменения.Следующий() Цикл
		Результат.Добавить(ВыборкаСтатусыСПричинойИзменения.Ссылка);
	КонецЦикла;
	
	Возврат Результат;
	
КонецФункции

&НаКлиенте
Процедура ПослеВыбораФилиалов(Знач ВыбранныеФилиалы, Знач ДополнительныеПараметры) Экспорт
	
	Если ВыбранныеФилиалы <> Неопределено Тогда
		Филиалы = ВыбранныеФилиалы;
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура СформироватьТаблицуЗаявок()
	
	КоллекцияЭлементовДерева = ДеревоЗаявок.ПолучитьЭлементы();
	КоллекцияЭлементовДерева.Очистить();
	
	// Не понятно использование в запросе РС ПричиныИзмененияСтатусаОбъектов, 
	// т.к. в нем не заполнена система и внутреннее соединение с ним по системе 
	// полностью исключает все записи из этого регистра
	// Так же в регистре хранится идентификатор комментария в виде Уникального идентификатора, 
	// который будет удален из остальных регистров
	Запрос = Новый Запрос;
	Запрос.Текст =
	#Область Текст_Запроса
	"ВЫБРАТЬ
	|	ИсторияСтатусовЗаявокПоСистемам.Заявка КАК Заявка,
	|	ИсторияСтатусовЗаявокПоСистемам.Система КАК Система,
	|	ИсторияСтатусовЗаявокПоСистемам.Период КАК Период
	|ПОМЕСТИТЬ ЗаявкиНаРассмотрении
	|ИЗ
	|	РегистрСведений.ИсторияСтатусовЗаявокПоСистемам КАК ИсторияСтатусовЗаявокПоСистемам
	|ГДЕ
	|	ИсторияСтатусовЗаявокПоСистемам.Статус = ЗНАЧЕНИЕ(Справочник.СтатусыОбъектов.Рассмотрение)
	|	И ИсторияСтатусовЗаявокПоСистемам.Период МЕЖДУ &ДатаНачала И &ДатаОкончания
	|	И ИсторияСтатусовЗаявокПоСистемам.Система В(&СистемыРазработки)
	|	И ВЫБОР
	|			КОГДА &ФильтрПоСтатусам
	|				ТОГДА ЗНАЧЕНИЕ(Справочник.СтатусыОбъектов.НаДоработку) В (&ВыбранныеСтатусы)
	|			ИНАЧЕ ИСТИНА
	|		КОНЕЦ
	|
	|ОБЪЕДИНИТЬ
	|
	|ВЫБРАТЬ
	|	ИсторияСтатусовЗаявокПоСистемамСрезПоследних.Заявка,
	|	ИсторияСтатусовЗаявокПоСистемамСрезПоследних.Система,
	|	ИсторияСтатусовЗаявокПоСистемамСрезПоследних.Период
	|ИЗ
	|	РегистрСведений.ИсторияСтатусовЗаявокПоСистемам.СрезПоследних(&ДатаНачала, Система В (&СистемыРазработки)) КАК ИсторияСтатусовЗаявокПоСистемамСрезПоследних
	|ГДЕ
	|	ИсторияСтатусовЗаявокПоСистемамСрезПоследних.Статус = ЗНАЧЕНИЕ(Справочник.СтатусыОбъектов.Рассмотрение)
	|	И ВЫБОР
	|			КОГДА &ФильтрПоСтатусам
	|				ТОГДА ЗНАЧЕНИЕ(Справочник.СтатусыОбъектов.НаДоработку) В (&ВыбранныеСтатусы)
	|			ИНАЧЕ ИСТИНА
	|		КОНЕЦ
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ИсторияСтатусовЗаявокПоСистемамСрезПоследних.Заявка КАК Заявка,
	|	ЗаявкаНаРазработку.Автор КАК Автор
	|ПОМЕСТИТЬ ВсегоЗаявокЗаПериод
	|ИЗ
	|	РегистрСведений.ИсторияСтатусовЗаявокПоСистемам.СрезПоследних(
	|			&ДатаОкончания,
	|			(Заявка, Система) В
	|				(ВЫБРАТЬ
	|					ЗаявкиНаРассмотрении.Заявка КАК Заявка,
	|					ЗаявкиНаРассмотрении.Система КАК Система
	|				ИЗ
	|					ЗаявкиНаРассмотрении КАК ЗаявкиНаРассмотрении)) КАК ИсторияСтатусовЗаявокПоСистемамСрезПоследних
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ЗаявкиНаРассмотрении КАК ЗаявкиНаРассмотрении
	|		ПО ИсторияСтатусовЗаявокПоСистемамСрезПоследних.Заявка = ЗаявкиНаРассмотрении.Заявка
	|			И ИсторияСтатусовЗаявокПоСистемамСрезПоследних.Система = ЗаявкиНаРассмотрении.Система
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ЗаявкаНаРазработку КАК ЗаявкаНаРазработку
	|		ПО ИсторияСтатусовЗаявокПоСистемамСрезПоследних.Заявка = ЗаявкаНаРазработку.Ссылка
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ЗаявкаНаРазработку.СистемыРазработки КАК ЗаявкаНаРазработкуСистемыРазработки
	|		ПО ИсторияСтатусовЗаявокПоСистемамСрезПоследних.Заявка = ЗаявкаНаРазработкуСистемыРазработки.Ссылка
	|			И ИсторияСтатусовЗаявокПоСистемамСрезПоследних.Система = ЗаявкаНаРазработкуСистемыРазработки.Система
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.Пользователи КАК Пользователи
	|		ПО (ЗаявкаНаРазработку.Автор = Пользователи.Ссылка)
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ГруппыЗаказчиковНаправлений КАК ГруппыЗаказчиковНаправлений
	|		ПО (ЗаявкаНаРазработкуСистемыРазработки.ГруппаЗаказчиков = ГруппыЗаказчиковНаправлений.Ссылка)
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.Продукты КАК Продукты
	|		ПО (ЗаявкаНаРазработку.Продукт = Продукты.Ссылка)
	|ГДЕ
	|	ИсторияСтатусовЗаявокПоСистемамСрезПоследних.Период <> ЗаявкиНаРассмотрении.Период
	|	И ЗаявкаНаРазработку.Направление В(&Направление)
	|	И ВЫБОР
	|			КОГДА НЕ &ОтборПродуктГруппаЗаказчиков
	|				ТОГДА ИСТИНА
	|			ИНАЧЕ ЗаявкаНаРазработку.Продукт В (&ПродуктыИЗаказчикиЗаявки)
	|					ИЛИ ЕСТЬNULL(ЗаявкаНаРазработкуСистемыРазработки.ГруппаЗаказчиков, ЗНАЧЕНИЕ(Справочник.ГруппыЗаказчиковНаправлений.ПустаяСсылка)) В (&ПродуктыИЗаказчикиЗаявки)
	|		КОНЕЦ
	|	И ВЫБОР
	|			КОГДА НЕ &ОтборПоФилиалу
	|				ТОГДА ИСТИНА
	|			ИНАЧЕ Пользователи.Филиал В (&Филиалы)
	|		КОНЕЦ
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ РАЗЛИЧНЫЕ
	|	ПричиныИзмененияСтатусаОбъектов.НомерПричины КАК НомерПричины,
	|	ПричиныИзмененияСтатусаОбъектов.Объект КАК Объект,
	|	ЗаявкаНаРазработку.Автор КАК Автор,
	|	ПричиныИзмененияСтатусаОбъектов.ПричинаИзмененияСтатуса КАК ПричинаИзмененияСтатуса,
	|	СтатусыОбъектов.ИндексКартинки КАК ИндексКартинки,
	|	ПричиныИзмененияСтатусаОбъектов.Комментарий КАК Комментарий,
	|	ПричиныИзмененияСтатусаОбъектов.Статус КАК Статус
	|ПОМЕСТИТЬ ПричиныИзмененияОтобранныхСтатусов
	|ИЗ
	|	РегистрСведений.ПричиныИзмененияСтатусаОбъектов КАК ПричиныИзмененияСтатусаОбъектов
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ЗаявкаНаРазработку КАК ЗаявкаНаРазработку
	|		ПО ПричиныИзмененияСтатусаОбъектов.Объект = ЗаявкаНаРазработку.Ссылка
	|			И (ЗаявкаНаРазработку.Направление В (&Направление))
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ЗаявкаНаРазработку.СистемыРазработки КАК ЗаявкаНаРазработкуСистемыРазработки
	|		ПО ПричиныИзмененияСтатусаОбъектов.Объект = ЗаявкаНаРазработкуСистемыРазработки.Ссылка
	|			И ПричиныИзмененияСтатусаОбъектов.Система = ЗаявкаНаРазработкуСистемыРазработки.Система
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.СтатусыОбъектов КАК СтатусыОбъектов
	|		ПО ПричиныИзмененияСтатусаОбъектов.Статус = СтатусыОбъектов.Ссылка
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.Пользователи КАК Пользователи
	|		ПО (ЗаявкаНаРазработку.Автор = Пользователи.Ссылка)
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ГруппыЗаказчиковНаправлений КАК ГруппыЗаказчиковНаправлений
	|		ПО (ЗаявкаНаРазработкуСистемыРазработки.ГруппаЗаказчиков = ГруппыЗаказчиковНаправлений.Ссылка)
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.Продукты КАК Продукты
	|		ПО (ЗаявкаНаРазработку.Продукт = Продукты.Ссылка)
	|ГДЕ
	|	ПричиныИзмененияСтатусаОбъектов.Система В(&СистемыРазработки)
	|	И ВЫБОР
	|			КОГДА &ФильтрПоСтатусам
	|				ТОГДА ПричиныИзмененияСтатусаОбъектов.Статус В (&ВыбранныеСтатусы)
	|			ИНАЧЕ ИСТИНА
	|		КОНЕЦ
	|	И ПричиныИзмененияСтатусаОбъектов.Период МЕЖДУ &ДатаНачала И &ДатаОкончания
	|	И ВЫБОР
	|			КОГДА НЕ &ОтборПродуктГруппаЗаказчиков
	|				ТОГДА ИСТИНА
	|			ИНАЧЕ ЗаявкаНаРазработку.Продукт В (&ПродуктыИЗаказчикиЗаявки)
	|					ИЛИ ЕСТЬNULL(ЗаявкаНаРазработкуСистемыРазработки.ГруппаЗаказчиков, ЗНАЧЕНИЕ(Справочник.ГруппыЗаказчиковНаправлений.ПустаяСсылка)) В (&ПродуктыИЗаказчикиЗаявки)
	|		КОНЕЦ
	|	И ВЫБОР
	|			КОГДА НЕ &ОтборПоФилиалу
	|				ТОГДА ИСТИНА
	|			ИНАЧЕ Пользователи.Филиал В (&Филиалы)
	|		КОНЕЦ
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ВсегоЗаявокЗаПериод.Заявка КАК Объект,
	|	ВсегоЗаявокЗаПериод.Автор КАК Заказчик,
	|	ЕСТЬNULL(ПричиныИзмененияСтатусаОбъектов.Комментарий, """") КАК Комментарий 
	|ПОМЕСТИТЬ РассмотренныеСКомментарием
	|ИЗ
	|	ВсегоЗаявокЗаПериод КАК ВсегоЗаявокЗаПериод
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ПричиныИзмененияСтатусаОбъектов КАК ПричиныИзмененияСтатусаОбъектов
	|		ПО ВсегоЗаявокЗаПериод.Заявка = ПричиныИзмененияСтатусаОбъектов.Объект
	|			И (ПричиныИзмененияСтатусаОбъектов.Статус = ЗНАЧЕНИЕ(Справочник.СтатусыОбъектов.НаДоработку))
	|;
	|
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	""Всего заявок рассмотрено за период"" КАК ПричинаИзмененияСтатуса,
	|	РассмотренныеСКомментарием.Объект КАК Объект,
	|	РассмотренныеСКомментарием.Заказчик КАК Заказчик,
	|	ЕСТЬNULL(Комментарии.Комментарий, """") КАК КомментарийКИзменениюСтатуса
	|ИЗ
	|	РассмотренныеСКомментарием КАК РассмотренныеСКомментарием
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.Комментарии КАК Комментарии
	|		ПО (РассмотренныеСКомментарием.Объект = Комментарии.Объект)
	|			И (РассмотренныеСКомментарием.Комментарий = Комментарии.Идентификатор)
	|ИТОГИ
	|	КОЛИЧЕСТВО(РАЗЛИЧНЫЕ Объект)
	|ПО
	|	ПричинаИзмененияСтатуса
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ПричиныИзмененияОтобранныхСтатусов.НомерПричины КАК НомерПричины,
	|	ПричиныИзмененияОтобранныхСтатусов.ПричинаИзмененияСтатуса КАК ПричинаИзмененияСтатуса,
	|	ПричиныИзмененияОтобранныхСтатусов.ИндексКартинки КАК ИндексКартинки,
	|	ПричиныИзмененияОтобранныхСтатусов.Объект КАК Объект,
	|	ПричиныИзмененияОтобранныхСтатусов.Автор КАК Заказчик,
	|	ЕСТЬNULL(Комментарии.Комментарий, """") КАК КомментарийКИзменениюСтатуса,
	|	ПричиныИзмененияОтобранныхСтатусов.Статус КАК Статус
	|ИЗ
	|	ПричиныИзмененияОтобранныхСтатусов КАК ПричиныИзмененияОтобранныхСтатусов
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.Комментарии КАК Комментарии
	|		ПО ПричиныИзмененияОтобранныхСтатусов.Объект = Комментарии.Объект
	|			И ПричиныИзмененияОтобранныхСтатусов.Комментарий = Комментарии.Идентификатор
	|
	|УПОРЯДОЧИТЬ ПО
	|	ПричиныИзмененияОтобранныхСтатусов.Статус,
	|	ПричиныИзмененияОтобранныхСтатусов.НомерПричины,
	|	ПричиныИзмененияОтобранныхСтатусов.Объект
	|ИТОГИ
	|	МАКСИМУМ(ИндексКартинки),
	|	КОЛИЧЕСТВО(РАЗЛИЧНЫЕ Объект)
	|ПО
	|	Статус,
	|	ПричинаИзмененияСтатуса";
	#КонецОбласти

	ПродуктыИЗаказчикиЗаявки = Новый Массив;
	
	Для Каждого Элемент Из ГруппаЗаказчиков Цикл
		ПродуктыИЗаказчикиЗаявки.Добавить(Элемент.Значение);
	КонецЦикла;
	
	Для Каждого Элемент Из Продукты Цикл
		ПродуктыИЗаказчикиЗаявки.Добавить(Элемент.Значение);
	КонецЦикла;
	
	Запрос.УстановитьПараметр("ДатаНачала", Период.ДатаНачала);
	Запрос.УстановитьПараметр("ДатаОкончания", Период.ДатаОкончания);
	Запрос.УстановитьПараметр("Направление", Направление);
	Запрос.УстановитьПараметр("СистемыРазработки", СистемыРазработки);
	Запрос.УстановитьПараметр("ФильтрПоСтатусам", (Статусы.Количество() > 0));
	Запрос.УстановитьПараметр("ВыбранныеСтатусы", Статусы);
	Запрос.УстановитьПараметр("ОтборПродуктГруппаЗаказчиков", (ПродуктыИЗаказчикиЗаявки.Количество() > 0));
	Запрос.УстановитьПараметр("ПродуктыИЗаказчикиЗаявки", ПродуктыИЗаказчикиЗаявки);
	Запрос.УстановитьПараметр("ОтборПоФилиалу", (Филиалы.Количество() > 0));
	Запрос.УстановитьПараметр("Филиалы", Филиалы);
	Запрос.УстановитьПараметр("ПустойИдентификатор", "00000000-0000-0000-0000-000000000000");
	
	Пакеты = Запрос.ВыполнитьПакет();
	КоличествоПакетов = Пакеты.ВГраница();
	
	РезультатЗапроса = Пакеты.Получить(КоличествоПакетов);
	РассмотренныеЗаявки = Пакеты.Получить(КоличествоПакетов - 1);

	ВыборкаРассмотренные = РассмотренныеЗаявки.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	
	КоличествоРассмотренных = 1; // чтобы не было деления на 0
	Если ВыборкаРассмотренные.Следующий() Тогда
		КоличествоРассмотренных = ВыборкаРассмотренные.Объект;
	КонецЕсли;
	
	Если НЕ РезультатЗапроса.Пустой() Тогда
				
		ВыборкаСтатусЗаявки = РезультатЗапроса.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
		
		Пока ВыборкаСтатусЗаявки.Следующий() Цикл	
			
			НоваяСтрока = КоллекцияЭлементовДерева.Добавить();
			НоваяСтрока.ИндексКартинки = ВыборкаСтатусЗаявки.ИндексКартинки;
			НоваяСтрока.Статус = ВыборкаСтатусЗаявки.Статус;
			НоваяСтрока.КоличествоЗаявок = ВыборкаСтатусЗаявки.Объект;
			
			КоллекцияСтрокВторогоУровня = НоваяСтрока.ПолучитьЭлементы();
			ВыборкаПричиныПереводаСтатуса = ВыборкаСтатусЗаявки.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
			

			Пока ВыборкаПричиныПереводаСтатуса.Следующий() Цикл
				Если ВыборкаСтатусЗаявки.Статус = Справочники.СтатусыОбъектов.НаДоработку Тогда 
					ПроцентЗаявок = (ВыборкаПричиныПереводаСтатуса.Объект / КоличествоРассмотренных) * 100;
					Если ПроцентЗаявок > 100 Тогда
						ПроцентЗаявок = Неопределено;
					КонецЕсли;
				КонецЕсли;
				
				ЗаполнитьСтрокиРазделов(ВыборкаПричиныПереводаСтатуса, КоллекцияСтрокВторогоУровня, ПроцентЗаявок);
			КонецЦикла;
			
			Если ВыборкаСтатусЗаявки.Статус = Справочники.СтатусыОбъектов.НаДоработку Тогда
				ПроцентЗаявок = (ВыборкаСтатусЗаявки.Объект / КоличествоРассмотренных) * 100;
				
				Если ПроцентЗаявок > 100 Тогда
					ПроцентЗаявок = Неопределено;
				КонецЕсли;
				
				НоваяСтрока.ПроцентЗаявок = ПроцентЗаявок;
				ЗаполнитьСтрокиРазделов(ВыборкаРассмотренные, КоллекцияСтрокВторогоУровня);
			КонецЕсли;
			
			ПроцентЗаявок = Неопределено; 
		КонецЦикла;
		
	ИначеЕсли НЕ РассмотренныеЗаявки.Пустой() Тогда
		НоваяСтрока = КоллекцияЭлементовДерева.Добавить();
		
		НоваяСтрока.ИндексКартинки =  Справочники.СтатусыОбъектов.НаДоработку.ИндексКартинки;
		НоваяСтрока.Статус = Справочники.СтатусыОбъектов.НаДоработку;
		
		КоллекцияСтрокВторогоУровня = НоваяСтрока.ПолучитьЭлементы();			
		ЗаполнитьСтрокиРазделов(ВыборкаРассмотренные, КоллекцияСтрокВторогоУровня);
	КонецЕсли;
	
	Если СкрыватьНастройкиПослеФормированияОтчета Тогда
		Элементы.Настройки.Пометка = Ложь;
		Элементы.ГруппаНастройки.Видимость = Ложь; 
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьСтрокиРазделов(ВыборкаРаздел, КоллекцияСтрокВторогоУровня, ПроцентЗаявок = Неопределено)
	
	НоваяСтрока = КоллекцияСтрокВторогоУровня.Добавить();
	НоваяСтрока.ПричинаИзмененияСтатуса = ВыборкаРаздел.ПричинаИзмененияСтатуса; 				
	НоваяСтрока.КоличествоЗаявок = ВыборкаРаздел.Объект;
	НоваяСтрока.ПроцентЗаявок = ПроцентЗаявок;
	
	Выборка = ВыборкаРаздел.Выбрать();
	КоллекцияСтрокТретьегоУровня = НоваяСтрока.ПолучитьЭлементы();
	
	Пока Выборка.Следующий() Цикл
		КомментарийКИзменениюСтатуса = КонвертацияОписания.ОчиститьHTMLОтТегов(Выборка.КомментарийКИзменениюСтатуса);
		
		ПоискКомментария = СтрНайти(КомментарийКИзменениюСтатуса, "с комментарием:");
		Если ПоискКомментария <> 0 Тогда
			НачалоКомментария = ПоискКомментария + 16;
			КомментарийКИзменениюСтатуса = СокрЛП(Сред(КомментарийКИзменениюСтатуса, НачалоКомментария));
		Иначе
			ПоискКомментария = СтрНайти(КомментарийКИзменениюСтатуса, "по причине");
			НачалоКомментария = ПоискКомментария + 11;
			КомментарийКИзменениюСтатуса = СокрЛП(Сред(КомментарийКИзменениюСтатуса, НачалоКомментария));
		КонецЕсли;
		
		НоваяСтрока = КоллекцияСтрокТретьегоУровня.Добавить();
		
		НоваяСтрока.КомментарийКИзменениюСтатуса = КомментарийКИзменениюСтатуса;
		НоваяСтрока.Заявка = Выборка.Объект;
		НоваяСтрока.Заказчик = Выборка.Заказчик;
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти
