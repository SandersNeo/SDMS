///////////////////////////////////////////////////////////////////////////////////////////////////////
// Copyright (c) 2025, ООО ДНС Технологии
// SDMS (Software Development Management System) — это корпоративная система учета разработки и управления проектами 
// Все права защищены. Эта программа и сопроводительные материалы предоставляются 
// в соответствии с условиями лицензии General Public License (GNU GPL v3)
// Текст лицензии доступен по ссылке:
// https://www.gnu.org/licenses/gpl-3.0.html
///////////////////////////////////////////////////////////////////////////////////////////////////////

#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
		
	// По умолчанию период с начала текущего месяца по текущий день
	ТекущаяДата = ТекущаяДатаСеанса();
	Период.ДатаНачала    = НачалоМесяца(ТекущаяДата);
	Период.ДатаОкончания = КонецДня(ТекущаяДата);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийКомандФорм

&НаКлиенте
Процедура СформироватьОтчет(Команда)
	
	Если ЗначениеЗаполнено(Период.ДатаНачала) И ЗначениеЗаполнено(Период.ДатаОкончания) 
		И Период.ДатаОкончания > Период.ДатаНачала Тогда
		
		СформироватьОтчетНаСервере();
	Иначе
		ПоказатьПредупреждение(, "Неверно задан период отчета.");
	КонецЕсли;	
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервереБезКонтекста
Функция ПолучитьПакетыАнализДней(Знач ДатаНачала, Знач ДатаОкончания)
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	Задача.Ссылка КАК Ссылка,
	|	Задача.СистемаУчета КАК СистемаУчета,
	|	Задача.Направление КАК Направление,
	|	Задача.ДатаНачалаРазработки КАК ДатаНачалаРазработки,
	|	ВЫБОР
	|		КОГДА Задача.ДатаПоследнегоТестирования = ДАТАВРЕМЯ(1, 1, 1)
	|			ТОГДА &ДатаОкончания
	|		ИНАЧЕ Задача.ДатаПоследнегоТестирования
	|	КОНЕЦ КАК ДатаПоследнегоТестирования,
	|	ЗаявкаНаРазработку.Ссылка КАК СсылкаЗаявка
	|ПОМЕСТИТЬ втИсходнаяТаблица
	|ИЗ
	|	Документ.Задача КАК Задача
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ЗаявкаНаРазработку КАК ЗаявкаНаРазработку
	|		ПО Задача.ОбъектОснование = ЗаявкаНаРазработку.Ссылка
	|ГДЕ
	|	(Задача.ДатаНачалаРазработки МЕЖДУ &ДатаНачала И &ДатаОкончания
	|			ИЛИ Задача.ДатаПоследнегоТестирования МЕЖДУ &ДатаНачала И &ДатаОкончания)
	|	И НЕ Задача.ДатаНачалаРазработки = ДАТАВРЕМЯ(1, 1, 1)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	вз.Ссылка КАК СсылкаЗаявка,
	|	вз.Направление КАК Направление,
	|	вз.Филиал КАК Филиал,
	|	вз.Статус КАК СтатусЗаявки,
	|	Задача.ДатаПоследнегоТестирования КАК ДатаПоследнегоТестирования,
	|	Задача.ДатаЗакрытия КАК ДатаЗакрытия,
	|	Задача.Ссылка КАК СсылкаЗадача,
	|	ЕСТЬNULL(Задача.СистемаУчета, ЕСТЬNULL(ЗаявкаНаРазработкуСистемыРазработки.Система, ЗНАЧЕНИЕ(Документ.ЗаявкаНаРазработку.ПустаяСсылка))) КАК СистемаУчета,
	|	НАЧАЛОПЕРИОДА(вз.Дата, ДЕНЬ) КАК Дата,
	|	НАЧАЛОПЕРИОДА(ЕСТЬNULL(Задача.Дата, &ДатаОкончания), ДЕНЬ) КАК ДатаСозданияЗадачи
	|ПОМЕСТИТЬ втИсходнаяТаблицаПоЗаявкам
	|ИЗ
	|	(ВЫБРАТЬ
	|		ЗаявкаНаРазработку.Ссылка КАК Ссылка,
	|		ЗаявкаНаРазработку.Направление КАК Направление,
	|		СтатусыЗаявокПоГруппамРазработки.Филиал КАК Филиал,
	|		СтатусыЗаявокПоГруппамРазработки.Статус КАК Статус,
	|		ЗаявкаНаРазработку.Дата КАК Дата
	|	ИЗ
	|		Документ.ЗаявкаНаРазработку КАК ЗаявкаНаРазработку
	|			ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.СтатусыЗаявокПоГруппамРазработки КАК СтатусыЗаявокПоГруппамРазработки
	|			ПО ЗаявкаНаРазработку.Ссылка = СтатусыЗаявокПоГруппамРазработки.Заявка
	|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.Филиалы КАК Филиалы
	|			ПО (Филиалы.Ссылка = СтатусыЗаявокПоГруппамРазработки.Филиал)
	|				И (Филиалы.Родитель <> ЗНАЧЕНИЕ(Справочник.Филиалы.ПустаяСсылка))
	|	ГДЕ
	|		ЗаявкаНаРазработку.Дата МЕЖДУ &ДатаНачала И &ДатаОкончания) КАК вз
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.Задача КАК Задача
	|		ПО вз.Ссылка = Задача.ОбъектОснование
	|			И вз.Филиал = Задача.Филиал
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.ЗаявкаНаРазработку.СистемыРазработки КАК ЗаявкаНаРазработкуСистемыРазработки
	|		ПО вз.Ссылка = ЗаявкаНаРазработкуСистемыРазработки.Ссылка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Вз.СистемаУчета КАК Группировка,
	|	ВЫРАЗИТЬ(СУММА(Вз.РазностьДнейРазработка) КАК ЧИСЛО(6, 2)) КАК РазностьДнейРазработка,
	|	ВЫРАЗИТЬ(СУММА(Вз.РазностьДнейРассмотрение) КАК ЧИСЛО(6, 2)) КАК РазностьДнейРассмотрение
	|ИЗ
	|	(ВЫБРАТЬ
	|		ВложенныйЗапрос.СистемаУчета КАК СистемаУчета,
	|		СРЕДНЕЕ(РАЗНОСТЬДАТ(ВложенныйЗапрос.ДатаНачалаРазработки, ВложенныйЗапрос.ДатаПоследнегоТестирования, ДЕНЬ)) КАК РазностьДнейРазработка,
	|		СУММА(0) КАК РазностьДнейРассмотрение
	|	ИЗ
	|		(ВЫБРАТЬ
	|			втИсходнаяТаблица.СсылкаЗаявка КАК СсылкаЗаявка,
	|			втИсходнаяТаблица.СистемаУчета КАК СистемаУчета,
	|			МИНИМУМ(втИсходнаяТаблица.ДатаНачалаРазработки) КАК ДатаНачалаРазработки,
	|			МАКСИМУМ(втИсходнаяТаблица.ДатаПоследнегоТестирования) КАК ДатаПоследнегоТестирования
	|		ИЗ
	|			втИсходнаяТаблица КАК втИсходнаяТаблица
	|		
	|		СГРУППИРОВАТЬ ПО
	|			втИсходнаяТаблица.СсылкаЗаявка,
	|			втИсходнаяТаблица.СистемаУчета) КАК ВложенныйЗапрос
	|	
	|	СГРУППИРОВАТЬ ПО
	|		ВложенныйЗапрос.СистемаУчета
	|	
	|	ОБЪЕДИНИТЬ
	|	
	|	ВЫБРАТЬ
	|		ВложенныйЗапрос.СистемаУчета,
	|		СУММА(0),
	|		СРЕДНЕЕ(РАЗНОСТЬДАТ(ВложенныйЗапрос.Дата, ВложенныйЗапрос.ДатаСозданияЗадачи, ДЕНЬ))
	|	ИЗ
	|		(ВЫБРАТЬ
	|			втИсходнаяТаблицаПоЗаявкам.СсылкаЗаявка КАК СсылкаЗаявка,
	|			втИсходнаяТаблицаПоЗаявкам.СистемаУчета КАК СистемаУчета,
	|			втИсходнаяТаблицаПоЗаявкам.Дата КАК Дата,
	|			МИНИМУМ(втИсходнаяТаблицаПоЗаявкам.ДатаСозданияЗадачи) КАК ДатаСозданияЗадачи
	|		ИЗ
	|			втИсходнаяТаблицаПоЗаявкам КАК втИсходнаяТаблицаПоЗаявкам
	|		
	|		СГРУППИРОВАТЬ ПО
	|			втИсходнаяТаблицаПоЗаявкам.СсылкаЗаявка,
	|			втИсходнаяТаблицаПоЗаявкам.СистемаУчета,
	|			втИсходнаяТаблицаПоЗаявкам.Дата) КАК ВложенныйЗапрос
	|	
	|	СГРУППИРОВАТЬ ПО
	|		ВложенныйЗапрос.СистемаУчета) КАК Вз
	|
	|СГРУППИРОВАТЬ ПО
	|	Вз.СистемаУчета
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Вз.Направление КАК Группировка,
	|	ВЫРАЗИТЬ(СУММА(Вз.РазностьДнейРазработка) КАК ЧИСЛО(6, 2)) КАК РазностьДнейРазработка,
	|	ВЫРАЗИТЬ(СУММА(Вз.РазностьДнейРассмотрение) КАК ЧИСЛО(6, 2)) КАК РазностьДнейРассмотрение
	|ИЗ
	|	(ВЫБРАТЬ
	|		ВложенныйЗапрос.Направление КАК Направление,
	|		СРЕДНЕЕ(РАЗНОСТЬДАТ(ВложенныйЗапрос.ДатаНачалаРазработки, ВложенныйЗапрос.ДатаПоследнегоТестирования, ДЕНЬ)) КАК РазностьДнейРазработка,
	|		СУММА(0) КАК РазностьДнейРассмотрение
	|	ИЗ
	|		(ВЫБРАТЬ
	|			втИсходнаяТаблица.СсылкаЗаявка КАК СсылкаЗаявка,
	|			втИсходнаяТаблица.Направление КАК Направление,
	|			МИНИМУМ(втИсходнаяТаблица.ДатаНачалаРазработки) КАК ДатаНачалаРазработки,
	|			МАКСИМУМ(втИсходнаяТаблица.ДатаПоследнегоТестирования) КАК ДатаПоследнегоТестирования
	|		ИЗ
	|			втИсходнаяТаблица КАК втИсходнаяТаблица
	|		
	|		СГРУППИРОВАТЬ ПО
	|			втИсходнаяТаблица.СсылкаЗаявка,
	|			втИсходнаяТаблица.Направление) КАК ВложенныйЗапрос
	|	
	|	СГРУППИРОВАТЬ ПО
	|		ВложенныйЗапрос.Направление
	|	
	|	ОБЪЕДИНИТЬ
	|	
	|	ВЫБРАТЬ
	|		ВложенныйЗапрос.Направление,
	|		СУММА(0),
	|		СРЕДНЕЕ(РАЗНОСТЬДАТ(ВложенныйЗапрос.Дата, ВложенныйЗапрос.ДатаСозданияЗадачи, ДЕНЬ))
	|	ИЗ
	|		(ВЫБРАТЬ
	|			втИсходнаяТаблицаПоЗаявкам.СсылкаЗаявка КАК СсылкаЗаявка,
	|			втИсходнаяТаблицаПоЗаявкам.Направление КАК Направление,
	|			втИсходнаяТаблицаПоЗаявкам.Дата КАК Дата,
	|			МИНИМУМ(втИсходнаяТаблицаПоЗаявкам.ДатаСозданияЗадачи) КАК ДатаСозданияЗадачи
	|		ИЗ
	|			втИсходнаяТаблицаПоЗаявкам КАК втИсходнаяТаблицаПоЗаявкам
	|		
	|		СГРУППИРОВАТЬ ПО
	|			втИсходнаяТаблицаПоЗаявкам.СсылкаЗаявка,
	|			втИсходнаяТаблицаПоЗаявкам.Направление,
	|			втИсходнаяТаблицаПоЗаявкам.Дата) КАК ВложенныйЗапрос
	|	
	|	СГРУППИРОВАТЬ ПО
	|		ВложенныйЗапрос.Направление) КАК Вз
	|
	|СГРУППИРОВАТЬ ПО
	|	Вз.Направление";
	
	Запрос.УстановитьПараметр("ДатаНачала", ДатаНачала);
	Запрос.УстановитьПараметр("ДатаОкончания", КонецДня(ДатаОкончания));

	Возврат Запрос.ВыполнитьПакет();
	
КонецФункции

&НаСервере
Процедура СформироватьОтчетНаСервере()
	
	СформироватьОтчетНаСервере_Новая();
	
	Макет = Отчеты.АнализДеятельности.ПолучитьМакетОтчета();    
	тдАнализДней.Очистить();
	
	ЗаполнитьДанныеПоРассмотрениюИРаботе(Макет, тдАнализДней, Период.ДатаНачала, Период.ДатаОкончания);
	
КонецПроцедуры

&НаСервере
Процедура СформироватьОтчетНаСервере_Новая()
	
	УстановитьПривилегированныйРежим(Истина);
	
	тдВыполненияЗадач.Очистить();
	
	Макет = Отчеты.АнализДеятельности.ПолучитьМакетОтчета();
	ОбластьШапкаТаблицы  = Макет.ПолучитьОбласть("ОбластьШапка");
	ОбластьСтрокаТаблицы = Макет.ПолучитьОбласть("ОбластьСтрока");
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ВЫБОР
	|		КОГДА НАЧАЛОПЕРИОДА(ВложенныйЗапрос.Период, ДЕНЬ) < &ДатаНачала
	|			ТОГДА &ДатаНачала
	|		ИНАЧЕ НАЧАЛОПЕРИОДА(ВложенныйЗапрос.Период, ДЕНЬ)
	|	КОНЕЦ КАК Период,
	|	ВложенныйЗапрос.Направление КАК Направление,
	|	ВложенныйЗапрос.Система КАК Система,
	|	СУММА(ВложенныйЗапрос.КоличествоСозданных) КАК КоличествоСозданных,
	|	СУММА(ВложенныйЗапрос.КоличествоЗакрытых) КАК КоличествоЗакрытых,
	|	СУММА(ВложенныйЗапрос.КоличествоНаТестировании) КАК КоличествоНаТестировании
	|ПОМЕСТИТЬ СтатистикаЗаявок
	|ИЗ
	|	(ВЫБРАТЬ
	|		НАЧАЛОПЕРИОДА(ЗаявкаНаРазработку.Дата, ДЕНЬ) КАК Период,
	|		ЗаявкаНаРазработку.Направление КАК Направление,
	|		ЕСТЬNULL(РазрабатываемыеСистемы.Система, ""<Система не указана>"") КАК Система,
	|		КОЛИЧЕСТВО(РАЗЛИЧНЫЕ ЗаявкаНаРазработку.Ссылка) КАК КоличествоСозданных,
	|		0 КАК КоличествоЗакрытых,
	|		0 КАК КоличествоНаТестировании
	|	ИЗ
	|		Документ.ЗаявкаНаРазработку КАК ЗаявкаНаРазработку
	|			ЛЕВОЕ СОЕДИНЕНИЕ Документ.ЗаявкаНаРазработку.СистемыРазработки КАК РазрабатываемыеСистемы
	|			ПО ЗаявкаНаРазработку.Ссылка = РазрабатываемыеСистемы.Ссылка
	|	ГДЕ
	|		ЗаявкаНаРазработку.Дата МЕЖДУ &ДатаНачала И &ДатаОкончания
	|		И НЕ ЗаявкаНаРазработку.Черновик
	|	
	|	СГРУППИРОВАТЬ ПО
	|		НАЧАЛОПЕРИОДА(ЗаявкаНаРазработку.Дата, ДЕНЬ),
	|		ЗаявкаНаРазработку.Направление,
	|		ЕСТЬNULL(РазрабатываемыеСистемы.Система, ""<Система не указана>"")
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		МАКСИМУМ(ДокументЗадача.ДатаЗакрытия),
	|		ДокументЗадача.ОбъектОснование.Направление,
	|		ЕСТЬNULL(РазрабатываемыеСистемы.Система, ""<Система не указана>""),
	|		0,
	|		КОЛИЧЕСТВО(РАЗЛИЧНЫЕ ДокументЗадача.ОбъектОснование),
	|		0
	|	ИЗ
	|		Документ.Задача КАК ДокументЗадача
	|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.МинимальныеСтатусыЗаявок КАК МинимальныеСтатусы
	|			ПО ((ВЫРАЗИТЬ(ДокументЗадача.ОбъектОснование КАК Документ.ЗаявкаНаРазработку)) = МинимальныеСтатусы.Заявка)
	|				И (МинимальныеСтатусы.Статус = ЗНАЧЕНИЕ(Справочник.СтатусыОбъектов.Решен))
	|			ЛЕВОЕ СОЕДИНЕНИЕ Документ.ЗаявкаНаРазработку.СистемыРазработки КАК РазрабатываемыеСистемы
	|			ПО ((ВЫРАЗИТЬ(ДокументЗадача.ОбъектОснование КАК Документ.ЗаявкаНаРазработку)) = РазрабатываемыеСистемы.Ссылка)
	|	ГДЕ
	|		ДокументЗадача.Статус = ЗНАЧЕНИЕ(Справочник.СтатусыОбъектов.Решен)
	|		И ДокументЗадача.ДатаЗакрытия МЕЖДУ &ДатаНачала И &ДатаОкончания
	|		И ДокументЗадача.ОбъектОснование ССЫЛКА Документ.ЗаявкаНаРазработку
	|		И ВЫБОР
	|				КОГДА &ТолькоИТОтделы
	|					ТОГДА ДокументЗадача.Филиал.ТипФилиала <> ЗНАЧЕНИЕ(Перечисление.ТипыФилиалов.НеIT)
	|				ИНАЧЕ ИСТИНА
	|			КОНЕЦ
	|	
	|	СГРУППИРОВАТЬ ПО
	|		ДокументЗадача.ОбъектОснование,
	|		ДокументЗадача.ОбъектОснование.Направление,
	|		ЕСТЬNULL(РазрабатываемыеСистемы.Система, ""<Система не указана>"")
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		НАЧАЛОПЕРИОДА(МАКСИМУМ(СвойстваЗадачСрезПоследних.Период), ДЕНЬ),
	|		СвойстваЗадачСрезПоследних.Объект.Направление,
	|		ЕСТЬNULL(РазрабатываемыеСистемы.Система, ""<Система не указана>""),
	|		0,
	|		0,
	|		КОЛИЧЕСТВО(РАЗЛИЧНЫЕ СвойстваЗадачСрезПоследних.Объект.ОбъектОснование)
	|	ИЗ
	|		РегистрСведений.СвойстваЗадач.СрезПоследних(&ДатаОкончания, Объект.ОбъектОснование ССЫЛКА Документ.ЗаявкаНаРазработку) КАК СвойстваЗадачСрезПоследних
	|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.МинимальныеСтатусыЗаявок КАК МинимальныеСтатусыЗаявок
	|			ПО ((ВЫРАЗИТЬ(СвойстваЗадачСрезПоследних.Объект.ОбъектОснование КАК Документ.ЗаявкаНаРазработку)) = МинимальныеСтатусыЗаявок.Заявка)
	|				И (МинимальныеСтатусыЗаявок.Статус = ЗНАЧЕНИЕ(Справочник.СтатусыОбъектов.Тестирование))
	|			ЛЕВОЕ СОЕДИНЕНИЕ Документ.ЗаявкаНаРазработку.СистемыРазработки КАК РазрабатываемыеСистемы
	|			ПО ((ВЫРАЗИТЬ(СвойстваЗадачСрезПоследних.Объект.ОбъектОснование КАК Документ.ЗаявкаНаРазработку)) = РазрабатываемыеСистемы.Ссылка)
	|	ГДЕ
	|		ВЫБОР
	|				КОГДА &ТолькоИТОтделы
	|					ТОГДА СвойстваЗадачСрезПоследних.Объект.Филиал.ТипФилиала <> ЗНАЧЕНИЕ(Перечисление.ТипыФилиалов.НеIT)
	|				ИНАЧЕ ИСТИНА
	|			КОНЕЦ
	|	
	|	СГРУППИРОВАТЬ ПО
	|		СвойстваЗадачСрезПоследних.Объект.ОбъектОснование,
	|		СвойстваЗадачСрезПоследних.Объект.Направление,
	|		ЕСТЬNULL(РазрабатываемыеСистемы.Система, ""<Система не указана>"")) КАК ВложенныйЗапрос
	|
	|СГРУППИРОВАТЬ ПО
	|	ВЫБОР
	|		КОГДА НАЧАЛОПЕРИОДА(ВложенныйЗапрос.Период, ДЕНЬ) < &ДатаНачала
	|			ТОГДА &ДатаНачала
	|		ИНАЧЕ НАЧАЛОПЕРИОДА(ВложенныйЗапрос.Период, ДЕНЬ)
	|	КОНЕЦ,
	|	ВложенныйЗапрос.Направление,
	|	ВложенныйЗапрос.Система
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	СтатистикаЗаявок.Направление КАК Группировка,
	|	СУММА(СтатистикаЗаявок.КоличествоСозданных) КАК ВсегоЗаявок,
	|	СУММА(СтатистикаЗаявок.КоличествоЗакрытых) КАК РешеныхЗаявок,
	|	СУММА(СтатистикаЗаявок.КоличествоНаТестировании) КАК ЗаявокНаТестировании
	|ИЗ
	|	СтатистикаЗаявок КАК СтатистикаЗаявок
	|
	|СГРУППИРОВАТЬ ПО
	|	СтатистикаЗаявок.Направление
	|
	|УПОРЯДОЧИТЬ ПО
	|	СтатистикаЗаявок.Направление
	|АВТОУПОРЯДОЧИВАНИЕ
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	СтатистикаЗаявок.Система КАК Группировка,
	|	СУММА(СтатистикаЗаявок.КоличествоСозданных) КАК ВсегоЗаявок,
	|	СУММА(СтатистикаЗаявок.КоличествоЗакрытых) КАК РешеныхЗаявок,
	|	СУММА(СтатистикаЗаявок.КоличествоНаТестировании) КАК ЗаявокНаТестировании
	|ИЗ
	|	СтатистикаЗаявок КАК СтатистикаЗаявок
	|
	|СГРУППИРОВАТЬ ПО
	|	СтатистикаЗаявок.Система
	|
	|УПОРЯДОЧИТЬ ПО
	|	СтатистикаЗаявок.Система
	|АВТОУПОРЯДОЧИВАНИЕ";
	
	Запрос.УстановитьПараметр("ДатаНачала", Период.ДатаНачала);
	Запрос.УстановитьПараметр("ДатаОкончания", Период.ДатаОкончания);
	Запрос.УстановитьПараметр("ТолькоИТОтделы", ТолькоИТОтделы);
	
	РезультатыЗапроса = Запрос.ВыполнитьПакет();
	
	// Количество заявок по направлению
	ОбластьШапкаТаблицы.Параметры.НазваниеГруппировки = "Направления";
	тдВыполненияЗадач.Вывести(ОбластьШапкаТаблицы);
	
	Выборка = РезультатыЗапроса[1].Выбрать();
	Пока Выборка.Следующий() Цикл
		ОбластьСтрокаТаблицы.Параметры.Заполнить(Выборка);
		тдВыполненияЗадач.Вывести(ОбластьСтрокаТаблицы);
	КонецЦикла;
	
	тдВыполненияЗадач.Вывести(Макет.ПолучитьОбласть("ОбластьПровал"));
	
	// Количество заявок по системе
	ОбластьШапкаТаблицы.Параметры.НазваниеГруппировки = "Системы";
	тдВыполненияЗадач.Вывести(ОбластьШапкаТаблицы);
	
	Выборка = РезультатыЗапроса[2].Выбрать();
	Пока Выборка.Следующий() Цикл
		ОбластьСтрокаТаблицы.Параметры.Заполнить(Выборка);
		тдВыполненияЗадач.Вывести(ОбластьСтрокаТаблицы);
	КонецЦикла;
	
КонецПроцедуры

&НаСервереБезКонтекста
Процедура ВывестиДанныеПоВыполнениюЗадач(Макет, Группировка, тзДанных, ДопСтрока = "", тдДляВывода)
	
	областьШапка  = Макет.ПолучитьОбласть("областьШапка"+ДопСтрока);
	областьСтрока = Макет.ПолучитьОбласть("областьСтрока"+ДопСтрока);
	
	областьШапка.Параметры.НазваниеГруппировки = Группировка;
	
	тдДляВывода.Вывести(областьШапка);
	
	Для Каждого элТз из тзДанных Цикл
		областьСтрока.Параметры.Заполнить(элТз);
		тдДляВывода.Вывести(областьСтрока);
	КонецЦикла;
	
КонецПроцедуры

//Заполнение страницы средний срок
&НаСервереБезКонтекста
Процедура ЗаполнитьДанныеПоРассмотрениюИРаботе(Макет, тдДляВывода, ДатаНачала, ДатаОкончания)
	
	ПакетыАнализДней = ПолучитьПакетыАнализДней(ДатаНачала, ДатаОкончания);	
	
	ВывестиДанныеПоВыполнениюЗадач(Макет, "Направление", ПакетыАнализДней[3].Выгрузить(), "АнализДней", тдДляВывода);
	тдДляВывода.Вывести(Макет.ПолучитьОбласть("ОбластьПровал"));
	ВывестиДанныеПоВыполнениюЗадач(Макет, "Система учета", ПакетыАнализДней[2].Выгрузить(), "АнализДней", тдДляВывода);
	
КонецПроцедуры

#КонецОбласти
