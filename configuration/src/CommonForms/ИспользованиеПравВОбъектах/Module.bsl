///////////////////////////////////////////////////////////////////////////////////////////////////////
// Copyright (c) 2025, ООО ДНС Технологии
// SDMS (Software Development Management System) — это корпоративная система учета разработки и управления проектами 
// Все права защищены. Эта программа и сопроводительные материалы предоставляются 
// в соответствии с условиями лицензии General Public License (GNU GPL v3)
// Текст лицензии доступен по ссылке:
// https://www.gnu.org/licenses/gpl-3.0.html
///////////////////////////////////////////////////////////////////////////////////////////////////////

#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ПолучитьТаблицуИспользованияПравВОбъектах();
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовФормы

&НаКлиенте
Процедура СписокПравПриАктивизацииСтроки(Элемент)
	
	ТекущиеДанные = Элементы.СписокПрав.ТекущиеДанные;
	Если ТекущиеДанные = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ВыбранноеПраво = ТекущиеДанные.Значение;
	ПараметрыОтбора = Новый Структура("Право", ВыбранноеПраво);
	Элементы.ТаблицаПравИОбъектов.ОтборСтрок = Новый ФиксированнаяСтруктура(ПараметрыОтбора);

КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Функция ПолучитьТаблицуИспользованияПравВОбъектах()
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	СвязьОбъектовИРолей.Роль КАК Право,
	|	СвязьОбъектовИРолей.Объект КАК Объект,
	|	СвязьОбъектовИРолей.Использование КАК Использование
	|ИЗ
	|	РегистрСведений.СвязьОбъектовИРолей КАК СвязьОбъектовИРолей
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.ПрограммныеРоли КАК ПрограммныеРоли
	|		ПО СвязьОбъектовИРолей.Роль = ПрограммныеРоли.Ссылка
	|УПОРЯДОЧИТЬ ПО
	|	ПрограммныеРоли.Наименование
	|ИТОГИ
	|	МАКСИМУМ(Использование)
	|ПО
	|	Право,
	|	Объект ИЕРАРХИЯ";
	
	ВыборкаПраво = Запрос.Выполнить().Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам, "Право");
	
	Пока ВыборкаПраво.Следующий() Цикл
		// Сначала выбираем все права, используемые в объектах.
		СписокПрав.Добавить(ВыборкаПраво.Право);
				
		ВыборкаГруппаМетаданных = ВыборкаПраво.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкамСИерархией, "Объект");
		Пока ВыборкаГруппаМетаданных.Следующий() Цикл
			
			ВыборкаМетаданные = ВыборкаГруппаМетаданных.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкамСИерархией, "Объект");
			Пока ВыборкаМетаданные.Следующий() Цикл												
				
				// На последнем этапе заполняем данные для таблички - право -> объекты метаданных.
				ВыборкаДетальныеЗаписи = ВыборкаМетаданные.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкамСИерархией, "Объект");
				Пока ВыборкаДетальныеЗаписи.Следующий() Цикл			
					ИмяДляОбъекта = Строка(ВыборкаМетаданные.Объект);
					
					Если ИмяДляОбъекта <> Строка(ВыборкаДетальныеЗаписи.Объект) Тогда
						ИмяДляОбъекта = ИмяДляОбъекта + СтрШаблон(".%1", ВыборкаДетальныеЗаписи.Объект);
					Иначе
						ИмяДляОбъекта = Строка(ВыборкаДетальныеЗаписи.Объект);
					КонецЕсли;
					
					СтрокаТаблицы = ТаблицаПравИОбъектов.Добавить();
					СтрокаТаблицы.Право = ВыборкаДетальныеЗаписи.Право;
					СтрокаТаблицы.ПредставлениеОбъект = ИмяДляОбъекта;
					СтрокаТаблицы.Использование = ВыборкаДетальныеЗаписи.Использование;
				КонецЦикла;
			КонецЦикла;
		КонецЦикла;
	КонецЦикла;

КонецФункции

#КонецОбласти
