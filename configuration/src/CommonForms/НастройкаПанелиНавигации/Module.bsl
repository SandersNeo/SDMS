///////////////////////////////////////////////////////////////////////////////////////////////////////
// Copyright (c) 2025, ООО ДНС Технологии
// SDMS (Software Development Management System) — это корпоративная система учета разработки и управления проектами 
// Все права защищены. Эта программа и сопроводительные материалы предоставляются 
// в соответствии с условиями лицензии General Public License (GNU GPL v3)
// Текст лицензии доступен по ссылке:
// https://www.gnu.org/licenses/gpl-3.0.html
///////////////////////////////////////////////////////////////////////////////////////////////////////

#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Объект = Параметры.Ссылка;	
	СписокКоманд = Параметры.СписокКоманд;
	
	ЗагрузитьНастройкиВидимостиКоманд();
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовФормы

&НаКлиенте
Процедура СписокКомандВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	 
	ЭлементСписка = СписокКоманд.Получить(ВыбраннаяСтрока);			
	ЭлементСписка.Пометка = НЕ ЭлементСписка.Пометка;
	
	СписокКомандПометкаПриИзменении(Неопределено);
	
КонецПроцедуры

&НаКлиенте
Процедура СписокКомандПометкаПриИзменении(Элемент)
	
	МаксимальноеКоличествоВидимых = ПанельНавигацииКлиентСервер.МаксимальноеКоличествоВидимыхКоманд();
	ВсегоВыбрано = 0;
	
	Для Каждого Команда Из СписокКоманд Цикл
		Если Команда.Пометка Тогда
			ВсегоВыбрано = ВсегоВыбрано + 1;
		КонецЕсли;
	КонецЦикла;
	
	Если ВсегоВыбрано > МаксимальноеКоличествоВидимых Тогда
		ЭлементСписка = СписокКоманд.НайтиПоЗначению(Элементы.СписокКоманд.ТекущиеДанные.Значение);
		ЭлементСписка.Пометка = Ложь;
		
		ШаблонСообщения = "Возможен выбор не более %1 дополнительных команд выводимых в основной панели";
		ПоказатьПредупреждение(, СтрШаблон(ШаблонСообщения, МаксимальноеКоличествоВидимых));
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура Выбрать(Команда)
	
	СохранитьНастройкиВидимостиКоманд();
	
	Закрыть(СписокКоманд);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура ЗагрузитьНастройкиВидимостиКоманд()

	Ключи = ПанельНавигации.КлючиНастроек(Объект);
	
	Если Ключи = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Настройки = ОбщегоНазначенияВызовСервера.ЗагрузитьНастройкиДанныхФормы(Ключи.КлючОбъекта, Ключи.КлючНастроек);
	
	Если Настройки = Неопределено Тогда
		МаксимальноеКоличествоВидимых = ПанельНавигацииКлиентСервер.МаксимальноеКоличествоВидимыхКоманд();
		КоличествоВидимых = 0;
		
		Для Каждого Элемент Из СписокКоманд Цикл
			Если КоличествоВидимых < МаксимальноеКоличествоВидимых Тогда 
				Элемент.Пометка = Истина;
				КоличествоВидимых = КоличествоВидимых + 1;
			КонецЕсли;
		КонецЦикла;
	Иначе
		Для Каждого Элемент Из СписокКоманд Цикл
			ЭлементСписка = Настройки.НайтиПоЗначению(Элемент.Значение);
			
			Если ЭлементСписка <> Неопределено Тогда
				Элемент.Пометка = ЭлементСписка.Пометка;
			КонецЕсли;
		КонецЦикла;	
	КонецЕсли;	
	
КонецПроцедуры

&НаСервере
Процедура СохранитьНастройкиВидимостиКоманд()

	Ключи = ПанельНавигации.КлючиНастроек(Объект);
	ОбщегоНазначенияВызовСервера.СохранитьНастройкиДанныхФормы(Ключи.КлючОбъекта, Ключи.КлючНастроек, СписокКоманд);
	
КонецПроцедуры

#КонецОбласти
