///////////////////////////////////////////////////////////////////////////////////////////////////////
// Copyright (c) 2025, ООО ДНС Технологии
// SDMS (Software Development Management System) — это корпоративная система учета разработки и управления проектами 
// Все права защищены. Эта программа и сопроводительные материалы предоставляются 
// в соответствии с условиями лицензии General Public License (GNU GPL v3)
// Текст лицензии доступен по ссылке:
// https://www.gnu.org/licenses/gpl-3.0.html
///////////////////////////////////////////////////////////////////////////////////////////////////////

#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.Свойство("АдресДополнительныхРеквизитов") И ЭтоАдресВременногоХранилища(Параметры.АдресДополнительныхРеквизитов) Тогда
		АдресДополнительныхРеквизитов = Параметры.АдресДополнительныхРеквизитов;
	Иначе
		Отказ = Истина;
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю("Не указан адрес для сохранения дополнительных реквизитов.");
		Возврат;
	КонецЕсли;
	
	Объект = Параметры.Объект;
			
	// Проверка наличия значений по адресу дополнительных реквизитов
	ДанныеИзХранилища = ПолучитьИзВременногоХранилища(АдресДополнительныхРеквизитов);
	
	Если ДанныеИзХранилища <> Неопределено И ТипЗнч(ДанныеИзХранилища) = Тип("ТаблицаЗначений") Тогда	
		// Если есть значения реквизитов на форме объекта - дополним таблицу этими значениями
		Если Параметры.Свойство("ЗначенияРеквизитов") Тогда
			ЗначенияРеквизитов = Параметры.ЗначенияРеквизитов;
			
			Для Каждого Строка Из ДанныеИзХранилища Цикл
				ЗначениеРеквизита = ЗначенияРеквизитов.Получить(Строка.ВидРеквизита.ИмяПредопределенныхДанных);
				
				Если ЗначениеРеквизита <> Неопределено Тогда
					Строка.Значение = ЗначениеРеквизита; 
				КонецЕсли;		
			КонецЦикла;	
		КонецЕсли;
		
		ТаблицаРеквизитов.Загрузить(ДанныеИзХранилища);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ОтменитьИзменения(Команда)
	
	// Возвращаем в обработчик Ложь, так как пользователь отказался от записи
	Закрыть(Ложь);
	
КонецПроцедуры

&НаКлиенте
Процедура СохранитьИзменения(Команда)
	
	СохранитьИзмененияНаСервере();
	
	// Возвращаем в обработчик Истина, так как изменения сохранены
	Закрыть(Истина);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыФункции

&НаСервере
Процедура СохранитьИзмененияНаСервере()
	
	ТаблицаЗначений = РеквизитФормыВЗначение("ТаблицаРеквизитов", Тип("ТаблицаЗначений"));
	ПоместитьВоВременноеХранилище(ТаблицаЗначений, АдресДополнительныхРеквизитов);	
	
КонецПроцедуры

&НаКлиенте
Процедура ТаблицаРеквизитовВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	ТекущиеДанные = Элемент.ТекущиеДанные;
	
	Если ТекущиеДанные = Неопределено ИЛИ ТекущиеДанные.РедактированиеВИнтерфейсе Тогда 
		Возврат;
	КонецЕсли;
	
	Если ТипЗнч(ТекущиеДанные.Значение) = Тип("СправочникСсылка.Пользователи") Тогда
		ИнтерфейсПриложенияКлиент.ОткрытьИнформациюОПользователе(ЭтотОбъект, ТекущиеДанные.Значение, СтандартнаяОбработка);
	КонецЕсли;
		
КонецПроцедуры

#КонецОбласти
