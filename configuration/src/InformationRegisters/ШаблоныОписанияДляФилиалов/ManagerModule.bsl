///////////////////////////////////////////////////////////////////////////////////////////////////////
// Copyright (c) 2025, ООО ДНС Технологии
// SDMS (Software Development Management System) — это корпоративная система учета разработки и управления проектами 
// Все права защищены. Эта программа и сопроводительные материалы предоставляются 
// в соответствии с условиями лицензии General Public License (GNU GPL v3)
// Текст лицензии доступен по ссылке:
// https://www.gnu.org/licenses/gpl-3.0.html
///////////////////////////////////////////////////////////////////////////////////////////////////////

#Область ПрограммныйИнтерфейс

Процедура Записать(Знач Филиал, Знач Шаблоны) Экспорт
	
	ДатаИзменения = ТекущаяДатаСеанса();
	Пользователь = ПараметрыСеанса.ТекущийПользователь;	

	НаборЗаписей = СоздатьНаборЗаписей();
	НаборЗаписей.Отбор.Филиал.Установить(Филиал);

	Для Каждого Шаблон Из Шаблоны Цикл	
		НоваяЗапись = НаборЗаписей.Добавить();
		НоваяЗапись.Филиал = Филиал;
		НоваяЗапись.Шаблон = Шаблон;
		НоваяЗапись.ДатаИзменения = ДатаИзменения;
		НоваяЗапись.Пользователь = Пользователь;
	КонецЦикла;
	
	НаборЗаписей.Записать();
	
КонецПроцедуры

// Функция - возврашает соответствие назначения и шаблона описания из настроек филиала пользователя
// 
// Возвращаемое значение:
//  Соответствие - назначение и шаблон описания
//
Функция ПолучитьШаблоныОписания() Экспорт
	
	Результат = Новый Соответствие;
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ШаблоныОписанияДокументов.ШаблонHTML КАК ШаблонHTML,
	|	ШаблоныОписанияДокументов.Назначение КАК Назначение
	|ИЗ
	|	Справочник.Пользователи КАК Пользователи
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.Филиалы КАК Филиалы
	|		ПО Пользователи.Филиал = Филиалы.Ссылка
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.ШаблоныОписанияДляФилиалов КАК ШаблоныОписанияДляФилиалов
	|		ПО (Филиалы.Ссылка = ШаблоныОписанияДляФилиалов.Филиал)
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.ШаблоныОписанияДокументов КАК ШаблоныОписанияДокументов
	|		ПО (ШаблоныОписанияДляФилиалов.Шаблон = ШаблоныОписанияДокументов.Ссылка)
	|			И (ШаблоныОписанияДокументов.ТипОбъекта = ЗНАЧЕНИЕ(Справочник.ТипыОбъектовВладельцев.Документ_ЗаявкаНаРазработку))
	|ГДЕ
	|	Пользователи.Ссылка = &Пользователь";
	
	Запрос.УстановитьПараметр("Пользователь", ПараметрыСеанса.ТекущийПользователь);

	Выборка = Запрос.Выполнить().Выбрать();
	
	Пока Выборка.Следующий() Цикл
		Результат.Вставить(Выборка.Назначение, Выборка.ШаблонHTML);
	КонецЦикла;
	
	Возврат Результат;
	
КонецФункции

#КонецОбласти
