///////////////////////////////////////////////////////////////////////////////////////////////////////
// Copyright (c) 2025, ООО ДНС Технологии
// SDMS (Software Development Management System) — это корпоративная система учета разработки и управления проектами 
// Все права защищены. Эта программа и сопроводительные материалы предоставляются 
// в соответствии с условиями лицензии General Public License (GNU GPL v3)
// Текст лицензии доступен по ссылке:
// https://www.gnu.org/licenses/gpl-3.0.html
///////////////////////////////////////////////////////////////////////////////////////////////////////

#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ОтборПоТегам = Параметры.Отбор.Тег;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовФормы

&НаСервере
Процедура ОтборПоТегамОкончаниеВводаТекстаНаСервере(Знач Текст)
	
	МассивСтрок = СтрРазделить(Текст, ",");
	МассивСсылок = Новый Массив;
	Теги = Справочники.Теги;
	
	Для Каждого Представление ИЗ МассивСтрок Цикл
		Представление = СокрЛП(Представление);
		Ссылка = Теги.НайтиПоНаименованию(Представление);
		Если НЕ Ссылка.Пустая() Тогда
			МассивСсылок.Добавить(Ссылка);
		КонецЕсли;	
	КонецЦикла;
	
	ЭлементыОтобра = Список.Отбор.Элементы;
	
	ЭлементыОтобра.Очистить();
	ЭлементОтбора = ЭлементыОтобра.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ЭлементОтбора.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("Тег");
	ЭлементОтбора.ВидСравнения = ВидСравненияКомпоновкиДанных.ВСписке; 
	ЭлементОтбора.ПравоеЗначение = МассивСсылок;
	
КонецПроцедуры

&НаКлиенте
Процедура ОтборПоТегамОкончаниеВводаТекста(Элемент, Текст, ДанныеВыбора, ПараметрыПолученияДанных, СтандартнаяОбработка)
	
	Если НЕ ЗначениеЗаполнено(Текст) Тогда
		Возврат;
	КонецЕсли;	
	
	ОтборПоТегамОкончаниеВводаТекстаНаСервере(НРег(СокрЛП(Текст)));
	
КонецПроцедуры

&НаКлиенте
Процедура СписокВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	Ссылка = Элемент.ТекущиеДанные.Объект;
	ТипЗначения = ТипЗнч(Элемент.ТекущиеДанные.Объект);
	
	Если ТипЗначения = Тип("ДокументСсылка.Задача") Тогда
		ИмяОткрываемойФормы = "Документ.Задача.Форма.ФормаДокумента";
	ИначеЕсли ТипЗначения = Тип("ДокументСсылка.ВнутреннееЗадание") Тогда
		ИмяОткрываемойФормы = "Документ.ВнутреннееЗадание.Форма.ФормаДокумента";
	Иначе
	    ИмяОткрываемойФормы = "Документ.ЗаявкаНаРазработку.Форма.ФормаДокумента";
	КонецЕсли;
	
	ПараметрыОткрытия = Новый Структура("Ключ", Ссылка); 
 	ОткрытьФорму(ИмяОткрываемойФормы, ПараметрыОткрытия, ЭтотОбъект);
		
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура НаложитьОтборПоТипуОбъекта(Команда)
	
	Список.Параметры.УстановитьЗначениеПараметра("ТипОбъекта", Команда.Имя);
	
КонецПроцедуры

#КонецОбласти
