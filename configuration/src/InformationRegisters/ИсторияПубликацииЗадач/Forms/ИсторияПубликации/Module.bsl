///////////////////////////////////////////////////////////////////////////////////////////////////////
// Copyright (c) 2025, ООО ДНС Технологии
// SDMS (Software Development Management System) — это корпоративная система учета разработки и управления проектами 
// Все права защищены. Эта программа и сопроводительные материалы предоставляются 
// в соответствии с условиями лицензии General Public License (GNU GPL v3)
// Текст лицензии доступен по ссылке:
// https://www.gnu.org/licenses/gpl-3.0.html
///////////////////////////////////////////////////////////////////////////////////////////////////////

#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если НЕ Параметры.Свойство("Задача") Тогда
		Возврат;
	КонецЕсли;

	Задача = Параметры.Задача;
	РазрешеноРедактирование = Ложь;

	ФилиалЗадачи = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Задача, "Филиал");
	
	Если ЗначениеЗаполнено(ФилиалЗадачи) Тогда
		РеквизитыФилиала = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(ФилиалЗадачи, "ЗаместительРуководителя, Руководитель");
		
		РазрешеноРедактирование = (УправлениеДоступомПовтИсп.ПроверитьДоступностьРоли("Администратор") 
									ИЛИ (РеквизитыФилиала.Руководитель = ПараметрыСеанса.ТекущийПользователь)
									ИЛИ (РеквизитыФилиала.ЗаместительРуководителя = ПараметрыСеанса.ТекущийПользователь))
	КонецЕсли;
		
	Элементы.ГруппаКоманднаяПанель.Видимость = РазрешеноРедактирование;			
	ЗаполнитьТаблицуИсторияПубликации();
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовФормы

&НаКлиенте
Процедура ИсторияПубликацииВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	Если Элементы.ГруппаКоманднаяПанель.Видимость Тогда	
		РедактироватьЗапись(Элемент.ТекущиеДанные);
	КонецЕсли;
		
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура Добавить(Команда)
	
	ПараметрыОткрытия = Новый Структура;
	ПараметрыОткрытия.Вставить("Задача", Задача);
	
	ОбработчикЗакрытия = Новый ОписаниеОповещения("ПослеЗакрытияОкнаРедактированияЗаписи", ЭтотОбъект);
	
	ОткрытьФорму("РегистрСведений.ИсторияПубликацииЗадач.Форма.РедактированиеЗаписи", ПараметрыОткрытия, ЭтотОбъект, , , ,
		ОбработчикЗакрытия, РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
	
КонецПроцедуры

&НаКлиенте
Процедура Изменить(Команда)
	
	ТекущиеДанные = Элементы.ИсторияПубликации.ТекущиеДанные;
	
	Если ТекущиеДанные = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	РедактироватьЗапись(ТекущиеДанные);
		
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура ЗаполнитьТаблицуИсторияПубликации()
	
	ИсторияПубликации.Очистить();
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ИсторияПубликацииЗадач.Период КАК Период,
	|	ИсторияПубликацииЗадач.Задача КАК Задача,
	|	ИсторияПубликацииЗадач.ДатаИсторииХранилища КАК ДатаИсторииХранилища,
	|	ИсторияПубликацииЗадач.ДатаДобавления КАК ДатаДобавления,
	|	ИсторияПубликацииЗадач.УведомленияОтправлены КАК УведомленияОтправлены,
	|	ИсторияПубликацииЗадач.Автор КАК Автор
	|ИЗ
	|	РегистрСведений.ИсторияПубликацииЗадач КАК ИсторияПубликацииЗадач
	|ГДЕ
	|	ИсторияПубликацииЗадач.Задача = &Задача
	|
	|УПОРЯДОЧИТЬ ПО
	|	Период";
	
	Запрос.УстановитьПараметр("Задача", Задача);	
	РезультатЗапроса = Запрос.Выполнить();
	
	Если НЕ РезультатЗапроса.Пустой() Тогда
		Выборка = РезультатЗапроса.Выбрать();
		
		Пока Выборка.Следующий() Цикл
			ЗаполнитьЗначенияСвойств(ИсторияПубликации.Добавить(), Выборка);
		КонецЦикла;	
	КонецЕсли;
	
КонецПроцедуры
	
&НаКлиенте
Процедура ПослеЗакрытияОкнаРедактированияЗаписи(Результат, ДополнительныеПараметры) Экспорт
		
	Если Результат <> Неопределено Тогда
		ЗаполнитьТаблицуИсторияПубликации();
	КонецЕсли;
			
КонецПроцедуры

&НаКлиенте
Процедура РедактироватьЗапись(Знач ТекущиеДанные)
	
	СтруктураЗаписи = Новый Структура;
	СтруктураЗаписи.Вставить("Период", ТекущиеДанные.Период);
	СтруктураЗаписи.Вставить("ДатаИсторииХранилища", ТекущиеДанные.ДатаИсторииХранилища);
	СтруктураЗаписи.Вставить("ДатаДобавления", ТекущиеДанные.ДатаДобавления);
	СтруктураЗаписи.Вставить("Автор", ТекущиеДанные.Автор);
	СтруктураЗаписи.Вставить("УведомленияОтправлены", ТекущиеДанные.УведомленияОтправлены);
	
	ПараметрыОткрытия = Новый Структура;
	ПараметрыОткрытия.Вставить("Задача", Задача);
	ПараметрыОткрытия.Вставить("СтруктураЗаписи", СтруктураЗаписи);
		
	ОбработчикЗакрытия = Новый ОписаниеОповещения("ПослеЗакрытияОкнаРедактированияЗаписи", ЭтотОбъект);
	
	ОткрытьФорму("РегистрСведений.ИсторияПубликацииЗадач.Форма.РедактированиеЗаписи", ПараметрыОткрытия, ЭтотОбъект, , , ,
		ОбработчикЗакрытия, РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
	
КонецПроцедуры

#КонецОбласти
