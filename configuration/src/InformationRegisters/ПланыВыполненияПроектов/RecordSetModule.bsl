///////////////////////////////////////////////////////////////////////////////////////////////////////
// Copyright (c) 2025, ООО ДНС Технологии
// SDMS (Software Development Management System) — это корпоративная система учета разработки и управления проектами 
// Все права защищены. Эта программа и сопроводительные материалы предоставляются 
// в соответствии с условиями лицензии General Public License (GNU GPL v3)
// Текст лицензии доступен по ссылке:
// https://www.gnu.org/licenses/gpl-3.0.html
///////////////////////////////////////////////////////////////////////////////////////////////////////

#Если Сервер ИЛИ ВнешнееСоединение ИЛИ ТолстыйКлиентОбычноеПриложение Тогда

#Область ОбъявленияПеременных

// Массив из структур вида "Период, Объект, КлючСобытия, Статус"
// для записи в историю статусов элементов планов проектов
Перем ОбъектыДляЗаписиВИсторию;

#КонецОбласти

#Область ОбработчикиСобытий

Процедура ПередЗаписью(Отказ, Замещение)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	ОбъектыДляЗаписиВИсторию = Новый Массив;
	
	ТекущаяДата = ТекущаяДатаСеанса();
	
	Для Каждого СтрокаНабора Из ЭтотОбъект Цикл
		ПредыдущийСтатус = ПолучитьСтатус(СтрокаНабора.Объект, СтрокаНабора.КлючСобытия);
		
		Если ПредыдущийСтатус <> СтрокаНабора.Статус Тогда
			СтруктураДанных = Новый Структура("Период, Объект, КлючСобытия, Статус");
			ЗаполнитьЗначенияСвойств(СтруктураДанных, СтрокаНабора);
			СтруктураДанных.Период = ТекущаяДата;
			
			ОбъектыДляЗаписиВИсторию.Добавить(СтруктураДанных);
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры

Процедура ПриЗаписи(Отказ, Замещение)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	Если ОбъектыДляЗаписиВИсторию <> Неопределено Тогда
		Для Каждого СтруктураДанных Из ОбъектыДляЗаписиВИсторию Цикл
			РегистрыСведений.ИсторияСтатусовЭлементовПлановПроектов.Добавить(СтруктураДанных);
		КонецЦикла;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция ПолучитьСтатус(Знач Объект, Знач КлючЗаписи)
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ИсторияСтатусовЭлементовПлановПроектовСрезПоследних.Статус КАК Статус
	|ИЗ
	|	РегистрСведений.ИсторияСтатусовЭлементовПлановПроектов.СрезПоследних(
	|			&ТекущаяДата,
	|			Объект = &Объект
	|				И КлючСобытия = &КлючСобытия) КАК ИсторияСтатусовЭлементовПлановПроектовСрезПоследних";
	
	Запрос.УстановитьПараметр("ТекущаяДата", ТекущаяДатаСеанса());
	Запрос.УстановитьПараметр("Объект", Объект);
	Запрос.УстановитьПараметр("КлючСобытия", КлючЗаписи);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Если НЕ РезультатЗапроса.Пустой() Тогда
		Выборка = РезультатЗапроса.Выбрать();
		Выборка.Следующий();
		
		Результат = Выборка.Статус;
	Иначе
		Результат = Неопределено;
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

#КонецОбласти

#КонецЕсли
