///////////////////////////////////////////////////////////////////////////////////////////////////////
// Copyright (c) 2025, ООО ДНС Технологии
// SDMS (Software Development Management System) — это корпоративная система учета разработки и управления проектами 
// Все права защищены. Эта программа и сопроводительные материалы предоставляются 
// в соответствии с условиями лицензии General Public License (GNU GPL v3)
// Текст лицензии доступен по ссылке:
// https://www.gnu.org/licenses/gpl-3.0.html
///////////////////////////////////////////////////////////////////////////////////////////////////////

#Если Сервер ИЛИ ТолстыйКлиентОбычноеПриложение ИЛИ ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

// Сохраняет значения заполненных дополнительных реквизитов
//
// Параметры:
//  Объект				 - ОпределяемыйТип.ВладелецДополнительныхРеквизитов	 - ссылка на объект
//  ТаблицаРеквизитов	 - ТаблицаЗначений	 - таблица дополнительных ревизитов
//		* ВидРеквизита	- ПланВидовХарактеристикСсылка.ВидыДополнительныхРеквизитов	- дополнительный реквизит
//		* Значение		- Характеристика.ВидыДополнительныхРеквизитов	- значение дополнительного реквизита
//  Отказ				 - Булево	 - переменная из обработчика подписки на события
//
Процедура ЗаписатьЗначенияРеквизитов(Знач Объект, Знач ТаблицаРеквизитов, Отказ = Ложь) Экспорт
		
	НаборЗаписей = СоздатьНаборЗаписей();
	НаборЗаписей.Отбор.Объект.Установить(Объект); 
	
	Попытка
		Для Каждого СтрокаРеквизита Из ТаблицаРеквизитов Цикл
			НаборЗаписей.Отбор.Реквизит.Установить(СтрокаРеквизита.ВидРеквизита);
			
			Если ЗначениеЗаполнено(СтрокаРеквизита.Значение) Тогда
				НаборЗаписей.Прочитать();
				
				Если НаборЗаписей.Количество() = 0 ИЛИ НаборЗаписей[0].Значение <> СтрокаРеквизита.Значение Тогда
					НаборЗаписей.Очистить();
					Добавить(НаборЗаписей, Объект, СтрокаРеквизита.ВидРеквизита, СтрокаРеквизита.Значение);						
				КонецЕсли;
			КонецЕсли;
			
			НаборЗаписей.Записать();
			НаборЗаписей.Очистить();
		КонецЦикла;
	Исключение
		Отказ = Истина;
		ВызватьИсключение "Ошибка записи дополнительных реквизитов по причине: " + ОписаниеОшибки();
	КонецПопытки;
	
КонецПроцедуры

// Возвращает значение дополнительного реквизита объекта.
//
// Параметры:
//  Объект	 - ОпределяемыйТип.ВладелецДополнительныхСвойств	 - ссылка на объект
//  Реквизит - ПланВидовХарактеристикСсылка.ВидыДополнительныхРеквизитов	 - реквизит, значение которого требуется получить
// 
// Возвращаемое значение:
//   - Произвольный
//
Функция ЗначениеДополнительногоРеквизита(Знач Объект, Знач Реквизит, Знач МожноПустое = Ложь) Экспорт
	
	Результат = Неопределено;
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ВидыДополнительныхРеквизитов.ТипЗначения КАК ТипЗначения,
	|	ЗначенияДополнительныхРеквизитовОбъектов.Значение КАК Значение
	|ИЗ
	|	ПланВидовХарактеристик.ВидыДополнительныхРеквизитов КАК ВидыДополнительныхРеквизитов
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ЗначенияДополнительныхРеквизитовОбъектов КАК ЗначенияДополнительныхРеквизитовОбъектов
	|		ПО ВидыДополнительныхРеквизитов.Ссылка = ЗначенияДополнительныхРеквизитовОбъектов.Реквизит
	|		И ЗначенияДополнительныхРеквизитовОбъектов.Объект = &Объект
	|ГДЕ
	|	ВидыДополнительныхРеквизитов.Ссылка = &Реквизит";
			
	Запрос.УстановитьПараметр("Объект", Объект);
	Запрос.УстановитьПараметр("Реквизит", Реквизит);
	
	Выборка = Запрос.Выполнить().Выбрать();
	Выборка.Следующий();
	
	// Если значение заполнено, возвращаем его.
	// Или же приводим его к стандартному значению (Ложь, "", ПустаяСсылка) - для того чтобы
	// избавиться от лишних проверок в дальнейшем.
	Если ЗначениеЗаполнено(Выборка.Значение) Тогда
		Результат = Выборка.Значение;
	ИначеЕсли НЕ МожноПустое Тогда
		ОписаниеТипов = Новый ОписаниеТипов(Выборка.ТипЗначения);
		Результат = ОписаниеТипов.ПривестиЗначение();
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

Процедура УстановитьЗначениеДополнительногоРеквизита(Знач Объект, Знач Реквизит, Знач Значение, Знач МожноПустое = Ложь) Экспорт
	
	НаборЗаписей = СоздатьНаборЗаписей();
	НаборЗаписей.Отбор.Объект.Установить(Объект);
	НаборЗаписей.Отбор.Реквизит.Установить(Реквизит);
	
	Если ЗначениеЗаполнено(Значение) ИЛИ МожноПустое Тогда
		НоваяСтрока = НаборЗаписей.Добавить();
		НоваяСтрока.Объект = Объект;
		НоваяСтрока.Реквизит = Реквизит;
		НоваяСтрока.Значение = Значение;
		НоваяСтрока.Автор = ПараметрыСеанса.ТекущийПользователь;
		НоваяСтрока.ДатаДобавления = ТекущаяДатаСеанса();
	КонецЕсли;
	
	НаборЗаписей.Записать();
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Процедура Добавить(НаборЗаписей, Знач Объект, Знач Реквизит, Знач Значение)

	Если ТипЗнч(Значение) = Тип("Булево") И Значение = Ложь Тогда
		Возврат;
	КонецЕсли;
	
	НоваяСтрока = НаборЗаписей.Добавить();
	НоваяСтрока.Объект = Объект;
	НоваяСтрока.Реквизит = Реквизит;
	НоваяСтрока.Значение = Значение;
	НоваяСтрока.Автор = ПараметрыСеанса.ТекущийПользователь;
	НоваяСтрока.ДатаДобавления = ТекущаяДатаСеанса();	

КонецПроцедуры

#КонецОбласти

#КонецЕсли
