///////////////////////////////////////////////////////////////////////////////////////////////////////
// Copyright (c) 2025, ООО ДНС Технологии
// SDMS (Software Development Management System) — это корпоративная система учета разработки и управления проектами 
// Все права защищены. Эта программа и сопроводительные материалы предоставляются 
// в соответствии с условиями лицензии General Public License (GNU GPL v3)
// Текст лицензии доступен по ссылке:
// https://www.gnu.org/licenses/gpl-3.0.html
///////////////////////////////////////////////////////////////////////////////////////////////////////

#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ЗаполнитьСписокТипов();
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	УстановитьОтборДинамическогоСписка();
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКоманд

&НаКлиенте
Процедура ТипыОбъектовПриАктивизацииСтроки(Элемент)
	
	УстановитьОтборДинамическогоСписка();
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементов

&НаКлиенте
Процедура РеквизитыОбъектовПередНачаломДобавления(Элемент, Отказ, Копирование, Родитель, Группа, Параметр)
	
	Отказ = Истина;
	
	ТекущиеДанные = Элементы.ТипыОбъектов.ТекущиеДанные;
	Если ТекущиеДанные = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ПараметрыОткрытия = Новый Структура("ТипОбъекта", ТекущиеДанные.Значение);
	ОткрытьФорму("РегистрСведений.НастройкиОповещенияПриИзмененииРеквизитов.ФормаЗаписи", ПараметрыОткрытия,
		ЭтотОбъект, КлючУникальности, , , , РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура ЗаполнитьСписокТипов()
	
	МассивТипов = Метаданные.РегистрыСведений.НастройкиОповещенияПриИзмененииРеквизитов.Измерения.ТипОбъекта.Тип.Типы();
	
	Для Каждого ТипОбъекта Из МассивТипов Цикл
		ТипыОбъектов.Добавить(Новый (ТипОбъекта), Строка(ТипОбъекта));
	КонецЦикла;
	
КонецПроцедуры

&НаКлиенте
Процедура УстановитьОтборДинамическогоСписка()
	
	ТекущиеДанные = Элементы.ТипыОбъектов.ТекущиеДанные;
	Если ТекущиеДанные = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	РеквизитыОбъектов.Параметры.УстановитьЗначениеПараметра("ТипОбъекта", ТекущиеДанные.Значение);
	
КонецПроцедуры

#КонецОбласти
