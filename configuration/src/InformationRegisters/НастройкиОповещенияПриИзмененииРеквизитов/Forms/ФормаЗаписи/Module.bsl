///////////////////////////////////////////////////////////////////////////////////////////////////////
// Copyright (c) 2025, ООО ДНС Технологии
// SDMS (Software Development Management System) — это корпоративная система учета разработки и управления проектами 
// Все права защищены. Эта программа и сопроводительные материалы предоставляются 
// в соответствии с условиями лицензии General Public License (GNU GPL v3)
// Текст лицензии доступен по ссылке:
// https://www.gnu.org/licenses/gpl-3.0.html
///////////////////////////////////////////////////////////////////////////////////////////////////////

#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Запись.ИсходныйКлючЗаписи.Пустой() Тогда
		Если Параметры.Свойство("ТипОбъекта") Тогда
			Запись.ТипОбъекта = Параметры.ТипОбъекта;
		Иначе
			Отказ = Истина;
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю("Не указан тип объекта");
			Возврат;
		КонецЕсли;
		
		Запись.Автор = ПараметрыСеанса.ТекущийПользователь;
	КонецЕсли;
	
	ЗаполнитьСписокРеквизитов();
	ЗаполнитьСписокКатегорий();
	
	ПредставлениеТипаОбъекта = Строка(ТипЗнч(Запись.ТипОбъекта));
	
КонецПроцедуры

&НаСервере
Процедура ПередЗаписьюНаСервере(Отказ, ТекущийОбъект, ПараметрыЗаписи)
	
	ТекущийОбъект.ДатаИзменения = ТекущаяДатаСеанса();
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура ЗаполнитьСписокКатегорий()
	
	Элементы.КатегорияСообщения.СписокВыбора.ЗагрузитьЗначения(Перечисления.КатегорииСообщенийПользователя.КатегорииОповещенияПользователямПриИзмененииОбъекта());
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьСписокРеквизитов()
	
	СсылкаОбъекта = Запись.ТипОбъекта;
	
	Менеджер = ОбщегоНазначения.МенеджерОбъектаПоСсылке(СсылкаОбъекта);
	
	Если Менеджер = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	СписокРеквизитов = Менеджер.ПроверяемыеСвойстваОбъекта();
	
	Для Каждого Реквизит Из СписокРеквизитов.Реквизиты Цикл
		Элементы.Реквизит.СписокВыбора.Добавить(Реквизит);
	КонецЦикла;
	
	Для Каждого ТабличнаяЧасть Из СписокРеквизитов.ТабличныеЧасти Цикл
		Элементы.Реквизит.СписокВыбора.Добавить(ТабличнаяЧасть.Ключ);
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти
