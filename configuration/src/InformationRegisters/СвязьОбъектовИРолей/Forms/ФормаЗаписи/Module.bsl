///////////////////////////////////////////////////////////////////////////////////////////////////////
// Copyright (c) 2025, ООО ДНС Технологии
// SDMS (Software Development Management System) — это корпоративная система учета разработки и управления проектами 
// Все права защищены. Эта программа и сопроводительные материалы предоставляются 
// в соответствии с условиями лицензии General Public License (GNU GPL v3)
// Текст лицензии доступен по ссылке:
// https://www.gnu.org/licenses/gpl-3.0.html
///////////////////////////////////////////////////////////////////////////////////////////////////////

#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Запись.ИсходныйКлючЗаписи.Пустой() Тогда
		Если Параметры.Свойство("Объект") Тогда
			Запись.Объект = Параметры.Объект;
		КонецЕсли;
		
		Запись.ЗначениеПоУмолчанию = Перечисления.ВидыПравДоступа.Разрешено;
	КонецЕсли;
	
	ПриИзмененииРасчетаПрава(Запись.РассчитыватьПраво, Элементы.ЗначениеПоУмолчанию, Элементы.ИмяМетода);
	УстановитьДоступностьМетодаНастройки(Элементы.ИмяМетодаНастройки, Запись.Роль);
	
	// Параметры выбора ролей (отсеиваем роли конфигурации).
	ПараметрВыбораРолей = Новый ПараметрВыбора("Отбор.ЭтоРольКонфигурации", Ложь);
	МассивВыбораРолей = Новый Массив;
	МассивВыбораРолей.Добавить(ПараметрВыбораРолей);
	ФиксированныеПараметрыВыбора = Новый ФиксированныйМассив(МассивВыбораРолей);

	Элементы.Роль.ПараметрыВыбора = ФиксированныеПараметрыВыбора;
	
КонецПроцедуры

&НаКлиенте
Процедура ПередЗаписью(Отказ, ПараметрыЗаписи)
	
	Если Запись.РассчитыватьПраво И ПустаяСтрока(Запись.ИмяМетода) Тогда
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю("Не указана процедура для расчета права.", 
			Запись.ИсходныйКлючЗаписи, "Запись.ИмяМетода", "ИмяМетода", Отказ);
		
	ИначеЕсли НЕ Запись.РассчитыватьПраво И НЕ ЗначениеЗаполнено(Запись.ЗначениеПоУмолчанию) Тогда
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю("Не указано значение по умолчанию.", 
			Запись.ИсходныйКлючЗаписи, "Запись.ЗначениеПоУмолчанию", "ЗначениеПоУмолчанию", Отказ);
	КонецЕсли;
	
	Если Элементы.ИмяМетодаНастройки.Доступность И НЕ ЗначениеЗаполнено(Запись.ИмяМетодаНастройки) Тогда
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю("Не указана процедура для настройки.", 
			Запись.ИсходныйКлючЗаписи, "Запись.ИмяМетодаНастройки", "ИмяМетодаНастройки", Отказ);
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ПередЗаписьюНаСервере(Отказ, ТекущийОбъект, ПараметрыЗаписи)
	
	//ТекущийОбъект.Автор = ПараметрыСеанса.ТекущийПользователь;
	//ТекущийОбъект.Дата = ТекущаяДатаСеанса();
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовФормы

&НаКлиенте
Процедура РассчитыватьПравоПриИзменении(Элемент)
	
	ПриИзмененииРасчетаПрава(Запись.РассчитыватьПраво, Элементы.ЗначениеПоУмолчанию, Элементы.ИмяМетода);
	
КонецПроцедуры

&НаКлиенте
Процедура РольПриИзменении(Элемент)
	
	УстановитьДоступностьМетодаНастройки(Элементы.ИмяМетодаНастройки, Запись.Роль);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиентеНаСервереБезКонтекста
Процедура ПриИзмененииРасчетаПрава(Знач РассчитыватьПраво, ЭлементЗначениеПоУмолчанию, ЭлементИмяМетода)
	
	ЭлементЗначениеПоУмолчанию.Доступность = НЕ РассчитыватьПраво;
	ЭлементИмяМетода.Доступность = РассчитыватьПраво;
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура УстановитьДоступностьМетодаНастройки(ЭлементФормы, Знач Роль)
	
	ЭлементФормы.Доступность = ?(ЗначениеЗаполнено(Роль), ЭтоРольТипИзменение(Роль), Ложь);
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция ЭтоРольТипИзменение(Знач Роль)
	
	Родитель = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Роль, "Родитель");
	
	Возврат (Родитель = Справочники.ПрограммныеРоли.Изменение);
	
КонецФункции

#КонецОбласти
