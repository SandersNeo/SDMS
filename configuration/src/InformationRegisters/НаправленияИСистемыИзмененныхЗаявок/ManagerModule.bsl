///////////////////////////////////////////////////////////////////////////////////////////////////////
// Copyright (c) 2025, ООО ДНС Технологии
// SDMS (Software Development Management System) — это корпоративная система учета разработки и управления проектами 
// Все права защищены. Эта программа и сопроводительные материалы предоставляются 
// в соответствии с условиями лицензии General Public License (GNU GPL v3)
// Текст лицензии доступен по ссылке:
// https://www.gnu.org/licenses/gpl-3.0.html
///////////////////////////////////////////////////////////////////////////////////////////////////////

#Если Сервер ИЛИ ТолстыйКлиентОбычноеПриложение ИЛИ ВнешнееСоединение Тогда
	
#Область ПрограммныйИнтерфейс

// Добавляет запись в регистр сведений о изменении заявки с направлением и системой
//
// Параметры:
//  Направление	 - СправочникСсылка.НаправленияРазработки	 - направление заявки
//  Система		 - СправочникСсылка.СистемыУчета	 - система заявки
//
Процедура ДобавитьЗапись(Знач Направление, Знач Система) Экспорт
	
	МассивСистем = Новый Массив;
	
	Если ТипЗнч(Система) = Тип("СправочникСсылка.СистемыУчета") Тогда
		МассивСистем.Добавить(Система);
	Иначе
		МассивСистем = Система;
	КонецЕсли;
	
	НаборЗаписей = СоздатьНаборЗаписей();
	НаборЗаписей.Отбор.Направление.Установить(Направление);
	
	Для Каждого СистемаУчета Из МассивСистем Цикл
		КлючЗаписи = Новый УникальныйИдентификатор;
	
		НаборЗаписей.Отбор.Система.Установить(СистемаУчета);
		НаборЗаписей.Отбор.КлючЗаписи.Установить(КлючЗаписи);
		
		НоваяЗапись = НаборЗаписей.Добавить();
		НоваяЗапись.Направление = Направление;
		НоваяЗапись.Система = СистемаУчета;
		НоваяЗапись.КлючЗаписи = КлючЗаписи;
		НоваяЗапись.ДатаОбновления = ТекущаяДатаСеанса();
		
		НаборЗаписей.Записать();
		НаборЗаписей.Очистить();
	КонецЦикла;
	
КонецПроцедуры

// Добавляет записи по измененным направлениям и системам
//
// Параметры:
//  НаправленияИСистемы	 - ТаблицаЗначений	 - таблица направлений и систем
//		* Направление	 - СправочникСсылка.НаправленияРазработки	 - направление
//		* Система	 - СправочникСсылка.СистемыУчета	 - система
//
Процедура ДобавитьЗаписи(Знач НаправленияИСистемы) Экспорт
	
	Если ТипЗнч(НаправленияИСистемы) = Тип("ТаблицаЗначений") Тогда
		Для Каждого Строка Из НаправленияИСистемы Цикл
			ДобавитьЗапись(Строка.Направление, Строка.Система);
		КонецЦикла;
	КонецЕсли;
	
КонецПроцедуры

// Проверяет наличие изменений по направлениям и системам с даты последнего обновления
//
// Параметры:
//  Направление				 - СправочникСсылка.НаправленияРазработки, Массив	 - направление
//  Система					 - СправочникСсылка.СистемыУчета, Массив	 - система
//  ДатаПоследнегоОбновления - Дата	 - дата последнего обновления
// 
// Возвращаемое значение:
//   - Булево
//
Функция ЕстьИзменения(Знач Направление, Знач Система, Знач ДатаПоследнегоОбновления) Экспорт
	
	Направления = Новый Массив;
	Если ТипЗнч(Направление) = Тип("СправочникСсылка.НаправленияРазработки") Тогда
		Направления.Добавить(Направление);
	Иначе
		Направления = Направление;
	КонецЕсли;
	
	Системы = Новый Массив;
	Если ТипЗнч(Система) = Тип("СправочникСсылка.СистемыУчета") Тогда
		Системы.Добавить(Система);
	Иначе
		Системы = Система;
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	НаправленияИСистемыИзмененныхЗаявок.Направление КАК Направление,
	|	НаправленияИСистемыИзмененныхЗаявок.Система КАК Система
	|ИЗ
	|	РегистрСведений.НаправленияИСистемыИзмененныхЗаявок КАК НаправленияИСистемыИзмененныхЗаявок
	|ГДЕ
	|	НаправленияИСистемыИзмененныхЗаявок.Направление В(&Направления)
	|	И НаправленияИСистемыИзмененныхЗаявок.Система В(&Системы)
	|	И НаправленияИСистемыИзмененныхЗаявок.ДатаОбновления > &ДатаПоследнегоОбновления";
	
	Запрос.УстановитьПараметр("Направления", Направления);
	Запрос.УстановитьПараметр("Системы", Системы);
	Запрос.УстановитьПараметр("ДатаПоследнегоОбновления", ДатаПоследнегоОбновления);
	
	Возврат НЕ Запрос.Выполнить().Пустой();
	
КонецФункции

// Производит очистку записей в регистре оставляя записи с самыми последними дата обновления
//
Процедура ОчиститьУстаревшие() Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	НаправленияИСистемыИзмененныхЗаявок.Направление КАК Направление,
	|	НаправленияИСистемыИзмененныхЗаявок.Система КАК Система,
	|	МАКСИМУМ(НаправленияИСистемыИзмененныхЗаявок.КлючЗаписи) КАК КлючЗаписи,
	|	МАКСИМУМ(НаправленияИСистемыИзмененныхЗаявок.ДатаОбновления) КАК ДатаОбновления
	|ИЗ
	|	РегистрСведений.НаправленияИСистемыИзмененныхЗаявок КАК НаправленияИСистемыИзмененныхЗаявок
	|
	|СГРУППИРОВАТЬ ПО
	|	НаправленияИСистемыИзмененныхЗаявок.Направление,
	|	НаправленияИСистемыИзмененныхЗаявок.Система";
	
	РезультатЗапроса = Запрос.Выполнить();
	Если НЕ РезультатЗапроса.Пустой() Тогда
		НаборЗаписей = СоздатьНаборЗаписей();
		НаборЗаписей.Загрузить(РезультатЗапроса.Выгрузить());
		НаборЗаписей.Записать();
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти
	
#КонецЕсли
