///////////////////////////////////////////////////////////////////////////////////////////////////////
// Copyright (c) 2025, ООО ДНС Технологии
// SDMS (Software Development Management System) — это корпоративная система учета разработки и управления проектами 
// Все права защищены. Эта программа и сопроводительные материалы предоставляются 
// в соответствии с условиями лицензии General Public License (GNU GPL v3)
// Текст лицензии доступен по ссылке:
// https://www.gnu.org/licenses/gpl-3.0.html
///////////////////////////////////////////////////////////////////////////////////////////////////////

#Область ПрограммныйИнтерфейс

// Добавляет запись при изменении использования юр лиц для направления
//
// Параметры:
//  Направление		   - СправочникСсылка.НаправлениеРазработки - ссылка на элемент справочника
//  ИспользованиеЮрЛиц - Булево - Истина, если используются юр лица, иначе Ложь 
//
Процедура ДобавитьЗапись(Знач Направление, Знач ИспользованиеЮрЛиц) Экспорт
	
	ТекущаяДата = ТекущаяДатаСеанса();
	Менеджер = СоздатьМенеджерЗаписи();
	
	Менеджер.Период = ТекущаяДата;
	Менеджер.Направление = Направление;
	Менеджер.Использование = ИспользованиеЮрЛиц;
	Менеджер.Автор = ПараметрыСеанса.ТекущийПользователь;
	Менеджер.ДатаДобавления = ТекущаяДата;
	
	Менеджер.Записать();
	
КонецПроцедуры

// Возвращает последнее значение ресурса Использование по Направлению
//
// Параметры:
//  Направление - СправочникСсылка.НаправленияРазработки - ссылка на элемент справочника 
// 
// Возвращаемое значение:
//  Булево - если запись в регистре есть, то значение записи, иначе Ложь
//
Функция Использует(Знач Направление) Экспорт
	
	ИспользуетЮридическиеЛица = Ложь;
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ИспользованиеЮридическихЛицНаправленийСрезПоследних.Использование КАК Использование
	|ИЗ
	|	РегистрСведений.ИспользованиеЮридическихЛицНаправлений.СрезПоследних(, Направление = &Направление) КАК ИспользованиеЮридическихЛицНаправленийСрезПоследних";
	
	Запрос.УстановитьПараметр("Направление", Направление);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Если НЕ РезультатЗапроса.Пустой() Тогда
		Выборка = РезультатЗапроса.Выбрать();
		Выборка.Следующий();
		
		ИспользуетЮридическиеЛица = Выборка.Использование;
	КонецЕсли;
	
	Возврат	ИспользуетЮридическиеЛица;
		
КонецФункции

#КонецОбласти
