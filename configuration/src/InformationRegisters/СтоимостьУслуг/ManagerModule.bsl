///////////////////////////////////////////////////////////////////////////////////////////////////////
// Copyright (c) 2025, ООО ДНС Технологии
// SDMS (Software Development Management System) — это корпоративная система учета разработки и управления проектами 
// Все права защищены. Эта программа и сопроводительные материалы предоставляются 
// в соответствии с условиями лицензии General Public License (GNU GPL v3)
// Текст лицензии доступен по ссылке:
// https://www.gnu.org/licenses/gpl-3.0.html
///////////////////////////////////////////////////////////////////////////////////////////////////////

#Область ПрограммныйИнтерфейс

// Возвращает стоимость услуги в зависимости от настроек юр лица
//
// Параметры:
//  Услуга				 - СправочникСсылка.Услуги, Массив - Услуга или массив услуг для которых нужно получить стоимость
//  ЮридическоеЛицо	 - СправочникСсылка.ЮридическиеЛица - Юридическое лицо, в зависимости от настроек которого определяется цена
//  ДатаДокумента		 - Дата, Граница - Дата или граница для среза последних
// 
// Возвращаемое значение:
//  Число - Стоимость услуги
//
Функция ПолучитьСтоимость(Знач Услуги, Знач ЮридическоеЛицо, Знач ДатаДокумента) Экспорт
	
	Если ТипЗнч(Услуги) <> Тип("Массив") Тогда
		СписокУслуг = Новый Массив;
		СписокУслуг.Добавить(Услуги);
		
		Стоимость = 0;
		ВернутьСоответствие = Ложь;
	Иначе
		СписокУслуг = Услуги;
		
		Стоимость = Новый Соответствие;
		ВернутьСоответствие = Истина;
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ВложенныйЗапрос.Услуга КАК Услуга,
	|	ВложенныйЗапрос.Приоритет КАК Приоритет,
	|	ВложенныйЗапрос.Цена КАК Цена
	|ПОМЕСТИТЬ ПриоритетыЦенУслуг
	|ИЗ
	|	(ВЫБРАТЬ
	|		0 КАК Приоритет,
	|		СтоимостьУслугСрезПоследних.Услуга КАК Услуга,
	|		СтоимостьУслугСрезПоследних.ЮридическоеЛицо КАК ЮридическоеЛицо,
	|		СтоимостьУслугСрезПоследних.Цена КАК Цена
	|	ИЗ
	|		РегистрСведений.СтоимостьУслуг.СрезПоследних(
	|				&Дата,
	|				Услуга В (&СписокУслуг)
	|					И ЮридическоеЛицо = &ЮридическоеЛицо) КАК СтоимостьУслугСрезПоследних
	|	ГДЕ
	|		&СпособВыбораЦены В (ЗНАЧЕНИЕ(Перечисление.СпособыВыбораСтоимостиУслуг.ПоЮридическомуЛицу), ЗНАЧЕНИЕ(Перечисление.СпособыВыбораСтоимостиУслуг.ПриоритетПоЮридическомуЛицу))
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		1,
	|		СтоимостьУслугСрезПоследних.Услуга,
	|		СтоимостьУслугСрезПоследних.ЮридическоеЛицо,
	|		СтоимостьУслугСрезПоследних.Цена
	|	ИЗ
	|		РегистрСведений.СтоимостьУслуг.СрезПоследних(
	|				&Дата,
	|				Услуга В (&СписокУслуг)
	|					И ЮридическоеЛицо = ЗНАЧЕНИЕ(Справочник.ЮридическиеЛица.ПустаяСсылка)) КАК СтоимостьУслугСрезПоследних
	|	ГДЕ
	|		&СпособВыбораЦены <> ЗНАЧЕНИЕ(Перечисление.СпособыВыбораСтоимостиУслуг.ПоЮридическомуЛицу)) КАК ВложенныйЗапрос
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ПриоритетыЦенУслуг.Услуга КАК Услуга,
	|	МИНИМУМ(ПриоритетыЦенУслуг.Приоритет) КАК Приоритет
	|ПОМЕСТИТЬ ПриоритетЦеныУслуг
	|ИЗ
	|	ПриоритетыЦенУслуг КАК ПриоритетыЦенУслуг
	|
	|СГРУППИРОВАТЬ ПО
	|	ПриоритетыЦенУслуг.Услуга
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ПриоритетыЦенУслуг.Услуга КАК Услуга,
	|	ПриоритетыЦенУслуг.Цена КАК Цена
	|ИЗ
	|	ПриоритетЦеныУслуг КАК ПриоритетЦеныУслуг
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ПриоритетыЦенУслуг КАК ПриоритетыЦенУслуг
	|		ПО ПриоритетЦеныУслуг.Услуга = ПриоритетыЦенУслуг.Услуга
	|			И ПриоритетЦеныУслуг.Приоритет = ПриоритетыЦенУслуг.Приоритет";
	
	Запрос.УстановитьПараметр("СписокУслуг", СписокУслуг);
	Запрос.УстановитьПараметр("ЮридическоеЛицо", ЮридическоеЛицо);
	Запрос.УстановитьПараметр("СпособВыбораЦены", ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ЮридическоеЛицо, "СпособВыбораСтоимостиУслуги"));
	Запрос.УстановитьПараметр("Дата", ДатаДокумента);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Если НЕ РезультатЗапроса.Пустой() Тогда
		Выборка = РезультатЗапроса.Выбрать();
		
		Если ВернутьСоответствие Тогда
			Пока Выборка.Следующий() Цикл
				Стоимость.Вставить(Выборка.Услуга, Выборка.Цена);
			КонецЦикла;
		Иначе
			Выборка.Следующий();
			Стоимость = Выборка.Цена;
		КонецЕсли;
	КонецЕсли;
	
	Возврат Стоимость;
	
КонецФункции

#КонецОбласти
