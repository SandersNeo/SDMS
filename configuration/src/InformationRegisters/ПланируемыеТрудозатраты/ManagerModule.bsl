///////////////////////////////////////////////////////////////////////////////////////////////////////
// Copyright (c) 2025, ООО ДНС Технологии
// SDMS (Software Development Management System) — это корпоративная система учета разработки и управления проектами 
// Все права защищены. Эта программа и сопроводительные материалы предоставляются 
// в соответствии с условиями лицензии General Public License (GNU GPL v3)
// Текст лицензии доступен по ссылке:
// https://www.gnu.org/licenses/gpl-3.0.html
///////////////////////////////////////////////////////////////////////////////////////////////////////

#Область ПрограммныйИнтерфейс

// Функция - Получить нераспределенное время объектов
//
// Параметры:
//  СписокОбъектов - ДокументСсылка.Задача, ДокументСсылка.ЗаявкаНаРазработку, 
//					 ДокументСсылка.ВнутреннееЗадание - ссылки на выбранные объекты
//  Спринт	 - ДокументСсылка.Спринт - ссылка на спринт
// 
// Возвращаемое значение:
// Соответствие  - Содержит ссылку на объект и нераспределенные трудозатраты
//
Функция ПолучитьНераспределенноеВремяОбъектов(СписокОбъектов, Спринт = Неопределено) Экспорт
	
	Запрос = Новый Запрос; 
	Запрос.Текст =
	"ВЫБРАТЬ
	|	Задача.Ссылка КАК Ссылка
	|ПОМЕСТИТЬ Объекты
	|ИЗ
	|	Документ.Задача КАК Задача
	|ГДЕ
	|	Задача.Ссылка В(&Ссылка)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ЗаявкаНаРазработку.Ссылка
	|ИЗ
	|	Документ.ЗаявкаНаРазработку КАК ЗаявкаНаРазработку
	|ГДЕ
	|	ЗаявкаНаРазработку.Ссылка В(&Ссылка)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ВнутреннееЗадание.Ссылка
	|ИЗ
	|	Документ.ВнутреннееЗадание КАК ВнутреннееЗадание
	|ГДЕ
	|	ВнутреннееЗадание.Ссылка В(&Ссылка)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ПланируемыеТрудозатратыПоСпринтам.Объект КАК Объект,
	|	СУММА(ПланируемыеТрудозатратыПоСпринтам.Трудозатраты) КАК Трудозатраты,
	|	ПланируемыеТрудозатратыПоСпринтам.ТипТрудозатрат КАК ТипТрудозатрат
	|ПОМЕСТИТЬ РаспределенныйПлан
	|ИЗ
	|	РегистрСведений.ПланируемыеТрудозатратыПоСпринтам КАК ПланируемыеТрудозатратыПоСпринтам
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Объекты КАК Объекты
	|		ПО (Объекты.Ссылка = ПланируемыеТрудозатратыПоСпринтам.Объект)
	|
	|СГРУППИРОВАТЬ ПО
	|	ПланируемыеТрудозатратыПоСпринтам.Объект,
	|	ПланируемыеТрудозатратыПоСпринтам.ТипТрудозатрат
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Объекты.Ссылка КАК Ссылка,
	|	ПланируемыеТрудозатраты.Трудозатраты - ЕСТЬNULL(РаспределенныйПланРазработка.Трудозатраты, 0) КАК Трудозатраты,
	|	ЕСТЬNULL(ПланТрудозатратТестирования.Трудозатраты, 0) - ЕСТЬNULL(РаспределенныйПланТестирование.Трудозатраты, 0) КАК ТрудозатратыТестирования
	|ИЗ
	|	Объекты КАК Объекты
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.ПланируемыеТрудозатраты КАК ПланируемыеТрудозатраты
	|		ПО Объекты.Ссылка = ПланируемыеТрудозатраты.Объект
	|			И ПланируемыеТрудозатраты.ТипТрудозатрат = ЗНАЧЕНИЕ(Перечисление.ТипыТрудозатрат.Разработка)
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.СоставСпринтов КАК СоставСпринтов
	|		ПО Объекты.Ссылка = СоставСпринтов.Объект
	|			И (СоставСпринтов.Спринт = &Спринт)
	|		ЛЕВОЕ СОЕДИНЕНИЕ РаспределенныйПлан КАК РаспределенныйПланРазработка
	|		ПО (РаспределенныйПланРазработка.Объект = Объекты.Ссылка)
	|			И (РаспределенныйПланРазработка.ТипТрудозатрат = ЗНАЧЕНИЕ(Перечисление.ТипыТрудозатрат.Разработка))
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ПланируемыеТрудозатраты КАК ПланТрудозатратТестирования
	|		ПО (ПланТрудозатратТестирования.Объект = Объекты.Ссылка)
	|			И ПланТрудозатратТестирования.ТипТрудозатрат = ЗНАЧЕНИЕ(Перечисление.ТипыТрудозатрат.Тестирование)
	|		ЛЕВОЕ СОЕДИНЕНИЕ РаспределенныйПлан КАК РаспределенныйПланТестирование
	|		ПО (РаспределенныйПланТестирование.Объект = Объекты.Ссылка)
	|			И (РаспределенныйПланТестирование.ТипТрудозатрат = ЗНАЧЕНИЕ(Перечисление.ТипыТрудозатрат.Тестирование))
	|ГДЕ
	|	СоставСпринтов.Объект ЕСТЬ NULL
	|	И ПланируемыеТрудозатраты.Трудозатраты - ЕСТЬNULL(РаспределенныйПланРазработка.Трудозатраты, 0) > 0";
	
	Запрос.УстановитьПараметр("Ссылка", СписокОбъектов);
	Запрос.УстановитьПараметр("Спринт", Спринт);
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	СвободныеЧасыОбъектов = Новый Соответствие;
	
	Пока Выборка.Следующий() Цикл
		ВремяНаСпринт = Новый Структура("Трудозатраты, ТрудозатратыТестирования");
		ЗаполнитьЗначенияСвойств(ВремяНаСпринт, Выборка);	

		СвободныеЧасыОбъектов.Вставить(Выборка.Ссылка, ВремяНаСпринт);
	КонецЦикла;
	
	Возврат СвободныеЧасыОбъектов;
	
КонецФункции

#КонецОбласти
