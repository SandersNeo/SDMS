///////////////////////////////////////////////////////////////////////////////////////////////////////
// Copyright (c) 2025, ООО ДНС Технологии
// SDMS (Software Development Management System) — это корпоративная система учета разработки и управления проектами 
// Все права защищены. Эта программа и сопроводительные материалы предоставляются 
// в соответствии с условиями лицензии General Public License (GNU GPL v3)
// Текст лицензии доступен по ссылке:
// https://www.gnu.org/licenses/gpl-3.0.html
///////////////////////////////////////////////////////////////////////////////////////////////////////

#Область ПрограммныйИнтерфейс

// Добавить запись в регистр
//
// Параметры:
//  Спринт				 - ДокументСсылка.Спринт - Спринт
//  ОбъектыСпринта		 - СписокЗначений		 - Список объектов спринта
//  ТекущаяДатаСеанса	 - Дата					 - Текущая Дата
//
Процедура Добавить(Знач Спринт, Знач ОбъектыСпринта, Знач ТекущаяДатаСеанса) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	#Область ТекстЗапроса
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	ДокументЗадача.Ссылка КАК Объект
	|ПОМЕСТИТЬ НовыеОбъектыСпринта
	|ИЗ
	|	Документ.Задача КАК ДокументЗадача
	|ГДЕ
	|	ДокументЗадача.Ссылка В(&СписокОбъектов)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ДокументВнутреннееЗадание.Ссылка
	|ИЗ
	|	Документ.ВнутреннееЗадание КАК ДокументВнутреннееЗадание
	|ГДЕ
	|	ДокументВнутреннееЗадание.Ссылка В(&СписокОбъектов)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ДокументЗаявкаНаРазработку.Ссылка
	|ИЗ
	|	Документ.ЗаявкаНаРазработку КАК ДокументЗаявкаНаРазработку
	|ГДЕ
	|	ДокументЗаявкаНаРазработку.Ссылка В(&СписокОбъектов)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ИсторияУчастияОбъектовВСпринтахСрезПоследних.Объект КАК Объект
	|ПОМЕСТИТЬ СтарыеЗадачиСпринта
	|ИЗ
	|	РегистрСведений.ИсторияУчастияОбъектовВСпринтах.СрезПоследних(, Спринт = &Спринт) КАК ИсторияУчастияОбъектовВСпринтахСрезПоследних
	|ГДЕ
	|	ИсторияУчастияОбъектовВСпринтахСрезПоследних.ВидДействия = 1
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	&Спринт КАК Спринт,
	|	ЕСТЬNULL(НовыеОбъектыСпринта.Объект, СтарыеЗадачиСпринта.Объект) КАК Объект,
	|	ВЫБОР
	|		КОГДА СтарыеЗадачиСпринта.Объект ЕСТЬ NULL
	|			ТОГДА 1
	|		ИНАЧЕ 2
	|	КОНЕЦ КАК ВидДействия,
	|	&ТекущаяДата КАК Период,
	|	&Автор КАК Автор
	|ИЗ
	|	НовыеОбъектыСпринта КАК НовыеОбъектыСпринта
	|		ПОЛНОЕ СОЕДИНЕНИЕ СтарыеЗадачиСпринта КАК СтарыеЗадачиСпринта
	|		ПО НовыеОбъектыСпринта.Объект = СтарыеЗадачиСпринта.Объект
	|ГДЕ
	|	(НовыеОбъектыСпринта.Объект ЕСТЬ NULL
	|			ИЛИ СтарыеЗадачиСпринта.Объект ЕСТЬ NULL)";
	#КонецОбласти
	
	Запрос.УстановитьПараметр("Спринт", Спринт);
	Запрос.УстановитьПараметр("СписокОбъектов", ОбъектыСпринта);
	Запрос.УстановитьПараметр("Автор", ПараметрыСеанса.ТекущийПользователь);
	Запрос.УстановитьПараметр("ТекущаяДата", ТекущаяДатаСеанса);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Если НЕ РезультатЗапроса.Пустой() Тогда
		НаборЗаписей = РегистрыСведений.ИсторияУчастияОбъектовВСпринтах.СоздатьНаборЗаписей();
		НаборЗаписей.Отбор.Спринт.Установить(Спринт);
		НаборЗаписей.Загрузить(РезультатЗапроса.Выгрузить());
		НаборЗаписей.Записать(Ложь);
	КонецЕсли;

КонецПроцедуры

// Добавить запись в регистр для новых объектов
//
// Параметры:
//  Спринт				 - ДокументСсылка.Спринт - Спринт
//  ОбъектыСпринта		 - СписокЗначений		 - Список объектов спринта
//  ТекущаяДатаСеанса	 - Дата					 - Текущая Дата
//
Процедура ДобавитьНовыйОбъект(Знач Спринт, Знач ОбъектыСпринта, Знач ТекущаяДатаСеанса) Экспорт
	
	НаборЗаписей = РегистрыСведений.ИсторияУчастияОбъектовВСпринтах.СоздатьНаборЗаписей();

	Для Каждого ЭлементСписка Из ОбъектыСпринта Цикл
		НаборЗаписей.Отбор.Спринт.Установить(Спринт);
		НаборЗаписей.Отбор.Объект.Установить(ЭлементСписка.Значение);
		
		НоваяЗапись = НаборЗаписей.Добавить();
		НоваяЗапись.Спринт = Спринт;
		НоваяЗапись.Объект = ЭлементСписка.Значение;
		НоваяЗапись.Период = ТекущаяДатаСеанса;
		НоваяЗапись.ВидДействия = 1;
		НоваяЗапись.Автор = ПараметрыСеанса.ТекущийПользователь;
		
		НаборЗаписей.Записать();		
		НаборЗаписей.Очистить();
	КонецЦикла;	
	
КонецПроцедуры

#КонецОбласти
