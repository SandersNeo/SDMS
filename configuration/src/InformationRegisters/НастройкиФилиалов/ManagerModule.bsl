///////////////////////////////////////////////////////////////////////////////////////////////////////
// Copyright (c) 2025, ООО ДНС Технологии
// SDMS (Software Development Management System) — это корпоративная система учета разработки и управления проектами 
// Все права защищены. Эта программа и сопроводительные материалы предоставляются 
// в соответствии с условиями лицензии General Public License (GNU GPL v3)
// Текст лицензии доступен по ссылке:
// https://www.gnu.org/licenses/gpl-3.0.html
///////////////////////////////////////////////////////////////////////////////////////////////////////

#Если Сервер ИЛИ ВнешнееСоединение ИЛИ ТолстыйКлиентОбычноеПриложение Тогда

#Область ПрограммныйИнтерфейс

// Возвращает значение настройки для филиала, если настройка не найдена, возвращает Неопределено
//
// Параметры:
//  Филиал		 - СправочникСсылка.Филиалы	 - филиал-владелец настройки
//  Настройка	 - ПланВидовХарактеристикСсылка.ВидыНастроекФилиалов	 - сслыка на получаемую настройку
// 
// Возвращаемое значение:
//  Характеристика.ВидыНастроекФилиалов, Неопределено - значение настройки или Неопределено если настройка отсутствует
//
Функция ЗначениеНастройки(Знач Филиал, Знач Настройка) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ВидыНастроекФилиалов.ТипЗначения КАК ТипЗначения,
	|	ЕСТЬNULL(НастройкиФилиалов.Значение, НЕОПРЕДЕЛЕНО) КАК Значение
	|ИЗ
	|	ПланВидовХарактеристик.ВидыНастроекФилиалов КАК ВидыНастроекФилиалов
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.НастройкиФилиалов КАК НастройкиФилиалов
	|		ПО ВидыНастроекФилиалов.Ссылка = НастройкиФилиалов.Настройка
	|			И (НастройкиФилиалов.Филиал = &Филиал)
	|ГДЕ
	|	ВидыНастроекФилиалов.Ссылка = &Настройка";
	
	Запрос.УстановитьПараметр("Филиал", Филиал);
	Запрос.УстановитьПараметр("Настройка", Настройка);
	
	Выборка = Запрос.Выполнить().Выбрать();
	Выборка.Следующий();
	
	// Если значение заполнено, возвращаем его.
	// Или же приводим его к стандартному значению (Ложь, "", ПустаяСсылка) - для того чтобы
	// избавиться от лишних проверок в дальнейшем.
	Если ЗначениеЗаполнено(Выборка.Значение) Тогда
		Результат = Выборка.Значение;
	Иначе
		ОписаниеТипов = Новый ОписаниеТипов(Выборка.ТипЗначения);
		Результат = ОписаниеТипов.ПривестиЗначение();
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

// Возвращает значения настроек филиала в виде структуры 
//
// Параметры:
//  Филиал		 - СправочникСсылка.Филиалы	 - ссылка на филиал-владелец настроек
//  Настройки	 - Массив	 - содержит ссылки на виды настроек из ПланВидовХарактеристик.ВидыНастроекФилиалов
// 
// Возвращаемое значение:
//  Структура - значения настроек
//		* Ключ	 - Строка	 - имя предопределенных данных настройки
//		* Значение	 - Характеристика.ВидыНастроекФилиала	 - значение настройки или значение по-умолчанию если не заполнена
//
Функция ЗначенияНастроек(Знач Филиал, Знач Настройки) Экспорт
	
	Результат = Новый Структура;
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ВидыНастроекФилиалов.ТипЗначения КАК ТипЗначения,
	|	ЕСТЬNULL(НастройкиФилиалов.Значение, НЕОПРЕДЕЛЕНО) КАК Значение,
	|	ВидыНастроекФилиалов.ИмяПредопределенныхДанных КАК Наименование
	|ИЗ
	|	ПланВидовХарактеристик.ВидыНастроекФилиалов КАК ВидыНастроекФилиалов
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.НастройкиФилиалов КАК НастройкиФилиалов
	|		ПО ВидыНастроекФилиалов.Ссылка = НастройкиФилиалов.Настройка
	|			И (НастройкиФилиалов.Филиал = &Филиал)
	|ГДЕ
	|	ВидыНастроекФилиалов.Ссылка В(&Настройка)";
	
	Запрос.УстановитьПараметр("Филиал", Филиал);
	Запрос.УстановитьПараметр("Настройка", Настройки);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Если НЕ РезультатЗапроса.Пустой() Тогда
		
		Выборка = РезультатЗапроса.Выбрать();
		Пока Выборка.Следующий() Цикл
			
			// Если значение заполнено, возвращаем его.
			// Или же приводим его к стандартному значению (Ложь, "", ПустаяСсылка) - для того чтобы
			// избавиться от лишних проверок в дальнейшем.
			Если ЗначениеЗаполнено(Выборка.Значение) Тогда
				ЗначениеНастройки = Выборка.Значение;
			Иначе
				ОписаниеТипов = Новый ОписаниеТипов(Выборка.ТипЗначения);
				ЗначениеНастройки = ОписаниеТипов.ПривестиЗначение();
			КонецЕсли;
			
			Результат.Вставить(Выборка.Наименование, ЗначениеНастройки);
		КонецЦикла;
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

// Сохраняет значения нескольких настроек. Если вторым параметром передан массив,
// то элементами массива должны быть структуры
//
// Параметры:
//  Филиал			 - СправочникСсылка.Филиалы	 - филиал-владелец настроек
//  ТаблицаНастроек	 - ТаблицаЗначений, Массив	 - данные настроек
//		* Настройка	 - ПланВидовХарактеристикСсылка.ВидыНастроекФилиалов	 - настройка
//		* Значение	 - Характеристика.ВидыНастроекФилиалов	 - значение настройки
//
Процедура СохранитьНастройки(Знач Филиал, Знач ТаблицаНастроек) Экспорт
	
	Для Каждого Строка Из ТаблицаНастроек Цикл
		СохранитьНастройку(Филиал, Строка.Настройка, Строка.Значение);
	КонецЦикла;
	
КонецПроцедуры

// Сохраняет значение настройки для филиала
//
// Параметры:
//  Филиал		 - СправочникСсылка.Филиалы						 - филиал-владелец настройки
//  Настройка	 - ПланВидовХарактеристикСсылка.ВидыНастроекФилиала	 - настройка
//  Значение	 - Характеристика.ВидыНастроекФилиала				 - значение настройки
//
Процедура СохранитьНастройку(Знач Филиал, Знач Настройка, Знач Значение) Экспорт
	
	НаборЗаписей = СоздатьНаборЗаписей();
	НаборЗаписей.Отбор.Филиал.Установить(Филиал);
	НаборЗаписей.Отбор.Настройка.Установить(Настройка);
	
	НоваяЗапись = НаборЗаписей.Добавить();
	НоваяЗапись.Филиал = Филиал;
	НоваяЗапись.Настройка = Настройка;
	НоваяЗапись.Значение = Значение;
	НоваяЗапись.ДатаИзменения = ТекущаяДатаСеанса();
	НоваяЗапись.Автор = ПараметрыСеанса.ТекущийПользователь;
	
	НачатьТранзакцию();
	
	Попытка           
		
		Если ИнтеграцияДополнительныхПодсистем.ПодсистемаСуществует("КвалификацияСпециалистов") И
			Настройка = ПланыВидовХарактеристик.ВидыНастроекФилиалов.ПрофильКвалификации Тогда 
			
			ИнтеграцияДополнительныхПодсистем.ПрофильФилиалаПриИзменении(Филиал, Значение);
	
		ИначеЕсли Настройка = ПланыВидовХарактеристик.ВидыНастроекФилиалов.ПроцессУстановкиСтатусов Тогда
			РегистрыСведений.ПорядокЭтаповКанбана.ПроцессФилиалаПриИзменении(
				Филиал, Справочники.ТипыОбъектовВладельцев.Документ_Задача, Значение);
				
			РегистрыСведений.ОчередьКанбан.ЗаменитьЭтапыВОчереди(Филиал, Перечисления.ТипыКанбана.ЗадачаНовый);
			
		ИначеЕсли Настройка = ПланыВидовХарактеристик.ВидыНастроекФилиалов.ПроцессУстановкиСтатусовВнутреннееЗадание Тогда
			РегистрыСведений.ПорядокЭтаповКанбана.ПроцессФилиалаПриИзменении(
				Филиал, Справочники.ТипыОбъектовВладельцев.Документ_ВнутреннееЗадание, Значение);
				
			РегистрыСведений.ОчередьКанбан.ЗаменитьЭтапыВОчереди(Филиал, Перечисления.ТипыКанбана.ВнутреннееЗаданиеНовый);
		КонецЕсли;

		НаборЗаписей.Записать();
				
		ЗафиксироватьТранзакцию();
	Исключение
		ОтменитьТранзакцию();
		ТекстОшибки = ПодробноеПредставлениеОшибки(ИнформацияОбОшибке());
		ЗаписьЖурналаРегистрации("НастройкиФилиалов.СохранитьНастройку", УровеньЖурналаРегистрации.Ошибка, , , ТекстОшибки);
	КонецПопытки;	

КонецПроцедуры

// Удаляет настройку филиала
//
// Параметры:
//  Филиал		 - СправочникСсылка.Филиалы	 - филиал-владелец настройки
//  Настройка	 - ПланВидовХарактеристикСсылка.ВидыНастроекФилиалов	 - настройка, значение которой требуется удалить
//
Процедура УдалитьНастройку(Знач Филиал, Знач Настройка) Экспорт
	
	НаборЗаписей = СоздатьНаборЗаписей();
	НаборЗаписей.Отбор.Филиал.Установить(Филиал);
	НаборЗаписей.Отбор.Настройка.Установить(Настройка);
	
	НаборЗаписей.Записать();
	
КонецПроцедуры

#КонецОбласти

#КонецЕсли
