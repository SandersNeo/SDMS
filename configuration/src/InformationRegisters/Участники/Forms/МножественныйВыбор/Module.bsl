///////////////////////////////////////////////////////////////////////////////////////////////////////
// Copyright (c) 2025, ООО ДНС Технологии
// SDMS (Software Development Management System) — это корпоративная система учета разработки и управления проектами 
// Все права защищены. Эта программа и сопроводительные материалы предоставляются 
// в соответствии с условиями лицензии General Public License (GNU GPL v3)
// Текст лицензии доступен по ссылке:
// https://www.gnu.org/licenses/gpl-3.0.html
///////////////////////////////////////////////////////////////////////////////////////////////////////

#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ЗаполнитьРеквизитыФормы();	
	УстановитьЗаголовкиЭлементов();	
	
	ИнтерфейсПриложения.УстановитьУсловноеОформлениеСпискаДоступныхЗначений(
		ЭтотОбъект, "ДоступныеПользователи.Избранное", "ДоступныеПользователиНаименование");
	
	ЗаполнитьСпискиПользователей();
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовФормы

&НаКлиенте
Процедура СтрокаПоискаИзменениеТекстаРедактирования(Элемент, Текст, СтандартнаяОбработка)
	
	// Установка отбора на основании значения в строке поиска
	ИнтерфейсПриложенияКлиент.УстановитьОтборПриИзмененииСтрокиПоиска(
		ЭтотОбъект, "ДоступныеПользователи", Текст, "Наименование");	
	
КонецПроцедуры

&НаКлиенте
Процедура СтрокаПоискаОчистка(Элемент, СтандартнаяОбработка)
	
	ИнтерфейсПриложенияКлиент.СброситьПоиск(ЭтотОбъект, "ДоступныеПользователи");
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// Обработчики событий таблицы "ВыбранныеПользователи"

&НаКлиенте
Процедура ВыбранныеПользователиВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	Если Поле.Имя = "ВыбранныеПользователиИсключить" Тогда
		НайденнаяСтрока = ВыбранныеПользователи.НайтиПоИдентификатору(ВыбраннаяСтрока);
		НайденнаяСтрока = ДоступныеПользователи.НайтиПоИдентификатору(НайденнаяСтрока.ИдентификаторСтрокиСписка);
		
		ИсключитьПользователя(НайденнаяСтрока);
	КонецЕсли;
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// Обработчики событий таблицы "ДоступныеПользователи"

&НаКлиенте
Процедура ДоступныеПользователиВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	Если Поле.Имя = "ДоступныеПользователиНаименование" Тогда
		НайденнаяСтрока = ДоступныеПользователи.НайтиПоИдентификатору(ВыбраннаяСтрока);
		НайденнаяСтрока.Пометка = НЕ НайденнаяСтрока.Пометка;
		
		ОбработатьИзменениеСтрокиДоступныхПользователей(НайденнаяСтрока);
		
	ИначеЕсли Поле.Имя = "ДоступныеПользователиИзбранное" Тогда
		НайденнаяСтрока = ДоступныеПользователи.НайтиПоИдентификатору(ВыбраннаяСтрока);
		НайденнаяСтрока.Избранное = НЕ НайденнаяСтрока.Избранное;
		
		ОбщегоНазначенияВызовСервера.ОбработатьПометкуИзбранного(НайденнаяСтрока.Пользователь,
			НайденнаяСтрока.Избранное, "Пользователи");
		
		ДоступныеПользователи.Сортировать("Избранное УБЫВ, Наименование");
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ДоступныеПользователиПометкаПриИзменении(Элемент)
	
	ОбработатьИзменениеСтрокиДоступныхПользователей(Элементы.ДоступныеПользователи.ТекущиеДанные);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ОтметитьИзбранные(Команда)
	
	Для Каждого СтрокаТаблицы Из ДоступныеПользователи Цикл
		Если СтрокаТаблицы.Избранное Тогда
			Если НЕ СтрокаТаблицы.Пометка Тогда
				ДобавитьПользователя(СтрокаТаблицы);
			КонецЕсли;
		Иначе
			Если СтрокаТаблицы.Пометка Тогда
				ИсключитьПользователя(СтрокаТаблицы);
			КонецЕсли;
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры

&НаКлиенте
Процедура ОчиститьПоиск(Команда)
	
	ИнтерфейсПриложенияКлиент.СброситьПоиск(ЭтотОбъект, "ДоступныеПользователи");
	
КонецПроцедуры

&НаКлиенте
Процедура СохранитьИзменения(Команда)
	
	Результат = Новый Массив;
	
	Для Каждого Строка Из ВыбранныеПользователи Цикл
		Результат.Добавить(Строка.Пользователь);
	КонецЦикла;
	
	ЕстьИзменения = СохранитьИзмененияНаСервере(Результат);
	
	Закрыть(ЕстьИзменения);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

////////////////////////////////////////////////////////////////////////////////
// Управление пользователями в списках

&НаКлиенте
Процедура ДобавитьПользователя(СтрокаТаблицы)
	
	НоваяСтрокаВыбранных = ВыбранныеПользователи.Добавить();
	НоваяСтрокаВыбранных.Исключить = БиблиотекаКартинок.УдалитьЗначение;
	
	ЗаполнитьЗначенияСвойств(НоваяСтрокаВыбранных, СтрокаТаблицы, "Пользователь, Наименование");
	
	СтрокаТаблицы.Пометка = Истина;
	
	СтрокаТаблицы.ИдентификаторСтрокиСписка = НоваяСтрокаВыбранных.ПолучитьИдентификатор();
	НоваяСтрокаВыбранных.ИдентификаторСтрокиСписка = СтрокаТаблицы.ПолучитьИдентификатор();
	
КонецПроцедуры

&НаКлиенте
Процедура ИсключитьПользователя(СтрокаТаблицы)
	
	НайденнаяСтрока = ВыбранныеПользователи.НайтиПоИдентификатору(СтрокаТаблицы.ИдентификаторСтрокиСписка);
	Если НайденнаяСтрока <> Неопределено Тогда
		ВыбранныеПользователи.Удалить(НайденнаяСтрока);
	КонецЕсли;	
	
	СтрокаТаблицы.Пометка = Ложь;
	СтрокаТаблицы.ИдентификаторСтрокиСписка = -1;
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработатьИзменениеСтрокиДоступныхПользователей(СтрокаТаблицы)
	
	Если СтрокаТаблицы.Пометка = Истина Тогда
		ДобавитьПользователя(СтрокаТаблицы);	
	Иначе
		ИсключитьПользователя(СтрокаТаблицы);	
	КонецЕсли;			
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// Прочие процедуры и функции

&НаСервере
Процедура ДобавитьУчастников(Изменено, Знач ДобавляемыеПользователи)
	
	Если ДобавляемыеПользователи.Количество() > 0 Тогда	
		Для Каждого Пользователь Из ДобавляемыеПользователи Цикл	
			РегистрыСведений.Участники.Добавить(Объект, Пользователь, Роль, Истина);
		КонецЦикла;
		
		Изменено = Истина;
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьРеквизитыФормы()
	
	Объект = Параметры.Объект;
	Роль = Параметры.Роль;
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьСпискиПользователей()
		
	ВыбранныеПользователи.Очистить();
	ДоступныеПользователи.Очистить();
		
	Запрос = Новый Запрос;
	Запрос.Текст =
#Область ТекстЗапроса
	"ВЫБРАТЬ
	|	ВЫРАЗИТЬ(ИзбранныеПользователи.Объект КАК Справочник.Пользователи) КАК Пользователь
	|ПОМЕСТИТЬ ИзбранныеПользователи
	|ИЗ
	|	РегистрСведений.ИзбранныеОбъекты КАК ИзбранныеПользователи
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.Пользователи КАК Пользователи
	|		ПО ИзбранныеПользователи.Объект = Пользователи.Ссылка
	|			И (НЕ Пользователи.Недействителен)
	|			И (НЕ Пользователи.Служебный)
	|ГДЕ
	|	ИзбранныеПользователи.Владелец = &ТекущийПользователь
	|	И ИзбранныеПользователи.ТипОбъекта = ЗНАЧЕНИЕ(Перечисление.ТипыИзбранныхОбъектов.Пользователи)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Пользователи.Ссылка КАК Пользователь,
	|	ЛОЖЬ КАК Пометка
	|ПОМЕСТИТЬ ВсеВозможныеУчастники
	|ИЗ
	|	Справочник.Пользователи КАК Пользователи
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.Участники КАК Участники
	|		ПО Пользователи.Ссылка = Участники.Пользователь
	|			И (Участники.Роль = &Роль)
	|			И (Участники.Объект = &Объект)
	|			И (НЕ Участники.Скрытый)
	|ГДЕ
	|	НЕ Пользователи.Недействителен
	|	И НЕ Пользователи.Служебный
	|	И Участники.Пользователь ЕСТЬ NULL
	|
	|ОБЪЕДИНИТЬ
	|
	|ВЫБРАТЬ
	|	Участники.Пользователь,
	|	ИСТИНА
	|ИЗ
	|	РегистрСведений.Участники КАК Участники
	|ГДЕ
	|	НЕ Участники.Скрытый
	|	И Участники.Объект = &Объект
	|	И Участники.Роль = &Роль
	|	И Участники.Пользователь ССЫЛКА Справочник.Пользователи
	|	И ВЫБОР
	|			КОГДА &СуперПользователь
	|				ТОГДА ИСТИНА
	|			ИНАЧЕ Участники.Пользователь = &ТекущийПользователь
	|		КОНЕЦ
	|
	|ОБЪЕДИНИТЬ
	|
	|ВЫБРАТЬ
	|	Участники.Пользователь,
	|	ИСТИНА
	|ИЗ
	|	РегистрСведений.Участники КАК Участники
	|ГДЕ
	|	НЕ Участники.Скрытый
	|	И Участники.Объект = &Объект
	|	И Участники.Роль = &Роль
	|	И Участники.Пользователь ССЫЛКА Справочник.Пользователи
	|	И ВЫБОР
	|			КОГДА &СуперПользователь
	|				ТОГДА ИСТИНА
	|			ИНАЧЕ Участники.Автор = &ТекущийПользователь
	|		КОНЕЦ
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВсеВозможныеУчастники.Пользователь КАК Пользователь,
	|	ВсеВозможныеУчастники.Пометка КАК Пометка,
	|	Пользователи.Наименование КАК Наименование,
	|	-1 КАК ИдентификаторСтрокиСписка,
	|	ВЫБОР
	|		КОГДА ИзбранныеПользователи.Пользователь ЕСТЬ NULL
	|			ТОГДА ЛОЖЬ
	|		ИНАЧЕ ИСТИНА
	|	КОНЕЦ КАК Избранное
	|ИЗ
	|	ВсеВозможныеУчастники КАК ВсеВозможныеУчастники
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.Пользователи КАК Пользователи
	|		ПО ВсеВозможныеУчастники.Пользователь = Пользователи.Ссылка
	|		ЛЕВОЕ СОЕДИНЕНИЕ ИзбранныеПользователи КАК ИзбранныеПользователи
	|		ПО (Пользователи.Ссылка = ИзбранныеПользователи.Пользователь)
	|
	|УПОРЯДОЧИТЬ ПО
	|	Избранное УБЫВ,
	|	Наименование";
#КонецОбласти
	
	// Подготовка параметров запроса	
	СуперПользователь = (РольДоступна("Администратор") ИЛИ РольДоступна("ПолныеПрава"));	

	// Установка параметров запроса
	Запрос.УстановитьПараметр("Объект", Объект);
	Запрос.УстановитьПараметр("Роль", Роль);
	Запрос.УстановитьПараметр("ТекущийПользователь", ПараметрыСеанса.ТекущийПользователь);
	Запрос.УстановитьПараметр("СуперПользователь", СуперПользователь);
	
	// Запросы пакета:
	// 1 - Избранные пользователи
	// 3 - Список доступных пользователей
	РезультатЗапроса = Запрос.Выполнить();
		
	// Формирование списка доступных пользователей
	Если НЕ РезультатЗапроса.Пустой() Тогда
		Выборка = РезультатЗапроса.Выбрать();
		
		Пока Выборка.Следующий() Цикл
			// Заполнение общей таблицы пользователей
			НоваяСтрокаДоступных = ДоступныеПользователи.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяСтрокаДоступных, Выборка, 
				"Пометка, Пользователь, Наименование, Избранное, ИдентификаторСтрокиСписка");
			
			Если Выборка.Пометка = Истина Тогда
				НоваяСтрокаВыбранных = ВыбранныеПользователи.Добавить();
				НоваяСтрокаВыбранных.Исключить = БиблиотекаКартинок.УдалитьЗначение;
				
				НоваяСтрокаВыбранных.ИдентификаторСтрокиСписка = НоваяСтрокаДоступных.ПолучитьИдентификатор();
				НоваяСтрокаДоступных.ИдентификаторСтрокиСписка = НоваяСтрокаВыбранных.ПолучитьИдентификатор();
				
				ЗаполнитьЗначенияСвойств(НоваяСтрокаВыбранных, Выборка, "Пользователь, Наименование");
				
				// Сохранение изначально выбранных значений
				НачальныйВыбор.Добавить(Выборка.Пользователь);
			КонецЕсли;
		КонецЦикла;
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Функция СохранитьИзмененияНаСервере(Знач ДобавляемыеПользователи)
	
	Изменено = Ложь;
	УдаляемыеПользователи = Новый Массив;
	
	ПодготовитьНаборИзмененныхДанных(ДобавляемыеПользователи, УдаляемыеПользователи);
	
	НачатьТранзакцию();
	Попытка
		УдалитьУчастников(Изменено, УдаляемыеПользователи);
		ДобавитьУчастников(Изменено, ДобавляемыеПользователи);
		
		ЗафиксироватьТранзакцию();
	Исключение
		ОтменитьТранзакцию(); 
		ЗаписьЖурналаРегистрации("Регистр сведений Участники: множественный подбор", 
			УровеньЖурналаРегистрации.Ошибка,,, ОписаниеОшибки());
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю("Не удалось изменить участников.");
		Изменено = Ложь;
	КонецПопытки;
	
	Возврат Изменено;
		
КонецФункции

&НаСервере
Процедура ПодготовитьНаборИзмененныхДанных(Знач ДобавляемыеПользователи, Знач УдаляемыеПользователи)
	
	Для Каждого Пользователь Из НачальныйВыбор Цикл
		НайденноеЗначение = ДобавляемыеПользователи.Найти(Пользователь.Значение);
		
		Если НайденноеЗначение = Неопределено Тогда
			УдаляемыеПользователи.Добавить(Пользователь.Значение);
		Иначе
			ДобавляемыеПользователи.Удалить(НайденноеЗначение);
		КонецЕсли;
	КонецЦикла;

КонецПроцедуры

&НаСервере
Процедура УдалитьУчастников(Изменено, Знач УдаляемыеПользователи)
	
	Если УдаляемыеПользователи.Количество() > 0 Тогда
		Для Каждого Пользователь Из УдаляемыеПользователи Цикл	
			РегистрыСведений.Участники.Изменить(Объект, Пользователь, Роль, Ложь, Истина, , Истина);
		КонецЦикла;
		
		Изменено = Истина;
	КонецЕсли;

КонецПроцедуры

&НаСервере
Процедура УстановитьЗаголовкиЭлементов()
	
	Если Роль = Справочники.РолиУчастников.Наблюдатель Тогда
		Заголовок = "Наблюдатели";
		Элементы.НадписьДобавитьПользователей.Заголовок = "Доступные наблюдатели";
		Элементы.НадписьВыбранныеПользователи.Заголовок = "Выбранные наблюдатели";
		
	ИначеЕсли Роль = Справочники.РолиУчастников.УчастникКомандыУправленияПроектом Тогда
		Заголовок = "Участники команды управления проектом";
		Элементы.НадписьДобавитьПользователей.Заголовок = "Доступные участники";
		Элементы.НадписьВыбранныеПользователи.Заголовок = "Выбранные участники";
	КонецЕсли;
	
КонецПроцедуры
	
#КонецОбласти
