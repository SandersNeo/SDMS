///////////////////////////////////////////////////////////////////////////////////////////////////////
// Copyright (c) 2025, ООО ДНС Технологии
// SDMS (Software Development Management System) — это корпоративная система учета разработки и управления проектами 
// Все права защищены. Эта программа и сопроводительные материалы предоставляются 
// в соответствии с условиями лицензии General Public License (GNU GPL v3)
// Текст лицензии доступен по ссылке:
// https://www.gnu.org/licenses/gpl-3.0.html
///////////////////////////////////////////////////////////////////////////////////////////////////////

#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.Свойство("ОткрытиеИзОбъекта") Тогда
		ОткрытиеИзОбъекта = Истина;
		
		Для Каждого Строка Из Параметры.Автосохранения Цикл
			ЗаполнитьЗначенияСвойств(Автосохранения.Добавить(), Строка);
		КонецЦикла;
	Иначе
		ВывестиСписокАвтосохранений(Автосохранения);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовФормы

&НаКлиенте
Процедура АвтосохраненияВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	ПосмотретьСохраненноеОписание();
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКоманд

&НаКлиенте
Процедура ВосстановитьОбъект(Команда)
	
	ТекущиеДанные = Элементы.Автосохранения.ТекущиеДанные;
	
	Если ТекущиеДанные = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ПараметрыОбъекта = Новый Структура;
		
	ПараметрыОбъекта.Вставить("АвтосохранениеСсылка", ТекущиеДанные.АвтосохранениеСсылка);
	ПараметрыОбъекта.Вставить("АвтосохранениеТекстОписания", ТекущиеДанные.АвтосохранениеТекстОписания);
	
	Если ОткрытиеИзОбъекта Тогда
		Закрыть(ПараметрыОбъекта);
	Иначе
		ИмяФормыОбъекта = ИмяФормыОбъекта(ТекущиеДанные.АвтосохранениеСсылка);
		
		Если НЕ ТекущиеДанные.БитаяСсылка Тогда
			ПараметрыОбъекта.Вставить("Ключ", ТекущиеДанные.АвтосохранениеСсылка);
		КонецЕсли;
		
		ОткрытьФорму(ИмяФормыОбъекта, ПараметрыОбъекта);
		Закрыть();
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПосмотретьОписание(Команда)
	
	ПосмотретьСохраненноеОписание();
	
КонецПроцедуры

&НаКлиенте
Процедура УдалитьАвтосохранение(Команда)
	
	ТекущиеДанные = Элементы.Автосохранения.ТекущиеДанные;
	
	Если ТекущиеДанные = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ОписаниеОповещения = Новый ОписаниеОповещения("ОбработатьОтветНаУдаление", ЭтотОбъект, ТекущиеДанные);
	ТекстВопроса = "Вы уверены, что хотите удалить это автосохранение?";
	
	ПоказатьВопрос(ОписаниеОповещения, ТекстВопроса, РежимДиалогаВопрос.ДаНет, , ,"Удаление автосохранения");
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервереБезКонтекста
Процедура ВывестиСписокАвтосохранений(Автосохранения)
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	АвтосохраненныеОписания.Объект КАК Объект,
	|	АвтосохраненныеОписания.ДатаСоздания КАК ДатаСохранения,
	|	АвтосохраненныеОписания.Наименование КАК Наименование,
	|	АвтосохраненныеОписания.ОписаниеОбъекта КАК ХранилищеЗначения
	|ПОМЕСТИТЬ Автосохранения
	|ИЗ
	|	РегистрСведений.АвтосохраненныеОписания КАК АвтосохраненныеОписания
	|ГДЕ
	|	АвтосохраненныеОписания.Пользователь = &Пользователь
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВЫБОР
	|		КОГДА ТИПЗНАЧЕНИЯ(Автосохранения.Объект) = ТИП(Документ.Задача)
	|			ТОГДА ВЫБОР
	|					КОГДА Задача.Ссылка ЕСТЬ NULL
	|						ТОГДА ""[Новая задача] "" + Автосохранения.Наименование
	|					ИНАЧЕ ПРЕДСТАВЛЕНИЕ(Автосохранения.Объект)
	|				КОНЕЦ
	|		КОГДА ТИПЗНАЧЕНИЯ(Автосохранения.Объект) = ТИП(Документ.ЗаявкаНаРазработку)
	|			ТОГДА ВЫБОР
	|					КОГДА ЗаявкаНаРазработку.Ссылка ЕСТЬ NULL
	|						ТОГДА ""[Новая заявка] "" + Автосохранения.Наименование
	|					ИНАЧЕ ПРЕДСТАВЛЕНИЕ(Автосохранения.Объект)
	|				КОНЕЦ
	|		КОГДА ТИПЗНАЧЕНИЯ(Автосохранения.Объект) = ТИП(Документ.ВнутреннееЗадание)
	|			ТОГДА ВЫБОР
	|					КОГДА ВнутреннееЗадание.Ссылка ЕСТЬ NULL
	|						ТОГДА ""[Новое вн. задание] "" + Автосохранения.Наименование
	|					ИНАЧЕ ПРЕДСТАВЛЕНИЕ(Автосохранения.Объект)
	|				КОНЕЦ
	|		КОГДА ТИПЗНАЧЕНИЯ(Автосохранения.Объект) = ТИП(Справочник.Проекты)
	|			ТОГДА ВЫБОР
	|					КОГДА Проекты.Ссылка ЕСТЬ NULL
	|						ТОГДА ""[Новый проект] "" + Автосохранения.Наименование
	|					ИНАЧЕ ПРЕДСТАВЛЕНИЕ(Автосохранения.Объект)
	|				КОНЕЦ
	|		ИНАЧЕ ""<не определено>""
	|	КОНЕЦ КАК ОбъектПредставление,
	|	Автосохранения.ДатаСохранения КАК ДатаСохранения,
	|	Автосохранения.ХранилищеЗначения КАК ХранилищеЗначения,
	|	ВЫБОР
	|		КОГДА Проекты.Ссылка ЕСТЬ NULL
	|				И Задача.Ссылка ЕСТЬ NULL
	|				И ВнутреннееЗадание.Ссылка ЕСТЬ NULL
	|				И ЗаявкаНаРазработку.Ссылка ЕСТЬ NULL
	|			ТОГДА ИСТИНА
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ КАК БитаяСсылка,
	|	Автосохранения.Объект КАК АвтосохранениеСсылка
	|ИЗ
	|	Автосохранения КАК Автосохранения
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.Задача КАК Задача
	|		ПО ((ВЫРАЗИТЬ(Автосохранения.Объект КАК Документ.Задача)) = Задача.Ссылка)
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.ЗаявкаНаРазработку КАК ЗаявкаНаРазработку
	|		ПО ((ВЫРАЗИТЬ(Автосохранения.Объект КАК Документ.ЗаявкаНаРазработку)) = ЗаявкаНаРазработку.Ссылка)
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.ВнутреннееЗадание КАК ВнутреннееЗадание
	|		ПО ((ВЫРАЗИТЬ(Автосохранения.Объект КАК Документ.ВнутреннееЗадание)) = ВнутреннееЗадание.Ссылка)
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.Проекты КАК Проекты
	|		ПО ((ВЫРАЗИТЬ(Автосохранения.Объект КАК Справочник.Проекты)) = Проекты.Ссылка)
	|
	|УПОРЯДОЧИТЬ ПО
	|	ДатаСохранения";
	
	Запрос.УстановитьПараметр("Пользователь", ПараметрыСеанса.ТекущийПользователь);
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	Пока Выборка.Следующий() Цикл
		НоваяСтрока = Автосохранения.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяСтрока, Выборка);
		НоваяСтрока.АвтосохранениеТекстОписания = Выборка.ХранилищеЗначения.Получить();
	КонецЦикла;
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция ИмяФормыОбъекта(Знач Объект)
	
	Возврат Объект.Метаданные().ОсновнаяФормаОбъекта.ПолноеИмя();
	
КонецФункции

&НаКлиенте
Процедура ОбработатьВосстановлениеОписанияИзФормыПросмотра(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат = Истина Тогда
		Если ОткрытиеИзОбъекта Тогда
			Закрыть(ДополнительныеПараметры);
		Иначе
			ИмяФормыОбъекта = ИмяФормыОбъекта(ДополнительныеПараметры.АвтосохранениеСсылка);
			ОткрытьФорму(ИмяФормыОбъекта, ДополнительныеПараметры);
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработатьОтветНаУдаление(Результат, ТекущиеДанные) Экспорт
	
	Если Результат = КодВозвратаДиалога.Да Тогда
		УдалитьАвтосохранениеНаСервере(ТекущиеДанные.АвтосохранениеСсылка);
		Автосохранения.Удалить(ТекущиеДанные);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПосмотретьСохраненноеОписание()
	
	ТекущиеДанные = Элементы.Автосохранения.ТекущиеДанные;
	
	Если ТекущиеДанные = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	HTMLОписание = РедакторОписанияВызовСервера.ПолучитьРедактор(ТекущиеДанные.АвтосохранениеТекстОписания, , Истина);
	
	ПараметрыОткрытия = Новый Структура;
	ПараметрыОткрытия.Вставить("Заголовок", ТекущиеДанные.ОбъектПредставление);
	ПараметрыОткрытия.Вставить("ТекстHTML", HTMLОписание);
	ПараметрыОткрытия.Вставить("ТолькоПросмотрОписания", Истина);
	
	ПараметрыОповещения = Новый Структура;
	ПараметрыОповещения.Вставить("АвтосохранениеСсылка", ТекущиеДанные.АвтосохранениеСсылка);
	ПараметрыОповещения.Вставить("АвтосохранениеТекстОписания", ТекущиеДанные.АвтосохранениеТекстОписания);
	
	Если НЕ ТекущиеДанные.БитаяСсылка Тогда
		ПараметрыОповещения.Вставить("Ключ", ТекущиеДанные.АвтосохранениеСсылка);
	КонецЕсли;
	
	ОписаниеОповещения = Новый ОписаниеОповещения("ОбработатьВосстановлениеОписанияИзФормыПросмотра", ЭтотОбъект, ПараметрыОповещения);
	
	ОткрытьФорму("ОбщаяФорма.ПросмотрОписания", ПараметрыОткрытия, ЭтотОбъект, , , , ОписаниеОповещения);
	
КонецПроцедуры

&НаСервереБезКонтекста
Процедура УдалитьАвтосохранениеНаСервере(Знач Ссылка)
	
	РегистрыСведений.АвтосохраненныеОписания.УдалитьЗаписиПоОбъекту(Ссылка);
	
КонецПроцедуры

#КонецОбласти
