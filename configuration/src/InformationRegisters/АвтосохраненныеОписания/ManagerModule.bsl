///////////////////////////////////////////////////////////////////////////////////////////////////////
// Copyright (c) 2025, ООО ДНС Технологии
// SDMS (Software Development Management System) — это корпоративная система учета разработки и управления проектами 
// Все права защищены. Эта программа и сопроводительные материалы предоставляются 
// в соответствии с условиями лицензии General Public License (GNU GPL v3)
// Текст лицензии доступен по ссылке:
// https://www.gnu.org/licenses/gpl-3.0.html
///////////////////////////////////////////////////////////////////////////////////////////////////////

Процедура СохранитьОписание(Знач Пользователь, Знач Объект, Знач Описание) Экспорт
	
	МенеджерЗаписи = СоздатьМенеджерЗаписи();
	МенеджерЗаписи.Пользователь = Пользователь;
	МенеджерЗаписи.Объект = Объект;
	МенеджерЗаписи.ОписаниеОбъекта = Новый ХранилищеЗначения(Описание, Новый СжатиеДанных(9));
	МенеджерЗаписи.ДатаСоздания = ТекущаяДатаСеанса();
	МенеджерЗаписи.ТипДанных = 1;
	МенеджерЗаписи.Записать();
	
КонецПроцедуры

Процедура Добавить(Знач ЗначенияРеквизитов) Экспорт
	
	ВспомогательныеДанные = ЗначенияРеквизитов.ВспомогательныеДанные;
	Сжатие = Новый СжатиеДанных(9);
	
	ДатаЗаписи = ТекущаяДата();
	ТекущийПользователь = ПараметрыСеанса.ТекущийПользователь;
	ОбъектОснование = ВспомогательныеДанные.ОбъектОснование;
	РедактируемыйОбъект = ВспомогательныеДанные.СсылкаНаОбъектДляАвтосохранения;
	
	НаборЗаписей = СоздатьНаборЗаписей();
	НаборЗаписей.Отбор.Пользователь.Установить(ТекущийПользователь);
	НаборЗаписей.Отбор.Объект.Установить(РедактируемыйОбъект);
	
	НоваяЗапись = НаборЗаписей.Добавить();
	НоваяЗапись.Пользователь = ТекущийПользователь;
	НоваяЗапись.Объект = РедактируемыйОбъект;
	НоваяЗапись.ОписаниеОбъекта = Новый ХранилищеЗначения(ВспомогательныеДанные.АктуальныйТекстОписания, Сжатие);
	НоваяЗапись.ДатаСоздания = ДатаЗаписи;
	НоваяЗапись.ОбъектОснование = ?(ЗначениеЗаполнено(ОбъектОснование), ОбъектОснование, Неопределено);
	НоваяЗапись.Наименование = ВспомогательныеДанные.НаименованиеОбъекта;
	
	ЗначенияРеквизитов.Удалить("ВспомогательныеДанные");
	НоваяЗапись.ЗначенияРеквизитов = Новый ХранилищеЗначения(ЗначенияРеквизитов, Сжатие);
	
	НаборЗаписей.Записать();
	
КонецПроцедуры

Процедура УдалитьЗаписиПоОбъекту(Знач РедактируемыйОбъект) Экспорт
	
	ТекущийПользователь = ПараметрыСеанса.ТекущийПользователь;
	
	НаборЗаписей = СоздатьНаборЗаписей();
	НаборЗаписей.Отбор.Пользователь.Установить(ТекущийПользователь);
	НаборЗаписей.Отбор.Объект.Установить(РедактируемыйОбъект);
	НаборЗаписей.Записать();
	
КонецПроцедуры

Функция КоличествоАвтосохраненийТекущегоПользователя() Экспорт
	
	Количество = 0;
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	КОЛИЧЕСТВО(1) КАК Количество
	|ИЗ
	|	РегистрСведений.АвтосохраненныеОписания КАК АвтосохраненныеОписания
	|ГДЕ
	|	АвтосохраненныеОписания.Пользователь = &Пользователь";
	
	Запрос.УстановитьПараметр("Пользователь", ПараметрыСеанса.ТекущийПользователь);
	Выборка = Запрос.Выполнить().Выбрать();
	
	Если Выборка.Следующий() Тогда
		Количество = Выборка.Количество;
	КонецЕсли;
	
	Возврат Количество;
	
КонецФункции

Процедура ОчиститьНеактуальныеЗаписи() Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	АвтосохраненныеОписания.Пользователь КАК Пользователь,
	|	АвтосохраненныеОписания.Объект КАК Объект
	|ИЗ
	|	РегистрСведений.АвтосохраненныеОписания КАК АвтосохраненныеОписания
	|ГДЕ
	|	АвтосохраненныеОписания.ДатаСоздания < &ДатаХранения";
	
	ВремяХраненияАвтосохранений = 2592000; // 30 дней 
	Запрос.УстановитьПараметр("ДатаХранения", ТекущаяДата() - ВремяХраненияАвтосохранений);
	
	Результат = Запрос.Выполнить();
	Если НЕ Результат.Пустой() Тогда
		НаборЗаписейОписания = РегистрыСведений.АвтосохраненныеОписания.СоздатьНаборЗаписей();
		
		Выборка = Результат.Выбрать();
		Пока Выборка.Следующий() Цикл
			НаборЗаписейОписания.Отбор.Пользователь.Установить(Выборка.Пользователь);
			НаборЗаписейОписания.Отбор.Объект.Установить(Выборка.Объект);
			НаборЗаписейОписания.Записать();
		КонецЦикла
	КонецЕсли;
	
КонецПроцедуры
