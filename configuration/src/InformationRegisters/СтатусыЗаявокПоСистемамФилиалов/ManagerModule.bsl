///////////////////////////////////////////////////////////////////////////////////////////////////////
// Copyright (c) 2025, ООО ДНС Технологии
// SDMS (Software Development Management System) — это корпоративная система учета разработки и управления проектами 
// Все права защищены. Эта программа и сопроводительные материалы предоставляются 
// в соответствии с условиями лицензии General Public License (GNU GPL v3)
// Текст лицензии доступен по ссылке:
// https://www.gnu.org/licenses/gpl-3.0.html
///////////////////////////////////////////////////////////////////////////////////////////////////////

#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

// Вычисляет статусы заявки по системам для филиалов
//
// Параметры:
//  Заявка  - ДокументСсылка.ЗаявкаНаРазработку - ссылка на документ
//
Процедура Рассчитать(Знач Заявка) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	#Область ТекстЗапроса
	"ВЫБРАТЬ
	|	ЗаявкаНаРазработкуСистемыРазработки.Ссылка КАК Заявка,
	|	ФилиалыРазрабатываемыеСистемы.Ссылка КАК Филиал,
	|	СтатусыЗаявокПоСистемам.Статус КАК Статус,
	|	СтатусыОбъектов.Порядок КАК ПорядокСтатуса
	|ПОМЕСТИТЬ СтатусПоСистемам
	|ИЗ
	|	Документ.ЗаявкаНаРазработку.СистемыРазработки КАК ЗаявкаНаРазработкуСистемыРазработки
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.СтатусыЗаявокПоСистемам КАК СтатусыЗаявокПоСистемам
	|		ПО (СтатусыЗаявокПоСистемам.Заявка = ЗаявкаНаРазработкуСистемыРазработки.Ссылка)
	|			И (СтатусыЗаявокПоСистемам.Система = ЗаявкаНаРазработкуСистемыРазработки.Система)
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.СтатусыОбъектов КАК СтатусыОбъектов
	|		ПО (СтатусыЗаявокПоСистемам.Статус = СтатусыОбъектов.Ссылка)
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.Филиалы.РазрабатываемыеСистемы КАК ФилиалыРазрабатываемыеСистемы
	|		ПО ЗаявкаНаРазработкуСистемыРазработки.Система = ФилиалыРазрабатываемыеСистемы.Система
	|ГДЕ
	|	ЗаявкаНаРазработкуСистемыРазработки.Ссылка = &Заявка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	СтатусПоСистемам.Заявка КАК Заявка,
	|	СтатусПоСистемам.Филиал КАК Филиал,
	|	МИНИМУМ(СтатусПоСистемам.ПорядокСтатуса) КАК ПорядокСтатуса
	|ПОМЕСТИТЬ МинимальныйПорядокСтатусаПоСистемам
	|ИЗ
	|	СтатусПоСистемам КАК СтатусПоСистемам
	|
	|СГРУППИРОВАТЬ ПО
	|	СтатусПоСистемам.Заявка,
	|	СтатусПоСистемам.Филиал
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	СтатусПоСистемам.Заявка КАК Заявка,
	|	СтатусПоСистемам.Филиал КАК Филиал,
	|	МИНИМУМ(СтатусПоСистемам.Статус) КАК Статус
	|ПОМЕСТИТЬ МинимальныйСтатусПоСистемам
	|ИЗ
	|	СтатусПоСистемам КАК СтатусПоСистемам
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ МинимальныйПорядокСтатусаПоСистемам КАК МинимальныйПорядокСтатусаПоСистемам
	|		ПО (МинимальныйПорядокСтатусаПоСистемам.ПорядокСтатуса = СтатусПоСистемам.ПорядокСтатуса)
	|
	|СГРУППИРОВАТЬ ПО
	|	СтатусПоСистемам.Заявка,
	|	СтатусПоСистемам.Филиал
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	МинимальныйСтатусПоСистемам.Заявка КАК Заявка,
	|	МинимальныйСтатусПоСистемам.Филиал КАК Филиал,
	|	МинимальныйСтатусПоСистемам.Статус КАК Статус,
	|	&ТекущаяДата КАК ДатаИзменения
	|ИЗ
	|	МинимальныйСтатусПоСистемам КАК МинимальныйСтатусПоСистемам
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.МинимальныеСтатусыЗаявок КАК МинимальныеСтатусыЗаявок
	|		ПО (МинимальныеСтатусыЗаявок.Заявка = МинимальныйСтатусПоСистемам.Заявка)
	|			И (МинимальныеСтатусыЗаявок.Статус <> МинимальныйСтатусПоСистемам.Статус)";
	#КонецОбласти
	
	Запрос.УстановитьПараметр("Заявка", Заявка);
	Запрос.УстановитьПараметр("ТекущаяДата", ТекущаяДатаСеанса());	
	РезультатЗапроса = Запрос.Выполнить();
	
	НаборЗаписей = СоздатьНаборЗаписей();
	НаборЗаписей.Отбор.Заявка.Установить(Заявка);
	
	Если НЕ РезультатЗапроса.Пустой() Тогда
		ТаблицаЗаписейДляРегистра = РезультатЗапроса.Выгрузить();
		НаборЗаписей.Загрузить(ТаблицаЗаписейДляРегистра);
	КонецЕсли;
	
	НаборЗаписей.Записать();
	
КонецПроцедуры

// Вычисляет статусы заявок филиала по системам
//
// Параметры:
//  Филиал  - СправочникСсылка.Филиал - ссылка на филиал
//
Процедура РассчитатьСтатусыПоЗаявкамФилиала(Знач Филиал) Экспорт
		
	Запрос = Новый Запрос;
	Запрос.Текст = 
	#Область ТекстЗапроса
	"ВЫБРАТЬ
	|	ФилиалыРазрабатываемыеСистемы.Ссылка КАК Филиал,
	|	ЗаявкаНаРазработкуСистемыРазработки.Ссылка КАК Заявка,
	|	СтатусыОбъектов.Порядок КАК ПорядокСтатуса,
	|	СтатусыЗаявокПоСистемам.Статус КАК Статус
	|ПОМЕСТИТЬ СтатусПоСистемам
	|ИЗ
	|	Справочник.Филиалы.РазрабатываемыеСистемы КАК ФилиалыРазрабатываемыеСистемы
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ЗаявкаНаРазработку.СистемыРазработки КАК ЗаявкаНаРазработкуСистемыРазработки
	|		ПО (ЗаявкаНаРазработкуСистемыРазработки.Система = ФилиалыРазрабатываемыеСистемы.Система)
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.СтатусыЗаявокПоСистемам КАК СтатусыЗаявокПоСистемам
	|		ПО (СтатусыЗаявокПоСистемам.Заявка = ЗаявкаНаРазработкуСистемыРазработки.Ссылка)
	|			И (СтатусыЗаявокПоСистемам.Система = ЗаявкаНаРазработкуСистемыРазработки.Система)
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.СтатусыОбъектов КАК СтатусыОбъектов
	|		ПО (СтатусыЗаявокПоСистемам.Статус = СтатусыОбъектов.Ссылка)
	|ГДЕ
	|	ФилиалыРазрабатываемыеСистемы.Ссылка = &Филиал
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	СтатусПоСистемам.Филиал КАК Филиал,
	|	СтатусПоСистемам.Заявка КАК Заявка,
	|	МИНИМУМ(СтатусПоСистемам.ПорядокСтатуса) КАК ПорядокСтатуса
	|ПОМЕСТИТЬ МинимальныйПорядокПоСистемам
	|ИЗ
	|	СтатусПоСистемам КАК СтатусПоСистемам
	|
	|СГРУППИРОВАТЬ ПО
	|	СтатусПоСистемам.Филиал,
	|	СтатусПоСистемам.Заявка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	СтатусПоСистемам.Филиал КАК Филиал,
	|	СтатусПоСистемам.Заявка КАК Заявка,
	|	МИНИМУМ(СтатусПоСистемам.Статус) КАК Статус
	|ПОМЕСТИТЬ МинимальныйСтатусПоСистемам
	|ИЗ
	|	СтатусПоСистемам КАК СтатусПоСистемам
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ МинимальныйПорядокПоСистемам КАК МинимальныйПорядокПоСистемам
	|		ПО (МинимальныйПорядокПоСистемам.ПорядокСтатуса = СтатусПоСистемам.ПорядокСтатуса)
	|			И (МинимальныйПорядокПоСистемам.Филиал = СтатусПоСистемам.Филиал)
	|			И (МинимальныйПорядокПоСистемам.Заявка = СтатусПоСистемам.Заявка)
	|
	|СГРУППИРОВАТЬ ПО
	|	СтатусПоСистемам.Филиал,
	|	СтатусПоСистемам.Заявка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	МинимальныйСтатусПоСистемам.Заявка КАК Заявка,
	|	МинимальныйСтатусПоСистемам.Филиал КАК Филиал,
	|	МинимальныйСтатусПоСистемам.Статус КАК Статус,
	|	&ДатаИзменения КАК ДатаИзменения
	|ИЗ
	|	МинимальныйСтатусПоСистемам КАК МинимальныйСтатусПоСистемам
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.МинимальныеСтатусыЗаявок КАК МинимальныеСтатусыЗаявок
	|		ПО (МинимальныеСтатусыЗаявок.Заявка = МинимальныйСтатусПоСистемам.Заявка)
	|			И МинимальныйСтатусПоСистемам.Статус <> МинимальныеСтатусыЗаявок.Статус";
	#КонецОбласти
	
	Запрос.УстановитьПараметр("Филиал", Филиал);
	Запрос.УстановитьПараметр("ДатаИзменения", ТекущаяДатаСеанса());
	
	РезультатЗапроса = Запрос.Выполнить();
	
	НаборЗаписей = СоздатьНаборЗаписей();
	НаборЗаписей.Отбор.Филиал.Установить(Филиал);
	
	Если НЕ РезультатЗапроса.Пустой() Тогда
		ТаблицаЗаписейДляРегистра = РезультатЗапроса.Выгрузить();
		НаборЗаписей.Загрузить(ТаблицаЗаписейДляРегистра);
	КонецЕсли;
	
	НаборЗаписей.Записать();
	
КонецПроцедуры

// Удаляет все записи из регистра по указанной заявке
//
// Параметры:
//  Заявка	 - ДокументСсылка.ЗаявкаНаРазработку - Заявка на разработку
//
Процедура УдалитьЗаписиПоЗаявке(Знач Заявка) Экспорт
	
	НаборЗаписей = СоздатьНаборЗаписей();
	НаборЗаписей.Отбор.Заявка.Установить(Заявка);
	НаборЗаписей.Записать();
	
КонецПроцедуры

#КонецОбласти

#КонецЕсли
