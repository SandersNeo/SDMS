///////////////////////////////////////////////////////////////////////////////////////////////////////
// Copyright (c) 2025, ООО ДНС Технологии
// SDMS (Software Development Management System) — это корпоративная система учета разработки и управления проектами 
// Все права защищены. Эта программа и сопроводительные материалы предоставляются 
// в соответствии с условиями лицензии General Public License (GNU GPL v3)
// Текст лицензии доступен по ссылке:
// https://www.gnu.org/licenses/gpl-3.0.html
///////////////////////////////////////////////////////////////////////////////////////////////////////

#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если НЕ Запись.ИсходныйКлючЗаписи.Пустой() И Запись.Обработан Тогда
		ОтобразитьРезультатыПоиска();
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовФормы

&НаКлиенте
Процедура РезультатыПоискаПриАктивизацииСтроки(Элемент)
	
	ТекущиеДанные = Элемент.ТекущиеДанные;
	Если ТекущиеДанные <> Неопределено Тогда
		Шаблон = ПолучитьШаблонСтраницы();
		ПредставлениеТекстаСОшибкой = СтрШаблон(Шаблон, ТекущиеДанные.ТекстСОшибкой);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Функция ОтобразитьРезультатыПоиска()
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ТекстыОбъектовДляСемантическогоАнализа.ХранилищеРезультатовПроверки КАК ХранилищеРезультатовПроверки
	|ИЗ
	|	РегистрСведений.ТекстыОбъектовДляСемантическогоАнализа КАК ТекстыОбъектовДляСемантическогоАнализа
	|ГДЕ
	|	ТекстыОбъектовДляСемантическогоАнализа.Дата = &Дата
	|	И ТекстыОбъектовДляСемантическогоАнализа.Объект = &Объект
	|	И ТекстыОбъектовДляСемантическогоАнализа.ВидТекста = &ВидТекста";
	
	Запрос.УстановитьПараметр("Дата", Запись.Дата);
	Запрос.УстановитьПараметр("Объект", Запись.Объект);
	Запрос.УстановитьПараметр("ВидТекста", Запись.ВидТекста);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Если НЕ РезультатЗапроса.Пустой() Тогда
		Выборка = РезультатЗапроса.Выбрать();
		Выборка.Следующий();
		
		РезультатПроверки = Выборка.ХранилищеРезультатовПроверки.Получить();
		ТипРезультатаПроверки = ТипЗнч(РезультатПроверки);
		
		Если ТипРезультатаПроверки = Тип("Массив") Тогда
			Для Каждого СтруктураРезультата Из РезультатПроверки Цикл
				ЗаполнитьЗначенияСвойств(РезультатыПоиска.Добавить(), СтруктураРезультата);
			КонецЦикла;
			
			Элементы.ДатаПроверки.Видимость = Ложь;
			
		ИначеЕсли ТипРезультатаПроверки = Тип("Структура") Тогда
			Элементы.ПредставлениеТекстаСОшибкой.Вид = ВидПоляФормы.ПолеТекстовогоДокумента;
			Элементы.ПредставлениеТекстаСОшибкой.Заголовок = "Проверяемый текст";
			
			Элементы.РезультатыПоиска.Видимость = Ложь;
			
			ПредставлениеТекстаСОшибкой = РезультатПроверки.Текст;
			ДатаПроверки = РезультатПроверки.ДатаПроверки;
		КонецЕсли;
	КонецЕсли;
	
КонецФункции

&НаКлиенте
Функция ПолучитьШаблонСтраницы()
	
	Возврат	"<html><head></head><body>%1</body>";
	
КонецФункции

#КонецОбласти
