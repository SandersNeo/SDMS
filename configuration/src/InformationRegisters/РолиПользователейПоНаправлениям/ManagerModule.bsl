///////////////////////////////////////////////////////////////////////////////////////////////////////
// Copyright (c) 2025, ООО ДНС Технологии
// SDMS (Software Development Management System) — это корпоративная система учета разработки и управления проектами 
// Все права защищены. Эта программа и сопроводительные материалы предоставляются 
// в соответствии с условиями лицензии General Public License (GNU GPL v3)
// Текст лицензии доступен по ссылке:
// https://www.gnu.org/licenses/gpl-3.0.html
///////////////////////////////////////////////////////////////////////////////////////////////////////

#Область ПрограммныйИнтерфейс

// Устанавливает роли по направлениям
Процедура УстановитьРолиПоНаправлениям(Знач ТаблицаРолей, Знач Пользователь) Экспорт
	
	ДатаИзменения = ТекущаяДатаСеанса();
	Автор = ПараметрыСеанса.ТекущийПользователь;
	
	// Дополним данными таблицу ролей
	Для Каждого Строка Из ТаблицаРолей Цикл
		Строка.Пользователь = Пользователь;
		Строка.ДатаИзменения = ДатаИзменения;
		Строка.АвторИзменения = Автор;
	КонецЦикла;
	
	НаборЗаписей = СоздатьНаборЗаписей();
	НаборЗаписей.Отбор.Пользователь.Установить(Пользователь);
	НаборЗаписей.Загрузить(ТаблицаРолей);
	
	НаборЗаписей.Записать();
	
КонецПроцедуры

// Возвращает роль пользователя по направлению или пустую ссылку на справочник РолиПользователей
Функция ПолучитьРольПользователяПоНаправлению(Знач НаправлениеРазработки, Знач Пользователь) Экспорт
	
	Результат = Справочники.РолиПользователей.ПустаяСсылка();
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	РолиПользователейПоНаправлениям.Роль КАК Роль
	|ИЗ
	|	РегистрСведений.РолиПользователейПоНаправлениям КАК РолиПользователейПоНаправлениям
	|ГДЕ
	|	РолиПользователейПоНаправлениям.НаправлениеРазработки = &НаправлениеРазработки
	|	И РолиПользователейПоНаправлениям.Пользователь = &Пользователь";
	
	Запрос.УстановитьПараметр("НаправлениеРазработки", НаправлениеРазработки);
	Запрос.УстановитьПараметр("Пользователь", Пользователь);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Если НЕ РезультатЗапроса.Пустой() Тогда
		Выборка = РезультатЗапроса.Выбрать();
		Выборка.Следующий();
		Результат = Выборка.Роль;
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

#КонецОбласти
