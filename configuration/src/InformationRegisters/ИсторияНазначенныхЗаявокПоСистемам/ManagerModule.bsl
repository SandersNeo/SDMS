///////////////////////////////////////////////////////////////////////////////////////////////////////
// Copyright (c) 2025, ООО ДНС Технологии
// SDMS (Software Development Management System) — это корпоративная система учета разработки и управления проектами 
// Все права защищены. Эта программа и сопроводительные материалы предоставляются 
// в соответствии с условиями лицензии General Public License (GNU GPL v3)
// Текст лицензии доступен по ссылке:
// https://www.gnu.org/licenses/gpl-3.0.html
///////////////////////////////////////////////////////////////////////////////////////////////////////

#Если Сервер ИЛИ ТолстыйКлиентОбычноеПриложение ИЛИ ВнешнееСоединение Тогда
	
#Область ПрограммныйИнтерфейс

// Добавляет записи в регистр
//
// Параметры:
//  НаборЗаписей - РегистрСведенийНаборЗаписей - Набор записей регистра СистемыЗаявокНазначеныеПользователю
//
Процедура ДобавитьЗаписи(Знач НаборЗаписей) Экспорт

	Период = ТекущаяДатаСеанса();
	
	Если НаборЗаписей.Количество() = 0 Тогда	
		ПостроительЗапроса = Новый ПостроительЗапроса;
		ПостроительЗапроса.Текст =
		"ВЫБРАТЬ
		|	СистемыЗаявокНазначеныеПользователю.Заявка КАК Заявка,
		|	СистемыЗаявокНазначеныеПользователю.Система КАК Система,
		|	СистемыЗаявокНазначеныеПользователю.Филиал КАК Филиал,
		|	СистемыЗаявокНазначеныеПользователю.ТекущийИсполнитель КАК ТекущийИсполнитель
		|ИЗ
		|	РегистрСведений.СистемыЗаявокНазначеныеПользователю КАК СистемыЗаявокНазначеныеПользователю
		|{ГДЕ
		|	СистемыЗаявокНазначеныеПользователю.Заявка КАК Заявка,
		|	СистемыЗаявокНазначеныеПользователю.Система КАК Система,
		|	СистемыЗаявокНазначеныеПользователю.Филиал КАК Филиал}";
		
		Для Каждого ЭлементОтбора Из НаборЗаписей.Отбор Цикл
			Если ЗначениеЗаполнено(ЭлементОтбора.Значение) Тогда
				ОбщегоНазначения.ДобавитьЭлементОтбораПостроителяЗапросов(ПостроительЗапроса, 
					ЭлементОтбора.Имя, ВидСравнения.Равно, ЭлементОтбора.Значение);
			КонецЕсли;
		КонецЦикла;
		
		ПостроительЗапроса.Выполнить();
		Выборка = ПостроительЗапроса.Результат.Выбрать();
		
		Пока Выборка.Следующий() Цикл
			Добавить(Период, Выборка, Перечисления.ВидыОперацииНазначенияПоСистеме.Удаление);
		КонецЦикла;	
	Иначе
		Для Каждого Запись Из НаборЗаписей Цикл
			Добавить(Период, Запись, Перечисления.ВидыОперацииНазначенияПоСистеме.Добавление);
		КонецЦикла;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Процедура Добавить(Знач Период, Знач ДанныеЗаполнения, Знач ВидОперации)

	НаборЗаписей = СоздатьНаборЗаписей();
	
	НаборЗаписей.Отбор.Период.Установить(Период);
	НаборЗаписей.Отбор.Заявка.Установить(ДанныеЗаполнения.Заявка);
	НаборЗаписей.Отбор.Система.Установить(ДанныеЗаполнения.Система);
	НаборЗаписей.Отбор.Филиал.Установить(ДанныеЗаполнения.Филиал);
		
	НоваяЗапись = НаборЗаписей.Добавить();
	ЗаполнитьЗначенияСвойств(НоваяЗапись, ДанныеЗаполнения);
	НоваяЗапись.Период = Период;
	НоваяЗапись.ВидОперации = ВидОперации;
	
	НаборЗаписей.Записать();
	
КонецПроцедуры

#КонецОбласти

#КонецЕсли
