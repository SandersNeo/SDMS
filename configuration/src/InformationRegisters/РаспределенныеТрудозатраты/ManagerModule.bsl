///////////////////////////////////////////////////////////////////////////////////////////////////////
// Copyright (c) 2025, ООО ДНС Технологии
// SDMS (Software Development Management System) — это корпоративная система учета разработки и управления проектами 
// Все права защищены. Эта программа и сопроводительные материалы предоставляются 
// в соответствии с условиями лицензии General Public License (GNU GPL v3)
// Текст лицензии доступен по ссылке:
// https://www.gnu.org/licenses/gpl-3.0.html
///////////////////////////////////////////////////////////////////////////////////////////////////////

#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда
	
#Область ПрограммныйИнтерфейс

// Добавляет запись о документе трудозатраты
//
// Параметры:
//  ДокументТрудозатрат	- ДокументОбъект.Трудозатраты - Объект с данными
//
Процедура Добавить(Знач ДокументТрудозатрат) Экспорт
	
	НаборЗаписей = СоздатьНаборЗаписей();
	НаборЗаписей.Отбор.Период.Установить(ДокументТрудозатрат.Дата);
	НаборЗаписей.Отбор.Филиал.Установить(ДокументТрудозатрат.Филиал);
	НаборЗаписей.Отбор.Сотрудник.Установить(ДокументТрудозатрат.Пользователь);
	НаборЗаписей.Отбор.Объект.Установить(ДокументТрудозатрат.Объект);
	
	НоваяЗапись = НаборЗаписей.Добавить();
	НоваяЗапись.Период = ДокументТрудозатрат.Дата;
	НоваяЗапись.Филиал = ДокументТрудозатрат.Филиал;
	НоваяЗапись.Объект = ДокументТрудозатрат.Объект;
	НоваяЗапись.Сотрудник = ДокументТрудозатрат.Пользователь;	
	НоваяЗапись.ДокументТрудозатрат = ДокументТрудозатрат.Ссылка;
	НоваяЗапись.ДатаВнесения = ТекущаяДатаСеанса();
	НоваяЗапись.Автор = ПараметрыСеанса.ТекущийПользователь;
	
	НаборЗаписей.Записать();

КонецПроцедуры

// Удаляет запись о документе трудозатраты
//
// Параметры:
//  ДокументТрудозатрат	- ДокументОбъект.Трудозатраты - Объект с данными
//
Процедура Удалить(Знач ДокументТрудозатрат) Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	РаспределенныеТрудозатраты.Период КАК Период,
	|	РаспределенныеТрудозатраты.Филиал КАК Филиал,
	|	РаспределенныеТрудозатраты.Сотрудник КАК Сотрудник,
	|	РаспределенныеТрудозатраты.Объект КАК Объект
	|ИЗ
	|	РегистрСведений.РаспределенныеТрудозатраты КАК РаспределенныеТрудозатраты
	|ГДЕ
	|	РаспределенныеТрудозатраты.ДокументТрудозатрат = &ДокументТрудозатрат";  
	
	Запрос.УстановитьПараметр("ДокументТрудозатрат", ДокументТрудозатрат);
	Выборка = Запрос.Выполнить().Выбрать();
	
	Пока Выборка.Следующий() Цикл		
		НаборЗаписей = СоздатьНаборЗаписей();
		НаборЗаписей.Отбор.Период.Установить(Выборка.Период);
		НаборЗаписей.Отбор.Филиал.Установить(Выборка.Филиал);
		НаборЗаписей.Отбор.Сотрудник.Установить(Выборка.Сотрудник);
		НаборЗаписей.Отбор.Объект.Установить(Выборка.Объект);		
		НаборЗаписей.Записать();   
	КонецЦикла;  
	
	УстановитьПривилегированныйРежим(Ложь);

КонецПроцедуры

#КонецОбласти

#КонецЕсли
