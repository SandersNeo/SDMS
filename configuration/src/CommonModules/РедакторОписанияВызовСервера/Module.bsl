///////////////////////////////////////////////////////////////////////////////////////////////////////
// Copyright (c) 2025, ООО ДНС Технологии
// SDMS (Software Development Management System) — это корпоративная система учета разработки и управления проектами 
// Все права защищены. Эта программа и сопроводительные материалы предоставляются 
// в соответствии с условиями лицензии General Public License (GNU GPL v3)
// Текст лицензии доступен по ссылке:
// https://www.gnu.org/licenses/gpl-3.0.html
///////////////////////////////////////////////////////////////////////////////////////////////////////

#Область ПрограммныйИнтерфейс

// Возвращает html код для заполнения реквизита связанного с полем html документа.
//
// Параметры:
//  Описание				 - Строка - описание объекта в html формате.
//  Подсказка				 - Строка - Подсказка в поле ввода описания.
//  ЗапрещеноРедактировать	 - Булево - признак блокировки изменения описания.
//  ОграничитьТулбар	 	 - Булево - признак, что нужны не все функции панели.
// 
// Возвращаемое значение:
//  Строка - html описание страницы с редактором.
//
Функция ПолучитьРедактор(Знач Описание, Знач Подсказка = "", Знач ЗапрещеноРедактировать = Ложь, Знач ОграничитьТулбар = Ложь) Экспорт
		
	BaseURL = ПараметрыСеанса.АдресаWebОкружения.АдресПубликацииИнформационнойБазы;
	Версия = "v4"; // Нужно повышать при замене ассетов, т.к. они кэшируются в браузере.
	ПлагинВыбораЦвета = "";
	НастройкиТулбара  = "";
	
	ТекущееВремя = ТекущаяУниверсальнаяДатаВМиллисекундах();
	Шаблон = ПолучитьОбщийМакет("CKEditorHTML").ПолучитьТекст();
	
	ДополнительныеМетоды = Новый Массив;
	
	Если ЗапрещеноРедактировать Тогда
		КодЗапретаРедактирования = СтрШаблон("editor.enableReadOnlyMode('%1');", ТекущееВремя);
		ДополнительныеМетоды.Добавить(КодЗапретаРедактирования);
	КонецЕсли;
	
	Если ОграничитьТулбар Тогда 
		НастройкиТулбара = 
			",
			|toolbar: {
			|items: [
			|	'heading',
			|	'|',
			|	'bold',
			|	'italic',
			|	'underline',
			|	'strikethrough',
			|	'|',
			|	'bulletedList',
			|	'numberedList',
			|	'|',
			|	'imageUpload',
			|	'link',
			|	'|',
			|	'horizontalLine',
			|	'code',
			|	'codeBlock'
			|]
			|}"
	КонецЕсли;
	
	Если КлиентскоеПриложение.ТипПриложения() = ТипКлиентскогоПриложения.ТонкийКлиент Тогда
		ПлагинВыбораЦвета = 
			",
			|fontColor: {
			|	colorPicker: false
			|},
			|fontBackgroundColor: {
			|	colorPicker: false
			|}"; // Выбор цвета текста/фона из палитры не работает в тонком клиенте. Отключаем.
		
		ТекстЗапрета =
			"В тонком клиенте некорректно работает вставка описания с картинками из MS Word.\n\
			|Для вставки картинок воспользуйтесь командой ""Вставить изображение"".";
		
		КодЗапретаВставкиИзWord = СтрШаблон(
			"editor.editing.view.listenTo(editor.editing.view.document, 'paste', (evt, data) => {
			|	var clipboardData = data.dataTransfer.getData('text/html');
			|	if (clipboardData) {
			|		if (/\<v:imagedata src=""file:\/\/\//i.test(clipboardData)) {
			|			data.preventDefault();
			|			evt.stop();
			|			alert('%1');
			|		};
			|	};
			|}, { priority: 'highest' } );", ТекстЗапрета);
		ДополнительныеМетоды.Добавить(КодЗапретаВставкиИзWord);
	КонецЕсли;
	
	ДополнительныйКод = СтрСоединить(ДополнительныеМетоды, Символы.ПС);
	
	Возврат СтрШаблон(Шаблон, BaseURL, Описание, Версия, ТекущееВремя, 
		Подсказка, НастройкиТулбара, ПлагинВыбораЦвета, ДополнительныйКод);
	
КонецФункции

// Заблокировать элемент справочника описания по гуиду формы
//
// Параметры:
//  УникальныйИдентификатор	 - УникальныйИдентификатор			 - Уникальный идентификатор формы
//  Описание				 - СправочникСсылка.ДополнительныеОписанияОбъектов	 - дополнительное описание для задачи
//  РежимПроверки			 - Булево											 - признак снимать ли сразу блокировку
// 
// Возвращаемое значение:
//  Структура - результат блокировки
//
Функция ЗаблокироватьОписание(Знач УникальныйИдентификатор, Знач Описание, Знач РежимПроверки = Ложь) Экспорт
	
	Результат = Новый Структура("Успешно, ТекстОшибки", Истина, "");
	
	Попытка                                                        
		Если ЗначениеЗаполнено(Описание) Тогда
			ЗаблокироватьДанныеДляРедактирования(Описание,, УникальныйИдентификатор);
			
			Если РежимПроверки Тогда
				РазблокироватьДанныеДляРедактирования(Описание, УникальныйИдентификатор);
			КонецЕсли;
		КонецЕсли;
	Исключение   
		Результат.Успешно = Ложь;
		
		ТекстОшибки = ОписаниеОшибки();
		СтрокиСеанс = СтрНайтиВсеПоРегулярномуВыражению(Текстошибки, "(session|сеанс): \d+", Истина); 
		
		Если СтрокиСеанс.Количество() > 0 Тогда  		      
			НомерСеанса = Число(СтрРазделить(СтрокиСеанс[0].Значение, ":")[1]);
			
			УстановитьПривилегированныйРежим(Истина);
			АктивныеСеансы = ПолучитьСеансыИнформационнойБазы();
			УстановитьПривилегированныйРежим(Ложь);
			
			Для Каждого Элемент Из АктивныеСеансы Цикл
				Если Элемент.НомерСеанса = НомерСеанса Тогда
					Пользователь = Элемент.Пользователь;
					Прервать;
				КонецЕсли;
			КонецЦикла;                              
			
			СтрокиДата = СтрНайтиВсеПоРегулярномуВыражению(ТекстОшибки, "\d+(\.|\/)\d+(\.|\/)\d{2,4} (в|at) \d+:\d\d:\d\d"); 
			Дата = ?(СтрокиДата.Количество() > 0, СтрокиДата[0].Значение, "");
			ТекстОшибки = СтрШаблон("Заблокировано: %1 %2", Пользователь, Дата);   
		КонецЕсли;
		
		Результат.ТекстОшибки = ТекстОшибки;
	КонецПопытки;     
	
	Возврат Результат;
	
КонецФункции

// Снимает блокировки по гуиды формы
//
// Параметры:
//  УникальныйИдентификатор	 - УникальныйИдентификатор	 - Уникальный идентификатор формы
//
Процедура РазблокироватьОписания(УникальныйИдентификатор) Экспорт
	
	РазблокироватьДанныеДляРедактирования(, УникальныйИдентификатор);
	
КонецПроцедуры

#КонецОбласти
