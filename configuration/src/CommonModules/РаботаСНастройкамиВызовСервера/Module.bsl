///////////////////////////////////////////////////////////////////////////////////////////////////////
// Copyright (c) 2025, ООО ДНС Технологии
// SDMS (Software Development Management System) — это корпоративная система учета разработки и управления проектами 
// Все права защищены. Эта программа и сопроводительные материалы предоставляются 
// в соответствии с условиями лицензии General Public License (GNU GPL v3)
// Текст лицензии доступен по ссылке:
// https://www.gnu.org/licenses/gpl-3.0.html
///////////////////////////////////////////////////////////////////////////////////////////////////////

#Если Сервер ИЛИ ВнешнееСоединение ИЛИ ТолстыйКлиентОбычноеПриложение Тогда

#Область ПрограммныйИнтерфейс

// Возвращает список выбора направлений для филиала
//
// Параметры:
//  Объект	 - СправочникСсылка.Филиалы, СправочникСслыка.НаправленияРазработки	 - филиал или направление
// 
// Возвращаемое значение:
//  СписокЗначений - список юридических лиц для филиала
//
Функция ПолучитьСписокВыбораЮридическихЛиц(Знач Объект) Экспорт
	
	Результат = Новый СписокЗначений;
	ТипОбъекта = ТипЗнч(Объект);
	
	Запрос = Новый Запрос;
	
	Если ТипОбъекта = Тип("СправочникСсылка.Филиалы") Тогда
		Запрос.Текст = 
		"ВЫБРАТЬ
		|	ЮридическиеЛицаНаправлений.ЮридическоеЛицо КАК Значение,
		|	ПРЕДСТАВЛЕНИЕССЫЛКИ(ЮридическиеЛицаНаправлений.ЮридическоеЛицо) КАК Представление
		|ИЗ
		|	Справочник.Филиалы КАК Филиалы
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.НастройкиФилиалов КАК НастройкиФилиалов
		|		ПО Филиалы.Ссылка = НастройкиФилиалов.Филиал
		|			И (НастройкиФилиалов.Настройка = ЗНАЧЕНИЕ(ПланВидовХарактеристик.ВидыНастроекФилиалов.НаправлениеРазработки))
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.НаправленияРазработки.ЮридическиеЛица КАК ЮридическиеЛицаНаправлений
		|		ПО ((ВЫРАЗИТЬ(НастройкиФилиалов.Значение КАК Справочник.НаправленияРазработки)) = ЮридическиеЛицаНаправлений.Ссылка)
		|ГДЕ
		|	Филиалы.Ссылка = &Объект";
	Иначе
		Запрос.Текст = 
		"ВЫБРАТЬ
		|	ЮридическиеЛицаНаправлений.ЮридическоеЛицо КАК Значение,
		|	ПРЕДСТАВЛЕНИЕССЫЛКИ(ЮридическиеЛицаНаправлений.ЮридическоеЛицо) КАК Представление
		|ИЗ
		|	Справочник.НаправленияРазработки.ЮридическиеЛица КАК ЮридическиеЛицаНаправлений
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ИспользованиеЮридическихЛицНаправлений.СрезПоследних(&Период, ) КАК ИспользованиеЮридическихЛицНаправленийСрезПоследних
		|		ПО ЮридическиеЛицаНаправлений.Ссылка = ИспользованиеЮридическихЛицНаправленийСрезПоследних.Направление
		|ГДЕ
		|	ЮридическиеЛицаНаправлений.Ссылка = &Объект
		|	И ЕСТЬNULL(ИспользованиеЮридическихЛицНаправленийСрезПоследних.Использование, ЛОЖЬ)";
		
		Запрос.УстановитьПараметр("Период", НачалоДня(ТекущаяДатаСеанса()));

	КонецЕсли;
	
	Запрос.УстановитьПараметр("Объект", Объект);
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	Пока Выборка.Следующий() Цикл
		Результат.Добавить(Выборка.Значение, Выборка.Представление);
	КонецЦикла;
	
	Возврат Результат;
	
КонецФункции

// Возвращает список выбора направлений для филиала
//
// Параметры:
//  Объект	 - СправочникСсылка.Филиалы, СправочникСслыка.НаправленияРазработки	 - филиал или направление
// 
// Возвращаемое значение:
//  СписокЗначений - список юридических лиц для филиала
//
Функция СписокВыбораГруппЗаказчиков(Знач Направление, Знач Система) Экспорт
	
	Результат = Новый СписокЗначений;
	
	Если НЕ ЗначениеЗаполнено(Направление)
		ИЛИ НЕ ЗначениеЗаполнено(Система) Тогда
		Возврат Результат;
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ГруппыЗаказчиковНаправленийНаправления.Ссылка КАК Значение,
	|	ПРЕДСТАВЛЕНИЕССЫЛКИ(ГруппыЗаказчиковНаправленийНаправления.Ссылка) КАК Представление
	|ИЗ
	|	Справочник.ГруппыЗаказчиковНаправлений.Направления КАК ГруппыЗаказчиковНаправленийНаправления
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.ГруппыЗаказчиковНаправлений.Системы КАК ГруппыЗаказчиковНаправленийСистемы
	|		ПО ГруппыЗаказчиковНаправленийНаправления.Ссылка = ГруппыЗаказчиковНаправленийСистемы.Ссылка
	|			И (ГруппыЗаказчиковНаправленийСистемы.Система = &Система)
	|			И (ГруппыЗаказчиковНаправленийНаправления.Направление = &Направление)";
	
	Запрос.УстановитьПараметр("Направление", Направление);
	Запрос.УстановитьПараметр("Система", Система);

	РезультатЗапроса = Запрос.Выполнить();
	
	Если НЕ РезультатЗапроса.Пустой() Тогда
		Выборка = РезультатЗапроса.Выбрать();
		Пока Выборка.Следующий() Цикл
			Результат.Добавить(Выборка.Значение, Выборка.Представление);
		КонецЦикла;
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

// Возвращает принадлежность филиала юридическому лицу, являющемуся IT-организацией
// 
// Параметры:
//  Филиал - СправочникСсылка.Филиалы - филиал
// 
// Возвращаемое значение:
//  Булево - Истина, если установлен признак, иначе Ложь
//
Функция ЭтоФилиалИТОрганизации(Знач Филиал) Экспорт

	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ИСТИНА
	|ИЗ
	|	Справочник.Филиалы КАК Филиалы
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.ЮридическиеЛица КАК ЮридическиеЛица
	|		ПО Филиалы.ЮридическоеЛицо = ЮридическиеЛица.Ссылка
	|			И (ЮридическиеЛица.ЯвляетсяИТОрганизацией
	|				И Филиалы.Ссылка = &Филиал)";
	
	Запрос.УстановитьПараметр("Филиал", Филиал);
	
	Возврат НЕ Запрос.Выполнить().Пустой();

КонецФункции

#КонецОбласти

#КонецЕсли
