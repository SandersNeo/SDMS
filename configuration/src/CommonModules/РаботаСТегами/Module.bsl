///////////////////////////////////////////////////////////////////////////////////////////////////////
// Copyright (c) 2025, ООО ДНС Технологии
// SDMS (Software Development Management System) — это корпоративная система учета разработки и управления проектами 
// Все права защищены. Эта программа и сопроводительные материалы предоставляются 
// в соответствии с условиями лицензии General Public License (GNU GPL v3)
// Текст лицензии доступен по ссылке:
// https://www.gnu.org/licenses/gpl-3.0.html
///////////////////////////////////////////////////////////////////////////////////////////////////////

#Область ПрограммныйИнтерфейс

// Заполняет поле html тегами объекта
//
// Параметры:
//  Форма	 - ФормаКлиентскогоПриложения	 - Управляемая форма
//  Цвет	 - Цвет, ЦветСтиля				 - Цвет фона тега
//
Процедура Заполнить(Форма, Знач Цвет = Неопределено) Экспорт
		
	СписокТегов = Форма.СписокТегов;
	ПараметрыФормы = Форма.Параметры;
	
	// Если объект создается на основании/скопирован, получаем теги родителя
	Если ЗначениеЗаполнено(ПараметрыФормы.Основание) Тогда
		Ссылка = ПараметрыФормы.Основание;
		Сохранен = Ложь;
	ИначеЕсли ЗначениеЗаполнено(ПараметрыФормы.ЗначениеКопирования) Тогда
		Ссылка = ПараметрыФормы.ЗначениеКопирования;
		Сохранен = Ложь;
	Иначе
		Ссылка = Форма.Объект.Ссылка;
		Сохранен = Истина;
	КонецЕсли;
	
	Если ЗначениеЗаполнено(Ссылка) Тогда
		Запрос = Новый Запрос;
		Запрос.Текст =
		"ВЫБРАТЬ
		|	ТегиОбъектов.Тег КАК Тег,
		|	Теги.Наименование КАК ТегПредставление
		|ИЗ
		|	РегистрСведений.ТегиОбъектов КАК ТегиОбъектов
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.Теги КАК Теги
		|		ПО ТегиОбъектов.Тег = Теги.Ссылка
		|ГДЕ
		|	ТегиОбъектов.Объект = &Объект
		|
		|УПОРЯДОЧИТЬ ПО
		|	ТегПредставление";
		
		Запрос.УстановитьПараметр("Объект", Ссылка);
		РезультатЗапроса = Запрос.Выполнить();
		
		Если НЕ РезультатЗапроса.Пустой() Тогда
			Выборка = РезультатЗапроса.Выбрать();
			
			Пока Выборка.Следующий() Цикл
				НоваяСтрока = СписокТегов.Добавить();
				НоваяСтрока.Тег = Выборка.Тег;
				НоваяСтрока.ТегПредставление = Выборка.ТегПредставление;
				НоваяСтрока.Идентификатор = Новый УникальныйИдентификатор;
				НоваяСтрока.Сохранен = Сохранен;
			КонецЦикла;
		КонецЕсли;
	КонецЕсли;
		
	Форма.ТегиHTML = РаботаСТегамиКлиентСервер.ПредставлениеТегов(СписокТегов,, Цвет);
	
КонецПроцедуры

#КонецОбласти
