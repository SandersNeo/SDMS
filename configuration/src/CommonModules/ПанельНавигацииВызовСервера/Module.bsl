///////////////////////////////////////////////////////////////////////////////////////////////////////
// Copyright (c) 2025, ООО ДНС Технологии
// SDMS (Software Development Management System) — это корпоративная система учета разработки и управления проектами 
// Все права защищены. Эта программа и сопроводительные материалы предоставляются 
// в соответствии с условиями лицензии General Public License (GNU GPL v3)
// Текст лицензии доступен по ссылке:
// https://www.gnu.org/licenses/gpl-3.0.html
///////////////////////////////////////////////////////////////////////////////////////////////////////
	  
#Область ПрограммныйИнтерфейс

// Возвращает заголовок для кнопки панели навигации
//
// Параметры:
//  Ссылка - ДокументСсылка, СправочникСсылка - ссылка на объект, подключенный к механизму панели навигации 
//  ИмяКоманды - Строка - Имя команды
//  ЗаголовокПоУмолчанию - Строка - заголовок команды по умолчанию 
// 
// Возвращаемое значение:
//  Строка - заголовок команды
//
Функция ЗаголовокКоманды(Знач Ссылка, Знач ИмяКоманды, Знач ЗаголовокПоУмолчанию) Экспорт
	
	Если ИмяКоманды = "Участники" Тогда
		Заголовок = ЗаголовокВкладкиУчастники(Ссылка);
	ИначеЕсли ИмяКоманды = "Комментарии" Тогда
		Заголовок = ЗаголовокВкладкиКомментарии(Ссылка);
	ИначеЕсли ИмяКоманды = "Файлы" Тогда
		Заголовок = ЗаголовокВкладкиПрикрепленныеФайлы(Ссылка);
	ИначеЕсли ИмяКоманды = "Связи" Тогда 
		Заголовок = ЗаголовокВкладкиСвязи(Ссылка);
	ИначеЕсли ИмяКоманды = "ЗаинтересованныеЛица" Тогда
		Заголовок = ЗаголовокВкладкиЗаинтересованныеЛица(Ссылка);
	ИначеЕсли ИмяКоманды = "МатрицаТребований" Тогда
		Заголовок = ЗаголовокВкладкиМатрицаТребований(Ссылка);
	Иначе
		Заголовок = ЗаголовокПоУмолчанию;
	КонецЕсли;
	
	Возврат Заголовок;
	
КонецФункции

// Возвращает заголовок для страницы или кнопки панели навигации "Комментарии"
//
// Параметры:
//  Ссылка - ДокументСсылка, СправочникСсылка - ссылка на объект, подключенный к механизму панели навигации  
// 
// Возвращаемое значение:
//  Строка - Текст заголовка
//
Функция ЗаголовокВкладкиКомментарии(Знач Ссылка) Экспорт
	
	КоличествоКомментариев = РегистрыСведений.Комментарии.КоличествоКомментариев(Ссылка);
	Заголовок = "Комментарии (" + Формат(КоличествоКомментариев, "ЧЦ=15; ЧН=0; ЧГ=0") + ")";
	
	Возврат Заголовок;
	
КонецФункции

// Возвращает заголовок для страницы или кнопки панели навигации "Прикрепленные файлы"
//
// Параметры:
//  Ссылка - ДокументСсылка, СправочникСсылка - ссылка на объект, подключенный к механизму панели навигации 
// 
// Возвращаемое значение:
//  Строка - Текст заголовка
//
Функция ЗаголовокВкладкиПрикрепленныеФайлы(Знач Ссылка) Экспорт
	
	ТекстЗаголовка   = "Файлы%1";
	КоличествоФайлов = "";
	
	Если ЗначениеЗаполнено(Ссылка) Тогда
		Запрос = Новый Запрос;
		Запрос.Текст = 
		"ВЫБРАТЬ
		|	СтруктураПодчиненности.Объект КАК Объект
		|ПОМЕСТИТЬ ВсеСвязанныеОбъекты
		|ИЗ
		|	РегистрСведений.СтруктураПодчиненности КАК СтруктураПодчиненности
		|ГДЕ
		|	СтруктураПодчиненности.Родитель = &Ссылка
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	СтруктураПодчиненности.Родитель
		|ИЗ
		|	РегистрСведений.СтруктураПодчиненности КАК СтруктураПодчиненности
		|ГДЕ
		|	СтруктураПодчиненности.Объект = &Ссылка
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	ВсеСвязанныеОбъекты.Объект КАК Объект,
		|	ПрикрепленныеФайлы.Файл КАК Файл
		|ПОМЕСТИТЬ ПрисоединенныеФайлыОбъектов
		|ИЗ
		|	ВсеСвязанныеОбъекты КАК ВсеСвязанныеОбъекты
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.ПрикрепленныеФайлы КАК ПрикрепленныеФайлы
		|		ПО ВсеСвязанныеОбъекты.Объект = ПрикрепленныеФайлы.Объект
		|			И (ПрикрепленныеФайлы.Назначение = ЗНАЧЕНИЕ(Перечисление.НазначенияФайлов.Объект))
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ЗаявкаНаРазработку КАК ЗаявкаНаРазработку
		|		ПО ((ВЫРАЗИТЬ(ВсеСвязанныеОбъекты.Объект КАК Документ.ЗаявкаНаРазработку)) = ЗаявкаНаРазработку.Ссылка)
		|ГДЕ
		|	ВсеСвязанныеОбъекты.Объект ССЫЛКА Документ.ЗаявкаНаРазработку
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	ВсеСвязанныеОбъекты.Объект,
		|	ПрикрепленныеФайлы.Файл
		|ИЗ
		|	ВсеСвязанныеОбъекты КАК ВсеСвязанныеОбъекты
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ЗаявкаНаРазработку КАК ЗаявкаНаРазработку
		|		ПО ((ВЫРАЗИТЬ(ВсеСвязанныеОбъекты.Объект КАК Документ.ЗаявкаНаРазработку)) = ЗаявкаНаРазработку.Ссылка)
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.Комментарии КАК Комментарии
		|		ПО ((ВЫРАЗИТЬ(ВсеСвязанныеОбъекты.Объект КАК Документ.ЗаявкаНаРазработку)) = Комментарии.Объект)
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.ПрикрепленныеФайлы КАК ПрикрепленныеФайлы
		|		ПО (Комментарии.Идентификатор = ПрикрепленныеФайлы.Объект)
		|			И (ПрикрепленныеФайлы.Назначение = ЗНАЧЕНИЕ(Перечисление.НазначенияФайлов.ФайлыКомментария))
		|ГДЕ
		|	ВсеСвязанныеОбъекты.Объект ССЫЛКА Документ.ЗаявкаНаРазработку
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	ВсеСвязанныеОбъекты.Объект,
		|	ПрикрепленныеФайлы.Файл
		|ИЗ
		|	ВсеСвязанныеОбъекты КАК ВсеСвязанныеОбъекты
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.ПрикрепленныеФайлы КАК ПрикрепленныеФайлы
		|		ПО ВсеСвязанныеОбъекты.Объект = ПрикрепленныеФайлы.Объект
		|			И (ПрикрепленныеФайлы.Назначение = ЗНАЧЕНИЕ(Перечисление.НазначенияФайлов.Объект))
		|ГДЕ
		|	НЕ ВсеСвязанныеОбъекты.Объект ССЫЛКА Документ.ЗаявкаНаРазработку
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	ВсеСвязанныеОбъекты.Объект,
		|	ПрикрепленныеФайлы.Файл
		|ИЗ
		|	ВсеСвязанныеОбъекты КАК ВсеСвязанныеОбъекты
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.Комментарии КАК Комментарии
		|		ПО ВсеСвязанныеОбъекты.Объект = Комментарии.Объект
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.ПрикрепленныеФайлы КАК ПрикрепленныеФайлы
		|		ПО (Комментарии.Идентификатор = ПрикрепленныеФайлы.Объект)
		|			И (ПрикрепленныеФайлы.Назначение = ЗНАЧЕНИЕ(Перечисление.НазначенияФайлов.ФайлыКомментария))
		|ГДЕ
		|	НЕ ВсеСвязанныеОбъекты.Объект ССЫЛКА Документ.ЗаявкаНаРазработку
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	ПрикрепленныеФайлы.Объект,
		|	ПрикрепленныеФайлы.Файл
		|ИЗ
		|	РегистрСведений.ПрикрепленныеФайлы КАК ПрикрепленныеФайлы
		|ГДЕ
		|	ПрикрепленныеФайлы.Объект = &Ссылка
		|	И ПрикрепленныеФайлы.Назначение = ЗНАЧЕНИЕ(Перечисление.НазначенияФайлов.Объект)
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	Комментарии.Объект,
		|	ПрикрепленныеФайлы.Файл
		|ИЗ
		|	РегистрСведений.Комментарии КАК Комментарии
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.ПрикрепленныеФайлы КАК ПрикрепленныеФайлы
		|		ПО Комментарии.Идентификатор = ПрикрепленныеФайлы.Объект
		|			И (ПрикрепленныеФайлы.Назначение = ЗНАЧЕНИЕ(Перечисление.НазначенияФайлов.ФайлыКомментария))
		|ГДЕ
		|	Комментарии.Объект = &Ссылка
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	КОЛИЧЕСТВО(*) КАК Количество
		|ИЗ
		|	ПрисоединенныеФайлыОбъектов КАК ПрисоединенныеФайлыОбъектов
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.Файлы КАК СправочникФайлы
		|		ПО ПрисоединенныеФайлыОбъектов.Файл = СправочникФайлы.Ссылка
		|			И (НЕ СправочникФайлы.ПометкаУдаления)";
				
		Запрос.УстановитьПараметр("Ссылка", Ссылка);
		
		Выборка = Запрос.Выполнить().Выбрать();
		Если Выборка.Следующий() Тогда
			КоличествоФайлов = " (" + Формат(Выборка.Количество, "ЧЦ=15; ЧН=0; ЧГ=0") + ")";
		КонецЕсли;
	КонецЕсли;
	
	Возврат СтрШаблон(ТекстЗаголовка, КоличествоФайлов);
	
КонецФункции

// Возвращает заголовок для Вкладки Участники
//
// Параметры:
//  Ссылка - ОпределяемыйТип.ОбъектНаблюдения - ссылка на объект наблюдения 
// 
// Возвращаемое значение:
//   Строка - текст заголовка.
//
Функция ЗаголовокВкладкиУчастники(Знач Ссылка) Экспорт
		
	Запрос = Новый Запрос;
	Запрос.Текст =	
	"ВЫБРАТЬ
	|	КОЛИЧЕСТВО(РАЗЛИЧНЫЕ Пользователь) КАК Количество
	|ИЗ
	|	РегистрСведений.Участники КАК Участники
	|ГДЕ
	|	Участники.Объект = &Ссылка
	|	И НЕ Участники.Скрытый";
	
	Запрос.Параметры.Вставить("Ссылка", Ссылка);
	
	Выборка = Запрос.Выполнить().Выбрать();
	Выборка.Следующий();
	
	Заголовок = "Участники (" + Формат(Выборка.Количество, "ЧЦ=15; ЧН=0; ЧГ=0") + ")";
	
	Возврат Заголовок;
	
КонецФункции

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция ЗаголовокВкладкиЗаинтересованныеЛица(Знач Ссылка)
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	КОЛИЧЕСТВО(*) КАК Количество
	|ИЗ
	|	РегистрСведений.ЗаинтересованныеЛица КАК РегистрСведенийЗаинтересованныеЛица
	|ГДЕ
	|	РегистрСведенийЗаинтересованныеЛица.Объект = &Объект";
	
	Запрос.Параметры.Вставить("Объект", Ссылка);
	
	Выборка = Запрос.Выполнить().Выбрать();
	Выборка.Следующий();
	
	Заголовок = "Заинтересованные лица (" + Формат(Выборка.Количество, "ЧЦ=15; ЧН=0; ЧГ=0") + ")";
	
	Возврат Заголовок;
	
КонецФункции

Функция ЗаголовокВкладкиМатрицаТребований(Знач Ссылка)
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	КОЛИЧЕСТВО(*) КАК Количество
	|ИЗ
	|	Документ.МатрицаТребований.Требования КАК МатрицаТребованийТребования
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.МатрицаТребований КАК МатрицаТребований
	|		ПО МатрицаТребованийТребования.Ссылка = МатрицаТребований.Ссылка
	|ГДЕ
	|	МатрицаТребований.ОбъектОснование = &Объект";
	
	Запрос.Параметры.Вставить("Объект", Ссылка);
	
	Выборка = Запрос.Выполнить().Выбрать();
	Выборка.Следующий();
	
	Заголовок = "Матрица требований (" + Формат(Выборка.Количество, "ЧЦ=15; ЧН=0; ЧГ=0") + ")";
	
	Возврат Заголовок;
	
КонецФункции

Функция ЗаголовокВкладкиСвязи(Знач Ссылка)
	
	Заголовок = "Связи";
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	СвязанныеОбъекты.ПриемникСвязи КАК Объект
	|ПОМЕСТИТЬ Объекты
	|ИЗ
	|	РегистрСведений.СвязанныеОбъекты КАК СвязанныеОбъекты
	|ГДЕ
	|	СвязанныеОбъекты.ИсточникСвязи = &ИсточникСвязи
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	СвязанныеОбъекты.ИсточникСвязи
	|ИЗ
	|	РегистрСведений.СвязанныеОбъекты КАК СвязанныеОбъекты
	|ГДЕ
	|	СвязанныеОбъекты.ПриемникСвязи = &ИсточникСвязи
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	Объекты.Объект КАК Объект
	|ПОМЕСТИТЬ ОтобранныеОбъекты
	|ИЗ
	|	Объекты КАК Объекты
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ЗаявкаНаРазработку КАК ЗаявкаНаРазработку
	|		ПО Объекты.Объект = ЗаявкаНаРазработку.Ссылка
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	Объекты.Объект
	|ИЗ
	|	Объекты КАК Объекты
	|ГДЕ
	|	НЕ Объекты.Объект ССЫЛКА Документ.ЗаявкаНаРазработку
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	КОЛИЧЕСТВО(*) КАК Количество
	|ИЗ
	|	ОтобранныеОбъекты КАК ОтобранныеОбъекты";
	
	Запрос.УстановитьПараметр("ИсточникСвязи", Ссылка);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Если НЕ РезультатЗапроса.Пустой() Тогда
		Выборка = РезультатЗапроса.Выбрать();
		Выборка.Следующий();
		
		Заголовок = Заголовок + " (" + Выборка.Количество + ")";	
	КонецЕсли;	
	
	Возврат Заголовок;
	
КонецФункции

#КонецОбласти
