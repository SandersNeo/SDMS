///////////////////////////////////////////////////////////////////////////////////////////////////////
// Copyright (c) 2025, ООО ДНС Технологии
// SDMS (Software Development Management System) — это корпоративная система учета разработки и управления проектами 
// Все права защищены. Эта программа и сопроводительные материалы предоставляются 
// в соответствии с условиями лицензии General Public License (GNU GPL v3)
// Текст лицензии доступен по ссылке:
// https://www.gnu.org/licenses/gpl-3.0.html
///////////////////////////////////////////////////////////////////////////////////////////////////////

#Если Сервер ИЛИ ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

Функция ПолучитьБезопасностьОбъекта(Знач МетаданныеФормы, Знач ТипПрава, Знач Объект = Неопределено) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ИдентификаторыОбъектовМетаданных.Ссылка КАК Ссылка
	|ПОМЕСТИТЬ ИдентификаторыПервогоУровня
	|ИЗ
	|	Справочник.ИдентификаторыОбъектовМетаданных КАК ИдентификаторыОбъектовМетаданных
	|ГДЕ
	|	ИдентификаторыОбъектовМетаданных.Наименование = &Тип
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ИдентификаторыОбъектовМетаданных.Ссылка КАК Ссылка
	|ПОМЕСТИТЬ ИдентификаторыВторогоУровня
	|ИЗ
	|	ИдентификаторыПервогоУровня КАК ИдентификаторыПервогоУровня
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.ИдентификаторыОбъектовМетаданных КАК ИдентификаторыОбъектовМетаданных
	|		ПО ИдентификаторыПервогоУровня.Ссылка = ИдентификаторыОбъектовМетаданных.Родитель
	|ГДЕ
	|	ИдентификаторыОбъектовМетаданных.Наименование = &Название
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ИдентификаторыОбъектовМетаданных.Ссылка КАК Ссылка
	|ПОМЕСТИТЬ ИдентификаторыМетаданныхОбъекта
	|ИЗ
	|	ИдентификаторыВторогоУровня КАК ИдентификаторыВторогоУровня
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.ИдентификаторыОбъектовМетаданных КАК ИдентификаторыОбъектовМетаданных
	|		ПО ИдентификаторыВторогоУровня.Ссылка = ИдентификаторыОбъектовМетаданных.Родитель
	|ГДЕ
	|	ИдентификаторыОбъектовМетаданных.Наименование = &Форма
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ИдентификаторыВторогоУровня.Ссылка
	|ИЗ
	|	ИдентификаторыВторогоУровня КАК ИдентификаторыВторогоУровня
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ИдентификаторыПервогоУровня.Ссылка
	|ИЗ
	|	ИдентификаторыПервогоУровня КАК ИдентификаторыПервогоУровня
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	СвязьОбъектовИРолей.Роль КАК Право,
	|	ПрограммныеРоли.Родитель КАК ТипПрава,
	|	ВЫБОР
	|		КОГДА НастройкиПравОбъектов.Объект ЕСТЬ NULL
	|			ТОГДА ВЫБОР
	|					КОГДА ЗначенияРолейДляГруппДоступа.Объект ЕСТЬ NULL
	|						ТОГДА ВЫБОР
	|								КОГДА СвязьОбъектовИРолей.РассчитыватьПраво
	|									ТОГДА НЕОПРЕДЕЛЕНО
	|								ИНАЧЕ СвязьОбъектовИРолей.ЗначениеПоУмолчанию
	|							КОНЕЦ
	|					ИНАЧЕ ЗначенияРолейДляГруппДоступа.ВидПраваДоступа
	|				КОНЕЦ
	|		ИНАЧЕ НастройкиПравОбъектов.ВидПраваДоступа
	|	КОНЕЦ КАК ВидПраваДоступа,
	|	СвязьОбъектовИРолей.ИмяМетода КАК ИмяМетода,
	|	СвязьОбъектовИРолей.ИмяМетодаНастройки КАК ИмяМетодаНастройки
	|ИЗ
	|	РегистрСведений.СвязьОбъектовИРолей КАК СвязьОбъектовИРолей
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ИдентификаторыМетаданныхОбъекта КАК ИдентификаторыМетаданныхОбъекта
	|		ПО СвязьОбъектовИРолей.Объект = ИдентификаторыМетаданныхОбъекта.Ссылка
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.ПрограммныеРоли КАК ПрограммныеРоли
	|		ПО СвязьОбъектовИРолей.Роль = ПрограммныеРоли.Ссылка
	|			И (ПрограммныеРоли.Родитель = &ТипПрава)
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ЗначенияРолейДляГруппДоступа КАК ЗначенияРолейДляГруппДоступа
	|		ПО (ЗначенияРолейДляГруппДоступа.ГруппаДоступа В (&ГруппыДоступа))
	|			И СвязьОбъектовИРолей.Объект = ЗначенияРолейДляГруппДоступа.Объект
	|			И СвязьОбъектовИРолей.Роль = ЗначенияРолейДляГруппДоступа.Роль
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.НастройкиПравОбъектов КАК НастройкиПравОбъектов
	|		ПО (НастройкиПравОбъектов.Объект = &ОбъектСсылка)
	|			И (НастройкиПравОбъектов.Пользователь = &Пользователь)
	|			И СвязьОбъектовИРолей.Роль = НастройкиПравОбъектов.Роль";
	
	Запрос.УстановитьПараметр("Тип", МетаданныеФормы.Тип);
	Запрос.УстановитьПараметр("Название", МетаданныеФормы.Название);
	Запрос.УстановитьПараметр("Форма", МетаданныеФормы.Форма);
	Запрос.УстановитьПараметр("ГруппыДоступа", БезопасностьПовтИсп.ПолучитьГруппыДоступаПользователя());
	Запрос.УстановитьПараметр("ОбъектСсылка", Объект);
	Запрос.УстановитьПараметр("Пользователь", ПараметрыСеанса.ТекущийПользователь);
	Запрос.УстановитьПараметр("ТипПрава", ТипПрава);
	
	Возврат Запрос.Выполнить().Выгрузить();
	
КонецФункции

#КонецОбласти
	
#КонецЕсли
