///////////////////////////////////////////////////////////////////////////////////////////////////////
// Copyright (c) 2025, ООО ДНС Технологии
// SDMS (Software Development Management System) — это корпоративная система учета разработки и управления проектами 
// Все права защищены. Эта программа и сопроводительные материалы предоставляются 
// в соответствии с условиями лицензии General Public License (GNU GPL v3)
// Текст лицензии доступен по ссылке:
// https://www.gnu.org/licenses/gpl-3.0.html
///////////////////////////////////////////////////////////////////////////////////////////////////////

Функция ПодготовитьДанныеИзмененияСтатуса(Знач Трудозатраты, Знач Исполнитель, Знач Причина, Знач СтруктураКомментарий, Знач СвойстваПричин) Экспорт
	
	Результат = Новый Структура;
	Результат.Вставить("Трудозатраты", Трудозатраты);
	Результат.Вставить("НазначитьНа", Исполнитель);
	Результат.Вставить("Комментарий", СтруктураКомментарий);
	
	Если ЗначениеЗаполнено(Причина) Тогда
		ОтправлятьУведомления = СвойстваПричин.Получить(Причина).НеобходимоОтправитьУведомление;
		
		Результат.Вставить("ПричинаИзмененияСтатуса", Причина);
		Результат.Вставить("НеобходимоОтправитьУведомление", ОтправлятьУведомления);
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

Функция ПодготовитьКомментарийСтатуса(Знач Комментарий, Знач Общедоступный, Знач Причина, Знач ПредставлениеНачальногоСтатуса, Знач ПредставлениеСтатуса) Экспорт
	
	Комментарий = ОбщегоНазначенияКлиентСервер.ЭкранироватьУгловыеСкобки(Комментарий);
	
	Если ЗначениеЗаполнено(Причина) Тогда
		КомментарийСтатуса = ?(ЗначениеЗаполнено(Комментарий), "с комментарием: " + Символы.ПС + Комментарий, "");
		ТекстКомментария = СтрШаблон("по причине <b>%1</b> %2", Строка(Причина), КомментарийСтатуса);
		КомментарийБезТегов = СтрШаблон("по причине %1 %2", Строка(Причина), КомментарийСтатуса);
	ИначеЕсли ЗначениеЗаполнено(Комментарий) Тогда
		ТекстКомментария = СтрШаблон("по причине:%1%2", Символы.ПС, Комментарий);
		КомментарийБезТегов = СтрШаблон("по причине:%1""%2""", Символы.ПС, Комментарий);
	Иначе
		ТекстКомментария = "";
		КомментарийБезТегов = "";
	КонецЕсли;
	
	РаботаСПроцессамиВызовСервера.ВыделитьСсылкиКомментария(ТекстКомментария);
	
	Если ЗначениеЗаполнено(ТекстКомментария) Тогда
		ТекстКомментария = СтрШаблон("Задача переведена из статуса <b>%1</b> в <b>%2</b> %3", 
			ПредставлениеНачальногоСтатуса, ПредставлениеСтатуса, ТекстКомментария);
			
		КомментарийБезТегов = СтрШаблон("Задача переведена из статуса %1 в %2 %3", 
			ПредставлениеНачальногоСтатуса, ПредставлениеСтатуса, КомментарийБезТегов);
	КонецЕсли;
	
	// Заменяем символы переноса строк атрибутом переноса строки в html для корректного 
	// отображения переноса в ленте комментариев
	Если НЕ ПустаяСтрока(ТекстКомментария) Тогда
		МассивСтрок = СтрРазделить(ТекстКомментария, Символы.ПС);
		ТекстКомментария = "";
		
		Для Каждого Строка Из МассивСтрок Цикл
			Если ПустаяСтрока(Строка) Тогда
				ТекстКомментария = ТекстКомментария + "<br>" + Символы.ПС;
			Иначе
				ТекстКомментария = ТекстКомментария + "<span>" + Строка + "<span><br>" + Символы.ПС;	
			КонецЕсли;	
		КонецЦикла;
	КонецЕсли;
	
	ДанныеКомментария = Новый Структура("ТекстКомментария, Общедоступный, КомментарийБезТегов, Идентификатор", 
		ТекстКомментария, Общедоступный, КомментарийБезТегов, Новый УникальныйИдентификатор);
	
	Возврат ДанныеКомментария;
	
КонецФункции
