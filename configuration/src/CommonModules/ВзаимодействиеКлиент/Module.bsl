///////////////////////////////////////////////////////////////////////////////////////////////////////
// Copyright (c) 2025, ООО ДНС Технологии
// SDMS (Software Development Management System) — это корпоративная система учета разработки и управления проектами 
// Все права защищены. Эта программа и сопроводительные материалы предоставляются 
// в соответствии с условиями лицензии General Public License (GNU GPL v3)
// Текст лицензии доступен по ссылке:
// https://www.gnu.org/licenses/gpl-3.0.html
///////////////////////////////////////////////////////////////////////////////////////////////////////

#Область ПрограммныйИнтерфейс

Процедура ПриВыбореДействияСообщения(Сообщение, Действие) Экспорт
	
	Если ВзаимодействиеКлиентСервер.ЭтоДействиеОткрытьКомментарии(Действие) Тогда
		ОткрытьКомментарииОбъекта(Сообщение);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныйПрограммныйИнтерфейс

Процедура ОткрытьКомментарииОбъекта(Сообщение) Экспорт
	
	Данные = Сообщение.Данные;
	
	ФормаКомментариев = "РегистрСведений.Комментарии.Форма.Комментарии";
	ПараметрыОткрытия = Новый Структура("Ключ, ОткрытьКомментарии", Данные.Ссылка, Истина);
	
	ШаблонИмениКнопки = ПанельНавигацииКлиентСервер.ШаблонИмениКнопки();	
	ИмяКоманды = СтрШаблон(ШаблонИмениКнопки, "Комментарии");
	
	ФормаОбъекта = НайтиФормуОбъекта(Данные.Ссылка);
	
	Если ФормаОбъекта = Неопределено Тогда
		ОткрытьФорму(Данные.ИмяФормы, ПараметрыОткрытия);
		
	ИначеЕсли ФормаОбъекта.ИскатьПодчиненныеФормы Тогда
		ПанельНавигацииКлиент.ОткрытьСтраницуОбъекта(ФормаОбъекта, ИмяКоманды, Данные);
		
	Иначе
		ДополнительныеПараметры = Новый Структура("ИдентификаторКомментария", Данные.ИдентификаторКомментария);
		
		ФормаОбъекта.Активизировать();
		ПанельНавигацииКлиент.НажатиеВнеПанели(ФормаОбъекта, ИмяКоманды, ДополнительныеПараметры);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция НайтиФормуОбъекта(Знач Ключ)
	
	Для Каждого Окно Из ПолучитьОкна() Цикл
		Если Окно.Основное ИЛИ Окно.НачальнаяСтраница Тогда
			Продолжить;
		КонецЕсли;
		
		Форма = Окно.Содержимое[0];
		
		Если Форма.Параметры.Свойство("Ключ") И Форма.Параметры.Ключ = Ключ Тогда
			Возврат Форма;
		КонецЕсли;
	КонецЦикла;
	
	Возврат Неопределено;
	
КонецФункции

#КонецОбласти
