///////////////////////////////////////////////////////////////////////////////////////////////////////
// Copyright (c) 2025, ООО ДНС Технологии
// SDMS (Software Development Management System) — это корпоративная система учета разработки и управления проектами 
// Все права защищены. Эта программа и сопроводительные материалы предоставляются 
// в соответствии с условиями лицензии General Public License (GNU GPL v3)
// Текст лицензии доступен по ссылке:
// https://www.gnu.org/licenses/gpl-3.0.html
///////////////////////////////////////////////////////////////////////////////////////////////////////

#Область ПрограммныйИнтерфейс

// Процедура - Добавить команды создания для глобального поиска при печати в поиске "+"
//
// Параметры:
//  СтрокаПоиска			 - Строка - Строка поиска
//  РезультатыПоиска		 - 	РезультатыПоиска - Результаты поиска
//  ДополнительныеПараметры	 - 	Структура -  Дополнительные параметры
//
Процедура ДобавитьКомандыСоздания(СтрокаПоиска, РезультатыПоиска, ДополнительныеПараметры) Экспорт 
	
	РезультатыПоиска.Добавить("e1cib/data/Документ.Задача", "Задачу", БиблиотекаКартинок.СоздатьЭлементСписка);
	РезультатыПоиска.Добавить("e1cib/data/Документ.ЗаявкаНаРазработку", "Заявку на разработку", 
		БиблиотекаКартинок.СоздатьЭлементСписка);
	РезультатыПоиска.Добавить("e1cib/data/Документ.ВнутреннееЗадание", "Внутреннее задание", 
		БиблиотекаКартинок.СоздатьЭлементСписка); 
	РезультатыПоиска.Добавить("e1cib/data/Документ.ВнутренниеТрудозатраты", "Внутренние трудозатраты",
		БиблиотекаКартинок.СоздатьЭлементСписка); 
	
КонецПроцедуры
	
// Процедура - проверяет является ли строка поиска навигационной ссылкой, если да, то добавляет в результат
//
// Параметры:
//  СтрокаПоиска			 - Строка - Строка поиска
//  РезультатыПоиска		 - 	РезультатыПоиска - Результаты поиска
//  ДополнительныеПараметры	 - 	Структура -  Дополнительные параметры
//
Процедура ДобавитьДанныеПоНавигационнойСсылке(СтрокаПоиска, РезультатыПоиска, ДополнительныеПараметры) Экспорт 
	
	НавигационнаяСсылка = ПолучитьНавигационнуюСсылкуИзВнешнейСсылки(СтрокаПоиска); 
	
	Если СтрНайти(НавигационнаяСсылка, "%") > 0 Тогда 
		НавигационнаяСсылка = ГлобальныйПоискВызовСервера.РаскодироватьURL(НавигационнаяСсылка);
	КонецЕсли;
	
	Массив = Новый Массив;    
	Массив.Добавить(НавигационнаяСсылка);
	МассивПредставлений = ПолучитьПредставленияНавигационныхСсылок(Массив);
	Представление = МассивПредставлений[0].Текст;
	
	Если Представление <> "" Тогда 
		РезультатыПоиска.Добавить(НавигационнаяСсылка, Представление, БиблиотекаКартинок.ПерейтиПоНавигационнойСсылке);
	Иначе
		РезультатыПоиска.Добавить(НавигационнаяСсылка, НавигационнаяСсылка, БиблиотекаКартинок.ПерейтиПоВнешнейНавигационнойСсылке);
	КонецЕсли;	
	
КонецПроцедуры 	  

// Процедура - Добавить ссылка по тегам для глобального поиска при печати в поиске "#"
//
// Параметры:
//  СтрокаПоиска			 - Строка - Строка поиска
//  РезультатыПоиска		 - 	РезультатыПоиска - Результаты поиска
//  ДополнительныеПараметры	 - 	Структура -  Дополнительные параметры
//
Процедура ДобавитьДанныеПоТегу(СтрокаПоиска, РезультатыПоиска, ДополнительныеПараметры) Экспорт 
	
	Данные = ГлобальныйПоискВызовСервера.ПолучитьОбъектыПоТэгу(СтрокаПоиска); 
	
	Для Каждого Элемент Из Данные Цикл 
		РезультатыПоиска.Добавить(Элемент.Ссылка, Элемент.Объект, БиблиотекаКартинок.Лупа);
	КонецЦикла;	
	
КонецПроцедуры 	  

// Процедура - Добавляет в результат поиск на рабочем столе
//
// Параметры:
//  СтрокаПоиска			 - Строка - Строка поиска
//  РезультатыПоиска		 - 	РезультатыПоиска - Результаты поиска
//  ДополнительныеПараметры	 - 	Структура -  Дополнительные параметры
//
Процедура ПоискНаРабочемСтоле(СтрокаПоиска, РезультатыПоиска, ДополнительныеПараметры) Экспорт 
	
	РезультатыПоиска.Добавить("###ПоискНаРабочемСтоле", "Показать больше результатов",
		БиблиотекаКартинок.РабочийСтол, , СтрокаПоиска);	
	
КонецПроцедуры 

// Процедура - Добавляет поиск на рабочем столе в План поиска
//
// Параметры:
//  ПланПоиска	 - 	План поиска - План поиска
//
Процедура ДобавитьПоискНаРабочемСтоле(ПланПоиска) Экспорт
	
	ПланПоиска.Добавить("ПоискНаРабочемСтоле", "ГлобальныйПоискКлиент", Ложь, Ложь, 999); 
	
КонецПроцедуры

// Получить навигационную ссылку из внешней ссылки
//
// Параметры:
//  СтрокаПоиска - Строка	 - строка поиска
// 
// Возвращаемое значение:
//  Строка - выделенная навигационная ссылка
//
Функция ПолучитьНавигационнуюСсылкуИзВнешнейСсылки(СтрокаПоиска) Экспорт	
	
	// У внешних ссылок формат такой <адрес хоста ИБ>#<внутренняя ссылка>?<параметры ссылки>
	МассивСтрок = СтрРазделить(СокрЛП(СтрокаПоиска), "#");
	НавигационнаяСсылка = "";
		
	Если МассивСтрок.Количество() = 2 Тогда 
		НавигационнаяСсылка = МассивСтрок[1];
		Если Прав(НавигационнаяСсылка, 1) = ")" Тогда 
			НавигационнаяСсылка = Лев(НавигационнаяСсылка, СтрДлина(НавигационнаяСсылка) - 1);
		КонецЕсли;
	ИначеЕсли СтрНайти(СтрокаПоиска, "%") > 0 Тогда 
		// навигационная ссылка, где есть процентное кодирование 
		НавигационнаяСсылка	= СтрокаПоиска;
	КонецЕсли;	

	Возврат НавигационнаяСсылка;
	
КонецФункции 

#КонецОбласти
