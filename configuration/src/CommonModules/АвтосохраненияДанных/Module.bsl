///////////////////////////////////////////////////////////////////////////////////////////////////////
// Copyright (c) 2025, ООО ДНС Технологии
// SDMS (Software Development Management System) — это корпоративная система учета разработки и управления проектами 
// Все права защищены. Эта программа и сопроводительные материалы предоставляются 
// в соответствии с условиями лицензии General Public License (GNU GPL v3)
// Текст лицензии доступен по ссылке:
// https://www.gnu.org/licenses/gpl-3.0.html
///////////////////////////////////////////////////////////////////////////////////////////////////////

#Область ПрограммныйИнтерфейс

Функция ПриСозданииНаСервере(Форма) Экспорт
	
	Если Форма.Параметры.Свойство("АвтосохранениеСсылка") Тогда
		ПараметрыЗаполнения = АвтосохраненияДанныхВызовСервера.ОписаниеИзАвтосохранения(Форма.Параметры, Форма.УникальныйИдентификатор);
		АвтосохраненияДанныхКлиентСервер.ЗаполнитьДанныеОбъектаИзАвтосохранения(Форма, ПараметрыЗаполнения);
		Форма.ЗагруженоАвтосохранение = Истина;
	КонецЕсли;
	
	Возврат ПараметрыЗаполнения;
	
КонецФункции

// Добавляет в кэш формы параметры необходимые для работы автосохранения
//
// Параметры:
//  Параметры		  - Структура - Кэш формы который создается при открытии.
//  Ссылка            - Ссылка - Ссылка на объект для которого проверяется наличие автосохранений
//  Основание         - Ссылка - Ссылка на объект-основание
//  ЭтоАвтосохранение - Булево - Если объект был загружен из автосохранения принимает значения Истина
//                      и не происходит повторная проверка автосохранений.
//
Процедура ДобавитьПараметрыДляАвтосохранения(Параметры, Знач Ссылка, Знач Основание, Знач ЭтоАвтосохранение) Экспорт
	
	Параметры.Вставить("ОбработчикАвтосохраненияЗапущен", Ложь);
	
	Если НЕ ЭтоАвтосохранение Тогда
		Автосохранения = АвтосохраненияПоОбъекту(Ссылка, Основание);
		
		Если Автосохранения <> Неопределено Тогда
			Параметры.Вставить("Автосохранения", Автосохранения);
		КонецЕсли;
	КонецЕсли;
	
	МенеджерОбъекта = ОбщегоНазначения.МенеджерОбъектаПоСсылке(Ссылка);
	Параметры.Вставить("АвтосохраняемыеРеквизиты", МенеджерОбъекта.АвтосохраняемыеРеквизиты());
		
КонецПроцедуры

// Возвращает ссылку на несуществующий объект.
//
// Параметры:
//  Объект - Ссылка - Ссылка на объект.
// 
// Возвращаемое значение:
//  Ссылка. Несуществующая ссылка такого же типа, как и переданный объект.
//
Функция ПолучитьСсылкуНовогоОбъекта(Знач Объект) Экспорт
	
	Тип = ТипЗнч(Объект);
	
	Если Тип = Тип("ДокументСсылка.Задача") Тогда
		НоваяСсылка = Документы.Задача.ПолучитьСсылку();
		
	ИначеЕсли Тип = Тип("ДокументСсылка.ЗаявкаНаРазработку") Тогда
		НоваяСсылка = Документы.ЗаявкаНаРазработку.ПолучитьСсылку();
		
	ИначеЕсли Тип = Тип("ДокументСсылка.ВнутреннееЗадание") Тогда
		НоваяСсылка = Документы.ВнутреннееЗадание.ПолучитьСсылку();
		
	ИначеЕсли Тип = Тип("СправочникСсылка.Проекты") Тогда
		НоваяСсылка = Справочники.Проекты.ПолучитьСсылку();
		
	Иначе
		НоваяСсылка = Неопределено;
	КонецЕсли;
	
	Возврат НоваяСсылка;
	
КонецФункции

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция АвтосохраненияПоОбъекту(Знач Ссылка, Знач ОбъектОснование)
	
	Результат = Неопределено;
	Запрос = Новый Запрос;
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	АвтосохраненныеОписания.Объект КАК Объект,
	|	АвтосохраненныеОписания.ДатаСоздания КАК ДатаСоздания,
	|	АвтосохраненныеОписания.Наименование КАК Наименование,
	|	АвтосохраненныеОписания.ОписаниеОбъекта КАК ОписаниеОбъекта
	|ИЗ
	|	РегистрСведений.АвтосохраненныеОписания КАК АвтосохраненныеОписания
	|		ЛЕВОЕ СОЕДИНЕНИЕ #ИмяТаблицы КАК Таблица
	|		ПО АвтосохраненныеОписания.Объект = Таблица.Ссылка
	|ГДЕ
	|	&Условие
	|	И АвтосохраненныеОписания.Пользователь = &Пользователь
	|
	|УПОРЯДОЧИТЬ ПО
	|	ДатаСоздания УБЫВ";
	
	ИмяТаблицы = Ссылка.Метаданные().ПолноеИмя();
	
	Если ЗначениеЗаполнено(Ссылка) Тогда
		Условие = "АвтосохраненныеОписания.Объект = &Ссылка";
		Запрос.УстановитьПараметр("Ссылка", Ссылка);
	Иначе
		ОбъектОснование = ?(ЗначениеЗаполнено(ОбъектОснование), ОбъектОснование, Неопределено);
		
		Условие = 
		"АвтосохраненныеОписания.Объект ССЫЛКА #ИмяТаблицы
		|И Таблица.Ссылка ЕСТЬ NULL
		|И АвтосохраненныеОписания.ОбъектОснование = &ОбъектОснование";
			
		Запрос.УстановитьПараметр("ОбъектОснование", ОбъектОснование);
	КонецЕсли;
	
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&Условие", Условие);
	
	Запрос.УстановитьПараметр("Пользователь", ПараметрыСеанса.ТекущийПользователь);
	Запрос.Текст = СтрЗаменить(ТекстЗапроса, "#ИмяТаблицы", ИмяТаблицы);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Если НЕ РезультатЗапроса.Пустой() Тогда
		Результат = Новый Массив;
		Выборка = РезультатЗапроса.Выбрать();
		
		Пока Выборка.Следующий() Цикл
			Если ЗначениеЗаполнено(Выборка.Наименование) Тогда
				ОбъектПредставление = Выборка.Наименование;
			Иначе
				ОбъектПредставление = "Новый объект";
			КонецЕсли;
			
			Строка = Новый Структура;
			Строка.Вставить("АвтосохранениеСсылка", Выборка.Объект);
			Строка.Вставить("ДатаСохранения", Выборка.ДатаСоздания);
			Строка.Вставить("АвтосохранениеТекстОписания", Выборка.ОписаниеОбъекта.Получить());
			Строка.Вставить("ОбъектПредставление", ОбъектПредставление);
			
			Результат.Добавить(Строка);
		КонецЦикла;
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

#КонецОбласти
