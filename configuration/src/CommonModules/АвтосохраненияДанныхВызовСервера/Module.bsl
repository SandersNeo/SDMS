///////////////////////////////////////////////////////////////////////////////////////////////////////
// Copyright (c) 2025, ООО ДНС Технологии
// SDMS (Software Development Management System) — это корпоративная система учета разработки и управления проектами 
// Все права защищены. Эта программа и сопроводительные материалы предоставляются 
// в соответствии с условиями лицензии General Public License (GNU GPL v3)
// Текст лицензии доступен по ссылке:
// https://www.gnu.org/licenses/gpl-3.0.html
///////////////////////////////////////////////////////////////////////////////////////////////////////

#Область ПрограммныйИнтерфейс

// Получает необходимые данные с формы и записывает данные в регистр Автосохраненные описания
//
// Параметры:
//  СохраняемыеКартинки	 - Массив - Массив добавленных изображений
//  ЗначенияРеквизитов	 - Структура - реквизиты, которые нужно сохранить
//
Процедура АвтосохранениеДанныхОбъекта(Знач ЗначенияРеквизитов) Экспорт
	
	РегистрыСведений.АвтосохраненныеОписания.Добавить(ЗначенияРеквизитов);
	
КонецПроцедуры

// Выбирает все данные из регистров с автосохранениями и собирает в структуру
//
// Параметры:
//  Параметры - Структура - текст описания и ссылка на редактируемый объект
//  ИдентификаторФормы - УникальныйИдентификатор - Идентификатор формы для сохранения изображений
//                       во временное хранилище.
// 
// Возвращаемое значение:
//  Структура - Восстанавливаемые данные
//
Функция ОписаниеИзАвтосохранения(Знач Параметры, Знач ИдентификаторФормы) Экспорт
	
	ТекущийПользователь = ПараметрыСеанса.ТекущийПользователь;
	ТекстОписания = Параметры.АвтосохранениеТекстОписания;
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	АвтосохраненныеОписания.ЗначенияРеквизитов КАК ЗначенияРеквизитов
	|ИЗ
	|	РегистрСведений.АвтосохраненныеОписания КАК АвтосохраненныеОписания
	|ГДЕ
	|	АвтосохраненныеОписания.Пользователь = &Пользователь
	|	И АвтосохраненныеОписания.Объект = &РедактируемыйОбъект";
	
	Запрос.УстановитьПараметр("Пользователь", ТекущийПользователь);
	Запрос.УстановитьПараметр("РедактируемыйОбъект", Параметры.АвтосохранениеСсылка);
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	Если Выборка.Следующий() Тогда
		ЗначенияРеквизитов = Выборка.ЗначенияРеквизитов.Получить();
		
		Если ТипЗнч(Параметры.АвтосохранениеСсылка) = Тип("ДокументСсылка.ЗаявкаНаРазработку") Тогда
			Направление = ЗначенияРеквизитов.РеквизитыОбъекта.Направление;
			
			Если ЗначениеЗаполнено(Направление)
				И РегистрыСведений.ИспользованиеЮридическихЛицНаправлений.Использует(Направление) Тогда
				
				ЮрЛицо = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Направление, "ОсновноеЮридическоеЛицо");
				ЗначенияРеквизитов.РеквизитыОбъекта.Вставить("ЮридическоеЛицо", ЮрЛицо);
			КонецЕсли;
		КонецЕсли;
	Иначе
		ЗначенияРеквизитов = Новый Структура;
		ЗначенияРеквизитов.Вставить("РеквизитыОбъекта", Новый Структура);
		ЗначенияРеквизитов.Вставить("ТабличныеЧасти", Новый Структура);
		ЗначенияРеквизитов.Вставить("РеквизитыФормы", Новый Структура);
	КонецЕсли;
	
	Результат = Новый Структура;
	Результат.Вставить("Ссылка", Параметры.АвтосохранениеСсылка);
	Результат.Вставить("ТекстОписания", ТекстОписания);
	Результат.Вставить("ЗначенияРеквизитов", ЗначенияРеквизитов);
	
	Возврат Результат;
	
КонецФункции

// Вызывает процедуру удаления автосохранения
//
// Параметры:
//  Ссылка	 - Ссылка - Ссылка на объект
//
Процедура УдалитьАвтосохраненноеОписание(Знач Ссылка) Экспорт
	
	РегистрыСведений.АвтосохраненныеОписания.УдалитьЗаписиПоОбъекту(Ссылка);
	
КонецПроцедуры

#КонецОбласти
