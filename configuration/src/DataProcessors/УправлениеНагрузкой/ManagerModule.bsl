///////////////////////////////////////////////////////////////////////////////////////////////////////
// Copyright (c) 2025, ООО ДНС Технологии
// SDMS (Software Development Management System) — это корпоративная система учета разработки и управления проектами 
// Все права защищены. Эта программа и сопроводительные материалы предоставляются 
// в соответствии с условиями лицензии General Public License (GNU GPL v3)
// Текст лицензии доступен по ссылке:
// https://www.gnu.org/licenses/gpl-3.0.html
///////////////////////////////////////////////////////////////////////////////////////////////////////

#Область ПрограммныйИнтерфейс

Процедура ПолучитьДанныеУправлениеНагрузкой(НастройкиСписка, АдресРезультата, УИДЗамера) Экспорт
	
	ВремяНачала = ОценкаПроизводительности.НачатьЗамерВремени();
	
	ДанныеДерева = СформироватьСписокУправлениеНагрузкой(НастройкиСписка);
	ИнструментыСервер.ОповеститьИнструментОбОбновлении(ДанныеДерева, АдресРезультата, УИДЗамера);
	
	ОценкаПроизводительности.ЗакончитьЗамерВремениТехнологический("Обработки.УправлениеНагрузкой.ПолучитьДанныеУправлениеНагрузкой.ВыполнениеФонового", ВремяНачала, , Строка(УИДЗамера));
	
КонецПроцедуры

// Дополнительно обрабатывает строки дерева, при универсальной обработке
//
// Параметры:
//  НоваяСтрока			 - СтрокаДереваЗначений	 - текущая строка дерева
//  ТекущаяВыборка		 - ВыборкаИзРезультатаЗапроса	 - выбока данных
//  ДополнительныеДанные - Структура					 - дополнительные данные
//  Уровень				 - Число						 - уровень иерархии
//
Процедура ДополнительнаяОбработкаСтрокиДерева(НоваяСтрока, ТекущаяВыборка, ДополнительныеДанные, Уровень) Экспорт
	
	Если Уровень = "Сотрудник" Тогда   
		
		Если НоваяСтрока.Сотрудник = Справочники.Пользователи.ПустаяСсылка() Тогда	
			НоваяСтрока.ФактическиеТрудозатраты = 0;
		Иначе				
			НоваяСтрока.ПланируемыеТрудозатраты = ТекущаяВыборка.ОбщееПлановоеВремя;
			НоваяСтрока.ФактическиеТрудозатраты = ТекущаяВыборка.ОбщиеТрудозатраты;
		КонецЕсли;
		
		ЗаполнитьПредставлениеЗадачи(НоваяСтрока, ДополнительныеДанные);	
	ИначеЕсли Уровень = "Детали" Тогда
		
		Если ЗначениеЗаполнено(ТекущаяВыборка.СрокРеализации) Тогда
			НоваяСтрока[ДополнительныеДанные.ПолеПредставлениеОбъекта] = СтрШаблон("%1 (Срок: %2)",
				ТекущаяВыборка[ДополнительныеДанные.ПолеПредставлениеОбъекта], Формат(ТекущаяВыборка.СрокРеализации, "ДФ=dd.MM.yy"));
		КонецЕсли;
		
		Если ТекущаяВыборка.ДнейВРаботе > 0 Тогда
			НоваяСтрока.ДнейВРаботе = СтрокаСЧислом("; %1 день; ; %1 дня; %1 дней; %1 дня", ТекущаяВыборка.ДнейВРаботе, 
			ВидЧисловогоЗначения.Количественное);
		Иначе
			НоваяСтрока.ДнейВРаботе = "";
		КонецЕсли;
		
		Если ТекущаяВыборка.ДнейНаРассмотрении > 0 Тогда
			НоваяСтрока.ДнейНаРассмотрении = СтрокаСЧислом("; %1 день; ; %1 дня; %1 дней; %1 дня", 
				ТекущаяВыборка.ДнейНаРассмотрении, ВидЧисловогоЗначения.Количественное);
		Иначе
			НоваяСтрока.ДнейНаРассмотрении = "";
		КонецЕсли;
		
		// TODO: после удаления рабочих столов дополнительное свойство "ВывестиДниНаСотруднике" будет не нужно
		Если ДополнительныеДанные.Свойство("ВывестиДниНаСотруднике") Тогда
			Если ТекущаяВыборка.ДнейНаСотруднике > 0 Тогда
				НоваяСтрока.ДнейНаСотруднике = СтрокаСЧислом("; %1 день; ; %1 дня; %1 дней; %1 дня", 
					ТекущаяВыборка.ДнейНаСотруднике, ВидЧисловогоЗначения.Количественное);
			Иначе
				НоваяСтрока.ДнейНаСотруднике = "";
			КонецЕсли;
		КонецЕсли;
		
		Если ЗначениеЗаполнено(ТекущаяВыборка.НазначениеИмяКартинки) Тогда
			НоваяСтрока.Назначение = БиблиотекаКартинок[ТекущаяВыборка.НазначениеИмяКартинки];	
		КонецЕсли;  
		
		Если ТекущаяВыборка.ДатаНачалаСпринт > ТекущаяДатаСеанса() Тогда 
			НоваяСтрока.Спринт = БиблиотекаКартинок["СпринтБудущий"];
		ИначеЕсли ЗначениеЗаполнено(ТекущаяВыборка.ДатаНачалаСпринт) Тогда
			НоваяСтрока.Спринт = БиблиотекаКартинок[ТекущаяВыборка.СпринтИмяКартинки];
		КонецЕсли;
		
		Если ТекущаяВыборка.ДатаНачалаРазработки <> Дата(1, 1, 1) Тогда
			Если ТекущаяВыборка.ПланируемыеТрудозатраты > 0 Тогда
				ПрогнозДатыЗавершения = ПолучитьПредположительнуюДатуЗавершения(
					ТекущаяВыборка.ПланируемыеТрудозатраты,	ТекущаяВыборка.ДатаНачалаРазработки, 
					ДополнительныеДанные.ТаблицаПроизводственныйКалендарь);					
				НоваяСтрока.НеСовпадаетСПрогнозом = ((ПрогнозДатыЗавершения -
					НачалоДня(ТекущаяВыборка.ДатаПоследнегоТестирования)) < 0);
				
				Если НоваяСтрока.НеСовпадаетСПрогнозом Тогда
					НоваяСтрока.Просрочен = БиблиотекаКартинок.КрайнийСрок;
				КонецЕсли;
			КонецЕсли;
			
			Если ТекущаяВыборка.ПланируемыеТрудозатраты <> 0 Тогда
				КоэффициентПланаКФакту = (ТекущаяВыборка.ФактическиеТрудозатраты / ТекущаяВыборка.ПланируемыеТрудозатраты) * 100;
				НоваяСтрока.ПроцентФактаКПлану = Окр(КоэффициентПланаКФакту) - 100;
			ИначеЕсли ТекущаяВыборка.ФактическиеТрудозатраты > 0 Тогда
				НоваяСтрока.ПроцентФактаКПлану = 25;
			КонецЕсли;	
		КонецЕсли;
		
		МассивГруппЗаказчиков = Новый Массив; 
		Найденное = ДополнительныеДанные.ГруппыЗаказчиков.НайтиСтроки(Новый Структура("Ссылка", НоваяСтрока.Задача));
		
		Для Каждого СтрокаНадйенное Из Найденное Цикл 
			Если СтрокаНадйенное.ГруппаЗаказчиков <> ДополнительныеДанные.СсылкаПрочиеЗаказчики Тогда
				МассивГруппЗаказчиков.Добавить(СтрокаНадйенное.ГруппаЗаказчиков);
			КонецЕсли;
		КонецЦикла;
		
		СтрокаГруппаЗаказчиков = СтрСоединить(МассивГруппЗаказчиков, ", ");
		
		НоваяСтрока.ГруппаЗаказчиков = СтрокаГруппаЗаказчиков;			
		НоваяСтрока.ОбъектОснование = ТекущаяВыборка.ОбъектОснование; 
		
		Если ДополнительныеДанные.Свойство("ПересчетРодителя") И 
				ДополнительныеДанные.ПересчетРодителя И НоваяСтрока.Родитель <> Неопределено Тогда      
				
			ПересчетДанныхРодителя(НоваяСтрока.Родитель, НоваяСтрока, 1, ДополнительныеДанные); 
			ДополнительныеДанные.Удалить("ПересчетРодителя");
		КонецЕсли; 
	КонецЕсли;    
	
КонецПроцедуры

// Процедура - Пересчет данных родителя управление нагрузкой
//
// Параметры:
//  Родитель			 - СтрокаДереваЗначений	 - текущий родитель
//  Строка				 - СтрокаДереваЗначений	 - текущая строка
//  Множитель			 - Число				 - множитель, добавить или убавить значениее
//  ДополнительныеДанные - Структура			 - дополнительные данные
//
Процедура ПересчетДанныхРодителя(Родитель, Строка, Множитель, ДополнительныеДанные) Экспорт
	
	// При добавлении/удалении строки в родителя, нужно пересчитать данные и переформировать представление.
	// При этом для пустого сотрудника и заполненного они разные
	Родитель.КоличествоЗадач = Родитель.КоличествоЗадач + Множитель * Строка.КоличествоЗадач;	
	Родитель.КоличествоЗаявок = Родитель.КоличествоЗаявок + Множитель * Строка.КоличествоЗаявок;	
	Родитель.КоличествоВнутреннихЗаданий = Родитель.КоличествоВнутреннихЗаданий + 
		Множитель * Строка.КоличествоВнутреннихЗаданий; 
	Родитель.ОбщийПланСотрудника = Родитель.ОбщийПланСотрудника + Множитель * Строка.ОбщийПланСотрудника;	
	
	Если Родитель.Сотрудник = Справочники.Пользователи.ПустаяСсылка() Тогда	
		Родитель.ФактическиеТрудозатраты = Родитель.ФактическиеТрудозатраты + Множитель * Строка.ФактическиеТрудозатраты;
		Родитель.ПланируемыеТрудозатраты = Родитель.ПланируемыеТрудозатраты + Множитель * Строка.ПланируемыеТрудозатраты;
	КонецЕсли;
	
	ЗаполнитьПредставлениеЗадачи(Родитель, ДополнительныеДанные);	
	
КонецПроцедуры

Функция ПолучитьПредположительнуюДатуЗавершения(Знач КоличествоЧасов, Знач ДатаНачала, Знач ТаблицаПроизводственныйКалендарь) Экспорт
	
	ТекущаяДата_НачалоДня = НачалоДня(ДатаНачала);
		
	Пока КоличествоЧасов > 0 Цикл
		СтрокаТаблицы = ТаблицаПроизводственныйКалендарь.Найти(ТекущаяДата_НачалоДня, "ДатаКалендаря");
		Если СтрокаТаблицы = Неопределено Тогда
			Прервать;
		ИначеЕсли СтрокаТаблицы.ВидДня = Перечисления.ВидыДнейПроизводственногоКалендаря.Рабочий Тогда
			КоличествоЧасов = КоличествоЧасов - 8;
		ИначеЕсли СтрокаТаблицы.ВидДня = Перечисления.ВидыДнейПроизводственногоКалендаря.Предпраздничный Тогда
			КоличествоЧасов = КоличествоЧасов - 7;
		КонецЕсли;
		ТекущаяДата_НачалоДня = ТекущаяДата_НачалоДня + 86400;
	КонецЦикла;
	
	Возврат ТекущаяДата_НачалоДня;
		
КонецФункции

#КонецОбласти

#Область СлужебныеПроцедурыИФункции 

Процедура ЗаполнитьПредставлениеЗадачи(НоваяСтрока, ДополнительныеДанные)
	
	Представление = Строка(НоваяСтрока.ПредставлениеСотрудник);
	ИндексКартинки = ДополнительныеДанные.ИндексКартинки_Группа; 
	
	КоличествоЗадач = СтрокаСЧислом("; %1 задача; ; %1 задачи; %1 задач; %1 задачи", 
		НоваяСтрока.КоличествоЗадач, ВидЧисловогоЗначения.Количественное, "ЧН=0");
	
	КоличествоЗаявок = СтрокаСЧислом("; %1 заявка; ; %1 заявки; %1 заявок; %1 заявки", 
		НоваяСтрока.КоличествоЗаявок, ВидЧисловогоЗначения.Количественное, "ЧН=0");
		
	СтрокаСклонений = "; %1 внутреннее задание; ; %1 внутренних задания; %1 внутренних заданий; %1 внутреннего задания";
	КоличествоВнутреннихЗаданий = СтрокаСЧислом(СтрокаСклонений, 
		НоваяСтрока.КоличествоВнутреннихЗаданий, ВидЧисловогоЗначения.Количественное, "ЧН=0");
		
	Если НоваяСтрока.Сотрудник = Справочники.Пользователи.ПустаяСсылка() Тогда	
		ПредставлениеЗадача = СтрШаблон("%1    |    %2", НоваяСтрока.ПредставлениеСотрудник, КоличествоЗадач);
		НоваяСтрока.ФактическиеТрудозатраты = 0;   
		
	Иначе
		КоличествоЧасов = СтрокаСЧислом("; %1 час; ; %1 часа; %1 часов; %1 часа", 
			НоваяСтрока.ОбщийПланСотрудника, ВидЧисловогоЗначения.Количественное, "ЧН=0");
		
		ДанныеКоличествоОбъектов = Новый Массив;
		
		Если ДополнительныеДанные.НастройкиСписка.ПоказатьЗаявки И НоваяСтрока.КоличествоЗаявок > 0 Тогда
			ДанныеКоличествоОбъектов.Добавить(КоличествоЗаявок);
		КонецЕсли; 
		
		Если НоваяСтрока.КоличествоЗадач > 0 Тогда
			ДанныеКоличествоОбъектов.Добавить(КоличествоЗадач);
		КонецЕсли; 
	
		Если ДополнительныеДанные.НастройкиСписка.ПоказатьВнутренниеЗадания И НоваяСтрока.КоличествоВнутреннихЗаданий > 0 Тогда
			ДанныеКоличествоОбъектов.Добавить(КоличествоВнутреннихЗаданий);
		КонецЕсли; 
		
		КоличествоОбъектов = СтрСоединить(ДанныеКоличествоОбъектов, ", ");
					
		ПредставлениеЗадача = СтрШаблон("%1    |    %2    |    %3    |    %4", 
			НоваяСтрока.ПредставлениеСотрудник, НоваяСтрока.Должность, КоличествоОбъектов, КоличествоЧасов);
	КонецЕсли; 
	
	НоваяСтрока[ДополнительныеДанные.ПолеПредставлениеОбъекта] = ПредставлениеЗадача;
		
КонецПроцедуры

Функция СформироватьСписокУправлениеНагрузкой(Знач НастройкиСписка) 
	
	КолонкиИнструмента = Новый Массив;
	КолонкиИнструмента.Добавить("ПредставлениеЗадача");
	КолонкиИнструмента.Добавить("Задача");
	КолонкиИнструмента.Добавить("ЦиклыCR");
	КолонкиИнструмента.Добавить("ЦиклыТестирования");
	КолонкиИнструмента.Добавить("ПланируемыеТрудозатраты");
	КолонкиИнструмента.Добавить("ФактическиеТрудозатраты");
	КолонкиИнструмента.Добавить("ДнейВРаботе");
	КолонкиИнструмента.Добавить("ПорядокЗаявки");
	КолонкиИнструмента.Добавить("ИндексКартинки");
	КолонкиИнструмента.Добавить("ПроцентФактаКПлану");
	КолонкиИнструмента.Добавить("UID");
	КолонкиИнструмента.Добавить("ЭтоГруппа");
	КолонкиИнструмента.Добавить("НеСовпадаетСПрогнозом");
	КолонкиИнструмента.Добавить("Просрочен");
	КолонкиИнструмента.Добавить("ДнейНаРассмотрении");
	КолонкиИнструмента.Добавить("Назначение");
	КолонкиИнструмента.Добавить("СложностьЗадачи");
	КолонкиИнструмента.Добавить("СрокРеализации");
	КолонкиИнструмента.Добавить("ГруппаЗаказчиков");
	КолонкиИнструмента.Добавить("ПредставлениеСистем");
	КолонкиИнструмента.Добавить("ОбъектОснование");
	КолонкиИнструмента.Добавить("Продукт");
	КолонкиИнструмента.Добавить("ПриоритетСтроки");
	КолонкиИнструмента.Добавить("ПорядокЗаявкиСортировка");
	КолонкиИнструмента.Добавить("ПорядокСтатуса");
	КолонкиИнструмента.Добавить("ДатаНачалаРазработки");
	КолонкиИнструмента.Добавить("ПредставлениеСотрудник");
	КолонкиИнструмента.Добавить("КоличествоЗадач");
	КолонкиИнструмента.Добавить("КоличествоЗаявок");
	КолонкиИнструмента.Добавить("КоличествоВнутреннихЗаданий");
	КолонкиИнструмента.Добавить("ОбщийПланСотрудника");
	КолонкиИнструмента.Добавить("Должность");
	КолонкиИнструмента.Добавить("Спринт");
	КолонкиИнструмента.Добавить("ЦелеваяЗаявка");
	КолонкиИнструмента.Добавить("ОценкаStoryPoint");
	КолонкиИнструмента.Добавить("ДнейНаСотруднике");
	КолонкиИнструмента.Добавить("СтатусПредставление");
	КолонкиИнструмента.Добавить("Сотрудник");
	
	Дерево = Новый ДеревоЗначений;
	Для Каждого Колонка Из КолонкиИнструмента Цикл
		Дерево.Колонки.Добавить(Колонка);
	КонецЦикла;
	
	ИндексКартинки_Группа = Справочники.СтатусыОбъектов.ИндексКартинкиГруппа();
	
	ИсключаемыеСтатусы = Новый СписокЗначений;
	ИсключаемыеСтатусы.Добавить(Справочники.СтатусыОбъектов.Отклонен);
	ИсключаемыеСтатусы.Добавить(Справочники.СтатусыОбъектов.Решен);
		
	ПостроительЗапроса = Новый ПостроительЗапроса;
 	ПостроительЗапроса.Текст =	
	#Область ТекстЗапроса
	"ВЫБРАТЬ
	|	Филиалы.Ссылка КАК Филиал
	|ПОМЕСТИТЬ ВсеФилиалы
	|ИЗ
	|	Справочник.Филиалы КАК Филиалы
	|ГДЕ
	|	Филиалы.Ссылка В(&ВыбранныеФилиалы)
	|	И Филиалы.Родитель = ЗНАЧЕНИЕ(Справочник.Филиалы.ПустаяСсылка)
	|
	|ОБЪЕДИНИТЬ
	|
	|ВЫБРАТЬ
	|	Филиалы.Родитель
	|ИЗ
	|	Справочник.Филиалы КАК Филиалы
	|ГДЕ
	|	Филиалы.Ссылка В(&ВыбранныеФилиалы)
	|	И Филиалы.Родитель <> ЗНАЧЕНИЕ(Справочник.Филиалы.ПустаяСсылка)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ЛичныеДелаФилиал.Сотрудник КАК Пользователь,
	|	ЛичныеДелаФилиал.Данные КАК Филиал
	|ПОМЕСТИТЬ ЛичныеДелаФилиал
	|ИЗ
	|	РегистрСведений.ЛичныеДела.СрезПоследних(&ТекущаяДата, Событие = ЗНАЧЕНИЕ(Перечисление.СобытияПоЛичнымДелам.ПереведенВДругоеПодразделение)) КАК ЛичныеДелаФилиал
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ЛичныеДелаФилиал.Филиал КАК ГоловнойФилиал,
	|	Команды.Ссылка КАК Команда,
	|	КомандыПользователей.Сотрудник КАК Пользователь
	|ПОМЕСТИТЬ ПользователиКоманд
	|ИЗ
	|	РегистрСведений.ЛичныеДела.СрезПоследних(&ТекущаяДата, Событие = ЗНАЧЕНИЕ(Перечисление.СобытияПоЛичнымДелам.ПереведенВКоманду)) КАК КомандыПользователей
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ЛичныеДелаФилиал КАК ЛичныеДелаФилиал
	|		ПО КомандыПользователей.Сотрудник = ЛичныеДелаФилиал.Пользователь
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.Филиалы КАК Команды
	|		ПО КомандыПользователей.Данные = Команды.Ссылка
	|			И (Команды.Родитель = ЛичныеДелаФилиал.Филиал)
	|ГДЕ
	|	КомандыПользователей.Данные В(&ВыбранныеФилиалы)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ПроизводственныйКалендарь.ДатаКалендаря КАК Период
	|ПОМЕСТИТЬ НаборДатЗаПериод
	|ИЗ
	|	РегистрСведений.ПроизводственныйКалендарь КАК ПроизводственныйКалендарь
	|ГДЕ
	|	ПроизводственныйКалендарь.ДатаКалендаря МЕЖДУ &НачалоМесяца И &КонецМесяца
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ЛичныеДелаФилиал.Сотрудник КАК Пользователь
	|ПОМЕСТИТЬ ОтобранныеПользователи
	|ИЗ
	|	РегистрСведений.ЛичныеДела.СрезПоследних(&ТекущаяДата, Событие = ЗНАЧЕНИЕ(Перечисление.СобытияПоЛичнымДелам.ПереведенВДругоеПодразделение)) КАК ЛичныеДелаФилиал
	|		ЛЕВОЕ СОЕДИНЕНИЕ ПользователиКоманд КАК ПользователиКоманд
	|		ПО ЛичныеДелаФилиал.Данные = ПользователиКоманд.ГоловнойФилиал
	|			И ЛичныеДелаФилиал.Сотрудник = ПользователиКоманд.Пользователь
	|ГДЕ
	|	ЛичныеДелаФилиал.Данные В(&ВыбранныеФилиалы)
	|	И ПользователиКоманд.Пользователь ЕСТЬ NULL
	|
	|ОБЪЕДИНИТЬ
	|
	|ВЫБРАТЬ
	|	ЛичныеДелаКоманда.Сотрудник
	|ИЗ
	|	РегистрСведений.ЛичныеДела.СрезПоследних(&ТекущаяДата, Событие = ЗНАЧЕНИЕ(Перечисление.СобытияПоЛичнымДелам.ПереведенВКоманду)) КАК ЛичныеДелаКоманда
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ЛичныеДелаФилиал КАК ЛичныеДелаФилиал
	|		ПО ЛичныеДелаКоманда.Сотрудник = ЛичныеДелаФилиал.Пользователь
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.Филиалы КАК Филиалы
	|		ПО ЛичныеДелаКоманда.Данные = Филиалы.Ссылка
	|			И (Филиалы.Родитель = ЛичныеДелаФилиал.Филиал)
	|ГДЕ
	|	ЛичныеДелаКоманда.Данные В(&ВыбранныеФилиалы)
	|
	|ОБЪЕДИНИТЬ
	|
	|ВЫБРАТЬ
	|	ЛичныеДелаКоманда.Сотрудник
	|ИЗ
	|	РегистрСведений.ЛичныеДела.СрезПоследних(&ТекущаяДата, Событие = ЗНАЧЕНИЕ(Перечисление.СобытияПоЛичнымДелам.ПереведенВКастомнуюКоманду)) КАК ЛичныеДелаКоманда
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ЛичныеДелаФилиал КАК ЛичныеДелаФилиал
	|		ПО ЛичныеДелаКоманда.Сотрудник = ЛичныеДелаФилиал.Пользователь
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.Филиалы КАК Филиалы
	|		ПО ЛичныеДелаКоманда.Данные = Филиалы.Ссылка
	|ГДЕ
	|	ЛичныеДелаКоманда.Данные В(&ВыбранныеФилиалы)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	НАЧАЛОПЕРИОДА(НаборДатЗаПериод.Период, ДЕНЬ) КАК Период,
	|	ОтобранныеПользователи.Пользователь КАК Пользователь
	|ПОМЕСТИТЬ ПользователиДляОтчетаПериод
	|ИЗ
	|	НаборДатЗаПериод КАК НаборДатЗаПериод,
	|	ОтобранныеПользователи КАК ОтобранныеПользователи
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Период,
	|	ОтобранныеПользователи.Пользователь
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ОтсутствияСотрудниковНаРабочемМесте.Сотрудник КАК Сотрудник,
	|	ОтсутствияСотрудниковНаРабочемМесте.ДатаОтсутствия КАК ДатаОтсутствия
	|ПОМЕСТИТЬ ДатыОтсутствияСотрудников
	|ИЗ
	|	РегистрСведений.ОтсутствияСотрудниковНаРабочемМесте КАК ОтсутствияСотрудниковНаРабочемМесте
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ОтобранныеПользователи КАК ОтобранныеПользователи
	|		ПО ОтсутствияСотрудниковНаРабочемМесте.Сотрудник = ОтобранныеПользователи.Пользователь
	|ГДЕ
	|	ОтсутствияСотрудниковНаРабочемМесте.ДатаОтсутствия МЕЖДУ &НачалоМесяца И &КонецМесяца
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТабельУчетаРабочегоВремени.Дата КАК Дата,
	|	ТабельУчетаРабочегоВремени.Сотрудник КАК Сотрудник,
	|	МАКСИМУМ(ТабельУчетаРабочегоВремени.ПриоритетДляТабеля) КАК ПриоритетДляТабеля,
	|	ПользователиДляОтчетаПериод.Период КАК Период,
	|	ПользователиДляОтчетаПериод.Пользователь КАК Пользователь
	|ПОМЕСТИТЬ АктуальныеЗаписиТабеля
	|ИЗ
	|	РегистрСведений.ТабельУчетаРабочегоВремени КАК ТабельУчетаРабочегоВремени
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ПользователиДляОтчетаПериод КАК ПользователиДляОтчетаПериод
	|		ПО ТабельУчетаРабочегоВремени.Дата = ПользователиДляОтчетаПериод.Период
	|			И ТабельУчетаРабочегоВремени.Сотрудник = ПользователиДляОтчетаПериод.Пользователь
	|
	|СГРУППИРОВАТЬ ПО
	|	ТабельУчетаРабочегоВремени.Дата,
	|	ТабельУчетаРабочегоВремени.Сотрудник,
	|	ПользователиДляОтчетаПериод.Период,
	|	ПользователиДляОтчетаПериод.Пользователь
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТабельУчетаРабочегоВремени.Дата КАК Дата,
	|	ТабельУчетаРабочегоВремени.Сотрудник КАК Сотрудник,
	|	ТабельУчетаРабочегоВремени.ВидВремени КАК ВидВремени
	|ПОМЕСТИТЬ ПриведенныйТабельУчетаРабочегоВремени
	|ИЗ
	|	РегистрСведений.ТабельУчетаРабочегоВремени КАК ТабельУчетаРабочегоВремени
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ АктуальныеЗаписиТабеля КАК АктуальныеЗаписиТабеля
	|		ПО ТабельУчетаРабочегоВремени.Дата = АктуальныеЗаписиТабеля.Дата
	|			И ТабельУчетаРабочегоВремени.Сотрудник = АктуальныеЗаписиТабеля.Сотрудник
	|			И ТабельУчетаРабочегоВремени.ПриоритетДляТабеля = АктуальныеЗаписиТабеля.ПриоритетДляТабеля
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	НАЧАЛОПЕРИОДА(ПользователиДляОтчетаПериод.Период, ДЕНЬ) КАК Период,
	|	ПользователиДляОтчетаПериод.Пользователь КАК Пользователь,
	|	ВЫБОР
	|		КОГДА НЕ ТабельУчетаРабочегоВремени.Дата ЕСТЬ NULL
	|			ТОГДА ВЫБОР
	|					КОГДА ЕСТЬNULL(КлассификаторИспользованияРабочегоВремени.РабочееВремя, ИСТИНА)
	|						ТОГДА ПроизводственныйКалендарь.КоличествоРабочихЧасов
	|					ИНАЧЕ 0
	|				КОНЕЦ
	|		КОГДА ДатыОтсутствияСотрудников.Сотрудник ЕСТЬ NULL
	|			ТОГДА ПроизводственныйКалендарь.КоличествоРабочихЧасов
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК ПланЗаПериод
	|ПОМЕСТИТЬ ПланируемоеВремяРазработкиБезГруппировки
	|ИЗ
	|	ПользователиДляОтчетаПериод КАК ПользователиДляОтчетаПериод
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ПроизводственныйКалендарь КАК ПроизводственныйКалендарь
	|		ПО (ПроизводственныйКалендарь.ДатаКалендаря = ПользователиДляОтчетаПериод.Период)
	|		ЛЕВОЕ СОЕДИНЕНИЕ ПриведенныйТабельУчетаРабочегоВремени КАК ТабельУчетаРабочегоВремени
	|		ПО (ТабельУчетаРабочегоВремени.Дата = ПользователиДляОтчетаПериод.Период)
	|			И (ТабельУчетаРабочегоВремени.Сотрудник = ПользователиДляОтчетаПериод.Пользователь)
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.КлассификаторИспользованияРабочегоВремени КАК КлассификаторИспользованияРабочегоВремени
	|		ПО (ТабельУчетаРабочегоВремени.ВидВремени = КлассификаторИспользованияРабочегоВремени.Ссылка)
	|		ЛЕВОЕ СОЕДИНЕНИЕ ДатыОтсутствияСотрудников КАК ДатыОтсутствияСотрудников
	|		ПО ПользователиДляОтчетаПериод.Пользователь = ДатыОтсутствияСотрудников.Сотрудник
	|			И ПользователиДляОтчетаПериод.Период = ДатыОтсутствияСотрудников.ДатаОтсутствия
	|		ЛЕВОЕ СОЕДИНЕНИЕ Константа.КоэффициентМинимальногоВремениТрудозатрат КАК КоэффициентМинимальногоВремениТрудозатрат
	|		ПО (ИСТИНА)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ПланируемоеВремяРазработкиБезГруппировки.Пользователь КАК Сотрудник,
	|	СУММА(ПланируемоеВремяРазработкиБезГруппировки.ПланЗаПериод) КАК ПлановоеВремя
	|ПОМЕСТИТЬ ПланируемоеВремяСотрудника
	|ИЗ
	|	ПланируемоеВремяРазработкиБезГруппировки КАК ПланируемоеВремяРазработкиБезГруппировки
	|
	|СГРУППИРОВАТЬ ПО
	|	ПланируемоеВремяРазработкиБезГруппировки.Пользователь
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ЗНАЧЕНИЕ(Справочник.Пользователи.ПустаяСсылка) КАК Ссылка,
	|	ЗНАЧЕНИЕ(Справочник.Должности.ПустаяСсылка) КАК Должность,
	|	""Не назначена"" КАК ФИО,
	|	ЛОЖЬ КАК Уволен,
	|	1 КАК ПриоритетСтроки
	|ПОМЕСТИТЬ ПользователиДляВывода
	|{ГДЕ
	|	(1) КАК ПриоритетСтроки}
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ОтобранныеПользователи.Пользователь,
	|	Пользователи.Должность,
	|	ВЫБОР
	|		КОГДА Пользователи.Недействителен
	|			ТОГДА ""(УВОЛЕН) "" + Пользователи.ФИО
	|		ИНАЧЕ Пользователи.ФИО
	|	КОНЕЦ,
	|	Пользователи.Недействителен,
	|	ВЫБОР
	|		КОГДА Пользователи.Недействителен
	|			ТОГДА 3
	|		ИНАЧЕ 2
	|	КОНЕЦ
	|ИЗ
	|	ОтобранныеПользователи КАК ОтобранныеПользователи
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.Пользователи КАК Пользователи
	|		ПО ОтобранныеПользователи.Пользователь = Пользователи.Ссылка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ОтобранныеПользователи.Пользователь КАК Ссылка,
	|	СУММА(ЕСТЬNULL(Трудозатраты.Затрата, 0)) КАК ОбщиеТрудозатраты
	|ПОМЕСТИТЬ ОбщиеТрудозатратыПоСотрудникам
	|ИЗ
	|	ОтобранныеПользователи КАК ОтобранныеПользователи
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.Трудозатраты КАК Трудозатраты
	|		ПО ОтобранныеПользователи.Пользователь = Трудозатраты.Пользователь
	|			И (Трудозатраты.Период МЕЖДУ &НачалоМесяца И &КонецМесяца)
	|
	|СГРУППИРОВАТЬ ПО
	|	ОтобранныеПользователи.Пользователь
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	СтатусыЗаявокПоСистемам.Заявка КАК Заявка,
	|	СтатусыЗаявокПоСистемам.Система КАК Система,
	|	СтатусыЗаявокПоСистемам.Статус КАК Статус,
	|	МАКСИМУМ(ИсторияСтатусовЗаявокПоСистемам.Период) КАК Период
	|ПОМЕСТИТЬ ПериодПоследнегоСтатусаВозвращенныхЗаявок
	|ИЗ
	|	РегистрСведений.СтатусыЗаявокПоСистемам КАК СтатусыЗаявокПоСистемам
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.ИсторияСтатусовЗаявокПоСистемам КАК ИсторияСтатусовЗаявокПоСистемам
	|		ПО СтатусыЗаявокПоСистемам.Заявка = ИсторияСтатусовЗаявокПоСистемам.Заявка
	|			И СтатусыЗаявокПоСистемам.Система = ИсторияСтатусовЗаявокПоСистемам.Система
	|			И СтатусыЗаявокПоСистемам.Статус = ИсторияСтатусовЗаявокПоСистемам.Статус
	|ГДЕ
	|	&Задача = НЕОПРЕДЕЛЕНО
	|	И СтатусыЗаявокПоСистемам.Статус В (ЗНАЧЕНИЕ(Справочник.СтатусыОбъектов.НаДоработку))
	|
	|СГРУППИРОВАТЬ ПО
	|	СтатусыЗаявокПоСистемам.Заявка,
	|	СтатусыЗаявокПоСистемам.Система,
	|	СтатусыЗаявокПоСистемам.Статус
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ПериодПоследнегоСтатусаВозвращенныхЗаявок.Заявка КАК Заявка,
	|	ПериодПоследнегоСтатусаВозвращенныхЗаявок.Система КАК Система,
	|	ПериодПоследнегоСтатусаВозвращенныхЗаявок.Статус КАК Статус,
	|	ИсторияНазначенныхЗаявокПоСистемам.Филиал КАК Филиал,
	|	МАКСИМУМ(ИсторияНазначенныхЗаявокПоСистемам.Период) КАК Период
	|ПОМЕСТИТЬ ПоследниеЗаписиНазначенныхЗаявокПоСистемам
	|ИЗ
	|	ПериодПоследнегоСтатусаВозвращенныхЗаявок КАК ПериодПоследнегоСтатусаВозвращенныхЗаявок
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.ИсторияНазначенныхЗаявокПоСистемам КАК ИсторияНазначенныхЗаявокПоСистемам
	|		ПО ПериодПоследнегоСтатусаВозвращенныхЗаявок.Заявка = ИсторияНазначенныхЗаявокПоСистемам.Заявка
	|			И ПериодПоследнегоСтатусаВозвращенныхЗаявок.Система = ИсторияНазначенныхЗаявокПоСистемам.Система
	|			И ПериодПоследнегоСтатусаВозвращенныхЗаявок.Период > ИсторияНазначенныхЗаявокПоСистемам.Период
	|
	|СГРУППИРОВАТЬ ПО
	|	ПериодПоследнегоСтатусаВозвращенныхЗаявок.Заявка,
	|	ПериодПоследнегоСтатусаВозвращенныхЗаявок.Система,
	|	ПериодПоследнегоСтатусаВозвращенныхЗаявок.Статус,
	|	ИсторияНазначенныхЗаявокПоСистемам.Филиал
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ РАЗЛИЧНЫЕ
	|	СистемыЗаявокНазначеныеПользователю.Заявка КАК Ссылка,
	|	СистемыЗаявокНазначеныеПользователю.ТекущийИсполнитель КАК Пользователь,
	|	СистемыЗаявокНазначеныеПользователю.Система КАК Система,
	|	СтатусыЗаявокПоСистемам.Статус КАК Статус
	|ПОМЕСТИТЬ НазначенныеЗаявкиБезОчередей
	|ИЗ
	|	РегистрСведений.СистемыЗаявокНазначеныеПользователю КАК СистемыЗаявокНазначеныеПользователю
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.СтатусыЗаявокПоСистемам КАК СтатусыЗаявокПоСистемам
	|		ПО СистемыЗаявокНазначеныеПользователю.Заявка = СтатусыЗаявокПоСистемам.Заявка
	|			И СистемыЗаявокНазначеныеПользователю.Система = СтатусыЗаявокПоСистемам.Система
	|			И (СтатусыЗаявокПоСистемам.Статус В (ЗНАЧЕНИЕ(Справочник.СтатусыОбъектов.Рассмотрение), ЗНАЧЕНИЕ(Справочник.СтатусыОбъектов.РевьюТребований), ЗНАЧЕНИЕ(Справочник.СтатусыОбъектов.Рассмотрен), ЗНАЧЕНИЕ(Справочник.СтатусыОбъектов.Проработка)))
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ОтобранныеПользователи КАК ОтобранныеПользователи
	|		ПО (ОтобранныеПользователи.Пользователь = СистемыЗаявокНазначеныеПользователю.ТекущийИсполнитель)
	|ГДЕ
	|	&Задача = НЕОПРЕДЕЛЕНО
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ПоследниеЗаписиНазначенныхЗаявокПоСистемам.Заявка,
	|	ИсторияНазначенныхЗаявокПоСистемам.ТекущийИсполнитель,
	|	ПоследниеЗаписиНазначенныхЗаявокПоСистемам.Система,
	|	ПоследниеЗаписиНазначенныхЗаявокПоСистемам.Статус
	|ИЗ
	|	ПоследниеЗаписиНазначенныхЗаявокПоСистемам КАК ПоследниеЗаписиНазначенныхЗаявокПоСистемам
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.ИсторияНазначенныхЗаявокПоСистемам КАК ИсторияНазначенныхЗаявокПоСистемам
	|		ПО ПоследниеЗаписиНазначенныхЗаявокПоСистемам.Период = ИсторияНазначенныхЗаявокПоСистемам.Период
	|			И ПоследниеЗаписиНазначенныхЗаявокПоСистемам.Заявка = ИсторияНазначенныхЗаявокПоСистемам.Заявка
	|			И ПоследниеЗаписиНазначенныхЗаявокПоСистемам.Система = ИсторияНазначенныхЗаявокПоСистемам.Система
	|			И ПоследниеЗаписиНазначенныхЗаявокПоСистемам.Филиал = ИсторияНазначенныхЗаявокПоСистемам.Филиал
	|			И (ИсторияНазначенныхЗаявокПоСистемам.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацииНазначенияПоСистеме.Добавление))
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ОтобранныеПользователи КАК ОтобранныеПользователи
	|		ПО (ОтобранныеПользователи.Пользователь = ИсторияНазначенныхЗаявокПоСистемам.ТекущийИсполнитель)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	НазначенныеЗаявкиБезОчередей.Ссылка КАК Ссылка,
	|	НазначенныеЗаявкиБезОчередей.Пользователь КАК Пользователь,
	|	НазначенныеЗаявкиБезОчередей.Статус КАК Статус,
	|	ЗаявкаНаРазработку.Продукт КАК Продукт,
	|	ВЫБОР
	|		КОГДА НЕ ОчередьЗаявокПродуктСистема.Порядок ЕСТЬ NULL
	|			ТОГДА ЕСТЬNULL(ОчередьЗаявокПродуктСистема.Порядок, 0)
	|		КОГДА НЕ ОчередьЗаявокПродукт.Порядок ЕСТЬ NULL
	|			ТОГДА ЕСТЬNULL(ОчередьЗаявокПродукт.Порядок, 0)
	|		ИНАЧЕ ЕСТЬNULL(ОчередьЗаявокСистема.Порядок, 0)
	|	КОНЕЦ КАК ПорядокЗаявки
	|ПОМЕСТИТЬ НазначенныеЗаявки
	|ИЗ
	|	НазначенныеЗаявкиБезОчередей КАК НазначенныеЗаявкиБезОчередей
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ЗаявкаНаРазработку КАК ЗаявкаНаРазработку
	|		ПО НазначенныеЗаявкиБезОчередей.Ссылка = ЗаявкаНаРазработку.Ссылка
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.Продукты КАК Продукты
	|		ПО (Продукты.Ссылка = ЗаявкаНаРазработку.Продукт)
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ОчередиЗаявок КАК ОчередьЗаявокСистема
	|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.КлючОчереди КАК КлючОчереди
	|			ПО (КлючОчереди.Ссылка = ОчередьЗаявокСистема.КлючОчередиОбъекта)
	|				И (КлючОчереди.ВидОчереди = ЗНАЧЕНИЕ(Перечисление.ВидыОчереди.НаправлениеСистемаГруппаЗаказчиков))
	|		ПО НазначенныеЗаявкиБезОчередей.Ссылка = ОчередьЗаявокСистема.Заявка
	|			И (КлючОчереди.Система = НазначенныеЗаявкиБезОчередей.Система)
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ОчередиЗаявок КАК ОчередьЗаявокПродукт
	|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.КлючОчереди КАК КлючОчередиПродукт
	|			ПО (КлючОчередиПродукт.Ссылка = ОчередьЗаявокПродукт.КлючОчередиОбъекта)
	|				И (КлючОчередиПродукт.ВидОчереди = ЗНАЧЕНИЕ(Перечисление.ВидыОчереди.Продукт))
	|		ПО НазначенныеЗаявкиБезОчередей.Ссылка = ОчередьЗаявокПродукт.Заявка
	|			И (ЕСТЬNULL(Продукты.Ссылка, НЕОПРЕДЕЛЕНО) = КлючОчередиПродукт.Продукт)
	|			И (ЕСТЬNULL(Продукты.ВидОчереди, НЕОПРЕДЕЛЕНО) = ЗНАЧЕНИЕ(Перечисление.ВидыОчереди.Продукт))
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ОчередиЗаявок КАК ОчередьЗаявокПродуктСистема
	|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.КлючОчереди КАК КлючОчередиПродуктСистема
	|			ПО (КлючОчередиПродуктСистема.Ссылка = ОчередьЗаявокПродуктСистема.КлючОчередиОбъекта)
	|				И (КлючОчередиПродуктСистема.ВидОчереди = ЗНАЧЕНИЕ(Перечисление.ВидыОчереди.ПродуктСистема))
	|		ПО НазначенныеЗаявкиБезОчередей.Ссылка = ОчередьЗаявокПродуктСистема.Заявка
	|			И (ЕСТЬNULL(Продукты.ВидОчереди, НЕОПРЕДЕЛЕНО) = ЗНАЧЕНИЕ(Перечисление.ВидыОчереди.ПродуктСистема))
	|			И (КлючОчередиПродуктСистема.Система = НазначенныеЗаявкиБезОчередей.Система)
	|			И (ЕСТЬNULL(Продукты.Ссылка, НЕОПРЕДЕЛЕНО) = КлючОчередиПродуктСистема.Продукт)
	|{ГДЕ
	|	(1) КАК ПоказатьЗаявки}
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	НазначенныеЗаявки.Ссылка КАК Ссылка,
	|	НазначенныеЗаявки.Пользователь КАК Пользователь,
	|	НазначенныеЗаявки.Продукт КАК Продукт,
	|	МИНИМУМ(НазначенныеЗаявки.ПорядокЗаявки) КАК ПорядокЗаявки,
	|	МИНИМУМ(СтатусыОбъектов.Порядок) КАК ПорядокСтатуса,
	|	МИНИМУМ(ИсторияСтатусовЗаявокПоСистемам.Период) КАК ДатаРассмотрения,
	|	ЕСТЬNULL(ЗначенияДополнительныхРеквизитовОбъектов.Значение, ЛОЖЬ) КАК ЦелеваяЗаявка
	|ПОМЕСТИТЬ ОтобранныеЗаявки
	|ИЗ
	|	НазначенныеЗаявки КАК НазначенныеЗаявки
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.СтатусыОбъектов КАК СтатусыОбъектов
	|		ПО (СтатусыОбъектов.Ссылка = НазначенныеЗаявки.Статус)
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.ИсторияСтатусовЗаявокПоСистемам КАК ИсторияСтатусовЗаявокПоСистемам
	|		ПО НазначенныеЗаявки.Ссылка = ИсторияСтатусовЗаявокПоСистемам.Заявка
	|			И (ИсторияСтатусовЗаявокПоСистемам.Статус В (ЗНАЧЕНИЕ(Справочник.СтатусыОбъектов.Рассмотрение), ЗНАЧЕНИЕ(Справочник.СтатусыОбъектов.НаДоработку)))
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ЗначенияДополнительныхРеквизитовОбъектов КАК ЗначенияДополнительныхРеквизитовОбъектов
	|		ПО НазначенныеЗаявки.Ссылка = ЗначенияДополнительныхРеквизитовОбъектов.Объект
	|			И (ЗначенияДополнительныхРеквизитовОбъектов.Реквизит = ЗНАЧЕНИЕ(ПланВидовХарактеристик.ВидыДополнительныхРеквизитов.ЦелеваяЗаявка))
	|
	|СГРУППИРОВАТЬ ПО
	|	НазначенныеЗаявки.Ссылка,
	|	НазначенныеЗаявки.Пользователь,
	|	НазначенныеЗаявки.Продукт,
	|	ЕСТЬNULL(ЗначенияДополнительныхРеквизитовОбъектов.Значение, ЛОЖЬ)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ДокументЗадача.Ссылка КАК Ссылка,
	|	""["" + ДокументЗадача.Номер + ""] "" + ДокументЗадача.Наименование КАК Представление,
	|	ДокументЗадача.Назначена КАК Пользователь,
	|	ДокументЗадача.Назначение КАК Назначение,
	|	ДокументЗадача.СерьезностьОшибки КАК СерьезностьОшибки,
	|	ДокументЗадача.Статус КАК Статус,
	|	ДокументЗадача.ОбъектОснование КАК ОбъектОснование,
	|	ДокументЗадача.СистемаУчета КАК СистемаУчета,
	|	ДокументЗадача.ОценкаStoryPoint КАК ОценкаStoryPoint,
	|	ДокументЗадача.ДатаНачалаРазработки КАК ДатаНачалаРазработки,
	|	ВЫБОР
	|		КОГДА ДокументЗадача.Статус = ЗНАЧЕНИЕ(Справочник.СтатусыОбъектов.ВРаботе)
	|			ТОГДА &ТекущаяДата
	|		ИНАЧЕ ДокументЗадача.ДатаПоследнегоТестирования
	|	КОНЕЦ КАК ДатаПоследнегоТестирования,
	|	ВЫБОР
	|		КОГДА ДокументЗадача.ДатаНачалаРазработки = ДАТАВРЕМЯ(1, 1, 1)
	|			ТОГДА 0
	|		ИНАЧЕ РАЗНОСТЬДАТ(ДокументЗадача.ДатаНачалаРазработки, &ТекущаяДата, ДЕНЬ)
	|	КОНЕЦ КАК ДнейВРаботе,
	|	0 КАК ДнейНаРассмотрении,
	|	ЕСТЬNULL(ЗаявкаНаРазработку.Продукт, ЗНАЧЕНИЕ(Справочник.Продукты.ПустаяСсылка)) КАК Продукт,
	|	ЕСТЬNULL(ЗначенияДополнительныхРеквизитовОбъектов.Значение, ЛОЖЬ) КАК ЦелеваяЗаявка
	|ПОМЕСТИТЬ ОтобранныеЗадачи
	|ИЗ
	|	ОтобранныеПользователи КАК ОтобранныеПользователи
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.Задача КАК ДокументЗадача
	|		ПО ОтобранныеПользователи.Пользователь = ДокументЗадача.Назначена
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.СтатусыОбъектов КАК СтатусыОбъектов
	|		ПО (ДокументЗадача.Статус = СтатусыОбъектов.Ссылка)
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.ЗаявкаНаРазработку КАК ЗаявкаНаРазработку
	|		ПО (ДокументЗадача.ОбъектОснование = ЗаявкаНаРазработку.Ссылка)
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ЗначенияДополнительныхРеквизитовОбъектов КАК ЗначенияДополнительныхРеквизитовОбъектов
	|		ПО (ДокументЗадача.ОбъектОснование = ЗначенияДополнительныхРеквизитовОбъектов.Объект)
	|			И (ЗначенияДополнительныхРеквизитовОбъектов.Реквизит = ЗНАЧЕНИЕ(ПланВидовХарактеристик.ВидыДополнительныхРеквизитов.ЦелеваяЗаявка))
	|ГДЕ
	|	ВЫБОР
	|			КОГДА ДокументЗадача.Статус В (&ИсключаемыеСтатусы)
	|					ИЛИ СтатусыОбъектов.Родитель В (&ИсключаемыеСтатусы)
	|				ТОГДА ЛОЖЬ
	|			ИНАЧЕ ИСТИНА
	|		КОНЕЦ
	|	И ВЫБОР
	|			КОГДА &Задача = НЕОПРЕДЕЛЕНО
	|				ТОГДА ИСТИНА
	|			ИНАЧЕ ДокументЗадача.Ссылка = &Задача
	|		КОНЕЦ
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ДокументЗадача.Ссылка,
	|	""["" + ДокументЗадача.Номер + ""] "" + ДокументЗадача.Наименование,
	|	ДокументЗадача.Назначена,
	|	ДокументЗадача.Назначение,
	|	ДокументЗадача.СерьезностьОшибки,
	|	ДокументЗадача.Статус,
	|	ДокументЗадача.ОбъектОснование,
	|	ДокументЗадача.СистемаУчета,
	|	ДокументЗадача.ОценкаStoryPoint,
	|	ДокументЗадача.ДатаНачалаРазработки,
	|	ДАТАВРЕМЯ(1, 1, 1),
	|	ВЫБОР
	|		КОГДА ДокументЗадача.ДатаНачалаРазработки = ДАТАВРЕМЯ(1, 1, 1)
	|			ТОГДА 0
	|		ИНАЧЕ РАЗНОСТЬДАТ(ДокументЗадача.ДатаНачалаРазработки, &ТекущаяДата, ДЕНЬ)
	|	КОНЕЦ,
	|	ВЫБОР
	|		КОГДА ДокументЗадача.Статус = ЗНАЧЕНИЕ(Справочник.СтатусыОбъектов.ВРеализацию)
	|				И ДокументЗадача.ДатаГотовностиРеализации > ДАТАВРЕМЯ(1, 1, 1)
	|			ТОГДА РАЗНОСТЬДАТ(ДокументЗадача.ДатаГотовностиРеализации, &ТекущаяДата, ДЕНЬ)
	|		ИНАЧЕ 0
	|	КОНЕЦ,
	|	ЕСТЬNULL(ЗаявкаНаРазработку.Продукт, ЗНАЧЕНИЕ(Справочник.Продукты.ПустаяСсылка)),
	|	ЕСТЬNULL(ЗначенияДополнительныхРеквизитовОбъектов.Значение, ЛОЖЬ)
	|ИЗ
	|	Документ.Задача КАК ДокументЗадача
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.СтатусыОбъектов КАК СтатусыОбъектов
	|		ПО ДокументЗадача.Статус = СтатусыОбъектов.Ссылка
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВсеФилиалы КАК ВсеФилиалы
	|		ПО ДокументЗадача.Филиал = ВсеФилиалы.Филиал
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.Филиалы КАК КастомныеКоманды
	|		ПО ДокументЗадача.КомандаРазработчиков = КастомныеКоманды.Ссылка
	|			И (КастомныеКоманды.Родитель = ЗНАЧЕНИЕ(Справочник.Филиалы.КастомныеКоманды))
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.ЗаявкаНаРазработку КАК ЗаявкаНаРазработку
	|		ПО ДокументЗадача.ОбъектОснование = ЗаявкаНаРазработку.Ссылка
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ЗначенияДополнительныхРеквизитовОбъектов КАК ЗначенияДополнительныхРеквизитовОбъектов
	|		ПО ДокументЗадача.ОбъектОснование = ЗначенияДополнительныхРеквизитовОбъектов.Объект
	|			И (ЗначенияДополнительныхРеквизитовОбъектов.Реквизит = ЗНАЧЕНИЕ(ПланВидовХарактеристик.ВидыДополнительныхРеквизитов.ЦелеваяЗаявка))
	|ГДЕ
	|	ДокументЗадача.Назначена = ЗНАЧЕНИЕ(Справочник.Пользователи.ПустаяСсылка)
	|	И ВЫБОР
	|			КОГДА ДокументЗадача.Статус В (&ИсключаемыеСтатусы)
	|					ИЛИ СтатусыОбъектов.Родитель В (&ИсключаемыеСтатусы)
	|				ТОГДА ЛОЖЬ
	|			ИНАЧЕ ИСТИНА
	|		КОНЕЦ
	|	И ВЫБОР
	|			КОГДА КастомныеКоманды.Ссылка ЕСТЬ NULL
	|				ТОГДА НЕ ВсеФилиалы.Филиал ЕСТЬ NULL
	|			ИНАЧЕ КастомныеКоманды.Ссылка В (&ВыбранныеФилиалы)
	|		КОНЕЦ
	|	И ВЫБОР
	|			КОГДА &Задача = НЕОПРЕДЕЛЕНО
	|				ТОГДА ИСТИНА
	|			ИНАЧЕ ДокументЗадача.Ссылка = &Задача
	|		КОНЕЦ
	|{ГДЕ
	|	(1) КАК ПриоритетСтроки,
	|	(ВЫБОР
	|			КОГДА ДокументЗадача.КомандаРазработчиков = ЗНАЧЕНИЕ(Справочник.Филиалы.ПустаяСсылка)
	|				ТОГДА ДокументЗадача.Филиал
	|			ИНАЧЕ ДокументЗадача.КомандаРазработчиков
	|		КОНЕЦ) КАК КомандаРазработчиков}
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	СвойстваЗадачСрезПоследних.Объект,
	|	""["" + ДокументЗадача.Номер + ""] "" + ДокументЗадача.Наименование,
	|	СвойстваЗадачСрезПоследних.Автор,
	|	ДокументЗадача.Назначение,
	|	ДокументЗадача.СерьезностьОшибки,
	|	СвойстваЗадачСрезПоследних.Статус,
	|	ДокументЗадача.ОбъектОснование,
	|	ДокументЗадача.СистемаУчета,
	|	ДокументЗадача.ОценкаStoryPoint,
	|	ДокументЗадача.ДатаНачалаРазработки,
	|	ДокументЗадача.ДатаПоследнегоТестирования,
	|	ВЫБОР
	|		КОГДА ДокументЗадача.ДатаНачалаРазработки = ДАТАВРЕМЯ(1, 1, 1)
	|			ТОГДА 0
	|		ИНАЧЕ РАЗНОСТЬДАТ(ДокументЗадача.ДатаНачалаРазработки, &ТекущаяДата, ДЕНЬ)
	|	КОНЕЦ,
	|	0,
	|	ЕСТЬNULL(ЗаявкаНаРазработку.Продукт, ЗНАЧЕНИЕ(Справочник.Продукты.ПустаяСсылка)),
	|	ЕСТЬNULL(ЗначенияДополнительныхРеквизитовОбъектов.Значение, ЛОЖЬ)
	|ИЗ
	|	РегистрСведений.СвойстваЗадач.СрезПоследних(
	|			,
	|			Объект В
	|				(ВЫБРАТЬ
	|					Задача.Ссылка КАК Ссылка
	|				ИЗ
	|					Документ.Задача КАК Задача
	|				ГДЕ
	|					Задача.Статус = ЗНАЧЕНИЕ(Справочник.СтатусыОбъектов.НаДоработку))) КАК СвойстваЗадачСрезПоследних
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ОтобранныеПользователи КАК ОтобранныеПользователи
	|		ПО СвойстваЗадачСрезПоследних.Автор = ОтобранныеПользователи.Пользователь
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.Задача КАК ДокументЗадача
	|		ПО СвойстваЗадачСрезПоследних.Объект = ДокументЗадача.Ссылка
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.ЗаявкаНаРазработку КАК ЗаявкаНаРазработку
	|		ПО (ДокументЗадача.ОбъектОснование = ЗаявкаНаРазработку.Ссылка)
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ЗначенияДополнительныхРеквизитовОбъектов КАК ЗначенияДополнительныхРеквизитовОбъектов
	|		ПО (ДокументЗадача.ОбъектОснование = ЗначенияДополнительныхРеквизитовОбъектов.Объект)
	|			И (ЗначенияДополнительныхРеквизитовОбъектов.Реквизит = ЗНАЧЕНИЕ(ПланВидовХарактеристик.ВидыДополнительныхРеквизитов.ЦелеваяЗаявка))
	|ГДЕ
	|	СвойстваЗадачСрезПоследних.Автор <> ДокументЗадача.Назначена
	|	И ВЫБОР
	|			КОГДА &Задача = НЕОПРЕДЕЛЕНО
	|				ТОГДА ИСТИНА
	|			ИНАЧЕ ДокументЗадача.Ссылка = &Задача
	|		КОНЕЦ
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВнутренниеТрудозатраты.Ссылка КАК Ссылка,
	|	""["" + ВнутренниеТрудозатраты.Номер + ""] "" + ВнутренниеТрудозатраты.Наименование КАК Представление,
	|	ОтобранныеПользователи.Пользователь КАК Пользователь,
	|	ВнутренниеТрудозатраты.Дата КАК ДатаНачала,
	|	ВЫБОР
	|		КОГДА ВнутренниеТрудозатраты.Дата = ДАТАВРЕМЯ(1, 1, 1)
	|			ТОГДА 0
	|		ИНАЧЕ РАЗНОСТЬДАТ(ВнутренниеТрудозатраты.Дата, &ТекущаяДата, ДЕНЬ)
	|	КОНЕЦ КАК ДнейВРаботе
	|ПОМЕСТИТЬ ОтобранныеВнутренниеТрудозатраты
	|ИЗ
	|	ОтобранныеПользователи КАК ОтобранныеПользователи
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.УчастникиВнутреннихТрудозатрат КАК УчастникиВнутреннихТрудозатрат
	|		ПО ОтобранныеПользователи.Пользователь = УчастникиВнутреннихТрудозатрат.Пользователь
	|			И (УчастникиВнутреннихТрудозатрат.Активный)
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ВнутренниеТрудозатраты КАК ВнутренниеТрудозатраты
	|		ПО (УчастникиВнутреннихТрудозатрат.Объект = ВнутренниеТрудозатраты.Ссылка)
	|ГДЕ
	|	&Задача = НЕОПРЕДЕЛЕНО
	|{ГДЕ
	|	(1) КАК ПоказыватьВнутренниеТрудозатраты}
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ДокументВнутреннееЗадание.Ссылка КАК Ссылка,
	|	""["" + ДокументВнутреннееЗадание.Номер + ""] "" + ДокументВнутреннееЗадание.Наименование КАК Представление,
	|	ДокументВнутреннееЗадание.Назначена КАК Пользователь,
	|	ДокументВнутреннееЗадание.Назначение КАК Назначение,
	|	ДокументВнутреннееЗадание.СерьезностьОшибки КАК СерьезностьОшибки,
	|	ДокументВнутреннееЗадание.Статус КАК Статус,
	|	ДокументВнутреннееЗадание.ОбъектОснование КАК ОбъектОснование,
	|	ДокументВнутреннееЗадание.СистемаУчета КАК СистемаУчета,
	|	ДокументВнутреннееЗадание.ДатаНачалаРазработки КАК ДатаНачалаРазработки,
	|	ВЫБОР
	|		КОГДА ДокументВнутреннееЗадание.Статус = ЗНАЧЕНИЕ(Справочник.СтатусыОбъектов.ВРаботе)
	|			ТОГДА &ТекущаяДата
	|		ИНАЧЕ ДокументВнутреннееЗадание.ДатаПоследнегоТестирования
	|	КОНЕЦ КАК ДатаПоследнегоТестирования,
	|	ВЫБОР
	|		КОГДА ДокументВнутреннееЗадание.ДатаНачалаРазработки = ДАТАВРЕМЯ(1, 1, 1)
	|			ТОГДА 0
	|		ИНАЧЕ РАЗНОСТЬДАТ(ДокументВнутреннееЗадание.ДатаНачалаРазработки, &ТекущаяДата, ДЕНЬ)
	|	КОНЕЦ КАК ДнейВРаботе,
	|	0 КАК ДнейНаРассмотрении,
	|	ДокументВнутреннееЗадание.Продукт КАК Продукт
	|ПОМЕСТИТЬ ОтобранныеВнутренниеЗадания
	|ИЗ
	|	ОтобранныеПользователи КАК ОтобранныеПользователи
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ВнутреннееЗадание КАК ДокументВнутреннееЗадание
	|		ПО ОтобранныеПользователи.Пользователь = ДокументВнутреннееЗадание.Назначена
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.СтатусыОбъектов КАК СтатусыОбъектов
	|		ПО (ДокументВнутреннееЗадание.Статус = СтатусыОбъектов.Ссылка)
	|ГДЕ
	|	ВЫБОР
	|			КОГДА ДокументВнутреннееЗадание.Статус В (&ИсключаемыеСтатусы)
	|					ИЛИ СтатусыОбъектов.Родитель В (&ИсключаемыеСтатусы)
	|				ТОГДА ЛОЖЬ
	|			ИНАЧЕ ИСТИНА
	|		КОНЕЦ
	|	И &Задача = НЕОПРЕДЕЛЕНО
	|{ГДЕ
	|	(1) КАК ПоказатьВнутренниеЗадания}
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ОтобранныеЗаявки.Ссылка КАК Заявка,
	|	СУММА(Трудозатраты.Затрата) КАК ФактическиеТрудозатраты
	|ПОМЕСТИТЬ ТрудозатратыПоЗаявкам
	|ИЗ
	|	ОтобранныеЗаявки КАК ОтобранныеЗаявки
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.Трудозатраты КАК Трудозатраты
	|		ПО (Трудозатраты.Объект = ОтобранныеЗаявки.Ссылка)
	|			И (Трудозатраты.Пользователь = ОтобранныеЗаявки.Пользователь)
	|
	|СГРУППИРОВАТЬ ПО
	|	ОтобранныеЗаявки.Ссылка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ОтобранныеЗадачи.Ссылка КАК Задача,
	|	ОтобранныеЗадачи.Пользователь КАК Пользователь,
	|	СУММА(Трудозатраты.Затрата) КАК ФактическиеТрудозатраты
	|ПОМЕСТИТЬ ТрудозатратыПоЗадачам
	|ИЗ
	|	ОтобранныеЗадачи КАК ОтобранныеЗадачи
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.Трудозатраты КАК Трудозатраты
	|		ПО (Трудозатраты.Объект = ОтобранныеЗадачи.Ссылка)
	|
	|СГРУППИРОВАТЬ ПО
	|	ОтобранныеЗадачи.Ссылка,
	|	ОтобранныеЗадачи.Пользователь
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ОтобранныеВнутренниеЗадания.Ссылка КАК Ссылка,
	|	СУММА(Трудозатраты.Затрата) КАК ФактическиеТрудозатраты
	|ПОМЕСТИТЬ ТрудозатратыПоВнутреннимЗаданиям
	|ИЗ
	|	ОтобранныеВнутренниеЗадания КАК ОтобранныеВнутренниеЗадания
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.Трудозатраты КАК Трудозатраты
	|		ПО (Трудозатраты.Объект = ОтобранныеВнутренниеЗадания.Ссылка)
	|
	|СГРУППИРОВАТЬ ПО
	|	ОтобранныеВнутренниеЗадания.Ссылка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ОтобранныеВнутренниеТрудозатраты.Ссылка КАК Задача,
	|	ОтобранныеВнутренниеТрудозатраты.Пользователь КАК Пользователь,
	|	СУММА(Трудозатраты.Затрата) КАК ФактическиеТрудозатраты
	|ПОМЕСТИТЬ ТрудозатратыПоВнутреннимТрудозатратам
	|ИЗ
	|	ОтобранныеВнутренниеТрудозатраты КАК ОтобранныеВнутренниеТрудозатраты
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.Трудозатраты КАК Трудозатраты
	|		ПО (Трудозатраты.Объект = ОтобранныеВнутренниеТрудозатраты.Ссылка)
	|			И (Трудозатраты.Пользователь = ОтобранныеВнутренниеТрудозатраты.Пользователь)
	|
	|СГРУППИРОВАТЬ ПО
	|	ОтобранныеВнутренниеТрудозатраты.Ссылка,
	|	ОтобранныеВнутренниеТрудозатраты.Пользователь
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ОтобранныеЗаявки.Ссылка КАК Ссылка,
	|	СистемыЗаявки.ГруппаЗаказчиков КАК ГруппаЗаказчиков
	|ПОМЕСТИТЬ ГруппыЗаказчиковОбъектов
	|ИЗ
	|	ОтобранныеЗаявки КАК ОтобранныеЗаявки
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ЗаявкаНаРазработку.СистемыРазработки КАК СистемыЗаявки
	|		ПО ОтобранныеЗаявки.Ссылка = СистемыЗаявки.Ссылка
	|ГДЕ
	|	ВЫБОР
	|			КОГДА НЕ &ОтборПродуктГруппаЗаказчиков
	|				ТОГДА ИСТИНА
	|			ИНАЧЕ СистемыЗаявки.ГруппаЗаказчиков В (&СписокПродуктГруппаЗаказчиков)
	|		КОНЕЦ
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ОтобранныеЗадачи.Ссылка,
	|	СистемыЗаявки.ГруппаЗаказчиков
	|ИЗ
	|	ОтобранныеЗадачи КАК ОтобранныеЗадачи
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.СвязьОбъектовСЗаявкамиНаРазработку КАК СвязьОбъектовСЗаявкамиНаРазработку
	|		ПО (СвязьОбъектовСЗаявкамиНаРазработку.Объект = ОтобранныеЗадачи.Ссылка)
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ЗаявкаНаРазработку.СистемыРазработки КАК СистемыЗаявки
	|		ПО (СистемыЗаявки.Ссылка = СвязьОбъектовСЗаявкамиНаРазработку.Заявка_Новая)
	|			И ОтобранныеЗадачи.СистемаУчета = СистемыЗаявки.Система
	|ГДЕ
	|	ВЫБОР
	|			КОГДА НЕ &ОтборПродуктГруппаЗаказчиков
	|				ТОГДА ИСТИНА
	|			ИНАЧЕ СистемыЗаявки.ГруппаЗаказчиков В (&СписокПродуктГруппаЗаказчиков)
	|		КОНЕЦ
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ОтобранныеЗаявки.Ссылка КАК Ссылка,
	|	ВЫРАЗИТЬ(ЗаявкаНаРазработку.ПредставлениеСистем КАК СТРОКА(1000)) КАК ПредставлениеСистем
	|ПОМЕСТИТЬ СистемыОбъектов
	|ИЗ
	|	ОтобранныеЗаявки КАК ОтобранныеЗаявки
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ЗаявкаНаРазработку КАК ЗаявкаНаРазработку
	|		ПО ОтобранныеЗаявки.Ссылка = ЗаявкаНаРазработку.Ссылка
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ЗаявкаНаРазработку.СистемыРазработки КАК СистемыЗаявки
	|		ПО (ЗаявкаНаРазработку.Ссылка = СистемыЗаявки.Ссылка)
	|{ГДЕ
	|	СистемыЗаявки.Система КАК Система}
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ОтобранныеЗадачи.Ссылка,
	|	ВЫРАЗИТЬ(СистемыУчета.Наименование КАК СТРОКА(1000))
	|ИЗ
	|	ОтобранныеЗадачи КАК ОтобранныеЗадачи
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.Задача КАК ДокументЗадача
	|		ПО ОтобранныеЗадачи.Ссылка = ДокументЗадача.Ссылка
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.СистемыУчета КАК СистемыУчета
	|		ПО (ДокументЗадача.СистемаУчета = СистемыУчета.Ссылка)
	|{ГДЕ
	|	ДокументЗадача.СистемаУчета КАК Система}
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ОтобранныеЗадачи.Ссылка КАК Ссылка,
	|	ОтобранныеЗадачи.Представление КАК Представление,
	|	ОтобранныеЗадачи.Пользователь КАК Пользователь,
	|	ОтобранныеЗадачи.ДатаНачалаРазработки КАК ДатаНачалаРазработки,
	|	ОтобранныеЗадачи.ДатаПоследнегоТестирования КАК ДатаПоследнегоТестирования,
	|	ОтобранныеЗадачи.ДнейВРаботе КАК ДнейВРаботе,
	|	ОтобранныеЗадачи.ДнейНаРассмотрении КАК ДнейНаРассмотрении,
	|	ОтобранныеЗадачи.ОценкаStoryPoint КАК ОценкаStoryPoint,
	|	СтатусыОбъектов.Порядок КАК ПорядокСтатуса,
	|	СтатусыОбъектов.ИндексКартинки КАК ИндексКартинки,
	|	СтатусыОбъектов.Наименование КАК СтатусПредставление,
	|	ЕСТЬNULL(СерьезностьОшибок.ИмяКартинки, НазначенияЗадач.ИмяКартинки) КАК НазначениеИмяКартинки,
	|	ЕСТЬNULL(ПланируемыеТрудозатраты.Трудозатраты, 0) КАК ПланируемыеТрудозатраты,
	|	ВЫБОР
	|		КОГДА ЕСТЬNULL(СтатусыОбъектов.Порядок, 0) < &ПорядокСтатусаПриостановлен
	|			ТОГДА ЕСТЬNULL(ПланируемыеТрудозатраты.Трудозатраты, 0)
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК ОбщийПланСотрудника,
	|	ВЫБОР
	|		КОГДА НЕ ОчередьЗаявокПродуктСистема.Порядок ЕСТЬ NULL
	|			ТОГДА ЕСТЬNULL(ОчередьЗаявокПродуктСистема.Порядок, 0)
	|		КОГДА НЕ ОчередьЗаявокПродукт.Порядок ЕСТЬ NULL
	|			ТОГДА ЕСТЬNULL(ОчередьЗаявокПродукт.Порядок, 0)
	|		КОГДА НЕ ОчередьЗаявок.Порядок ЕСТЬ NULL
	|			ТОГДА ЕСТЬNULL(ОчередьЗаявок.Порядок, 0)
	|		КОГДА ЕСТЬNULL(СтатусыОбъектов.Порядок, 0) < &ПорядокСтатусаВРаботе
	|			ТОГДА ЕСТЬNULL(СохраненныеПорядкиЗаявок.Порядок, 0)
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК ПорядокЗаявки,
	|	ЕСТЬNULL(ТрудозатратыПоЗадачам.ФактическиеТрудозатраты, 0) КАК ФактическиеТрудозатраты,
	|	ЕСТЬNULL(КоличествоЦикловОперацийНадЗадачами.Тестирование, 0) КАК ЦиклыТестирования,
	|	ЕСТЬNULL(КоличествоЦикловОперацийНадЗадачами.CodeReview, 0) КАК ЦиклыCR,
	|	1 КАК КоличествоЗадач,
	|	0 КАК КоличествоЗаявок,
	|	0 КАК КоличествоВнутреннихЗаданий,
	|	0 КАК КоличествоВнутреннихТрудозатрат,
	|	ЕСТЬNULL(СрокиСдачиЗадач.СрокСдачи, ДАТАВРЕМЯ(1, 1, 1)) КАК СрокРеализации,
	|	ОтобранныеЗадачи.ОбъектОснование КАК ОбъектОснование,
	|	ОтобранныеЗадачи.Продукт КАК Продукт,
	|	ОтобранныеЗадачи.ЦелеваяЗаявка КАК ЦелеваяЗаявка
	|ПОМЕСТИТЬ ОтобранныеОбъекты
	|ИЗ
	|	ОтобранныеЗадачи КАК ОтобранныеЗадачи
	|		ЛЕВОЕ СОЕДИНЕНИЕ ГруппыЗаказчиковОбъектов КАК ГруппыЗаказчиковОбъектов
	|		ПО ОтобранныеЗадачи.Ссылка = ГруппыЗаказчиковОбъектов.Ссылка
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ СистемыОбъектов КАК СистемыОбъектов
	|		ПО ОтобранныеЗадачи.Ссылка = СистемыОбъектов.Ссылка
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.СтатусыОбъектов КАК СтатусыОбъектов
	|		ПО ОтобранныеЗадачи.Статус = СтатусыОбъектов.Ссылка
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.НазначенияЗадач КАК НазначенияЗадач
	|		ПО ОтобранныеЗадачи.Назначение = НазначенияЗадач.Ссылка
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.СерьезностьОшибок КАК СерьезностьОшибок
	|		ПО ОтобранныеЗадачи.СерьезностьОшибки = СерьезностьОшибок.Ссылка
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.Продукты КАК Продукты
	|		ПО (Продукты.Ссылка = ОтобранныеЗадачи.Продукт)
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ОчередиЗаявок КАК ОчередьЗаявок
	|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.КлючОчереди КАК КлючОчереди
	|			ПО (КлючОчереди.Ссылка = ОчередьЗаявок.КлючОчередиОбъекта)
	|				И (КлючОчереди.ВидОчереди = ЗНАЧЕНИЕ(Перечисление.ВидыОчереди.НаправлениеСистемаГруппаЗаказчиков))
	|		ПО ОтобранныеЗадачи.ОбъектОснование = ОчередьЗаявок.Заявка
	|			И (КлючОчереди.Система = ОтобранныеЗадачи.СистемаУчета)
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ОчередиЗаявок КАК ОчередьЗаявокПродукт
	|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.КлючОчереди КАК КлючОчередиПродукт
	|			ПО (КлючОчередиПродукт.Ссылка = ОчередьЗаявокПродукт.КлючОчередиОбъекта)
	|				И (КлючОчередиПродукт.ВидОчереди = ЗНАЧЕНИЕ(Перечисление.ВидыОчереди.Продукт))
	|		ПО ОтобранныеЗадачи.ОбъектОснование = ОчередьЗаявокПродукт.Заявка
	|			И (ЕСТЬNULL(Продукты.Ссылка, НЕОПРЕДЕЛЕНО) = КлючОчередиПродукт.Продукт)
	|			И (ЕСТЬNULL(Продукты.ВидОчереди, НЕОПРЕДЕЛЕНО) = ЗНАЧЕНИЕ(Перечисление.ВидыОчереди.Продукт))
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ОчередиЗаявок КАК ОчередьЗаявокПродуктСистема
	|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.КлючОчереди КАК КлючОчередиПродуктСистема
	|			ПО (КлючОчередиПродуктСистема.Ссылка = ОчередьЗаявокПродуктСистема.КлючОчередиОбъекта)
	|				И (КлючОчередиПродуктСистема.ВидОчереди = ЗНАЧЕНИЕ(Перечисление.ВидыОчереди.ПродуктСистема))
	|		ПО ОтобранныеЗадачи.ОбъектОснование = ОчередьЗаявокПродуктСистема.Заявка
	|			И (ЕСТЬNULL(Продукты.ВидОчереди, НЕОПРЕДЕЛЕНО) = ЗНАЧЕНИЕ(Перечисление.ВидыОчереди.ПродуктСистема))
	|			И (КлючОчередиПродуктСистема.Система = ОтобранныеЗадачи.СистемаУчета)
	|			И (ЕСТЬNULL(Продукты.Ссылка, НЕОПРЕДЕЛЕНО) = КлючОчередиПродуктСистема.Продукт)
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.СохраненныеПорядкиЗаявок КАК СохраненныеПорядкиЗаявок
	|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.КлючОчереди КАК КлючОчередиСохраненныеПорядки
	|			ПО (КлючОчередиСохраненныеПорядки.Ссылка = СохраненныеПорядкиЗаявок.КлючОчередиОбъекта)
	|				И (КлючОчередиСохраненныеПорядки.ВидОчереди = ЗНАЧЕНИЕ(Перечисление.ВидыОчереди.НаправлениеСистемаГруппаЗаказчиков))
	|		ПО ОтобранныеЗадачи.ОбъектОснование = СохраненныеПорядкиЗаявок.Заявка
	|			И (ОтобранныеЗадачи.СистемаУчета = КлючОчередиСохраненныеПорядки.Система)
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ПланируемыеТрудозатраты КАК ПланируемыеТрудозатраты
	|		ПО ОтобранныеЗадачи.Ссылка = ПланируемыеТрудозатраты.Объект
	|			И (ПланируемыеТрудозатраты.ТипТрудозатрат = ЗНАЧЕНИЕ(Перечисление.ТипыТрудозатрат.Разработка))
	|		ЛЕВОЕ СОЕДИНЕНИЕ ТрудозатратыПоЗадачам КАК ТрудозатратыПоЗадачам
	|		ПО ОтобранныеЗадачи.Ссылка = ТрудозатратыПоЗадачам.Задача
	|			И ОтобранныеЗадачи.Пользователь = ТрудозатратыПоЗадачам.Пользователь
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.КоличествоЦикловОперацийНадЗадачами КАК КоличествоЦикловОперацийНадЗадачами
	|		ПО ОтобранныеЗадачи.Ссылка = КоличествоЦикловОперацийНадЗадачами.Задача
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.СрокиСдачиЗадач.СрезПоследних(, ) КАК СрокиСдачиЗадач
	|		ПО ОтобранныеЗадачи.Ссылка = СрокиСдачиЗадач.Задача
	|ГДЕ
	|	ВЫБОР
	|			КОГДА НЕ &ОтборПродуктГруппаЗаказчиков
	|				ТОГДА ИСТИНА
	|			КОГДА НЕ Продукты.Ссылка ЕСТЬ NULL
	|				ТОГДА Продукты.Ссылка В (&СписокПродуктГруппаЗаказчиков)
	|			ИНАЧЕ ГруппыЗаказчиковОбъектов.ГруппаЗаказчиков В (&СписокПродуктГруппаЗаказчиков)
	|		КОНЕЦ
	|{ГДЕ
	|	СтатусыОбъектов.Ссылка КАК Статус}
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ОтобранныеЗаявки.Ссылка,
	|	""["" + ЗаявкаНаРазработку.Номер + ""] "" + ЗаявкаНаРазработку.Наименование,
	|	ОтобранныеЗаявки.Пользователь,
	|	ДАТАВРЕМЯ(1, 1, 1),
	|	ДАТАВРЕМЯ(1, 1, 1),
	|	РАЗНОСТЬДАТ(ОтобранныеЗаявки.ДатаРассмотрения, &ТекущаяДата, ДЕНЬ),
	|	0,
	|	0,
	|	ОтобранныеЗаявки.ПорядокСтатуса,
	|	СтатусыОбъектов.ИндексКартинки,
	|	СтатусыОбъектов.Наименование,
	|	ЕСТЬNULL(СерьезностьОшибок.ИмяКартинки, НазначенияЗадач.ИмяКартинки),
	|	0,
	|	0,
	|	ОтобранныеЗаявки.ПорядокЗаявки,
	|	ЕСТЬNULL(ТрудозатратыПоЗаявкам.ФактическиеТрудозатраты, 0),
	|	0,
	|	0,
	|	0,
	|	1,
	|	0,
	|	0,
	|	ЗаявкаНаРазработку.СрокРеализации,
	|	ЗНАЧЕНИЕ(Документ.ЗаявкаНаРазработку.ПустаяСсылка),
	|	ОтобранныеЗаявки.Продукт,
	|	ОтобранныеЗаявки.ЦелеваяЗаявка
	|ИЗ
	|	ОтобранныеЗаявки КАК ОтобранныеЗаявки
	|		ЛЕВОЕ СОЕДИНЕНИЕ ГруппыЗаказчиковОбъектов КАК ГруппыЗаказчиковОбъектов
	|		ПО ОтобранныеЗаявки.Ссылка = ГруппыЗаказчиковОбъектов.Ссылка
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ СистемыОбъектов КАК СистемыОбъектов
	|		ПО ОтобранныеЗаявки.Ссылка = СистемыОбъектов.Ссылка
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ЗаявкаНаРазработку КАК ЗаявкаНаРазработку
	|		ПО (ЗаявкаНаРазработку.Ссылка = ОтобранныеЗаявки.Ссылка)
	|			И (НЕ ЗаявкаНаРазработку.Черновик)
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.СтатусыОбъектов КАК СтатусыОбъектов
	|		ПО (СтатусыОбъектов.Порядок = ОтобранныеЗаявки.ПорядокСтатуса)
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.НазначенияЗадач КАК НазначенияЗадач
	|		ПО (ЗаявкаНаРазработку.Назначение = НазначенияЗадач.Ссылка)
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.СерьезностьОшибок КАК СерьезностьОшибок
	|		ПО (ЗаявкаНаРазработку.СерьезностьОшибки = СерьезностьОшибок.Ссылка)
	|		ЛЕВОЕ СОЕДИНЕНИЕ ТрудозатратыПоЗаявкам КАК ТрудозатратыПоЗаявкам
	|		ПО ОтобранныеЗаявки.Ссылка = ТрудозатратыПоЗаявкам.Заявка
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.Продукты КАК Продукты
	|		ПО (Продукты.Ссылка = ОтобранныеЗаявки.Продукт)
	|ГДЕ
	|	ВЫБОР
	|			КОГДА НЕ &ОтборПродуктГруппаЗаказчиков
	|				ТОГДА ИСТИНА
	|			КОГДА НЕ Продукты.Ссылка ЕСТЬ NULL
	|				ТОГДА Продукты.Ссылка В (&СписокПродуктГруппаЗаказчиков)
	|			ИНАЧЕ ГруппыЗаказчиковОбъектов.ГруппаЗаказчиков В (&СписокПродуктГруппаЗаказчиков)
	|		КОНЕЦ
	|{ГДЕ
	|	СтатусыОбъектов.Ссылка КАК Статус}
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ОтобранныеВнутренниеЗадания.Ссылка,
	|	ОтобранныеВнутренниеЗадания.Представление,
	|	ОтобранныеВнутренниеЗадания.Пользователь,
	|	ОтобранныеВнутренниеЗадания.ДатаНачалаРазработки,
	|	ОтобранныеВнутренниеЗадания.ДатаПоследнегоТестирования,
	|	ОтобранныеВнутренниеЗадания.ДнейВРаботе,
	|	ОтобранныеВнутренниеЗадания.ДнейНаРассмотрении,
	|	0,
	|	СтатусыОбъектов.Порядок,
	|	СтатусыОбъектов.ИндексКартинки,
	|	СтатусыОбъектов.Наименование,
	|	ЕСТЬNULL(СерьезностьОшибок.ИмяКартинки, НазначенияЗадач.ИмяКартинки),
	|	0,
	|	0,
	|	0,
	|	ЕСТЬNULL(ТрудозатратыПоВнутреннимЗаданиям.ФактическиеТрудозатраты, 0),
	|	0,
	|	0,
	|	0,
	|	0,
	|	1,
	|	0,
	|	ДАТАВРЕМЯ(1, 1, 1),
	|	ОтобранныеВнутренниеЗадания.ОбъектОснование,
	|	ОтобранныеВнутренниеЗадания.Продукт,
	|	ЛОЖЬ
	|ИЗ
	|	ОтобранныеВнутренниеЗадания КАК ОтобранныеВнутренниеЗадания
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.СтатусыОбъектов КАК СтатусыОбъектов
	|		ПО ОтобранныеВнутренниеЗадания.Статус = СтатусыОбъектов.Ссылка
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.НазначенияЗадач КАК НазначенияЗадач
	|		ПО ОтобранныеВнутренниеЗадания.Назначение = НазначенияЗадач.Ссылка
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.СерьезностьОшибок КАК СерьезностьОшибок
	|		ПО ОтобранныеВнутренниеЗадания.СерьезностьОшибки = СерьезностьОшибок.Ссылка
	|		ЛЕВОЕ СОЕДИНЕНИЕ ТрудозатратыПоВнутреннимЗаданиям КАК ТрудозатратыПоВнутреннимЗаданиям
	|		ПО ОтобранныеВнутренниеЗадания.Ссылка = ТрудозатратыПоВнутреннимЗаданиям.Ссылка
	|{ГДЕ
	|	ОтобранныеВнутренниеЗадания.Продукт КАК ОтборПродукт,
	|	СтатусыОбъектов.Ссылка КАК Статус}
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ОтобранныеВнутренниеТрудозатраты.Ссылка,
	|	ОтобранныеВнутренниеТрудозатраты.Представление,
	|	ОтобранныеВнутренниеТрудозатраты.Пользователь,
	|	ОтобранныеВнутренниеТрудозатраты.ДатаНачала,
	|	ДАТАВРЕМЯ(1, 1, 1),
	|	ОтобранныеВнутренниеТрудозатраты.ДнейВРаботе,
	|	0,
	|	0,
	|	0,
	|	&ИндексКартинкиВнутренниеТрудозатраты,
	|	"""",
	|	"""",
	|	0,
	|	0,
	|	0,
	|	ЕСТЬNULL(ТрудозатратыПоВнутреннимТрудозатратам.ФактическиеТрудозатраты, 0),
	|	0,
	|	0,
	|	0,
	|	0,
	|	0,
	|	1,
	|	ДАТАВРЕМЯ(1, 1, 1),
	|	ЗНАЧЕНИЕ(Документ.ЗаявкаНаРазработку.ПустаяСсылка),
	|	ЗНАЧЕНИЕ(Справочник.Продукты.ПустаяСсылка),
	|	ЛОЖЬ
	|ИЗ
	|	ОтобранныеВнутренниеТрудозатраты КАК ОтобранныеВнутренниеТрудозатраты
	|		ЛЕВОЕ СОЕДИНЕНИЕ ТрудозатратыПоВнутреннимТрудозатратам КАК ТрудозатратыПоВнутреннимТрудозатратам
	|		ПО ОтобранныеВнутренниеТрудозатраты.Ссылка = ТрудозатратыПоВнутреннимТрудозатратам.Задача
	|			И ОтобранныеВнутренниеТрудозатраты.Пользователь = ТрудозатратыПоВнутреннимТрудозатратам.Пользователь
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ОтобранныеЗаявки.Ссылка КАК Объект,
	|	""СпринтПланирование"" КАК СпринтИмяКартинки,
	|	МИНИМУМ(ДокументСпринт.ДатаНачала) КАК ДатаНачала
	|ПОМЕСТИТЬ ОбъектыСоСпринтами
	|ИЗ
	|	РегистрСведений.СоставСпринтов КАК СоставСпринтов
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ОтобранныеЗаявки КАК ОтобранныеЗаявки
	|		ПО СоставСпринтов.Объект = ОтобранныеЗаявки.Ссылка
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.Спринт КАК ДокументСпринт
	|		ПО СоставСпринтов.Спринт = ДокументСпринт.Ссылка
	|ГДЕ
	|	КОНЕЦПЕРИОДА(ДокументСпринт.ДатаОкончания, ДЕНЬ) >= &ТекущаяДата
	|
	|СГРУППИРОВАТЬ ПО
	|	ОтобранныеЗаявки.Ссылка
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ОтобранныеЗадачи.Ссылка,
	|	""Спринт"",
	|	МИНИМУМ(ДокументСпринт.ДатаНачала)
	|ИЗ
	|	РегистрСведений.СоставСпринтов КАК СоставСпринтов
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ОтобранныеЗадачи КАК ОтобранныеЗадачи
	|		ПО СоставСпринтов.Объект = ОтобранныеЗадачи.Ссылка
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.Спринт КАК ДокументСпринт
	|		ПО СоставСпринтов.Спринт = ДокументСпринт.Ссылка
	|ГДЕ
	|	КОНЕЦПЕРИОДА(ДокументСпринт.ДатаОкончания, ДЕНЬ) >= &ТекущаяДата
	|
	|СГРУППИРОВАТЬ ПО
	|	ОтобранныеЗадачи.Ссылка
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ОтобранныеВнутренниеЗадания.Ссылка,
	|	""СпринтЗадания"",
	|	МИНИМУМ(ДокументСпринт.ДатаНачала)
	|ИЗ
	|	РегистрСведений.СоставСпринтов КАК СоставСпринтов
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ОтобранныеВнутренниеЗадания КАК ОтобранныеВнутренниеЗадания
	|		ПО СоставСпринтов.Объект = ОтобранныеВнутренниеЗадания.Ссылка
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.Спринт КАК ДокументСпринт
	|		ПО СоставСпринтов.Спринт = ДокументСпринт.Ссылка
	|ГДЕ
	|	КОНЕЦПЕРИОДА(ДокументСпринт.ДатаОкончания, ДЕНЬ) >= &ТекущаяДата
	|
	|СГРУППИРОВАТЬ ПО
	|	ОтобранныеВнутренниеЗадания.Ссылка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	СтатистикаСтатусовНазначенныхЗадачСрезПоследних.Задача КАК Задача,
	|	СтатистикаСтатусовНазначенныхЗадачСрезПоследних.Пользователь КАК Пользователь,
	|	СтатистикаСтатусовНазначенныхЗадачСрезПоследних.Период КАК Период,
	|	СтатистикаСтатусовНазначенныхЗадачСрезПоследних.Статус КАК Статус
	|ПОМЕСТИТЬ ПериодПоследнегоСтатусаЗадач
	|ИЗ
	|	РегистрСведений.СтатистикаСтатусовНазначенныхЗадач.СрезПоследних(
	|			,
	|			(Задача, Статус, Пользователь) В
	|				(ВЫБРАТЬ
	|					ОтобранныеЗадачи.Ссылка КАК Задача,
	|					ОтобранныеЗадачи.Статус КАК Статус,
	|					ОтобранныеЗадачи.Пользователь КАК Пользователь
	|				ИЗ
	|					ОтобранныеЗадачи КАК ОтобранныеЗадачи)) КАК СтатистикаСтатусовНазначенныхЗадачСрезПоследних
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ГруппыЗаказчиковОбъектов.Ссылка КАК Ссылка,
	|	ГруппыЗаказчиковОбъектов.ГруппаЗаказчиков КАК ГруппаЗаказчиков
	|ИЗ
	|	ГруппыЗаказчиковОбъектов КАК ГруппыЗаказчиковОбъектов
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ПользователиДляВывода.Ссылка КАК Сотрудник,
	|	ПользователиДляВывода.Должность КАК Должность,
	|	ПользователиДляВывода.ФИО КАК ПредставлениеСотрудник,
	|	ПользователиДляВывода.Уволен КАК Уволен,
	|	ПользователиДляВывода.ПриоритетСтроки КАК ПриоритетСтроки,
	|	ЕСТЬNULL(СистемыОбъектов.ПредставлениеСистем, """") КАК ПредставлениеСистем,
	|	ЕСТЬNULL(ОбщиеТрудозатратыПоСотрудникам.ОбщиеТрудозатраты, 0) КАК ОбщиеТрудозатраты,
	|	ЕСТЬNULL(ПланируемоеВремяСотрудника.ПлановоеВремя, 0) КАК ОбщееПлановоеВремя,
	|	ЕСТЬNULL(ОтобранныеОбъекты.Ссылка, ЗНАЧЕНИЕ(Документ.Задача.ПустаяСсылка)) КАК Задача,
	|	ЕСТЬNULL(ОтобранныеОбъекты.Представление, """") КАК ПредставлениеЗадача,
	|	ЕСТЬNULL(ОтобранныеОбъекты.ДатаНачалаРазработки, ДАТАВРЕМЯ(1, 1, 1)) КАК ДатаНачалаРазработки,
	|	ЕСТЬNULL(ОтобранныеОбъекты.ДатаПоследнегоТестирования, ДАТАВРЕМЯ(1, 1, 1)) КАК ДатаПоследнегоТестирования,
	|	ЕСТЬNULL(ОтобранныеОбъекты.ДнейВРаботе, 0) КАК ДнейВРаботе,
	|	ЕСТЬNULL(ОтобранныеОбъекты.ДнейНаРассмотрении, 0) КАК ДнейНаРассмотрении,
	|	ЕСТЬNULL(ОтобранныеОбъекты.ПорядокСтатуса, 0) КАК ПорядокСтатуса,
	|	ЕСТЬNULL(ОтобранныеОбъекты.ИндексКартинки, 0) КАК ИндексКартинки,
	|	ЕСТЬNULL(ОтобранныеОбъекты.СтатусПредставление, 0) КАК СтатусПредставление,
	|	ЕСТЬNULL(ОтобранныеОбъекты.НазначениеИмяКартинки, НЕОПРЕДЕЛЕНО) КАК НазначениеИмяКартинки,
	|	ЕСТЬNULL(ОбъектыСоСпринтами.СпринтИмяКартинки, НЕОПРЕДЕЛЕНО) КАК СпринтИмяКартинки,
	|	ЕСТЬNULL(ОтобранныеОбъекты.ОценкаStoryPoint, 0) КАК ОценкаStoryPoint,
	|	ЕСТЬNULL(ОтобранныеОбъекты.ПланируемыеТрудозатраты, 0) КАК ПланируемыеТрудозатраты,
	|	ЕСТЬNULL(ОтобранныеОбъекты.ОбщийПланСотрудника, 0) КАК ОбщийПланСотрудника,
	|	ЕСТЬNULL(ОтобранныеОбъекты.ПорядокЗаявки, 0) КАК ПорядокЗаявки,
	|	ВЫБОР
	|		КОГДА ПользователиДляВывода.ФИО = ""Не назначена""
	|			ТОГДА -1
	|		ИНАЧЕ 1
	|	КОНЕЦ * ЕСТЬNULL(ОтобранныеОбъекты.ПорядокЗаявки, 0) КАК ПорядокЗаявкиСортировка,
	|	ЕСТЬNULL(ОтобранныеОбъекты.ФактическиеТрудозатраты, 0) КАК ФактическиеТрудозатраты,
	|	ЕСТЬNULL(ОтобранныеОбъекты.ЦиклыТестирования, 0) КАК ЦиклыТестирования,
	|	ЕСТЬNULL(ОтобранныеОбъекты.ЦиклыCR, 0) КАК ЦиклыCR,
	|	ЕСТЬNULL(ОтобранныеОбъекты.КоличествоЗадач, 0) КАК КоличествоЗадач,
	|	ЕСТЬNULL(ОтобранныеОбъекты.КоличествоЗаявок, 0) КАК КоличествоЗаявок,
	|	ЕСТЬNULL(ОтобранныеОбъекты.КоличествоВнутреннихЗаданий, 0) КАК КоличествоВнутреннихЗаданий,
	|	ЕСТЬNULL(ОтобранныеОбъекты.КоличествоВнутреннихТрудозатрат, 0) КАК КоличествоВнутреннихТрудозатрат,
	|	ЕСТЬNULL(СложностиЗадач.Сложность, 0) КАК СложностьЗадачи,
	|	ЕСТЬNULL(ОтобранныеОбъекты.СрокРеализации, ДАТАВРЕМЯ(1, 1, 1)) КАК СрокРеализации,
	|	ЕСТЬNULL(ОбъектыСоСпринтами.ДатаНачала, ДАТАВРЕМЯ(1, 1, 1)) КАК ДатаНачалаСпринт,
	|	ОтобранныеОбъекты.ОбъектОснование КАК ОбъектОснование,
	|	ПРЕДСТАВЛЕНИЕ(ОтобранныеОбъекты.Продукт) КАК Продукт,
	|	ОтобранныеОбъекты.ЦелеваяЗаявка КАК ЦелеваяЗаявка,
	|	ЕСТЬNULL(РАЗНОСТЬДАТ(ПериодПоследнегоСтатусаЗадач.Период, &ТекущаяДата, ДЕНЬ), 0) КАК ДнейНаСотруднике
	|ИЗ
	|	ПользователиДляВывода КАК ПользователиДляВывода
	|		ЛЕВОЕ СОЕДИНЕНИЕ ОбщиеТрудозатратыПоСотрудникам КАК ОбщиеТрудозатратыПоСотрудникам
	|		ПО ПользователиДляВывода.Ссылка = ОбщиеТрудозатратыПоСотрудникам.Ссылка
	|		ЛЕВОЕ СОЕДИНЕНИЕ ПланируемоеВремяСотрудника КАК ПланируемоеВремяСотрудника
	|		ПО ПользователиДляВывода.Ссылка = ПланируемоеВремяСотрудника.Сотрудник
	|		ЛЕВОЕ СОЕДИНЕНИЕ ОтобранныеОбъекты КАК ОтобранныеОбъекты
	|		ПО ПользователиДляВывода.Ссылка = ОтобранныеОбъекты.Пользователь
	|		ЛЕВОЕ СОЕДИНЕНИЕ ОбъектыСоСпринтами КАК ОбъектыСоСпринтами
	|		ПО (ОтобранныеОбъекты.Ссылка = ОбъектыСоСпринтами.Объект)
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.СложностиЗадач КАК СложностиЗадач
	|		ПО (ОтобранныеОбъекты.Ссылка = СложностиЗадач.Объект)
	|		ЛЕВОЕ СОЕДИНЕНИЕ СистемыОбъектов КАК СистемыОбъектов
	|		ПО (ОтобранныеОбъекты.Ссылка = СистемыОбъектов.Ссылка)
	|		ЛЕВОЕ СОЕДИНЕНИЕ ПериодПоследнегоСтатусаЗадач КАК ПериодПоследнегоСтатусаЗадач
	|		ПО ПользователиДляВывода.Ссылка = ПериодПоследнегоСтатусаЗадач.Пользователь
	|			И (ОтобранныеОбъекты.Ссылка = ПериодПоследнегоСтатусаЗадач.Задача)
	|ГДЕ
	|	ВЫБОР
	|			КОГДА &Задача = НЕОПРЕДЕЛЕНО
	|				ТОГДА ИСТИНА
	|			ИНАЧЕ ЕСТЬNULL(ОтобранныеОбъекты.Ссылка, ЗНАЧЕНИЕ(Документ.Задача.ПустаяСсылка)) = &Задача
	|		КОНЕЦ";
	#КонецОбласти
	
	ТекущаяДата = ТекущаяДатаСеанса();

	СписокПродуктГруппаЗаказчиков = Новый СписокЗначений;
	
	Если НастройкиСписка.ФильтроватьПоГруппамЗаказчиков <> Неопределено
		И НастройкиСписка.ФильтроватьПоГруппамЗаказчиков.Количество() > 0 Тогда	
		Для Каждого Элемент Из НастройкиСписка.ФильтроватьПоГруппамЗаказчиков Цикл
			СписокПродуктГруппаЗаказчиков.Добавить(Элемент.Значение);
		КонецЦикла;
	КонецЕсли;
	
	Если НастройкиСписка.ФильтроватьПоПродуктам <> Неопределено
		И НастройкиСписка.ФильтроватьПоПродуктам.Количество() > 0 Тогда	
		
		Для Каждого Элемент Из НастройкиСписка.ФильтроватьПоПродуктам Цикл
			СписокПродуктГруппаЗаказчиков.Добавить(Элемент.Значение);
		КонецЦикла;
	КонецЕсли;
	
	ПостроительЗапроса.Параметры.Вставить("ИсключаемыеСтатусы", ИсключаемыеСтатусы);
	ПостроительЗапроса.Параметры.Вставить("ВыбранныеФилиалы", НастройкиСписка.ВыбранныйФилиалIT);
	ПостроительЗапроса.Параметры.Вставить("ТекущаяДата", ТекущаяДата);
	ПостроительЗапроса.Параметры.Вставить("Задача", Неопределено);
	ПостроительЗапроса.Параметры.Вставить("НачалоМесяца", НачалоМесяца(ТекущаяДата));
	ПостроительЗапроса.Параметры.Вставить("КонецМесяца", КонецМесяца(ТекущаяДата));
	ПостроительЗапроса.Параметры.Вставить("ПорядокСтатусаПриостановлен", 
		ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Справочники.СтатусыОбъектов.Приостановлен, "Порядок"));
	ПостроительЗапроса.Параметры.Вставить("ПорядокСтатусаВРаботе", 
		ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Справочники.СтатусыОбъектов.ВРаботе, "Порядок"));
	ПостроительЗапроса.Параметры.Вставить("ИндексКартинкиВнутренниеТрудозатраты", 
		Справочники.СтатусыОбъектов.ИндексКартинкиВнутренниеТрудозатраты());
	ПостроительЗапроса.Параметры.Вставить("ОтборПродуктГруппаЗаказчиков", СписокПродуктГруппаЗаказчиков.Количество() > 0);
	ПостроительЗапроса.Параметры.Вставить("СписокПродуктГруппаЗаказчиков", СписокПродуктГруппаЗаказчиков);
		
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	Филиалы.Родитель КАК Родитель
	|ИЗ
	|	Справочник.Филиалы КАК Филиалы
	|ГДЕ
	|	Филиалы.Ссылка В(&СписокФилиалов)
	|	И Филиалы.Родитель <> ЗНАЧЕНИЕ(Справочник.Филиалы.ПустаяСсылка)";
	
	Запрос.УстановитьПараметр("СписокФилиалов", НастройкиСписка.ВыбранныйФилиалIT);	
	СписокСодержитКоманды = НЕ Запрос.Выполнить().Пустой();
	
	// Если необходимо вывести только назначенные задачи, тогда отбираем по приоритету.
	Если НастройкиСписка.ПоказатьТолькоНазначенные Тогда
		ОбщегоНазначения.ПостроительЗапросаУстановитьОтбор(ПостроительЗапроса, "ПриоритетСтроки", 
			ВидСравнения.Больше, 2);           
		
	ИначеЕсли СписокСодержитКоманды Тогда 
		ОбщегоНазначения.ПостроительЗапросаУстановитьОтбор(ПостроительЗапроса, "КомандаРазработчиков", 
			ВидСравнения.ВСпискеПоИерархии, НастройкиСписка.ВыбранныйФилиалIT);   	
	КонецЕсли;
	
	Если НЕ НастройкиСписка.ПоказатьЗаявки Тогда
		ОбщегоНазначения.ПостроительЗапросаУстановитьОтбор(ПостроительЗапроса, "ПоказатьЗаявки", 
			ВидСравнения.Больше, 2);
	КонецЕсли;
	
	Если НЕ НастройкиСписка.ПоказатьВнутренниеЗадания Тогда
		ОбщегоНазначения.ПостроительЗапросаУстановитьОтбор(ПостроительЗапроса, "ПоказатьВнутренниеЗадания", 
			ВидСравнения.Больше, 2);
	КонецЕсли;
	
	Если НЕ НастройкиСписка.ПоказыватьВнутренниеТрудозатраты Тогда	
		ОбщегоНазначения.ПостроительЗапросаУстановитьОтбор(ПостроительЗапроса, "ПоказыватьВнутренниеТрудозатраты", 
			ВидСравнения.Больше, 2);
	КонецЕсли;  
		
	Если НастройкиСписка.ФильтроватьПоСистемам <> Неопределено
		И НастройкиСписка.ФильтроватьПоСистемам.Количество() > 0 Тогда
		
		ОбщегоНазначения.ПостроительЗапросаУстановитьОтбор(ПостроительЗапроса, "Система", 
			ВидСравнения.ВСписке, НастройкиСписка.ФильтроватьПоСистемам);
	КонецЕсли;
		
	Если НастройкиСписка.ФильтроватьПоСтатусам <> Неопределено
		И НастройкиСписка.ФильтроватьПоСтатусам.Количество() > 0 Тогда
		
		ОбщегоНазначения.ПостроительЗапросаУстановитьОтбор(ПостроительЗапроса, "Статус", 
			ВидСравнения.ВСписке, НастройкиСписка.ФильтроватьПоСтатусам);
	КонецЕсли;
		
	Запрос = ПостроительЗапроса.ПолучитьЗапрос();
	МассивПакетов = Запрос.ВыполнитьПакет();
	
	ИсточникДанных = Новый ОписаниеИсточникаДанных(МассивПакетов[МассивПакетов.ВГраница()]);	
	ИсточникДанных.Колонки.Сотрудник.Измерение = НастройкиСписка.ГруппироватьПоПользователям;	
	
	ПостроительЗапроса = Новый ПостроительЗапроса;
	ПостроительЗапроса.ИсточникДанных = ИсточникДанных;
	
	ИсточникДанных.Колонки.Должность.Итог = "МИНИМУМ(Должность)";
	ИсточникДанных.Колонки.ПредставлениеСотрудник.Итог = "МИНИМУМ(ПредставлениеСотрудник)";
	ИсточникДанных.Колонки.Уволен.Итог = "МИНИМУМ(Уволен)";
	ИсточникДанных.Колонки.ПриоритетСтроки.Итог = "МИНИМУМ(ПриоритетСтроки)";	
	
	ИсточникДанных.Колонки.ОбщиеТрудозатраты.Итог = "МАКСИМУМ(ОбщиеТрудозатраты)";
	ИсточникДанных.Колонки.ОбщееПлановоеВремя.Итог = "МАКСИМУМ(ОбщееПлановоеВремя)";
	
	ИсточникДанных.Колонки.КоличествоЗадач.Итог = "СУММА(КоличествоЗадач)";
	ИсточникДанных.Колонки.КоличествоЗаявок.Итог = "СУММА(КоличествоЗаявок)";
	ИсточникДанных.Колонки.КоличествоВнутреннихЗаданий.Итог = "СУММА(КоличествоВнутреннихЗаданий)";
	ИсточникДанных.Колонки.КоличествоВнутреннихТрудозатрат.Итог = "СУММА(КоличествоВнутреннихТрудозатрат)";	
	ИсточникДанных.Колонки.ПланируемыеТрудозатраты.Итог = "СУММА(ПланируемыеТрудозатраты)";
	ИсточникДанных.Колонки.ОбщийПланСотрудника.Итог = "СУММА(ОбщийПланСотрудника)";
	ИсточникДанных.Колонки.ОценкаStoryPoint.Итог = "СУММА(ОценкаStoryPoint)";
	
	ПостроительЗапроса.Порядок.Добавить("ПриоритетСтроки");
	ПостроительЗапроса.Порядок.Добавить("ПредставлениеСотрудник");
	ПостроительЗапроса.Порядок.Добавить("ПорядокЗаявкиСортировка",,, НаправлениеСортировки.Убыв);
	ПостроительЗапроса.Порядок.Добавить("ПорядокСтатуса");
	ПостроительЗапроса.Порядок.Добавить("ДатаНачалаРазработки");
	
	ПостроительЗапроса.Выполнить();
	
	ДополнительныеДанные = Новый Структура;
	ДополнительныеДанные.Вставить("Измерения", ПостроительЗапроса.Измерения);
	ДополнительныеДанные.Вставить("СтрокиКСортировке", Новый Соответствие);
	ДополнительныеДанные.Вставить("Объект", Неопределено);
	ДополнительныеДанные.Вставить("Перезаполнение", Ложь);
	ДополнительныеДанные.Вставить("ВидДерева", "УправлениеНагрузкой");
	ДополнительныеДанные.Вставить("ИндексКартинки_Группа", Справочники.СтатусыОбъектов.ИндексКартинкиГруппа());
	ДополнительныеДанные.Вставить("ТаблицаПроизводственныйКалендарь", ПолучитьТаблицуПроизводственногоКалендаря());
	ДополнительныеДанные.Вставить("СсылкаПрочиеЗаказчики", Справочники.ГруппыЗаказчиковНаправлений.ПрочиеЗаказчики);
	ДополнительныеДанные.Вставить("НастройкиСписка", НастройкиСписка);
	ДополнительныеДанные.Вставить("ПолеОбъекта", "Задача");
	ДополнительныеДанные.Вставить("ПолеПредставлениеОбъекта", "ПредставлениеЗадача");
	ДополнительныеДанные.Вставить("ГруппыЗаказчиков", МассивПакетов[МассивПакетов.ВГраница() - 1].Выгрузить());
	ДополнительныеДанные.Вставить("ВывестиДниНаСотруднике", Истина);
	
	УправлениеИнструментамиРазработки.ОбработатьВыборку(ПостроительЗапроса.Результат, Дерево, Новый Структура, Дерево, 0, ДополнительныеДанные);
	УправлениеИнструментамиРазработки.ОтсортироватьДерево(ДополнительныеДанные.СтрокиКСортировке, Дерево, ПостроительЗапроса.Порядок);
	
	ДанныеДерева = ИнструментыСервер.ПодготовитьДанныеИнструментаДляКлиента(Дерево, КолонкиИнструмента);
	
	Возврат ДанныеДерева;
	
КонецФункции

Функция ПолучитьТаблицуПроизводственногоКалендаря() Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ПроизводственныйКалендарь.ДатаКалендаря КАК ДатаКалендаря,
	|	ПроизводственныйКалендарь.ВидДня КАК ВидДня
	|ИЗ
	|	РегистрСведений.ПроизводственныйКалендарь КАК ПроизводственныйКалендарь";
	
	Возврат Запрос.Выполнить().Выгрузить(); 
	
КонецФункции

#КонецОбласти
