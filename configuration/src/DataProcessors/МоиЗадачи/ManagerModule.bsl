///////////////////////////////////////////////////////////////////////////////////////////////////////
// Copyright (c) 2025, ООО ДНС Технологии
// SDMS (Software Development Management System) — это корпоративная система учета разработки и управления проектами 
// Все права защищены. Эта программа и сопроводительные материалы предоставляются 
// в соответствии с условиями лицензии General Public License (GNU GPL v3)
// Текст лицензии доступен по ссылке:
// https://www.gnu.org/licenses/gpl-3.0.html
///////////////////////////////////////////////////////////////////////////////////////////////////////

#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

Процедура ПолучитьДанныеЗадачиНаМне(ВариантГруппировки, АдресРезультата, УИДЗамера) Экспорт
	
	ВремяНачала = ОценкаПроизводительности.НачатьЗамерВремени();
	
	ДанныеДерева = СформироватьСписокЗадачиНаМне(ВариантГруппировки);
	ИнструментыСервер.ОповеститьИнструментОбОбновлении(ДанныеДерева, АдресРезультата, УИДЗамера);
	
	ОценкаПроизводительности.ЗакончитьЗамерВремениТехнологический("Обработки.МоиЗадачи.ПолучитьДанныеЗадачиНаМне.ВыполнениеФонового", ВремяНачала, , Строка(УИДЗамера));
	
КонецПроцедуры

Процедура ПолучитьДанныеОтслеживаемыеЗадачи(ВариантГруппировки, АдресТаблицыОтслеживаемыеЗадачи, ПолноеОбновление, АдресРезультата, УИДЗамера) Экспорт
	
	ВремяНачала = ОценкаПроизводительности.НачатьЗамерВремени();
	
	ДанныеДерева = СформироватьСписокОтслеживаемыеЗадачи(ВариантГруппировки, АдресТаблицыОтслеживаемыеЗадачи, ПолноеОбновление);
	ИнструментыСервер.ОповеститьИнструментОбОбновлении(ДанныеДерева, АдресРезультата, УИДЗамера);
	
	ОценкаПроизводительности.ЗакончитьЗамерВремениТехнологический("Обработки.МоиЗадачи.ПолучитьДанныеОтслеживаемыеЗадачи.ВыполнениеФонового", ВремяНачала, , Строка(УИДЗамера));
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытий

#КонецОбласти

#Область СлужебныйПрограммныйИнтерфейс

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция ПолучитьДеревоОтслеживаемыеЗадачи(ВариантГруппировки, ТаблицаОтслеживаемыхЗадач, КолонкиИнструмента)
	
	ОтслеживаемыеЗадачи = Новый ДеревоЗначений;
	Для Каждого Колонка Из КолонкиИнструмента Цикл
		ОтслеживаемыеЗадачи.Колонки.Добавить(Колонка);
	КонецЦикла;
	
	ИсточникДанных = Новый ОписаниеИсточникаДанных(ТаблицаОтслеживаемыхЗадач);
	ИсточникДанных.Колонки.Статус.Измерение = Истина;
	ИсточникДанных.Колонки.Роль.Измерение = Истина;
	
	ПостроительЗапроса = Новый ПостроительЗапроса;
	ПостроительЗапроса.ИсточникДанных = ИсточникДанных;
	ПостроительЗапроса.Измерения.Очистить();
	
	Если ВариантГруппировки = 1 Тогда
		ПостроительЗапроса.Измерения.Добавить("Статус");
		ИсточникДанных.Колонки.ИндексКартинки.Итог = "Максимум(ИндексКартинки)";
		ИсточникДанных.Колонки.ПорядокСтатуса.Итог = "Максимум(ПорядокСтатуса)";
	ИначеЕсли ВариантГруппировки = 2 Тогда
		ПостроительЗапроса.Измерения.Добавить("Роль");
	КонецЕсли;
	
	ПостроительЗапроса.Порядок.Добавить("ПорядокСтатуса");
	ПостроительЗапроса.Порядок.Добавить("ПредставлениеЗадача");	

	ПостроительЗапроса.Выполнить();
	
	ДополнительныеДанные = Новый Структура;
	ДополнительныеДанные.Вставить("Измерения", ПостроительЗапроса.Измерения); 
	ДополнительныеДанные.Вставить("СтрокиКСортировке", Новый Соответствие); 
	ДополнительныеДанные.Вставить("Объект", Неопределено); 
	ДополнительныеДанные.Вставить("Перезаполнение", Истина);
	ДополнительныеДанные.Вставить("ВидДерева", "ОтслеживаемыеЗадачи");
	ДополнительныеДанные.Вставить("ВариантГруппировки", ВариантГруппировки);  
	ДополнительныеДанные.Вставить("ИндексКартинки_Группа", -1); 	
	ДополнительныеДанные.Вставить("ПолеОбъекта", "Ссылка"); 			                 
	ДополнительныеДанные.Вставить("ПолеПредставлениеОбъекта", "ПредставлениеЗадача"); 	
		
	УправлениеИнструментамиРазработки.ОбработатьВыборку(ПостроительЗапроса.Результат, ОтслеживаемыеЗадачи, 
		Новый Структура, ОтслеживаемыеЗадачи, 0, ДополнительныеДанные);
	УправлениеИнструментамиРазработки.ОтсортироватьДерево(ДополнительныеДанные.СтрокиКСортировке, 
		ОтслеживаемыеЗадачи, ПостроительЗапроса.Порядок);
	
	Возврат ОтслеживаемыеЗадачи;
	
КонецФункции

Функция ПолучитьТаблицуОтлеживаемыхЗадач()
	
	Запрос = Новый Запрос;	
	Запрос.Текст =
	#Область ТекстЗапроса
	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	Участники.Объект КАК Объект
	|ПОМЕСТИТЬ ВсеЗадачиУчастника
	|ИЗ
	|	РегистрСведений.Участники КАК Участники
	|ГДЕ
	|	Участники.Пользователь = &ТекущийПользователь
	|	И Участники.ПолучатьОповещения
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	СвойстваЗадачСрезПоследних.Объект КАК Объект,
	|	СвойстваЗадачСрезПоследних.Статус КАК Статус,
	|	СвойстваЗадачСрезПоследних.Исполнитель КАК Исполнитель
	|ПОМЕСТИТЬ ВсеЗадачиНеНаМне
	|ИЗ
	|	РегистрСведений.СвойстваЗадач.СрезПоследних(
	|			,
	|			Объект В
	|				(ВЫБРАТЬ
	|					ВсеЗадачиУчастника.Объект
	|				ИЗ
	|					ВсеЗадачиУчастника КАК ВсеЗадачиУчастника)) КАК СвойстваЗадачСрезПоследних
	|ГДЕ
	|	СвойстваЗадачСрезПоследних.Исполнитель <> &ТекущийПользователь
	|	И НЕ СвойстваЗадачСрезПоследних.Статус В (ЗНАЧЕНИЕ(Справочник.СтатусыОбъектов.Решен), ЗНАЧЕНИЕ(Справочник.СтатусыОбъектов.Отклонен))
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Задача.Ссылка КАК Объект,
	|	Задача.Статус КАК Статус,
	|	""["" + Задача.Номер + ""] "" + Задача.Наименование КАК Представление,
	|	Задача.ОбъектОснование КАК ОбъектОснование,
	|	СтатусыОбъектов.Порядок КАК ПорядокСтатуса,
	|	СтатусыОбъектов.ИндексКартинки КАК ИндексКартинки
	|ПОМЕСТИТЬ ЗадачиЗаказанныеМной
	|ИЗ
	|	Документ.Задача КАК Задача
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.СтатусыОбъектов КАК СтатусыОбъектов
	|		ПО Задача.Статус = СтатусыОбъектов.Ссылка
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВсеЗадачиНеНаМне КАК ВсеЗадачиНеНаМне
	|		ПО Задача.Ссылка = ВсеЗадачиНеНаМне.Объект
	|ГДЕ
	|	ВсеЗадачиНеНаМне.Объект ЕСТЬ NULL
	|	И Задача.Заказчик = &ТекущийПользователь
	|	И НЕ Задача.Статус В (ЗНАЧЕНИЕ(Справочник.СтатусыОбъектов.Решен), ЗНАЧЕНИЕ(Справочник.СтатусыОбъектов.Отклонен))
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ЗаявкаНаРазработку.Ссылка КАК Объект,
	|	МинимальныеСтатусыЗаявок.Статус КАК Статус,
	|	""["" + ЗаявкаНаРазработку.Номер + ""] "" + ЗаявкаНаРазработку.Наименование КАК Представление,
	|	ЗаявкаНаРазработку.Дата КАК Дата,
	|	СтатусыОбъектов.Порядок КАК ПорядокСтатуса,
	|	СтатусыОбъектов.ИндексКартинки КАК ИндексКартинки,
	|	ЗаявкаНаРазработку.СрокРеализации КАК СрокРеализации
	|ПОМЕСТИТЬ ЗаявкиЗаказанныеМной
	|ИЗ
	|	Документ.ЗаявкаНаРазработку КАК ЗаявкаНаРазработку
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.МинимальныеСтатусыЗаявок КАК МинимальныеСтатусыЗаявок
	|		ПО ЗаявкаНаРазработку.Ссылка = МинимальныеСтатусыЗаявок.Заявка
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.СтатусыОбъектов КАК СтатусыОбъектов
	|		ПО (МинимальныеСтатусыЗаявок.Статус = СтатусыОбъектов.Ссылка)
	|			И (СтатусыОбъектов.Ссылка <> ЗНАЧЕНИЕ(Справочник.СтатусыОбъектов.Отклонен))
	|			И (СтатусыОбъектов.Ссылка <> ЗНАЧЕНИЕ(Справочник.СтатусыОбъектов.Решен))
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ЗначенияДополнительныхРеквизитовОбъектов КАК КоличествоПодчиненныхЗадач
	|		ПО (КоличествоПодчиненныхЗадач.Объект = ЗаявкаНаРазработку.Ссылка)
	|			И (КоличествоПодчиненныхЗадач.Реквизит = ЗНАЧЕНИЕ(ПланВидовХарактеристик.ВидыДополнительныхРеквизитов.КоличествоПодчиненныхЗадач))
	|		ЛЕВОЕ СОЕДИНЕНИЕ ЗадачиЗаказанныеМной КАК ЗадачиЗаказанныеМной
	|		ПО (ЗадачиЗаказанныеМной.ОбъектОснование = ЗаявкаНаРазработку.Ссылка)
	|ГДЕ
	|	ЗаявкаНаРазработку.Автор = &ТекущийПользователь
	|	И ЕСТЬNULL(КоличествоПодчиненныхЗадач.Значение, 0) = 0
	|	И ЗадачиЗаказанныеМной.ОбъектОснование ЕСТЬ NULL
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	СрокиСдачиЗадач.Задача КАК Объект,
	|	СрокиСдачиЗадач.СрокСдачи КАК СрокСдачи
	|ПОМЕСТИТЬ СрокиСдачиЗадач
	|ИЗ
	|	РегистрСведений.СрокиСдачиЗадач.СрезПоследних(
	|			,
	|			Задача В
	|				(ВЫБРАТЬ
	|					ВсеЗадачиНеНаМне.Объект
	|				ИЗ
	|					ВсеЗадачиНеНаМне)) КАК СрокиСдачиЗадач
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	СрокиСдачиЗадач.Задача,
	|	СрокиСдачиЗадач.СрокСдачи
	|ИЗ
	|	РегистрСведений.СрокиСдачиЗадач.СрезПоследних(
	|			,
	|			Задача В
	|				(ВЫБРАТЬ
	|					ЗадачиЗаказанныеМной.Объект
	|				ИЗ
	|					ЗадачиЗаказанныеМной)) КАК СрокиСдачиЗадач
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВсеЗадачиНеНаМне.Объект КАК Объект,
	|	Участники.Роль КАК Роль,
	|	1 КАК Приоритет
	|ПОМЕСТИТЬ РолиПользователя
	|ИЗ
	|	ВсеЗадачиНеНаМне КАК ВсеЗадачиНеНаМне
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.Участники КАК Участники
	|		ПО ВсеЗадачиНеНаМне.Объект = Участники.Объект
	|			И (Участники.Пользователь = &ТекущийПользователь)
	|			И (Участники.Роль = ЗНАЧЕНИЕ(Справочник.РолиУчастников.Исполнитель))
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ВсеЗадачиНеНаМне.Объект,
	|	Участники.Роль,
	|	ВЫБОР
	|		КОГДА Участники.Роль = ЗНАЧЕНИЕ(Справочник.РолиУчастников.Заказчик)
	|			ТОГДА 0
	|		КОГДА Участники.Роль = ЗНАЧЕНИЕ(Справочник.РолиУчастников.Ревьюер)
	|			ТОГДА 2
	|		КОГДА Участники.Роль = ЗНАЧЕНИЕ(Справочник.РолиУчастников.Тестировщик)
	|			ТОГДА 3
	|		КОГДА Участники.Роль = ЗНАЧЕНИЕ(Справочник.РолиУчастников.Автор)
	|			ТОГДА 4
	|		КОГДА Участники.Роль = ЗНАЧЕНИЕ(Справочник.РолиУчастников.Наблюдатель)
	|			ТОГДА 5
	|		ИНАЧЕ 6
	|	КОНЕЦ
	|ИЗ
	|	ВсеЗадачиНеНаМне КАК ВсеЗадачиНеНаМне
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.Участники КАК Участники
	|		ПО ВсеЗадачиНеНаМне.Объект = Участники.Объект
	|			И (Участники.Пользователь = &ТекущийПользователь)
	|			И (Участники.ПолучатьОповещения)
	|			И (Участники.Роль <> ЗНАЧЕНИЕ(Справочник.РолиУчастников.Исполнитель))
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ЗадачиЗаказанныеМной.Объект,
	|	ЗНАЧЕНИЕ(Справочник.РолиУчастников.Заказчик),
	|	0
	|ИЗ
	|	ЗадачиЗаказанныеМной КАК ЗадачиЗаказанныеМной
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ЗаявкиЗаказанныеМной.Объект,
	|	ЗНАЧЕНИЕ(Справочник.РолиУчастников.Заказчик),
	|	0
	|ИЗ
	|	ЗаявкиЗаказанныеМной КАК ЗаявкиЗаказанныеМной
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	РолиПользователя.Объект КАК Объект,
	|	МИНИМУМ(РолиПользователя.Приоритет) КАК Приоритет
	|ПОМЕСТИТЬ ПриоритетРолиДляГруппировки
	|ИЗ
	|	РолиПользователя КАК РолиПользователя
	|
	|СГРУППИРОВАТЬ ПО
	|	РолиПользователя.Объект
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ПриоритетРолиДляГруппировки.Объект КАК Объект,
	|	МИНИМУМ(РолиПользователя.Роль) КАК Роль
	|ПОМЕСТИТЬ РольПользователя
	|ИЗ
	|	ПриоритетРолиДляГруппировки КАК ПриоритетРолиДляГруппировки
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РолиПользователя КАК РолиПользователя
	|		ПО ПриоритетРолиДляГруппировки.Объект = РолиПользователя.Объект
	|			И ПриоритетРолиДляГруппировки.Приоритет = РолиПользователя.Приоритет
	|
	|СГРУППИРОВАТЬ ПО
	|	ПриоритетРолиДляГруппировки.Объект
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВсеЗадачиНеНаМне.Объект КАК Ссылка,
	|	Задача.Статус КАК Статус,
	|	""["" + Задача.Номер + ""] "" + Задача.Наименование КАК ПредставлениеЗадача,
	|	СтатусыОбъектов.Порядок КАК ПорядокСтатуса,
	|	СтатусыОбъектов.ИндексКартинки КАК ИндексКартинки,
	|	РольПользователя.Роль КАК Роль,
	|	ЕСТЬNULL(СрокиСдачиЗадач.СрокСдачи, ДАТАВРЕМЯ(1, 1, 1)) КАК СрокРеализации
	|ИЗ
	|	ВсеЗадачиНеНаМне КАК ВсеЗадачиНеНаМне
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.Задача КАК Задача
	|		ПО ВсеЗадачиНеНаМне.Объект = Задача.Ссылка
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.СтатусыОбъектов КАК СтатусыОбъектов
	|		ПО (Задача.Статус = СтатусыОбъектов.Ссылка)
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РольПользователя КАК РольПользователя
	|		ПО ВсеЗадачиНеНаМне.Объект = РольПользователя.Объект
	|		ЛЕВОЕ СОЕДИНЕНИЕ СрокиСдачиЗадач КАК СрокиСдачиЗадач
	|		ПО ВсеЗадачиНеНаМне.Объект = СрокиСдачиЗадач.Объект
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ЗадачиЗаказанныеМной.Объект,
	|	ЗадачиЗаказанныеМной.Статус,
	|	ЗадачиЗаказанныеМной.Представление,
	|	ЗадачиЗаказанныеМной.ПорядокСтатуса,
	|	ЗадачиЗаказанныеМной.ИндексКартинки,
	|	РольПользователя.Роль,
	|	ЕСТЬNULL(СрокиСдачиЗадач.СрокСдачи, ДАТАВРЕМЯ(1, 1, 1))
	|ИЗ
	|	ЗадачиЗаказанныеМной КАК ЗадачиЗаказанныеМной
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РольПользователя КАК РольПользователя
	|		ПО ЗадачиЗаказанныеМной.Объект = РольПользователя.Объект
	|		ЛЕВОЕ СОЕДИНЕНИЕ СрокиСдачиЗадач КАК СрокиСдачиЗадач
	|		ПО ЗадачиЗаказанныеМной.Объект = СрокиСдачиЗадач.Объект
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ЗаявкиЗаказанныеМной.Объект,
	|	ЗаявкиЗаказанныеМной.Статус,
	|	ЗаявкиЗаказанныеМной.Представление,
	|	ЗаявкиЗаказанныеМной.ПорядокСтатуса,
	|	ЗаявкиЗаказанныеМной.ИндексКартинки,
	|	РольПользователя.Роль,
	|	ЗаявкиЗаказанныеМной.СрокРеализации
	|ИЗ
	|	ЗаявкиЗаказанныеМной КАК ЗаявкиЗаказанныеМной
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РольПользователя КАК РольПользователя
	|		ПО ЗаявкиЗаказанныеМной.Объект = РольПользователя.Объект";
	#КонецОбласти
	
	Запрос.УстановитьПараметр("ТекущаяДата", ТекущаяДатаСеанса());
	Запрос.УстановитьПараметр("Задача", Неопределено);
	Запрос.УстановитьПараметр("ТекущийПользователь", ПараметрыСеанса.ТекущийПользователь);
	Запрос.УстановитьПараметр("ФилиалИсполнителя", ПараметрыСеанса.Филиал);
	Запрос.УстановитьПараметр("ИндексКартинкиЧерновик", Справочники.СтатусыОбъектов.ИндексКартинкиЧерновик());
	
	Возврат Запрос.Выполнить().Выгрузить();
	
КонецФункции

Функция СформироватьСписокЗадачиНаМне(ВариантГруппировки)
	
	КолонкиИнструмента = Новый Массив;
	КолонкиИнструмента.Добавить("ПредставлениеЗадача");
	КолонкиИнструмента.Добавить("ЭтоГруппа");
	КолонкиИнструмента.Добавить("ИндексКартинки");
	КолонкиИнструмента.Добавить("Ссылка");
	КолонкиИнструмента.Добавить("UID");
	КолонкиИнструмента.Добавить("Статус");
	КолонкиИнструмента.Добавить("СрокРеализации");
	КолонкиИнструмента.Добавить("Группа");
	КолонкиИнструмента.Добавить("ПорядокСтатуса");
	КолонкиИнструмента.Добавить("ДатаНачалаРазработки");
	
	Дерево = Новый ДеревоЗначений;
	Для Каждого Колонка Из КолонкиИнструмента Цикл
		Дерево.Колонки.Добавить(Колонка);
	КонецЦикла;
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	#Область ТекстЗапроса
	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	СвойстваЗадач.Объект КАК Объект
	|ПОМЕСТИТЬ ВсеЗадачиИсполнителя
	|ИЗ
	|	РегистрСведений.СвойстваЗадач КАК СвойстваЗадач
	|ГДЕ
	|	СвойстваЗадач.Исполнитель = &ТекущийПользователь
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	СвойстваЗадачСрезПоследних.Объект КАК Объект
	|ПОМЕСТИТЬ СвойстваЗадачСрезПоследних
	|ИЗ
	|	РегистрСведений.СвойстваЗадач.СрезПоследних(, Объект В
	|		(ВЫБРАТЬ РАЗЛИЧНЫЕ
	|			ВсеЗадачиИсполнителя.Объект
	|		ИЗ
	|			ВсеЗадачиИсполнителя КАК ВсеЗадачиИсполнителя)) КАК СвойстваЗадачСрезПоследних
	|ГДЕ
	|	СвойстваЗадачСрезПоследних.Исполнитель = &ТекущийПользователь
	|	И НЕ СвойстваЗадачСрезПоследних.Статус В (ЗНАЧЕНИЕ(Справочник.СтатусыОбъектов.Решен),
	|		ЗНАЧЕНИЕ(Справочник.СтатусыОбъектов.Отклонен))
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	СвойстваЗадачСрезПоследних.Объект КАК Объект,
	|	Задача.Статус КАК Статус,
	|	""Задачи"" КАК Группа,
	|	""["" + Задача.Номер + ""] "" + Задача.Наименование КАК ПредставлениеЗадача,
	|	Задача.СистемаУчета КАК СистемаУчета,
	|	Задача.ДатаНачалаРазработки КАК ДатаНачалаРазработки,
	|	Задача.ОбъектОснование КАК ОбъектОснование,
	|	""["" + ЗаявкаНаРазработку.Номер + ""] "" + ЗаявкаНаРазработку.Наименование КАК ПредставлениеОбъектОснование
	|ПОМЕСТИТЬ ПодготовленныеЗадачиНаМне
	|ИЗ
	|	СвойстваЗадачСрезПоследних КАК СвойстваЗадачСрезПоследних
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.Задача КАК Задача
	|		ПО СвойстваЗадачСрезПоследних.Объект = Задача.Ссылка
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ЗаявкаНаРазработку КАК ЗаявкаНаРазработку
	|		ПО Задача.ОбъектОснование = ЗаявкаНаРазработку.Ссылка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	СвойстваВнутреннихЗаданий.Объект КАК Объект
	|ПОМЕСТИТЬ ВсеВнутренниеЗаданияИсполнителя
	|ИЗ
	|	РегистрСведений.СвойстваВнутреннихЗаданий КАК СвойстваВнутреннихЗаданий
	|ГДЕ
	|	СвойстваВнутреннихЗаданий.Исполнитель = &ТекущийПользователь
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	СвойстваВнутреннихЗаданийСрезПоследних.Объект КАК Объект
	|ПОМЕСТИТЬ СвойстваВнутреннихЗаданийСрезПоследних
	|ИЗ
	|	РегистрСведений.СвойстваВнутреннихЗаданий.СрезПоследних(, Объект В
	|		(ВЫБРАТЬ РАЗЛИЧНЫЕ
	|			ВсеВнутренниеЗаданияИсполнителя.Объект
	|		ИЗ
	|			ВсеВнутренниеЗаданияИсполнителя КАК ВсеВнутренниеЗаданияИсполнителя)) КАК СвойстваВнутреннихЗаданийСрезПоследних
	|ГДЕ
	|	СвойстваВнутреннихЗаданийСрезПоследних.Исполнитель = &ТекущийПользователь
	|	И НЕ СвойстваВнутреннихЗаданийСрезПоследних.Статус В (ЗНАЧЕНИЕ(Справочник.СтатусыОбъектов.Решен),
	|		ЗНАЧЕНИЕ(Справочник.СтатусыОбъектов.Отклонен))
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	СвойстваВнутреннихЗаданийСрезПоследних.Объект КАК Объект,
	|	ВнутреннееЗадание.Статус КАК Статус,
	|	""Внутренние задания"" КАК Группа,
	|	""["" + ВнутреннееЗадание.Номер + ""] "" + ВнутреннееЗадание.Наименование КАК Представление,
	|	ВнутреннееЗадание.ДатаНачалаРазработки КАК ДатаНачалаРазработки,
	|	ВнутреннееЗадание.ОбъектОснование КАК ОбъектОснование,
	|	ВЫБОР
	|		КОГДА НЕ ВнутреннееЗаданиеОснование.Ссылка ЕСТЬ NULL
	|			ТОГДА ""["" + ВнутреннееЗаданиеОснование.Номер + ""] "" + ВнутреннееЗаданиеОснование.Наименование
	|		КОГДА НЕ ПроектыОснование.Ссылка ЕСТЬ NULL
	|			ТОГДА ""["" + ПроектыОснование.Код + ""] "" + ПроектыОснование.Наименование
	|		ИНАЧЕ """"
	|	КОНЕЦ КАК ПредставлениеОбъектОснование,
	|	ВнутреннееЗадание.ИсполнитьДо КАК СрокСдачи
	|ПОМЕСТИТЬ ПодготовленныеВнутреннииеЗаданияНаМне
	|ИЗ
	|	СвойстваВнутреннихЗаданийСрезПоследних КАК СвойстваВнутреннихЗаданийСрезПоследних
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ВнутреннееЗадание КАК ВнутреннееЗадание
	|		ПО СвойстваВнутреннихЗаданийСрезПоследних.Объект = ВнутреннееЗадание.Ссылка
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.ВнутреннееЗадание КАК ВнутреннееЗаданиеОснование
	|		ПО ВнутреннееЗадание.ОбъектОснование = ВнутреннееЗаданиеОснование.Ссылка
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.Проекты КАК ПроектыОснование
	|		ПО ВнутреннееЗадание.ОбъектОснование = ПроектыОснование.Ссылка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВнутренниеТрудозатраты.Ссылка КАК Ссылка,
	|	ЗНАЧЕНИЕ(Справочник.СтатусыОбъектов.Новый) КАК Статус,
	|	""Внутренние трудозатраты"" КАК Группа,
	|	""["" + ВнутренниеТрудозатраты.Номер + ""] "" + ВнутренниеТрудозатраты.Наименование КАК Представление,
	|	0 КАК ПорядокСтатуса,
	|	&ИндексКартинкиВнутренниеТрудозатраты КАК ИндексКартинки
	|ПОМЕСТИТЬ ВнутренниеТрудозатраты
	|ИЗ
	|	Документ.ВнутренниеТрудозатраты КАК ВнутренниеТрудозатраты
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.УчастникиВнутреннихТрудозатрат КАК УчастникиВнутреннихТрудозатрат
	|		ПО УчастникиВнутреннихТрудозатрат.Объект = ВнутренниеТрудозатраты.Ссылка
	|ГДЕ
	|	УчастникиВнутреннихТрудозатрат.Пользователь = &ТекущийПользователь
	|	И УчастникиВнутреннихТрудозатрат.Активный
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	СрокиСдачиЗадач.Задача КАК Объект,
	|	СрокиСдачиЗадач.СрокСдачи КАК СрокСдачи
	|ПОМЕСТИТЬ СрокиСдачиЗадач
	|ИЗ
	|	РегистрСведений.СрокиСдачиЗадач.СрезПоследних(, Задача В
	|		(ВЫБРАТЬ
	|			ПодготовленныеЗадачиНаМне.Объект
	|		ИЗ
	|			ПодготовленныеЗадачиНаМне)) КАК СрокиСдачиЗадач
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	СистемыЗаявокНазначеныеПользователю.Заявка КАК Заявка,
	|	СтатусыЗаявокПоСистемам.Статус КАК Статус,
	|	СтатусыОбъектов.Порядок КАК Порядок,
	|	СтатусыОбъектов.ИндексКартинки КАК ИндексКартинки
	|ПОМЕСТИТЬ ЗаявкиНаПользователе
	|ИЗ
	|	РегистрСведений.СистемыЗаявокНазначеныеПользователю КАК СистемыЗаявокНазначеныеПользователю
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.СтатусыЗаявокПоСистемам КАК СтатусыЗаявокПоСистемам
	|		ПО СистемыЗаявокНазначеныеПользователю.Заявка = СтатусыЗаявокПоСистемам.Заявка
	|		И СистемыЗаявокНазначеныеПользователю.Система = СтатусыЗаявокПоСистемам.Система
	|		И НЕ СтатусыЗаявокПоСистемам.Статус В (ЗНАЧЕНИЕ(Справочник.СтатусыОбъектов.Решен),
	|			ЗНАЧЕНИЕ(Справочник.СтатусыОбъектов.Отклонен))
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.СтатусыОбъектов КАК СтатусыОбъектов
	|		ПО СтатусыЗаявокПоСистемам.Статус = СтатусыОбъектов.Ссылка
	|ГДЕ
	|	СистемыЗаявокНазначеныеПользователю.ТекущийИсполнитель = &ТекущийПользователь
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ЗаявкиНаПользователе.Заявка КАК Заявка,
	|	МАКСИМУМ(ЗаявкиНаПользователе.Порядок) КАК Порядок
	|ПОМЕСТИТЬ МаксимальныйПорядокСтатусаЗаявок
	|ИЗ
	|	ЗаявкиНаПользователе КАК ЗаявкиНаПользователе
	|СГРУППИРОВАТЬ ПО
	|	Заявка,
	|	ЗаявкиНаПользователе.Заявка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ПодготовленныеЗадачиНаМне.Объект КАК Ссылка,
	|	ПодготовленныеЗадачиНаМне.Статус КАК Статус,
	|	ПодготовленныеЗадачиНаМне.Группа КАК Группа,
	|	ПодготовленныеЗадачиНаМне.ПредставлениеЗадача КАК ПредставлениеЗадача,
	|	ПодготовленныеЗадачиНаМне.ПредставлениеОбъектОснование КАК ПредставлениеОбъектОснование,
	|	ПодготовленныеЗадачиНаМне.ДатаНачалаРазработки КАК ДатаНачалаРазработки,
	|	СтатусыОбъектов.Порядок КАК ПорядокСтатуса,
	|	СтатусыОбъектов.ИндексКартинки КАК ИндексКартинки,
	|	ЕСТЬNULL(СрокиСдачиЗадач.СрокСдачи, ДАТАВРЕМЯ(1, 1, 1)) КАК СрокСдачи
	|ПОМЕСТИТЬ ПодготовленныеДанные
	|ИЗ
	|	ПодготовленныеЗадачиНаМне КАК ПодготовленныеЗадачиНаМне
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.СтатусыОбъектов КАК СтатусыОбъектов
	|		ПО ПодготовленныеЗадачиНаМне.Статус = СтатусыОбъектов.Ссылка
	|		ЛЕВОЕ СОЕДИНЕНИЕ СрокиСдачиЗадач КАК СрокиСдачиЗадач
	|		ПО ПодготовленныеЗадачиНаМне.Объект = СрокиСдачиЗадач.Объект
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ВнутренниеТрудозатраты.Ссылка,
	|	ВнутренниеТрудозатраты.Статус,
	|	ВнутренниеТрудозатраты.Группа,
	|	ВнутренниеТрудозатраты.Представление,
	|	"""",
	|	ДАТАВРЕМЯ(1, 1, 1),
	|	ВнутренниеТрудозатраты.ПорядокСтатуса,
	|	ВнутренниеТрудозатраты.ИндексКартинки,
	|	ДАТАВРЕМЯ(1, 1, 1)
	|ИЗ
	|	ВнутренниеТрудозатраты КАК ВнутренниеТрудозатраты
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ПодготовленныеВнутреннииеЗаданияНаМне.Объект,
	|	ПодготовленныеВнутреннииеЗаданияНаМне.Статус,
	|	ПодготовленныеВнутреннииеЗаданияНаМне.Группа,
	|	ПодготовленныеВнутреннииеЗаданияНаМне.Представление,
	|	ПодготовленныеВнутреннииеЗаданияНаМне.ПредставлениеОбъектОснование,
	|	ПодготовленныеВнутреннииеЗаданияНаМне.ДатаНачалаРазработки,
	|	СтатусыОбъектов.Порядок,
	|	СтатусыОбъектов.ИндексКартинки,
	|	ПодготовленныеВнутреннииеЗаданияНаМне.СрокСдачи
	|ИЗ
	|	ПодготовленныеВнутреннииеЗаданияНаМне КАК ПодготовленныеВнутреннииеЗаданияНаМне
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.СтатусыОбъектов КАК СтатусыОбъектов
	|		ПО ПодготовленныеВнутреннииеЗаданияНаМне.Статус = СтатусыОбъектов.Ссылка
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ЗаявкиНаПользователе.Заявка,
	|	ЗаявкиНаПользователе.Статус,
	|	""Заявки на мне"",
	|	""["" + ЗаявкаНаРазработку.Номер + ""] "" + ЗаявкаНаРазработку.Наименование,
	|	"""",
	|	ДАТАВРЕМЯ(1, 1, 1),
	|	ЗаявкиНаПользователе.Порядок,
	|	ЗаявкиНаПользователе.ИндексКартинки,
	|	ДАТАВРЕМЯ(1, 1, 1)
	|ИЗ
	|	ЗаявкиНаПользователе КАК ЗаявкиНаПользователе
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ МаксимальныйПорядокСтатусаЗаявок КАК МаксимальныйПорядокСтатусаЗаявок
	|		ПО ЗаявкиНаПользователе.Заявка = МаксимальныйПорядокСтатусаЗаявок.Заявка
	|		И ЗаявкиНаПользователе.Порядок = МаксимальныйПорядокСтатусаЗаявок.Порядок
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ЗаявкаНаРазработку КАК ЗаявкаНаРазработку
	|		ПО ЗаявкиНаПользователе.Заявка = ЗаявкаНаРазработку.Ссылка
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.СтатусыОбъектов КАК СтатусыОбъектов
	|		ПО ЗаявкиНаПользователе.Статус = СтатусыОбъектов.Ссылка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ПодготовленныеДанные.Ссылка КАК Ссылка,
	|	ПодготовленныеДанные.Статус КАК Статус,
	|	ПодготовленныеДанные.Группа КАК Группа,
	|	ПодготовленныеДанные.ПредставлениеЗадача КАК ПредставлениеЗадача,
	|	ПодготовленныеДанные.ПредставлениеОбъектОснование КАК ПредставлениеОбъектОснование,
	|	ПодготовленныеДанные.ДатаНачалаРазработки КАК ДатаНачалаРазработки,
	|	ПодготовленныеДанные.ПорядокСтатуса КАК ПорядокСтатуса,
	|	ПодготовленныеДанные.ИндексКартинки КАК ИндексКартинки,
	|	ПодготовленныеДанные.СрокСдачи КАК СрокРеализации
	|ИЗ
	|	ПодготовленныеДанные КАК ПодготовленныеДанные";
	#КонецОбласти
		
	Запрос.УстановитьПараметр("ТекущийПользователь", ПараметрыСеанса.ТекущийПользователь);
	Запрос.УстановитьПараметр("ИндексКартинкиВнутренниеТрудозатраты", 
		Справочники.СтатусыОбъектов.ИндексКартинкиВнутренниеТрудозатраты());
	
	МассивПакетов = Запрос.ВыполнитьПакет();
	КоличествоПакетов = МассивПакетов.ВГраница();
	
	ИсточникДанных = Новый ОписаниеИсточникаДанных(МассивПакетов[КоличествоПакетов]);
	ИсточникДанных.Колонки.Группа.Измерение = Истина;
	ИсточникДанных.Колонки.Статус.Измерение = Истина;
	ИсточникДанных.Колонки.ПредставлениеОбъектОснование.Измерение = Истина;
	
	ПостроительЗапроса = Новый ПостроительЗапроса;
	ПостроительЗапроса.ИсточникДанных = ИсточникДанных;
		
	ПостроительЗапроса.Измерения.Очистить();
	ПостроительЗапроса.Измерения.Добавить("Группа");
	ПостроительЗапроса.Порядок.Добавить("Группа");
	
	ИсточникДанных.Колонки.ИндексКартинки.Итог = "Максимум(-1)";
	
	Если ВариантГруппировки = 1 Тогда
		// Группировка по статусу
		ПостроительЗапроса.Измерения.Добавить("Статус");
		ИсточникДанных.Колонки.ИндексКартинки.Итог = "Максимум(ИндексКартинки)";
		ИсточникДанных.Колонки.ПорядокСтатуса.Итог = "Максимум(ПорядокСтатуса)";
	ИначеЕсли ВариантГруппировки = 2 Тогда
		// Группировка по объекту основанию
		ИсточникДанных.Колонки.ИндексКартинки.Итог = СтрШаблон("Максимум(%1)", Справочники.СтатусыОбъектов.ИндексКартинкиГруппа());
		ПостроительЗапроса.Измерения.Добавить("ПредставлениеОбъектОснование");
	КонецЕсли;     
	
	ПостроительЗапроса.Порядок.Добавить("ПорядокСтатуса");
	ПостроительЗапроса.Порядок.Добавить("ДатаНачалаРазработки");
	ПостроительЗапроса.Порядок.Добавить("ПредставлениеЗадача");
				
	ПостроительЗапроса.Выполнить();
	
	ДополнительныеДанные = Новый Структура;
	ДополнительныеДанные.Вставить("Измерения", ПостроительЗапроса.Измерения);
	ДополнительныеДанные.Вставить("СтрокиКСортировке", Новый Соответствие);
	ДополнительныеДанные.Вставить("Объект", Неопределено);
	ДополнительныеДанные.Вставить("Перезаполнение", Истина);
	ДополнительныеДанные.Вставить("ВидДерева", "ЗадачиНаМне");
	ДополнительныеДанные.Вставить("ИндексКартинки_Группа", -1);
	ДополнительныеДанные.Вставить("ПолеОбъекта", "Ссылка");
	ДополнительныеДанные.Вставить("ПолеПредставлениеОбъекта", "ПредставлениеЗадача");
	
	УправлениеИнструментамиРазработки.ОбработатьВыборку(ПостроительЗапроса.Результат, Дерево, Новый Структура, Дерево, 0, ДополнительныеДанные);
	УправлениеИнструментамиРазработки.ОтсортироватьДерево(ДополнительныеДанные.СтрокиКСортировке, Дерево, ПостроительЗапроса.Порядок);
	
	ДанныеДерева = ИнструментыСервер.ПодготовитьДанныеИнструментаДляКлиента(Дерево, КолонкиИнструмента);
	
	Возврат ДанныеДерева;
	
КонецФункции

Функция СформироватьСписокОтслеживаемыеЗадачи(ВариантГруппировки, АдресТаблицыОтслеживаемыеЗадачи, ПолноеОбновление)
	
	Если ПолноеОбновление Тогда
		ТаблицаОтслеживаемыхЗадач = ПолучитьТаблицуОтлеживаемыхЗадач();
		ПоместитьВоВременноеХранилище(ТаблицаОтслеживаемыхЗадач, АдресТаблицыОтслеживаемыеЗадачи);
	Иначе
		ТаблицаОтслеживаемыхЗадач = ПолучитьИзВременногоХранилища(АдресТаблицыОтслеживаемыеЗадачи);
		
		Если ТаблицаОтслеживаемыхЗадач = Неопределено Тогда
			ТаблицаОтслеживаемыхЗадач = ПолучитьТаблицуОтлеживаемыхЗадач();
			ПоместитьВоВременноеХранилище(ТаблицаОтслеживаемыхЗадач, АдресТаблицыОтслеживаемыеЗадачи);
		КонецЕсли;
	КонецЕсли;
	
	КолонкиИнструмента = Новый Массив;
	КолонкиИнструмента.Добавить("Ссылка");
	КолонкиИнструмента.Добавить("ИндексКартинки");
	КолонкиИнструмента.Добавить("Статус");
	КолонкиИнструмента.Добавить("ПредставлениеЗадача");
	КолонкиИнструмента.Добавить("ПорядокСтатуса");
	КолонкиИнструмента.Добавить("СрокРеализации");
	КолонкиИнструмента.Добавить("Роль");
	КолонкиИнструмента.Добавить("ЭтоГруппа");
	КолонкиИнструмента.Добавить("UID");
	
	Дерево = ПолучитьДеревоОтслеживаемыеЗадачи(ВариантГруппировки, ТаблицаОтслеживаемыхЗадач, КолонкиИнструмента);
	ДанныеДерева = ИнструментыСервер.ПодготовитьДанныеИнструментаДляКлиента(Дерево, КолонкиИнструмента);
	
	Возврат ДанныеДерева;
	
КонецФункции

#КонецОбласти

#КонецЕсли
