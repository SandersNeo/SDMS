///////////////////////////////////////////////////////////////////////////////////////////////////////
// Copyright (c) 2025, ООО ДНС Технологии
// SDMS (Software Development Management System) — это корпоративная система учета разработки и управления проектами 
// Все права защищены. Эта программа и сопроводительные материалы предоставляются 
// в соответствии с условиями лицензии General Public License (GNU GPL v3)
// Текст лицензии доступен по ссылке:
// https://www.gnu.org/licenses/gpl-3.0.html
///////////////////////////////////////////////////////////////////////////////////////////////////////

#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ЗаполнитьПоСпискуЗначений(Параметры.ОтмеченныеЗначения);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовФормы

&НаКлиенте
Процедура СнятьФлажки(Команда)
	
	ИзменитьПометки(Ложь);
	
КонецПроцедуры

&НаКлиенте
Процедура УстановитьФлажки(Команда)
	
	ИзменитьПометки(Истина);
	
КонецПроцедуры

&НаКлиенте
Процедура СписокЭлементовВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	ТекущиеДанные = Элементы.СписокЭлементов.ТекущиеДанные;
	Если ТекущиеДанные <> Неопределено Тогда
		ТекущиеДанные.Пометка = НЕ ТекущиеДанные.Пометка;
	КонецЕсли;
		
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ЗавершитьВыбор(Команда)
	
	ЗавершитьВыборЗначений();
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура ИзменитьПометки(Знач Пометка)
	
	НайденныеСтроки = СписокЭлементов.НайтиСтроки(Новый Структура("Пометка", НЕ Пометка));
	Для Каждого СтрокаТаблицы Из НайденныеСтроки Цикл
		СтрокаТаблицы.Пометка = Пометка;
	КонецЦикла;
	
КонецПроцедуры

&НаКлиенте
Процедура ЗавершитьВыборЗначений()
	
	ВыбранныеЗначения = Новый Массив;
	
	НайденныеСтроки = СписокЭлементов.НайтиСтроки(Новый Структура("Пометка", Истина));
	Если НайденныеСтроки.Количество() > 0 Тогда	
		Для Каждого СтрокаТаблицы Из НайденныеСтроки Цикл
			ВыбранныеЗначения.Добавить(СтрокаТаблицы.Значение);
		КонецЦикла;
	КонецЕсли;	
	
	Закрыть(ВыбранныеЗначения);	
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьПоСпискуЗначений(Знач СписокЗначений)
	
	Для Каждого ЭлементСписка Из СписокЗначений Цикл
		НоваяСтрока = СписокЭлементов.Добавить();
		НоваяСтрока.Пометка = ЭлементСписка.Пометка;
		НоваяСтрока.Значение = ЭлементСписка.Значение;
		НоваяСтрока.Наименование = ЭлементСписка.Представление;
	КонецЦикла;
	
КонецПроцедуры

&НаКлиенте
Процедура ПоискПоСпискуЗначенийИзменениеТекстаРедактирования(Элемент, Текст, СтандартнаяОбработка)
	
	Если НЕ ПустаяСтрока(Текст) Тогда
		Элементы.СписокЭлементов.ОтборСтрок = Новый ФиксированнаяСтруктура("Значение", Текст);
	Иначе
		Элементы.СписокЭлементов.ОтборСтрок = Неопределено;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти
