///////////////////////////////////////////////////////////////////////////////////////////////////////
// Copyright (c) 2025, ООО ДНС Технологии
// SDMS (Software Development Management System) — это корпоративная система учета разработки и управления проектами 
// Все права защищены. Эта программа и сопроводительные материалы предоставляются 
// в соответствии с условиями лицензии General Public License (GNU GPL v3)
// Текст лицензии доступен по ссылке:
// https://www.gnu.org/licenses/gpl-3.0.html
///////////////////////////////////////////////////////////////////////////////////////////////////////

#Область ПрограммныйИнтерфейс

// Копирует все настройки пользователя. Если произошло переименование пользователя - то нужно удалить все настройки у источника
//
// Параметры:
//  УКогоСкопироватьНастройки		 - Строка	 - Имя пользователя у которого мы копируем настройки
//  КомуСкопироватьНастройки		 - Строка	 - Имя пользователя которому мы копируем настройки  
//  УдалитьНастрйокиПослеКопирования - Булево	 - Если поменяли имя пользователя. То после копирования настроек удалить у пользователя - источника. 
//
Процедура СкопироватьВсеНастройкиОтОдногоПользователяДругому(Знач УКогоСкопироватьНастройки, Знач КомуСкопироватьНастройки, УдалитьНастройкиПослеКопирования = Ложь) Экспорт 
	
	МассивКопируемыхНастроек = Новый Массив;
	
	ЗаполнитьНастройкиИзХранилища("ХранилищеВариантовОтчетов", УКогоСкопироватьНастройки, МассивКопируемыхНастроек);
	ЗаполнитьНастройкиИзХранилища("ХранилищеНастроекДанныхФорм", УКогоСкопироватьНастройки, МассивКопируемыхНастроек);
	ЗаполнитьНастройкиИзХранилища("ХранилищеОбщихНастроек", УКогоСкопироватьНастройки, МассивКопируемыхНастроек);
	ЗаполнитьНастройкиИзХранилища("ХранилищеПользовательскихНастроекДинамическихСписков", УКогоСкопироватьНастройки, МассивКопируемыхНастроек);
	ЗаполнитьНастройкиИзХранилища("ХранилищеПользовательскихНастроекОтчетов", УКогоСкопироватьНастройки, МассивКопируемыхНастроек);
	ЗаполнитьНастройкиИзХранилища("ХранилищеСистемныхНастроек", УКогоСкопироватьНастройки, МассивКопируемыхНастроек);
	
	Для Каждого Строка из МассивКопируемыхНастроек Цикл
		
		СкопироватьНастройкуПользователя(Строка, УКогоСкопироватьНастройки, КомуСкопироватьНастройки, УдалитьНастройкиПослеКопирования);
		
	КонецЦикла;

КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура ЗаполнитьНастройкиИзХранилища(Знач Хранилище, Знач Пользователь, МассивКопируемыхНастроек)
	
	СтруктураОтбора = Новый Структура("Пользователь", Пользователь);
	
	ВыборкаНастроек = ПолучитьМенеджерХранилища(Хранилище).Выбрать(СтруктураОтбора);
	Пока ВыборкаНастроек.Следующий() Цикл
		Если ВыборкаНастроек.Настройки <> Неопределено Тогда
			ТекущаяНастройкаПользователя = Новый Структура("Хранилище, КлючОбъекта, КлючНастроек");
			ТекущаяНастройкаПользователя.Хранилище = Хранилище;  
			ЗаполнитьЗначенияСвойств(ТекущаяНастройкаПользователя,ВыборкаНастроек); 
			МассивКопируемыхНастроек.Добавить(ТекущаяНастройкаПользователя);
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры

&НаСервере
Функция ПолучитьМенеджерХранилища(Знач Хранилище)
	
	Если Хранилище = "ХранилищеВариантовОтчетов" Тогда
		Возврат ХранилищеВариантовОтчетов;
	ИначеЕсли Хранилище = "ХранилищеНастроекДанныхФорм" Тогда
		Возврат ХранилищеНастроекДанныхФорм;
	ИначеЕсли Хранилище = "ХранилищеОбщихНастроек" Тогда
		Возврат ХранилищеОбщихНастроек;
	ИначеЕсли Хранилище = "ХранилищеПользовательскихНастроекДинамическихСписков" Тогда
		Возврат ХранилищеПользовательскихНастроекДинамическихСписков;
	ИначеЕсли Хранилище = "ХранилищеПользовательскихНастроекОтчетов" Тогда
		Возврат ХранилищеПользовательскихНастроекОтчетов;
	ИначеЕсли Хранилище = "ХранилищеСистемныхНастроек" Тогда
		Возврат ХранилищеСистемныхНастроек;
	КонецЕсли;
	
КонецФункции

&НаСервере
Процедура СкопироватьНастройкуПользователя(Знач ДанныеНастроек, Знач УКогоСкопироватьНастройки, Знач КомуСкопироватьНастройки, Знач УдалитьНастройкиПослеКопирования = Ложь)
	
	// Определение менеджера настроек
	МенеджерХранилища = ПолучитьМенеджерХранилища(ДанныеНастроек.Хранилище);
	
	// Чтение настроек
	Настройки = МенеджерХранилища.Загрузить(ДанныеНастроек.КлючОбъекта, ДанныеНастроек.КлючНастроек, , УКогоСкопироватьНастройки);
	
	Если Настройки = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	МенеджерХранилища.Сохранить(ДанныеНастроек.КлючОбъекта, ДанныеНастроек.КлючНастроек, Настройки, , КомуСкопироватьНастройки);
	Если УдалитьНастройкиПослеКопирования = Истина тогда
		МенеджерХранилища.Удалить(ДанныеНастроек.КлючОбъекта, ДанныеНастроек.КлючНастроек, УКогоСкопироватьНастройки);
	КонецЕсли;
	
	
КонецПроцедуры

#КонецОбласти
