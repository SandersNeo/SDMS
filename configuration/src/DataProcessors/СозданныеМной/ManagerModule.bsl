#Область ПрограммныйИнтерфейс

Процедура ПолучитьДанныеСписок_Заявки(НастройкиСписка, АдресРезультата, УИДЗамера) Экспорт

	ВремяНачала = ОценкаПроизводительности.НачатьЗамерВремени();

	ДанныеДерева = СформироватьСписокЗаявки(НастройкиСписка);
	ИнструментыСервер.ОповеститьИнструментОбОбновлении(ДанныеДерева, АдресРезультата, УИДЗамера);

	ОценкаПроизводительности.ЗакончитьЗамерВремениТехнологический(
		"Обработки.СозданныеМной.ПолучитьДанныеСписок_Заявки.ВыполнениеФонового", ВремяНачала, , Строка(УИДЗамера));

КонецПроцедуры

Процедура ПолучитьДанныеСписок_Задачи(НастройкиСписка, АдресРезультата, УИДЗамера) Экспорт

	ВремяНачала = ОценкаПроизводительности.НачатьЗамерВремени();

	ДанныеДерева = СформироватьСписокЗадачи(НастройкиСписка);
	ИнструментыСервер.ОповеститьИнструментОбОбновлении(ДанныеДерева, АдресРезультата, УИДЗамера);

	ОценкаПроизводительности.ЗакончитьЗамерВремениТехнологический(
		"Обработки.СозданныеМной.ПолучитьДанныеСписок_Задачи.ВыполнениеФонового", ВремяНачала, , Строка(УИДЗамера));

КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция СформироватьСписокЗаявки(Знач НастройкиСписка)

	КолонкиИнструмента = Новый Массив;
	КолонкиИнструмента.Добавить("Наименование");
	КолонкиИнструмента.Добавить("Назначена");
	КолонкиИнструмента.Добавить("СтатусПредставление");
	КолонкиИнструмента.Добавить("ИзменениеСтатуса");
	КолонкиИнструмента.Добавить("СистемаПредставление");
	КолонкиИнструмента.Добавить("Автор");
	КолонкиИнструмента.Добавить("ИндексКартинки");
	КолонкиИнструмента.Добавить("Заявка");
	КолонкиИнструмента.Добавить("UID");
	КолонкиИнструмента.Добавить("ДатаСоздания");

	Таблица = Новый ТаблицаЗначений;
	Для Каждого Колонка Из КолонкиИнструмента Цикл
		Таблица.Колонки.Добавить(Колонка);
	КонецЦикла;
	
	ДатаНачала = НастройкиСписка.Период.ДатаНачала;
	ДатаОкончания = НастройкиСписка.Период.ДатаОкончания;
	ВыбранныеСтатусы = НастройкиСписка.ВыбранныеСтатусы;
	ВыбранныеСистемы = НастройкиСписка.ВыбранныеСистемы;
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	#Область ТекстЗапроса
	"ВЫБРАТЬ
	|	ДокументЗаявка.Ссылка КАК Ссылка,
	|	ДокументЗаявка.Номер КАК Номер,
	|	ДокументЗаявка.Дата КАК Дата,
	|	ДокументЗаявка.Наименование КАК Наименование,
	|	ЗаявкаНаРазработкуСистемыРазработки.Система КАК Система
	|ПОМЕСТИТЬ ЗаявкиНаРазработку
	|ИЗ
	|	Документ.ЗаявкаНаРазработку КАК ДокументЗаявка
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ЗаявкаНаРазработку.СистемыРазработки КАК ЗаявкаНаРазработкуСистемыРазработки
	|		ПО ДокументЗаявка.Ссылка = ЗаявкаНаРазработкуСистемыРазработки.Ссылка
	|ГДЕ
	|	ДокументЗаявка.Дата МЕЖДУ &ДатаНачала И &ДатаОкончания
	|	И ДокументЗаявка.Автор = &Автор
	|	И ЗаявкаНаРазработкуСистемыРазработки.Система В(&ВыбранныеСистемы)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ЗаявкиНаРазработку.Ссылка КАК Заявка,
	|	ЗаявкиНаРазработку.Система КАК Система,
	|	СтатусыЗаявокПоСистемам.Статус КАК Статус,
	|	СтатусыОбъектов.Порядок КАК ПорядокСтатуса
	|ПОМЕСТИТЬ СтатусыПоСистемам
	|ИЗ
	|	ЗаявкиНаРазработку КАК ЗаявкиНаРазработку
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.СтатусыЗаявокПоСистемам КАК СтатусыЗаявокПоСистемам
	|		ПО ЗаявкиНаРазработку.Ссылка = СтатусыЗаявокПоСистемам.Заявка
	|			И ЗаявкиНаРазработку.Система = СтатусыЗаявокПоСистемам.Система
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.СтатусыОбъектов КАК СтатусыОбъектов
	|		ПО (СтатусыЗаявокПоСистемам.Статус = СтатусыОбъектов.Ссылка)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	СтатусыПоСистемам.Заявка КАК Заявка,
	|	МИНИМУМ(СтатусыПоСистемам.ПорядокСтатуса) КАК ПорядокСтатуса
	|ПОМЕСТИТЬ МинимальныйСтатусЗаявокПоСистемам
	|ИЗ
	|	СтатусыПоСистемам КАК СтатусыПоСистемам
	|
	|СГРУППИРОВАТЬ ПО
	|	СтатусыПоСистемам.Заявка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	0 КАК ПриоритетСтроки,
	|	МинимальныйСтатусЗаявокПоСистемам.Заявка КАК Заявка,
	|	МинимальныйСтатусЗаявокПоСистемам.ПорядокСтатуса КАК ПорядокСтатуса,
	|	СтатусыПоСистемам.Статус КАК Статус
	|ПОМЕСТИТЬ СтатусыЗаявокПоПриоритетам
	|ИЗ
	|	МинимальныйСтатусЗаявокПоСистемам КАК МинимальныйСтатусЗаявокПоСистемам
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ СтатусыПоСистемам КАК СтатусыПоСистемам
	|		ПО МинимальныйСтатусЗаявокПоСистемам.Заявка = СтатусыПоСистемам.Заявка
	|			И МинимальныйСтатусЗаявокПоСистемам.ПорядокСтатуса = СтатусыПоСистемам.ПорядокСтатуса
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	1,
	|	МинимальныеСтатусыЗаявок.Заявка,
	|	МинимальныеСтатусыЗаявок.Статус.Порядок,
	|	МинимальныеСтатусыЗаявок.Статус
	|ИЗ
	|	РегистрСведений.МинимальныеСтатусыЗаявок КАК МинимальныеСтатусыЗаявок
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ЗаявкиНаРазработку КАК ЗаявкиНаРазработку
	|		ПО МинимальныеСтатусыЗаявок.Заявка = ЗаявкиНаРазработку.Ссылка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	МИНИМУМ(СтатусыЗаявокПоПриоритетам.ПриоритетСтроки) КАК ПриоритетСтроки,
	|	СтатусыЗаявокПоПриоритетам.Заявка КАК Заявка
	|ПОМЕСТИТЬ СтатусыЗаявокМинимальныйПриоритет
	|ИЗ
	|	СтатусыЗаявокПоПриоритетам КАК СтатусыЗаявокПоПриоритетам
	|
	|СГРУППИРОВАТЬ ПО
	|	СтатусыЗаявокПоПриоритетам.Заявка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	СтатусыЗаявокПоПриоритетам.Заявка КАК Заявка,
	|	СтатусыОбъектов.Ссылка КАК Статус,
	|	СтатусыОбъектов.ИндексКартинки КАК ИндексКартинки,
	|	ЕСТЬNULL(МинимальныеСтатусыЗаявок.ДатаИзменения, ДАТАВРЕМЯ(1, 1, 1)) КАК ДатаИзменения
	|ПОМЕСТИТЬ ДополнительныеДанныеЗаявок
	|ИЗ
	|	СтатусыЗаявокПоПриоритетам КАК СтатусыЗаявокПоПриоритетам
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ СтатусыЗаявокМинимальныйПриоритет КАК СтатусыЗаявокМинимальныйПриоритет
	|		ПО СтатусыЗаявокПоПриоритетам.Заявка = СтатусыЗаявокМинимальныйПриоритет.Заявка
	|			И СтатусыЗаявокПоПриоритетам.ПриоритетСтроки = СтатусыЗаявокМинимальныйПриоритет.ПриоритетСтроки
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.СтатусыОбъектов КАК СтатусыОбъектов
	|		ПО СтатусыЗаявокПоПриоритетам.ПорядокСтатуса = СтатусыОбъектов.Порядок
	|			И СтатусыЗаявокПоПриоритетам.Статус = СтатусыОбъектов.Ссылка
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.МинимальныеСтатусыЗаявок КАК МинимальныеСтатусыЗаявок
	|		ПО СтатусыЗаявокПоПриоритетам.Заявка = МинимальныеСтатусыЗаявок.Заявка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ЗаявкиНаРазработку.Ссылка КАК Ссылка,
	|	ЗаявкиНаРазработку.Система КАК Система,
	|	ЕСТЬNULL(СтруктураПодчиненности.Объект, ЗНАЧЕНИЕ(Документ.Задача.ПустаяСсылка)) КАК Задача
	|ПОМЕСТИТЬ ЗаявкиПодчиненныеЗадачи
	|ИЗ
	|	ЗаявкиНаРазработку КАК ЗаявкиНаРазработку
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.СтруктураПодчиненности КАК СтруктураПодчиненности
	|		ПО (ЗаявкиНаРазработку.Ссылка = (ВЫРАЗИТЬ(СтруктураПодчиненности.Родитель КАК Документ.ЗаявкаНаРазработку)))
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ЛичныеДелаСрезПоследних.Сотрудник КАК Пользователь,
	|	ВЫРАЗИТЬ(ЛичныеДелаСрезПоследних.Данные КАК Справочник.Филиалы) КАК Филиал
	|ПОМЕСТИТЬ ЛичныеДелаФилиалы
	|ИЗ
	|	РегистрСведений.ЛичныеДела.СрезПоследних(&ТекущаяДата, Событие = ЗНАЧЕНИЕ(Перечисление.СобытияПоЛичнымДелам.ПереведенВДругоеПодразделение)) КАК ЛичныеДелаСрезПоследних
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ЗаявкиПодчиненныеЗадачи.Ссылка КАК Ссылка,
	|	ЗаявкиПодчиненныеЗадачи.Система КАК СистемаУчета,
	|	ЗаявкиПодчиненныеЗадачи.Задача КАК Задача,
	|	ВЫБОР
	|		КОГДА ЗаявкиПодчиненныеЗадачи.Задача.Назначена = ЗНАЧЕНИЕ(Справочник.Пользователи.ПустаяСсылка)
	|			ТОГДА """"
	|		КОГДА ЗаявкиПодчиненныеЗадачи.Задача.Назначена = &Автор
	|			ТОГДА ""На мне""
	|		КОГДА ЗаявкиПодчиненныеЗадачи.Задача.Назначена = ЗаявкиПодчиненныеЗадачи.Задача.Заказчик
	|			ТОГДА ""Заказчик""
	|		КОГДА НЕ Филиалы.ТипФилиала ЕСТЬ NULL
	|				И Филиалы.ТипФилиала <> ЗНАЧЕНИЕ(Перечисление.ТипыФилиалов.НеIT)
	|			ТОГДА ""IT""
	|		ИНАЧЕ """"
	|	КОНЕЦ КАК Назначена
	|ПОМЕСТИТЬ ЗаявкиИЗадачи
	|ИЗ
	|	ЗаявкиПодчиненныеЗадачи КАК ЗаявкиПодчиненныеЗадачи
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.Задача КАК ДокументЗадача
	|		ПО ЗаявкиПодчиненныеЗадачи.Задача = ДокументЗадача.Ссылка
	|		ЛЕВОЕ СОЕДИНЕНИЕ ЛичныеДелаФилиалы КАК ЛичныеДелаФилиалы
	|		ПО ЗаявкиПодчиненныеЗадачи.Задача.Назначена = ЛичныеДелаФилиалы.Пользователь
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.Филиалы КАК Филиалы
	|		ПО (ЛичныеДелаФилиалы.Филиал = Филиалы.Ссылка)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ЗаявкиНаРазработку.Ссылка КАК Заявка,
	|	ЗаявкиНаРазработку.Номер КАК Номер,
	|	ЗаявкиНаРазработку.Дата КАК ДатаСоздания,
	|	ЗаявкиНаРазработку.Наименование КАК Наименование,
	|	ПРЕДСТАВЛЕНИЕ(ЗаявкиИЗадачи.СистемаУчета) КАК СистемаПредставление,
	|	ЗаявкиИЗадачи.Назначена КАК Назначена,
	|	ПРЕДСТАВЛЕНИЕ(ДополнительныеДанныеЗаявок.Статус) КАК СтатусПредставление,
	|	ДополнительныеДанныеЗаявок.ИндексКартинки КАК ИндексКартинки,
	|	ДополнительныеДанныеЗаявок.ДатаИзменения КАК ИзменениеСтатуса
	|ИЗ
	|	ЗаявкиИЗадачи КАК ЗаявкиИЗадачи
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ДополнительныеДанныеЗаявок КАК ДополнительныеДанныеЗаявок
	|		ПО ЗаявкиИЗадачи.Ссылка = ДополнительныеДанныеЗаявок.Заявка
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ЗаявкиНаРазработку КАК ЗаявкиНаРазработку
	|		ПО ЗаявкиИЗадачи.Ссылка = ЗаявкиНаРазработку.Ссылка
	|			И ЗаявкиИЗадачи.СистемаУчета = ЗаявкиНаРазработку.Система
	|ГДЕ
	|	ДополнительныеДанныеЗаявок.Статус В(&ВыбранныеСтатусы)
	|
	|УПОРЯДОЧИТЬ ПО
	|	ДатаСоздания
	|ИТОГИ ПО
	|	Заявка";
	#КонецОбласти

	Запрос.УстановитьПараметр("ТекущаяДата", ТекущаяДатаСеанса());
	Запрос.УстановитьПараметр("ДатаНачала", ДатаНачала);
	Запрос.УстановитьПараметр("ДатаОкончания", ДатаОкончания);
	Запрос.УстановитьПараметр("Автор", ПараметрыСеанса.ТекущийПользователь);
	Запрос.УстановитьПараметр("РазрабатываемыеСистемы", ПараметрыСеанса.РазрабатываемыеСистемы);
	Запрос.УстановитьПараметр("ВыбранныеСистемы", ВыбранныеСистемы);
	Запрос.УстановитьПараметр("ВыбранныеСтатусы", ВыбранныеСтатусы);

	ВыборкаЗаявка = Запрос.Выполнить().Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	
	Пока ВыборкаЗаявка.Следующий() Цикл
		
		Выборка = ВыборкаЗаявка.Выбрать();
		Назначена = "";
		Системы = "";
		СозданаСтрока = Ложь;
		Пока Выборка.Следующий() Цикл
			Наименование = СтрШаблон("[%1] %2", Выборка.Номер, Выборка.Наименование);
			Если ЗначениеЗаполнено(Выборка.Назначена) Тогда
				Если СтрНайти(Назначена, Выборка.Назначена) = 0 Тогда
					Назначена = Назначена + ?(ЗначениеЗаполнено(Назначена), СтрШаблон(" / %1", Выборка.Назначена), Выборка.Назначена);
				КонецЕсли;
			КонецЕсли;
			
			Если ЗначениеЗаполнено(Выборка.СистемаПредставление) Тогда
				Если СтрНайти(Системы, Выборка.СистемаПредставление) = 0 Тогда
					Системы = Системы + ?(ЗначениеЗаполнено(Системы), 
						СтрШаблон(", %1", Выборка.СистемаПредставление), Выборка.СистемаПредставление);
				КонецЕсли;
			КонецЕсли;
			
			Если НЕ СозданаСтрока Тогда
				СтрокаТаблицы = Таблица.Добавить();
				ЗаполнитьЗначенияСвойств(СтрокаТаблицы, Выборка);
				СозданаСтрока = Истина;
			КонецЕсли;
		КонецЦикла;	
		
		UID = ОбщегоНазначенияВызовСервера.ХэшСумма(Строка(ВыборкаЗаявка.Заявка), Истина);
		СтрокаТаблицы.UID = UID;
		СтрокаТаблицы.Наименование = Наименование;
		СтрокаТаблицы.Назначена = Назначена;
		СтрокаТаблицы.СистемаПредставление = Системы;
	КонецЦикла;

	ДанныеДерева = ИнструментыСервер.ПодготовитьДанныеИнструментаДляКлиента(Таблица, КолонкиИнструмента);

	Возврат ДанныеДерева;

КонецФункции
Функция СформироватьСписокЗадачи(Знач НастройкиСписка)

	КолонкиИнструмента = Новый Массив;
	КолонкиИнструмента.Добавить("Наименование");
	КолонкиИнструмента.Добавить("СтатусПредставление");
	КолонкиИнструмента.Добавить("Назначена");
	КолонкиИнструмента.Добавить("ИзменениеСтатуса");
	КолонкиИнструмента.Добавить("СистемаПредставление");
	КолонкиИнструмента.Добавить("Автор");
	КолонкиИнструмента.Добавить("ИндексКартинки");
	КолонкиИнструмента.Добавить("Задача");
	КолонкиИнструмента.Добавить("UID");
	КолонкиИнструмента.Добавить("ДатаСоздания");

	Таблица = Новый ТаблицаЗначений;
	Для Каждого Колонка Из КолонкиИнструмента Цикл
		Таблица.Колонки.Добавить(Колонка);
	КонецЦикла;

	ДатаНачала = НастройкиСписка.Период.ДатаНачала;
	ДатаОкончания = НастройкиСписка.Период.ДатаОкончания;
	ВыбранныеСтатусы = НастройкиСписка.ВыбранныеСтатусы;
	ВыбранныеСистемы = НастройкиСписка.ВыбранныеСистемы;

	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	Задача.Ссылка КАК Задача,
	|	Задача.Номер КАК Номер,
	|	Задача.Дата КАК ДатаСоздания,
	|	Задача.Наименование КАК Наименование,
	|	ПРЕДСТАВЛЕНИЕ(Задача.Назначена) КАК Назначена,
	|	ПРЕДСТАВЛЕНИЕ(Задача.Статус) КАК СтатусПредставление,
	|	Задача.ДатаИзмененияСтатуса КАК ИзменениеСтатуса,
	|	ПРЕДСТАВЛЕНИЕ(Задача.СистемаУчета) КАК СистемаПредставление,
	|	СтатусыОбъектов.ИндексКартинки КАК ИндексКартинки
	|ИЗ
	|	Документ.Задача КАК Задача
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.СтатусыОбъектов КАК СтатусыОбъектов
	|		ПО Задача.Статус = СтатусыОбъектов.Ссылка
	|ГДЕ
	|	Задача.Дата МЕЖДУ &ДатаНачала И &ДатаОкончания
	|	И Задача.Автор = &Автор
	|	И Задача.СистемаУчета В(&ВыбранныеСистемы)
	|	И Задача.Статус В(&ВыбранныеСтатусы)
	|
	|УПОРЯДОЧИТЬ ПО
	|	Задача.Дата";

	Запрос.УстановитьПараметр("ДатаНачала", ДатаНачала);
	Запрос.УстановитьПараметр("ДатаОкончания", ДатаОкончания);
	Запрос.УстановитьПараметр("Автор", ПараметрыСеанса.ТекущийПользователь);
	Запрос.УстановитьПараметр("ВыбранныеСистемы", ВыбранныеСистемы);
	Запрос.УстановитьПараметр("ВыбранныеСтатусы", ВыбранныеСтатусы);

	Выборка = Запрос.Выполнить().Выбрать();

	Пока Выборка.Следующий() Цикл
		СтрокаТаблицы = Таблица.Добавить();
		ЗаполнитьЗначенияСвойств(СтрокаТаблицы, Выборка);
		Наименование = СтрШаблон("[%1] %2", Выборка.Номер, Выборка.Наименование);
		UID = ОбщегоНазначенияВызовСервера.ХэшСумма(Строка(Выборка.Задача), Истина);
		СтрокаТаблицы.UID = UID;
		СтрокаТаблицы.Наименование = Наименование;
		СтрокаТаблицы.Назначена = ОбщегоНазначенияКлиентСервер.ПолучитьФамилиюИнициалыПользователя(СтрокаТаблицы.Назначена);
	КонецЦикла;

	ДанныеДерева = ИнструментыСервер.ПодготовитьДанныеИнструментаДляКлиента(Таблица, КолонкиИнструмента);

	Возврат ДанныеДерева;

КонецФункции

#КонецОбласти