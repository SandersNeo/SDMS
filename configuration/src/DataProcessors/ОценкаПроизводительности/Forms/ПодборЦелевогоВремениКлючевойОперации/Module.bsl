///////////////////////////////////////////////////////////////////////////////////////////////////////
// Copyright (c) 2019, ООО 1С-Софт
// Все права защищены. Эта программа и сопроводительные материалы предоставляются 
// в соответствии с условиями лицензии Attribution 4.0 International (CC BY 4.0)
// Текст лицензии доступен по ссылке:
// https://creativecommons.org/licenses/by/4.0/legalcode
///////////////////////////////////////////////////////////////////////////////////////////////////////

#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Параметры.Свойство("КлючеваяОперация", КлючеваяОперация);
	Параметры.Свойство("APDEX", ТекущийApdex);
	
	Если Не ЗначениеЗаполнено(Период.ДатаНачала) Тогда
		Период.ДатаНачала = ДобавитьМесяц(НачалоДня(ТекущаяДатаСеанса()), -3);
	КонецЕсли;
	Если Не ЗначениеЗаполнено(Период.ДатаОкончания) Тогда
		Период.ДатаОкончания = НачалоДня(ТекущаяДатаСеанса());
	КонецЕсли;
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ПодобратьЦелевоеВремя(Команда)                             
	РезультатПроверки = ПроверкаЗаполнения();
	Если РезультатПроверки Тогда
		
		ПараметрыПодбора = Новый Структура;
		ПараметрыПодбора.Вставить("КлючеваяОперация", КлючеваяОперация);
		ПараметрыПодбора.Вставить("ДатаНачала", Период.ДатаНачала);
		ПараметрыПодбора.Вставить("ДатаОкончания", Период.ДатаОкончания);
		ПараметрыПодбора.Вставить("ЦелевойAPDEX", ТекущийAPDEX);		
		РезультатПодбора = ПодборЦелевоеВремяНаСервере(ПараметрыПодбора);
		Если РезультатПодбора.Свойство("ОписаниеОшибки") Тогда
			Сообщение = Новый СообщениеПользователю;
			Сообщение.Текст = РезультатПодбора.ОписаниеОшибки;
			Сообщение.Сообщить();
			Возврат;
		КонецЕсли;
		РасчетныйAPDEX = РезультатПодбора.РасчетныйAPDEX;
		КоличествоЗамеров = РезультатПодбора.КоличествоЗамеров;
		ЦелевоеВремя = РезультатПодбора.ЦелевоеВремя;
		
		ДиаграммаЗамеровВремени.ТипДиаграммы = ТипДиаграммы.График;
		ДиаграммаЗамеровВремени.ОбластьПостроения.ШкалаЗначений.ТекстЗаголовка = НСтр("ru = 'Количество замеров, шт'");
		ДиаграммаЗамеровВремени.Очистить();
		Серия = ДиаграммаЗамеровВремени.Серии.Добавить("Время выполнения, с");		
		Для Каждого Замер Из РезультатПодбора.Замеры Цикл
			Для Каждого Запись Из Замер Цикл
				Точка = ДиаграммаЗамеровВремени.Точки.Добавить(Запись.Ключ);
				Точка.Текст = Формат(Запись.Ключ, "ЧН=0");
				ДиаграммаЗамеровВремени.УстановитьЗначение(Точка, Серия, Запись.Значение);
			КонецЦикла;
		КонецЦикла;		
	КонецЕсли;
КонецПроцедуры


#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Функция ПроверкаЗаполнения()
	Успешно = Истина;
	Если КлючеваяОперация.Пустая() Тогда
		Сообщение = Новый СообщениеПользователю;
		Сообщение.Текст = НСтр("ru = 'Не указана ключевая операция.'");
		Сообщение.Поле = "КлючеваяОперация";
		Сообщение.Сообщить();
		Успешно = Ложь;
	КонецЕсли;
	Если Не ЗначениеЗаполнено(ТекущийApdex) Тогда
		Сообщение = Новый СообщениеПользователю;
		Сообщение.Текст = НСтр("ru = 'Не указан текущий APDEX.'");
		Сообщение.Поле = "ТекущийApdex";
		Сообщение.Сообщить();
		Успешно = Ложь;
	КонецЕсли;
	Если Не ЗначениеЗаполнено(Период.ДатаНачала) Тогда
		Сообщение = Новый СообщениеПользователю;
		Сообщение.Текст = НСтр("ru = 'Не указана дата начала периода.'");
		Сообщение.Поле = "Период";
		Сообщение.Сообщить();
		Успешно = Ложь;
	КонецЕсли;
	Если Не ЗначениеЗаполнено(Период.ДатаОкончания) Тогда
		Сообщение = Новый СообщениеПользователю;
		Сообщение.Текст = НСтр("ru = 'Не указана дата окончания периода.'");
		Сообщение.Поле = "Период";
		Сообщение.Сообщить();
		Успешно = Ложь;
	КонецЕсли;
	Возврат Успешно;
КонецФункции

&НаСервереБезКонтекста
Функция ПодборЦелевоеВремяНаСервере(ПараметрыПодбора)
	
	РезультатПодбора = Новый Структура;
	РезультатПодбора.Вставить("Замеры", Новый Массив);
	РезультатПодбора.Вставить("КоличествоЗамеров", 0);
	РезультатПодбора.Вставить("ЦелевоеВремя", 0);
	РезультатПодбора.Вставить("РасчетныйAPDEX", 0);
	Минимум = 0;
	Максимум = 0;
	ДопустимаяРазница = 0.01;
	МаксимальноеКоличествоИтераций = 1000;
	Счетчик = 0;
	МВТ = Новый МенеджерВременныхТаблиц;
	
	
	Запрос = Новый Запрос("ВЫБРАТЬ
	                      |	Замеры.ВремяВыполнения КАК ВремяВыполнения,
	                      |	1 КАК КоличествоЗамеров
	                      |ПОМЕСТИТЬ ЗамерыОперации
	                      |ИЗ
	                      |	РегистрСведений.ЗамерыВремени КАК Замеры
	                      |ГДЕ
	                      |	Замеры.ДатаНачалаЗамера МЕЖДУ &ДатаНачала И &ДатаОкончания
	                      |	И Замеры.КлючеваяОперация = &КлючеваяОперация
	                      |;
	                      |
	                      |////////////////////////////////////////////////////////////////////////////////
	                      |ВЫБРАТЬ
	                      |	ЕСТЬNULL(МАКСИМУМ(ЗамерыОперации.ВремяВыполнения), 0) КАК МАКСИМУМВремяВыполнения,
	                      |	ЕСТЬNULL(МИНИМУМ(ЗамерыОперации.ВремяВыполнения), 0) КАК МИНИМУМВремяВыполнения,
	                      |	ЕСТЬNULL(СУММА(ЗамерыОперации.КоличествоЗамеров), 0) КАК КоличествоЗамеров
	                      |ИЗ
	                      |	ЗамерыОперации КАК ЗамерыОперации");
	Запрос.МенеджерВременныхТаблиц = МВТ;
	Запрос.УстановитьПараметр("ДатаНачала", (ПараметрыПодбора.ДатаНачала - Дата(1,1,1)) * 1000);	
	Запрос.УстановитьПараметр("ДатаОкончания", (ПараметрыПодбора.ДатаОкончания - Дата(1,1,1)) * 1000);
	Запрос.УстановитьПараметр("КлючеваяОперация", ПараметрыПодбора.КлючеваяОперация);
	
	Выборка = Запрос.Выполнить().Выбрать();
	Если Выборка.Следующий() Тогда
		Минимум = Выборка.МИНИМУМВремяВыполнения;
		Максимум = Выборка.МАКСИМУМВремяВыполнения;
		РезультатПодбора.КоличествоЗамеров = Выборка.КоличествоЗамеров;
	Иначе
		РезультатПодбора.Вставить("ОписаниеОшибки", НСтр("ru = 'Не удалось получить данные о замерах, попробуйте изменить настройки.'"));
		Возврат РезультатПодбора;
	КонецЕсли;
	
	Если РезультатПодбора.КоличествоЗамеров = 0 Тогда
		РезультатПодбора.Вставить("ОписаниеОшибки",
			ПодставитьПараметрыВСтроку(
				НСтр("ru = 'Получено 0 замеров по ключевой операции %1. Измените период или выберите другую ключевую операцию.'"),
				ПараметрыПодбора.КлючеваяОперация));
		Возврат РезультатПодбора;
	КонецЕсли;
	
	ТекущееЦелевоеВремя = (Минимум + Максимум) / 2;
	РасчетныйAPDEX = ЗначениеAPDEX(МВТ, ТекущееЦелевоеВремя);
	Отклонение = Макс(РасчетныйAPDEX - ПараметрыПодбора.ЦелевойAPDEX, ПараметрыПодбора.ЦелевойAPDEX - РасчетныйAPDEX);
	
	Пока Отклонение > ДопустимаяРазница
		И Счетчик < МаксимальноеКоличествоИтераций
		Цикл
		Счетчик = Счетчик + 1;
		ДанныеМинимум = ОтклонениеAPDEX(Минимум, ТекущееЦелевоеВремя, МВТ, ПараметрыПодбора.ЦелевойAPDEX);
		ДанныеМаксимум = ОтклонениеAPDEX(Максимум, ТекущееЦелевоеВремя, МВТ, ПараметрыПодбора.ЦелевойAPDEX);
		
		Если Максимум - Минимум <= 0.002 Тогда
			Прервать;
		ИначеЕсли ДанныеМинимум.Отклонение <= ДанныеМаксимум.Отклонение Тогда
			Максимум = ТекущееЦелевоеВремя;
			ТекущееЦелевоеВремя = ДанныеМинимум.ТекущееЦелевоеВремя;			
			Отклонение = ДанныеМинимум.Отклонение;
			РасчетныйAPDEX = ДанныеМинимум.APDEX;
		ИначеЕсли ДанныеМинимум.Отклонение > ДанныеМаксимум.Отклонение Тогда
			Минимум = ТекущееЦелевоеВремя;
			ТекущееЦелевоеВремя = ДанныеМаксимум.ТекущееЦелевоеВремя;			
			Отклонение = ДанныеМаксимум.Отклонение;
			РасчетныйAPDEX = ДанныеМаксимум.APDEX;
		КонецЕсли;
		
	КонецЦикла;
	
	РезультатПодбора.ЦелевоеВремя = ТекущееЦелевоеВремя;
	РезультатПодбора.РасчетныйAPDEX = РасчетныйAPDEX; 
	РезультатПодбора.Замеры = СоответствиеЗамеров(МВТ);
		
	Возврат РезультатПодбора;
	
КонецФункции

&НаСервереБезКонтекста
Функция ОтклонениеAPDEX(ГраницаИнтервала, ТекущееЦелевоеВремя, МенеджерВременныхТаблиц, ЦелевойAPDEX)
	ТекущееЦелевоеВремяНовое = ОКР((ГраницаИнтервала + ТекущееЦелевоеВремя) / 2, 3);
	APDEX = ЗначениеAPDEX(МенеджерВременныхТаблиц, ТекущееЦелевоеВремяНовое);
	Отклонение = Макс(APDEX - ЦелевойAPDEX, ЦелевойAPDEX - APDEX);	
	Возврат Новый Структура("ТекущееЦелевоеВремя, APDEX, Отклонение", ТекущееЦелевоеВремяНовое, APDEX, Отклонение)
КонецФункции

&НаСервереБезКонтекста
Функция СоответствиеЗамеров(МенеджерВременныхТаблиц)
	СоответствиеЗамеров = Новый Массив;
	Запрос = Новый Запрос("ВЫБРАТЬ
	                      |	ВЫРАЗИТЬ(ЗамерыОперации.ВремяВыполнения КАК ЧИСЛО(15, 0)) КАК ВремяВыполнения,
	                      |	СУММА(ЗамерыОперации.КоличествоЗамеров) КАК КоличествоЗамеров
	                      |ИЗ
	                      |	ЗамерыОперации КАК ЗамерыОперации
	                      |
	                      |СГРУППИРОВАТЬ ПО
	                      |	ВЫРАЗИТЬ(ЗамерыОперации.ВремяВыполнения КАК ЧИСЛО(15, 0))
	                      |
	                      |УПОРЯДОЧИТЬ ПО
	                      |	ВремяВыполнения");
	Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		Замер = Новый Соответствие;
		Замер.Вставить(Выборка.ВремяВыполнения, Выборка.КоличествоЗамеров);
		СоответствиеЗамеров.Добавить(Замер);
	КонецЦикла;
	Возврат СоответствиеЗамеров;
КонецФункции

&НаСервереБезКонтекста
Функция ЗначениеAPDEX(МенеджерВременныхТаблиц, ТекущееЦелевоеВремя)
	Запрос = Новый Запрос("ВЫБРАТЬ
	               |	СУММА(ВЫБОР
	               |			КОГДА ЗамерыОперации.ВремяВыполнения <= &ЦелевоеВремя
	               |				ТОГДА ЗамерыОперации.КоличествоЗамеров
	               |			ИНАЧЕ 0
	               |		КОНЕЦ) КАК T,
	               |	СУММА(ВЫБОР
	               |			КОГДА ЗамерыОперации.ВремяВыполнения > &ЦелевоеВремя
	               |					И ЗамерыОперации.ВремяВыполнения <= 4 * &ЦелевоеВремя
	               |				ТОГДА ЗамерыОперации.КоличествоЗамеров
	               |			ИНАЧЕ 0
	               |		КОНЕЦ) КАК T_4T,
	               |	СУММА(ЗамерыОперации.КоличествоЗамеров) КАК N,
	               |	ЕСТЬNULL((СУММА(ВЫБОР
	               |			КОГДА ЗамерыОперации.ВремяВыполнения <= &ЦелевоеВремя
	               |				ТОГДА ЗамерыОперации.КоличествоЗамеров
	               |			ИНАЧЕ 0
	               |		КОНЕЦ) + СУММА(ВЫБОР
	               |			КОГДА ЗамерыОперации.ВремяВыполнения > &ЦелевоеВремя
	               |					И ЗамерыОперации.ВремяВыполнения <= 4 * &ЦелевоеВремя
	               |				ТОГДА ЗамерыОперации.КоличествоЗамеров
	               |			ИНАЧЕ 0
	               |		КОНЕЦ) / 2) / СУММА(ЗамерыОперации.КоличествоЗамеров),0) КАК APDEX
	               |ИЗ
	               |	ЗамерыОперации КАК ЗамерыОперации");
	Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
	Запрос.УстановитьПараметр("ЦелевоеВремя", ТекущееЦелевоеВремя);
	Выборка = Запрос.Выполнить().Выбрать();
	Выборка.Следующий();
	Возврат Окр(Выборка.APDEX, 3);
КонецФункции

// Подставляет параметры в строку. Максимально возможное число параметров - 9.
// Параметры в строке задаются как %<номер параметра>. Нумерация параметров начинается с единицы.
//
// Параметры:
//  ШаблонСтроки  - Строка - шаблон строки с параметрами (вхождениями вида "%<номер параметра>", 
//                           например "%1 пошел в %2");
//  Параметр<n>   - Строка - значение подставляемого параметра.
//
// Возвращаемое значение:
//  Строка   - текстовая строка с подставленными параметрами.
//
// Пример:
//  СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(НСтр("ru='%1 пошел в %2'"), "Вася", "Зоопарк") = "Вася пошел
//  в Зоопарк".
//
&НаСервереБезКонтекста
Функция ПодставитьПараметрыВСтроку(Знач ШаблонСтроки,
	Знач Параметр1, Знач Параметр2 = Неопределено, Знач Параметр3 = Неопределено,
	Знач Параметр4 = Неопределено, Знач Параметр5 = Неопределено, Знач Параметр6 = Неопределено,
	Знач Параметр7 = Неопределено, Знач Параметр8 = Неопределено, Знач Параметр9 = Неопределено) Экспорт
	
	ЕстьПараметрыСПроцентом = СтрНайти(Параметр1, "%")
		Или СтрНайти(Параметр2, "%")
		Или СтрНайти(Параметр3, "%")
		Или СтрНайти(Параметр4, "%")
		Или СтрНайти(Параметр5, "%")
		Или СтрНайти(Параметр6, "%")
		Или СтрНайти(Параметр7, "%")
		Или СтрНайти(Параметр8, "%")
		Или СтрНайти(Параметр9, "%");
		
	Если ЕстьПараметрыСПроцентом Тогда
		Возврат ПодставитьПараметрыСПроцентом(ШаблонСтроки, Параметр1,
			Параметр2, Параметр3, Параметр4, Параметр5, Параметр6, Параметр7, Параметр8, Параметр9);
	КонецЕсли;
	
	ШаблонСтроки = СтрЗаменить(ШаблонСтроки, "%1", Параметр1);
	ШаблонСтроки = СтрЗаменить(ШаблонСтроки, "%2", Параметр2);
	ШаблонСтроки = СтрЗаменить(ШаблонСтроки, "%3", Параметр3);
	ШаблонСтроки = СтрЗаменить(ШаблонСтроки, "%4", Параметр4);
	ШаблонСтроки = СтрЗаменить(ШаблонСтроки, "%5", Параметр5);
	ШаблонСтроки = СтрЗаменить(ШаблонСтроки, "%6", Параметр6);
	ШаблонСтроки = СтрЗаменить(ШаблонСтроки, "%7", Параметр7);
	ШаблонСтроки = СтрЗаменить(ШаблонСтроки, "%8", Параметр8);
	ШаблонСтроки = СтрЗаменить(ШаблонСтроки, "%9", Параметр9);
	Возврат ШаблонСтроки;
	
КонецФункции

// Вставляет параметры в строку, учитывая, что в параметрах могут использоваться подстановочные слова %1, %2 и т.д.
&НаСервереБезКонтекста
Функция ПодставитьПараметрыСПроцентом(Знач СтрокаПодстановки,
	Знач Параметр1, Знач Параметр2 = Неопределено, Знач Параметр3 = Неопределено,
	Знач Параметр4 = Неопределено, Знач Параметр5 = Неопределено, Знач Параметр6 = Неопределено,
	Знач Параметр7 = Неопределено, Знач Параметр8 = Неопределено, Знач Параметр9 = Неопределено)
	
	Результат = "";
	Позиция = СтрНайти(СтрокаПодстановки, "%");
	Пока Позиция > 0 Цикл 
		Результат = Результат + Лев(СтрокаПодстановки, Позиция - 1);
		СимволПослеПроцента = Сред(СтрокаПодстановки, Позиция + 1, 1);
		ПодставляемыйПараметр = Неопределено;
		Если СимволПослеПроцента = "1" Тогда
			ПодставляемыйПараметр = Параметр1;
		ИначеЕсли СимволПослеПроцента = "2" Тогда
			ПодставляемыйПараметр = Параметр2;
		ИначеЕсли СимволПослеПроцента = "3" Тогда
			ПодставляемыйПараметр = Параметр3;
		ИначеЕсли СимволПослеПроцента = "4" Тогда
			ПодставляемыйПараметр = Параметр4;
		ИначеЕсли СимволПослеПроцента = "5" Тогда
			ПодставляемыйПараметр = Параметр5;
		ИначеЕсли СимволПослеПроцента = "6" Тогда
			ПодставляемыйПараметр = Параметр6;
		ИначеЕсли СимволПослеПроцента = "7" Тогда
			ПодставляемыйПараметр = Параметр7
		ИначеЕсли СимволПослеПроцента = "8" Тогда
			ПодставляемыйПараметр = Параметр8;
		ИначеЕсли СимволПослеПроцента = "9" Тогда
			ПодставляемыйПараметр = Параметр9;
		КонецЕсли;
		Если ПодставляемыйПараметр = Неопределено Тогда
			Результат = Результат + "%";
			СтрокаПодстановки = Сред(СтрокаПодстановки, Позиция + 1);
		Иначе
			Результат = Результат + ПодставляемыйПараметр;
			СтрокаПодстановки = Сред(СтрокаПодстановки, Позиция + 2);
		КонецЕсли;
		Позиция = СтрНайти(СтрокаПодстановки, "%");
	КонецЦикла;
	Результат = Результат + СтрокаПодстановки;
	
	Возврат Результат;
КонецФункции

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	Если ЗначениеЗаполнено(КлючеваяОперация) и ЗначениеЗаполнено(ТекущийAPDEX) тогда
		ПодобратьЦелевоеВремя("");	
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ЗаписатьЦелевоеВремяВКлючевуюОперациюНаСервере()
	
	Если КлючеваяОперация.ЦелевоеВремя <> ЦелевоеВремя тогда
		
		
		МассивНаЗапись = Новый Массив;
		Запрос = Новый Запрос;
		Запрос.Текст = "ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	ПрофилиКлючевыхОперацийКлючевыеОперацииПрофиля.Ссылка КАК Ссылка
		|ИЗ
		|	Справочник.ПрофилиКлючевыхОпераций.КлючевыеОперацииПрофиля КАК ПрофилиКлючевыхОперацийКлючевыеОперацииПрофиля
		|ГДЕ
		|	ПрофилиКлючевыхОперацийКлючевыеОперацииПрофиля.КлючеваяОперация = &КлючеваяОперация";
		Запрос.УстановитьПараметр("КлючеваяОперация", КлючеваяОперация);
		
		Отбор = Новый Структура("КлючеваяОперация", КлючеваяОперация);
		
		Выборка = Запрос.Выполнить().Выбрать();
		Пока Выборка.Следующий() Цикл
			ОбъектПрофиль = Выборка.Ссылка.ПолучитьОбъект();
			Если Ложь тогда
				ОбъектПрофиль = Справочники.ПрофилиКлючевыхОпераций.СоздатьЭлемент();	
			КонецЕсли;
			
			СтрокиНаИзменениеВПрофиле = ОбъектПрофиль.КлючевыеОперацииПрофиля.НайтиСтроки(Отбор);
			
			Для Каждого Строка из СтрокиНаИзменениеВПрофиле Цикл
				Строка.ЦелевоеВремя = ЦелевоеВремя;	
			КонецЦикла;
			
			Попытка
				ОбъектПрофиль.Записать();
			Исключение
				Сообщить("Не удалось записать целевое время в профиле: " +ОбъектПрофиль + " по причине:" + ОписаниеОшибки());
			КонецПопытки;
			
		КонецЦикла;
		
		КлючеваяОперацияОбъект = КлючеваяОперация.ПолучитьОбъект();
		КлючеваяОперацияОбъект.ЦелевоеВремя = ЦелевоеВремя;
		
		Попытка
			КлючеваяОперацияОбъект.Записать();
			Сообщить("Целевое время установлено!!!");
		Исключение
			Сообщить("Не удалось записать целевое время для ключевой операции по причине:" + ОписаниеОшибки());
		КонецПопытки;
	Иначе
		Сообщить("Целевое время не изменилось!!!");
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ЗаписатьЦелевоеВремяВКлючевуюОперацию(Команда)
	ЗаписатьЦелевоеВремяВКлючевуюОперациюНаСервере();
КонецПроцедуры

#КонецОбласти
