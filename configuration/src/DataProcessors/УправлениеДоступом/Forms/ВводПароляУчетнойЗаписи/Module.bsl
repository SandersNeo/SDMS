///////////////////////////////////////////////////////////////////////////////////////////////////////
// Copyright (c) 2025, ООО ДНС Технологии
// SDMS (Software Development Management System) — это корпоративная система учета разработки и управления проектами 
// Все права защищены. Эта программа и сопроводительные материалы предоставляются 
// в соответствии с условиями лицензии General Public License (GNU GPL v3)
// Текст лицензии доступен по ссылке:
// https://www.gnu.org/licenses/gpl-3.0.html
///////////////////////////////////////////////////////////////////////////////////////////////////////

#Область ОбработчикиКоманд

&НаКлиенте
Процедура ПродолжитьВыполнение(Команда)
	
	Если ЗначениеЗаполнено(Пароль) Тогда
		Если ЭтотПарольКорректный(Пароль) Тогда
			Закрыть(Пароль);
		Иначе
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю("Введен неверный пароль.", , "Пароль", "Пароль");
		КонецЕсли;
	Иначе
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю("Не введен пароль.", , "Пароль", "Пароль");
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервереБезКонтекста
Функция ПолучитьХешПароля(Знач Пароль)
	
	ХэшСтрока = "";
	
    ХешSHA1 = Новый ХешированиеДанных(ХешФункция.SHA1);
    ХешSHA1.Добавить(Пароль);
	
    ХэшСтрока = Base64Строка(ХешSHA1.ХешСумма);
	
	ХешSHA1 = Новый ХешированиеДанных(ХешФункция.SHA1);
    ХешSHA1.Добавить(ВРег(Пароль));
	
	Возврат ХэшСтрока + "," + Base64Строка(ХешSHA1.ХешСумма);
    
КонецФункции

&НаСервереБезКонтекста
Функция ЭтотПарольКорректный(Знач Пароль)
	
	ТекущийПользовательИБ = ПользователиИнформационнойБазы.ТекущийПользователь();
	СохраненныйПароль = ТекущийПользовательИБ.СохраняемоеЗначениеПароля;
	
	ХешПароля = ПолучитьХешПароля(Пароль);
	
	Возврат (СохраненныйПароль = ХешПароля);
	
КонецФункции

#КонецОбласти
